#library "mineswpr"
#include "zcommon.acs"

int TIDIndexStart;
int TIDIndex = 60000; //Start for TIDs to assign to scannable things
bool sweeperActive[8]; //code portion taken from BoA_Compass shamelessly - Ozy81

/* Called by each mine (or other actor you want to scan for) */
Script "SetRadarTID" (void)
{
	If (!ActivatorTID())
	{
		If (TidIndexStart == 0) {TIDIndexStart = TIDIndex;}
		Thing_ChangeTID(0, TIDIndex++);
	}
}

/* Modified from the wiki - http://zdoom.org/wiki/Hudmessageonactor */
Function void HudMessageOnActor(int tid, int msgID, int Range, str Font, str text, int holdTime, str colour)
{
	int dist, ang, vang, pitch, x, y, multiplierX, multiplierY;
	int HUDX = 1024;
	int HUDY = 768;
	int ratio = 1.0;

	x = GetActorX(tid) - GetActorX(0);
	y = GetActorY(tid) - GetActorY(0); 

	vang = VectorAngle(x,y);
	ang = (vang - GetActorAngle(0) + 1.0) % 1.0;

	if(((vang+0.125)%0.5) > 0.25) dist = FixedDiv(y, sin(vang));
	else dist = FixedDiv(x, cos(vang));

	if ((ang < 0.2 || ang > 0.8) && (dist >> 16 < Range))
	{
		ratio = (Range * 1.0 - dist) / Range;

		HUDX = HUDX - (HUDX * ratio / 1.0);
		HUDY = HUDY - (HUDY * ratio / 1.0);

		SetHudSize(HUDX, HUDY, 1);

		int Zdist = GetActorZ(tid) - (GetActorZ(0) + GetActorViewHeight(0)); 

		pitch = VectorAngle(dist, ZDist);
		pitch = (pitch + GetActorPitch(0)) % 1.0;

		if ((hudX/2) * sin(ang) != 0 && cos(ang) != 0 && (hudX/2) * sin(pitch) != 0 && cos(pitch) != 0)
		{
			x = HUDX/2 - ((HUDX/2) * sin(ang) / cos(ang));
			y = HUDY/2 - ((HUDX/2) * sin(pitch) / cos(pitch));

			int bob = 0;
			int newy = y;

			If (GetSpeed(0))
			{ // Mostly negate movebob
				int angle = (Timer() * 1.0 / 20) % 1.0;
				bob = FixedMul(GetCvar("movebob"), sin(angle)) / 4;
				If (bob / 1.0 > 16) bob = 16;
				newy = y * (1.0 + bob / 4) / 1.0;
			}

			SetFont(Font);
			HudMessage(s:text; HUDMSG_PLAIN | HUDMSG_NOTWITHFULLMAP | HUDMSG_LAYER_UNDERHUD, msgID, colour, (x << 16) + 32.0, (newy << 16) + 32.0, holdTime);
		}
	}
}

Script "W_PlayerLoop" ENTER
{
	While (GameType() != GAME_TITLE_MAP)
	{
		If (CheckInventory("Power") && sweeperActive[playerNumber()] && TIDIndexStart > 0) //Only run if you have enough batteries, minesweeper & mines in place.
		{
			TakeInventory("Power",1);
			
			int HUDX = GetScreenWidth();
			int HUDY = GetScreenHeight();

			SetHudSize(HUDX, HUDY, 1);

			For (int i = TIDIndexStart; i <= TIDIndex; ++i)
			{
				If (
					(GetActorZ(i) < (GetActorZ(0) + GetActorViewHeight(0)) ||	//Only show the marker if it's below player view height or
					CheckSight(0, i, CSF_NOBLOCKALL)) && 				// if the mine is still otherwise in line of sight and
					StrCmp(GetActorClass(0), "Underwater", 10) 			// if they're not underwater mines
				)
				{
					HudMessageonActor(i, i, 512, "MSPUC0", "A", 0.1, -1); //Range and sprite can be changed
				}
				If (
					(CheckInventory("Power") < 1)
					)
					{
						TakeInventory("MineSweeper",9999);
					}
			}
		}
		Delay(1);
	}
}

Function int GetSpeed(int tid)
{
	int x = GetActorVelX(tid);
	int y = GetActorVelY(tid);
	int z = GetActorVelZ(tid);

	int speed = (FixedMul(x, x) + FixedMul(y, y) + FixedMul(z, z)) >> 16;

	return speed;
}

script "BoA_MineSweeper"(void) //code portion taken from BoA_Compass shamelessly - Ozy81
{
	if(sweeperActive[playerNumber()])
		sweeperActive[playerNumber()]=0;
	else
		sweeperActive[playerNumber()]=1;
}