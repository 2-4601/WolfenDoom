#library "boalib"
#include "zcommon.acs"
#import "stealth.acs" //mxd

global int 1:disable_mutants; //mxd. When set to true, Mutant and MutantMelee will immediately despawn after spawning.
global int 2:disable_big_mutants; //mxd. When set to true, BigMutant1 and BigMutant2 will immediately despawn after spawning.
global int 3:disable_supermutants; //mxd. When set to true, UberMutant and SuperMutant will immediately despawn after spawning.
global int 4:astrostein; //[Ed} When set to true, grenades will change to the Astrostein variant

str darrenmessage[21]=
{
	"\cCHey, Bill. Darren here. I found the back entrance of the headquarters, but there seems to be more nazi activity than we expected. Be careful!",
	"\cCTime to get out here! The alarm has been activated! There's a switch nearby to lower the gates. Go for it, if you haven't already. I'll be waiting for you outside the area.",
	"\cCLooks like we got in unnoticed. But be prepared. There will be heavy resistance!",
	"\cCWell done, BJ! I'm almost caught up with you two. Follow the railroad, but make sure to take that Panzerschreck with you. I spotted a German tank heading in your direction!",
	"\cCThe Germans haven't noticed us yet. They're concentrating on the beach area, so we have first strike advantage. Get the C4 explosives, bring their artillery down and get back here when you're done. I'll be waitng for you.",
	"\cCWell done! Time to get outta here and let the infantry finish up. I'm waiting for you at the drop point. Just head back here, but be careful... I saw some Nazi paratrooper reinforcements dropping in!",
	"\cCAlright, boy! Make your way back to the upper part of the dam. We'll be waiting you at the north gate.",
	"\cCGet on it, boy! I'll head back and lead the reinforcements to the center of the city to catch up with you as fast as I can!",
	"\cCHurry up! The Krauts are coming! They'll reach the city in about 30 seconds! I'm already on my way with the allied reinforcements, but you need to hold on for a while. Secure the Nebelwerfer at all costs! Check the area near the objective for some ammo and medikits!",
	"\cCYeeehaw! The cavalry has arrived! Time to beat these Krauts up! Let's bring them down for good!",
	"\cCBetter get in and out quickly. It's hot and I don't feel comfortable in my uniform anymore... Good luck, Bill!",
	"\cCBill? Can you hear me? Take the relic and get back to the meeting point. Nazi divisions are heading towards your position. C'mon, get out of there! Move it!",
	"\cCProf. Gutenberg was right, the Eisenmann files led us directly to the secret facility. The railroad brought us already very close but we can't follow you any further unsighted. Time for your one-man-superhero show Bill. The facility's entrance is east from us. We will follow the rail track and secure your escape, and you, make sure to blow up that bitch!",
	"\cCHey Bill, glad that you are still alive. Get to the facility and blow it up, we will wait for you here and secure the train station.",
	"\cCDamn, Bill! I could even feel the ground shaking here. Get out quickly before the whole facility colapses. Every second counts now!",
	"\cCWell done Bill, get on to the next one and place your C4 package quickly!",
	"\cCDamnit, the Nazis noticed you. You need to be quick now, they will flood the facility in about 20 minutes. Get through this, place your C4 and get out alive!",
	"\cCIt's beginning Bill, you need to get out now! Your time is over! Quick!",
	"\cCOkay Bill, we are very close to the town now. Get equipped, investigate the buildings and try to find a way into the secret underground labs. Get the files and return, we will secure the area here.",
	"\cCGood job so far Bill, but be careful now! There is a prominent treat somewhere down there, Graf Holzbein, and he is well known for his cruelties. Take care, Darren out.",
	"\cCI can't believe you found them! Well done, get out now Bill, and take care - I can hear tan chains closing in, your way back won't be as easy as your way in.",
};
str prisonermessage[3]=
{
	"\cCIt's about time, Man! Now, let's get out of this hell. Take the red key, and let's grab some equipment before leaving.",
	"\cCDamnit, they noticed us! Now we're really in trouble! C'mon!",
	"\cCDamn, William! That was a blast!"
};

script "BoADialogue"(int who, int which)
{
	str message;
	str headtalky;
	str headsilent;

	//mxd. Choose which graphics and text to display
	if(who == 1) // Darren
	{
		headtalky = "MS_DARR1";
		headsilent = "MS_DARR0";
		message = StrParam(s:"\cEDirty Darren\n", s:darrenmessage[which]);
	}
	else if(who == 2) // Prisoner
	{
		headtalky = "MS_PRIB1";
		headsilent = "MS_PRIB0";
		message = StrParam(s:"\cEAgent Ryan\n", s:prisonermessage[which]);
	}

	// Fade in HUD BGs
	SetHudSize(320, 200, true);
	SetFont("HEADBAR");
	hudmessagebold(s:"A"; HUDMSG_FADEINOUT, 4, CR_UNTRANSLATED, 160.0, 24.0, 9.0, 1.0, 1.0);
	SetFont("HEADBOVL"); //mxd. Frame overlay
	hudmessagebold(s:"A"; HUDMSG_FADEINOUT, 1, CR_UNTRANSLATED, 160.0, 24.0, 9.0, 1.0, 1.0); //mxd

	// Fade in the portrait
	SetFont(headsilent);
	hudmessagebold(s:"A"; HUDMSG_FADEINOUT, 3, CR_UNTRANSLATED, 21.0, 16.0, 999.0, 1.0, 999.0);

	// Start talk animation
	Delay(35);
	SetFont(headtalky);
	hudmessagebold(s:"A"; HUDMSG_PLAIN, 3, CR_UNTRANSLATED, 21.0, 16.0, 6.0);

	// Show message
	AmbientSound("RADIONOS",127);
	SetHudSize(640, 400, false);
	SetFont("SMALLFONT");
	SetHudWrapWidth(540);
	hudmessagebold(s:message; HUDMSG_TYPEON, 2, CR_GRAY, 100.1, 13.1, 6.0, 0.03);

	// Stop talk animation
	delay(5*35);
	SetHudSize(320, 200, true);
	SetFont(headsilent);
	hudmessagebold(s:"A"; HUDMSG_PLAIN, 3, CR_UNTRANSLATED, 21.0, 16.0, 2.0);

	// Fade out the portrait
	delay(2*35);
	hudmessagebold(s:"A"; HUDMSG_FADEINOUT, 3, CR_UNTRANSLATED, 21.0, 16.0, 0.0, 0.0, 1.0);
}
//Controls
Script "AutoReload"(void)
{
	SetResultValue(GetCVar("autoreload"));
}
Script "ShellLifetime"(void)
{
	SetResultValue(GetCVar("casinglifetime"));
}
Script "RecoilAmount"(void)
{
	SetResultValue(GetCVar("recoilamount"));
}
script "WWNaziSkillCheck" (void)
{
	SetResultValue(GameSkill());
}
//Weather
Script "RainSwitch"(void)
{
	SetResultValue(GetCVar("rainswitch"));
}
Script "SnowSwitch"(void)
{
	SetResultValue(GetCVar("snowswitch"));
}
Script "CinderSwitch"(void)
{
	SetResultValue(GetCVar("cinderswitch"));
}

//sprinting stamina
int cooldown[8];

script "Dashing" ENTER
{
	if(!CheckActorClass(0,"BoAPlayer")) terminate;

	int buttons = GetPlayerInput(-1,INPUT_BUTTONS);
	if((buttons & BT_SPEED) && (buttons & BT_FORWARD) && CheckInventory("Stamina") && cooldown[PlayerNumber()] <= 0)
	{
		SetActorProperty(0,APROP_SPEED,2.0);
		TakeInventory("Stamina",3);
	}
	else
	{
		SetActorProperty(0,APROP_SPEED,1.0);
		cooldown[PlayerNumber()]--;
		delay(random(1,5));
		GiveInventory("Stamina",1);
	}
	if(!CheckInventory("Stamina"))
		cooldown[PlayerNumber()]=100;
	delay(1);
	restart;
}

Script "Exhausted" ENTER
{
	if(!CheckActorClass(0,"BoAPlayer")||GameType()==GAME_TITLE_MAP)
		terminate;
	while(CheckInventory("Stamina")<25)
	{
		if(GetActorProperty(0,APROP_WATERLEVEL)<3)
			PlaySound(0,"player/breathing",CHAN_VOICE);
		Delay(50);
	}
	Delay(1);
	restart;
}

//mxd. Mutants despawn checks
script "DisableMutants" (void)
{
	if(disable_mutants) Thing_Remove(0);
}

script "DisableBigMutants" (void)
{
	if(disable_big_mutants) Thing_Remove(0);
}

script "DisableSuperMutants" (void)
{
	if(disable_supermutants) Thing_Remove(0);
}

//Astrostein check
script "AstrosteinCheck" (void)
{
	SetResultValue(astrostein);
}

//Compass
bool compassActive[8];

int dots;		//Items to appear on compass
int marks[50];	//Items with tids

//FUNCTIONS
function int _cos(int a)
{
	a%=1.0;
	if(a<0)
		a+=1.0;
	if(a>0.5)
		a=-a+1.0;
	return cos(a);
}

function int _sin(int a)
{
	return _cos(a+0.25);
}

script "BoA_Compass" ENTER
{
	if(compassActive[playerNumber()])
	{
		SetHUDSize(1024,768,1);
		int a=GetActorAngle(0);
		int x=-_cos(a)*34/1.0,y=_sin(a)*34/1.0;
		SetFont("COMPASS1");
		HUDMessage(s:"N"; HUDMSG_PLAIN,0,CR_UNTRANSLATED,x * 1.0 + 85.0,y * 1.0 + 170.0,0.03);
		HUDMessage(s:"S"; HUDMSG_PLAIN,0,CR_UNTRANSLATED,-x * 1.0 + 85.0,-y * 1.0 + 170.0,0.03);
		HUDMessage(s:"E"; HUDMSG_PLAIN,0,CR_UNTRANSLATED,-y * 1.0 + 85.0,x * 1.0 + 170.0,0.03);
		HUDMessage(s:"W"; HUDMSG_PLAIN,0,CR_UNTRANSLATED,y * 1.0 + 85.0,-x * 1.0 + 170.0,0.03);
		SetFont("COMPASS");
		HUDMessage(s:"A"; HUDMSG_PLAIN,0,CR_UNTRANSLATED,85.0,162.0,0.03);
		for(int i=0;i<dots;i++)
			drawdot(4000+i);
		for(i=0;marks[i]!=0;i++)
			drawdot(marks[i]);
	}
	Delay(1);
	restart;
}

script "BoA_CompassQueue"(void)
{
	if(ActivatorTID())
	{
		int i;
		while(marks[i]!=0)
			i++;
		marks[i]=ActivatorTID();
	}
	else
		Thing_ChangeTID(0,4000+dots++);
}

function void drawdot(int tid)
{
	SetHUDSize(1024,768,1);
	if(GetActorProperty(tid,APROP_Health)>0)
	{
		int dx = GetActorX(0) - GetActorX(tid),dy = GetActorY(0) - GetActorY(tid);
		int a = GetActorAngle(0) - VectorAngle(dx, dy) - 0.25;
		dx /= 1.0;
		dy /= 1.0;
		int dist = sqrt(dx*dx + dy*dy),rdist = dist / 10;
		if(rdist > 33)
			rdist = 33;
		if(CheckActorClass(tid,"Akten")||CheckActorClass(tid,"AktenEisenmann")||CheckActorClass(tid,"SpearOfDestiny"))
		{
			SetFont("GOAL1");
			HUDMessage(s:"A"; HUDMSG_PLAIN, 0, CR_UNTRANSLATED, (-_cos(a) * rdist / 1.0) * 1.0 + 85.0, (_sin(a) * rdist / 1.0) * 1.0 + 170.0,0.03);
		}
		else if(GetActorProperty(tid,APROP_RenderStyle)!=STYLE_None)
		{
			SetFont("GOAL2");
			HUDMessage(s:"A"; HUDMSG_PLAIN, 0, CR_UNTRANSLATED, (-_cos(a) * rdist / 1.0) * 1.0 + 85.0, (_sin(a) * rdist / 1.0) * 1.0 + 170.0,0.03);
		}
		/*SetFont("SmallFont");
		HUDMessage(i:dist / 32, s:" m"; HUDMSG_PLAIN, 0, CR_RED, 85.0, 240.1, 0.03);*/
	}
}

//SCRIPTS
script "BoA_Objectives"(void) //Call this script to activate and draw the compass
{
	if(compassActive[playerNumber()])
		compassActive[playerNumber()]=0;
	else
		compassActive [playerNumber()]=1;
}

script "UnderwaterBubbles" ENTER
{
	if(GetActorProperty(0,APROP_WATERLEVEL)>=3)
		SpawnForced("PlayerBubble",GetActorX(0)+random(4,8),GetActorY(0),GetActorZ(0)+random(48,52),0,random(0,255));
	delay(7);
	restart;
}

str BossHealth[11] =
{
	"HEALTH00","HEALTH10","HEALTH20","HEALTH30","HEALTH40","HEALTH50",
	"HEALTH60","HEALTH70","HEALTH80","HEALTH90","HEALTHMX"
};
script "MiniBossHealth" ENTER
{
	SetHudSize(640,400,1);
	while(GetActorProperty(0,APROP_Health)<=0)
		delay(1);
	int tid = UniqueTID();
	if(PickActor(0,GetActorAngle(0),GetActorPitch(0),2000.0,tid)&&CheckFlag(tid,"BOSS"))
	{
		SetFont("SMALLFONT");
		HudMessage(s:GetActorProperty(tid,APROP_NameTag); HUDMSG_PLAIN,21,CR_GRAY,320.0,41.0,0.03);
		SetFont(BossHealth[FixedDiv(GetActorProperty(tid,APROP_Health),GetActorProperty(tid,APROP_SpawnHealth))*10/65536]);
		HudMessage(s:"A"; HUDMSG_PLAIN,22,CR_GRAY,320.0,40.0,0.03);
		Thing_ChangeTID(tid,0);
	}
	delay(1);
	restart;
}
script "BossHealth"(int tid,int maxhealth)
{
	SetHudSize(640,400,1);
	while(GetActorProperty(tid,APROP_Health)>0)
	{
		//SetFont("HELTHBAR");
		//hudmessagebold(s:"A"; HUDMSG_PLAIN,3,CR_UNTRANSLATED,160.0,24.0,1.0);
		SetFont("SMALLFONT");
		HudMessageBold(s:GetActorProperty(tid,APROP_NameTag); HUDMSG_PLAIN,1,CR_GRAY,320.0,40.0,0.01);
		SetFont(BossHealth[FixedDiv(GetActorProperty(tid,APROP_Health),GetActorProperty(tid,APROP_SpawnHealth))*10/65536]);
		HudMessageBold(s:"A"; HUDMSG_PLAIN,10,CR_GRAY,320.0,40.0,1.0);
		delay(35);
	}
}

//Teleportscript from Re-Exhumed
script "ExTeleportPlayer" (int teleportID, int destinationID)
{
	int deltaX = GetActorX(playerID) - GetActorX(teleportID);
	int deltaY = GetActorY(playerID) - GetActorY(teleportID);
	//mxd. Warp won't work here, because the activator is ExTeleportIfPlayerBelow or ExTeleportIfPlayerAbove and not a player.
	//mxd. This kinda limits Warp usefulness, dont you think?..
	SetActorPosition(playerID, GetActorX(destinationID) + deltaX, GetActorY(destinationID) + deltaY, GetActorZ(playerID), false);
}

script "ExGetPlayerZ" (void)
{
	SetResultValue(GetActorZ(playerID) >> 16);
}

//Supply Chest Script
Script "SupplyChest"(int supplychestid,int prize)
{
	if(CheckInventory("ChestKey"))
	{
		Thing_Activate(supplychestid);
		Delay(20);
		ActivatorSound("misc/p_pkup",127);
		TakeInventory("ChestKey",1);
		if(prize==1)
			SpawnForced("SupplyChest100Coins",GetActorX(supplychestid),GetActorY(supplychestid),GetActorZ(supplychestid));
		else if(prize==2)
			SpawnForced("SupplyChest75Coins",GetActorX(supplychestid),GetActorY(supplychestid),GetActorZ(supplychestid));
		else if(prize==3)
		{
			SpawnForced("SupplyChest25Coins",GetActorX(supplychestid),GetActorY(supplychestid),GetActorZ(supplychestid));
			//Special event in C1M1
			spawnspotfacing("RifleGuard",51,0);
		}
		else if(prize==4)
			SpawnForced("SupplyChestKar98k",GetActorX(supplychestid),GetActorY(supplychestid),GetActorZ(supplychestid));
		else if(prize==5)
			SpawnForced("SupplyChestBackpack",GetActorX(supplychestid),GetActorY(supplychestid),GetActorZ(supplychestid));
		else if(prize==6)
			SpawnForced("SupplyChestGrenades",GetActorX(supplychestid),GetActorY(supplychestid),GetActorZ(supplychestid));
	}
	else
		ActivatorSound("treasure/locked",127);
}