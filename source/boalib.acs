#library "boalib"

#define MAX_PLAYERS 8
#define MAX_VOLUME 127
#define MAX_OBJECTIVES 6
#define MAX_PLAYER_HEALTH 150
#define PLAYER_TAG_OFFSET 1000

global int 1:disable_mutants; //mxd. When set to true, Mutant and MutantMelee will immediately despawn after spawning.
global int 2:disable_big_mutants; //mxd. When set to true, BigMutant1 and BigMutant2 will immediately despawn after spawning.
global int 3:disable_supermutants; //mxd. When set to true, UberMutant and SuperMutant will immediately despawn after spawning.
global int 4:astrostein; //[Ed} When set to true, grenades will change to the Astrostein variant
global int 5:HealthScriptDeactivate;
global int 6:mission_number; //The variable for the mission number
global int 60:time; // Used for day/night transition display on status bar

int playerID; 

#include "zcommon.acs"
#include "functions.acs"
#include "compass.acs"
#include "droplets.acs"
#include "flinch.acs"
#include "gameplay.acs"
#include "interactive.acs"
#include "kturret.acs"
#include "messages.acs"
#include "objectives.acs"
#include "sprint.acs"
#include "footsteps.acs"
#include "tilt.acs"
#include "wavespawn.acs"
#include "boptions.acs"

// Consolidated gameplay loop.  
// ALL restarting/perpetual functions should be called from here, not in their own ENTER scripts.
// The more ENTER scripts are constantly looping, the worse performance will be!
Script "BoA_PlayerLoop" ENTER
{
	playerID = PLAYER_TAG_OFFSET + PlayerNumber();
	Thing_ChangeTID(0, playerID);

	While (True)
	{
		// Do screen drawing actions
		DrawObjectives();
		DrawCompass();
		DrawScreenOverlays();

		// Do inventory checks
		DoSprinting();
		DoStaminaCheck();

		// Do sound effects
		DoFootstepSounds();
		DoUnderwaterEffects();

		// Do visual effects
		DoFlinchRecovery();
		DoTiltEffect();

		Delay(1);
	}
}