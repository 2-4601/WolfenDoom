#library "compass"
#include "zcommon.acs"

//DEFINES
#DEFINE MAXPLAYERS 8
bool compassActive[MAXPLAYERS];

int dots;	//Items to appear on compass

//FUNCTIONS

function int _cos(int a)
{
	a%=1.0;
	if(a<0)
		a+=1.0;
	if(a>0.5)
		a=-a+1.0;
	return cos(a);
}

function int _sin(int a)
{
	return _cos(a+0.25);
}

script "BoA_Compass" ENTER
{
	if(compassActive[playerNumber()])
	{
		SetHUDSize(1024,768,1);
		int a=GetActorAngle(0);
		int x=-_cos(a)*44/1.0,y=_sin(a)*44/1.0;
		SetFont("COMPASS1");
		HUDMessage(s:"N"; HUDMSG_PLAIN,0,CR_UNTRANSLATED,x * 1.0 + 85.0,y * 1.0 + 170.0,0.03);
		HUDMessage(s:"S"; HUDMSG_PLAIN,0,CR_UNTRANSLATED,-x * 1.0 + 85.0,-y * 1.0 + 170.0,0.03);
		HUDMessage(s:"E"; HUDMSG_PLAIN,0,CR_UNTRANSLATED,-y * 1.0 + 85.0,x * 1.0 + 170.0,0.03);
		HUDMessage(s:"W"; HUDMSG_PLAIN,0,CR_UNTRANSLATED,y * 1.0 + 85.0,-x * 1.0 + 170.0,0.03);
		SetFont("COMPASS");
		HUDMessage(s:"A"; HUDMSG_PLAIN,0,CR_UNTRANSLATED,85.0,170.0,0.03);
		for(int i=0;i<dots;i++)
			drawdot(4000+i);
	}
	Delay(1);
	restart;
}

script "BoA_CompassQueue"(void)
{
	Thing_ChangeTID(0,4000+dots++);
}

function void drawdot(int tid)
{
	SetHUDSize(1024,768,1);
	if(GetActorProperty(tid,APROP_Health)>0)
	{
		int dx = GetActorX(0) - GetActorX(tid),dy = GetActorY(0) - GetActorY(tid);
		int a = GetActorAngle(0) - VectorAngle(dx, dy) - 0.25;
		dx /= 1.0;
		dy /= 1.0;
		int dist = sqrt(dx*dx + dy*dy),rdist = dist / 10;
		if(rdist > 33)
			rdist = 33;
		if(CheckActorClass(tid,"Akten")||CheckActorClass(tid,"AktenEisenmann")||CheckActorClass(tid,"SpearOfDestiny")||
		CheckActorClass(tid,"AktenTest")||CheckActorClass(tid,"AktenEisenmannTest")||CheckActorClass(tid,"SpearOfDestinyTest"))
			SetFont("GOAL1");
		else
			SetFont("GOAL2");
		HUDMessage(s:"A"; HUDMSG_PLAIN, 0, CR_UNTRANSLATED, (-_cos(a) * rdist / 1.0) * 1.0 + 85.0, (_sin(a) * rdist / 1.0) * 1.0 + 170.0,0.03);
		/*SetFont("SmallFont");
		HUDMessage(i:dist / 32, s:" m"; HUDMSG_PLAIN, 0, CR_RED, 85.0, 240.1, 0.03);*/
	}
}

//SCRIPTS
script "BoA_Objectives"(void) //Call this script to activate and draw the compass
{
	if(compassActive[playerNumber()])
		compassActive[playerNumber()]=0;
	else
		compassActive [playerNumber()]=1;
}