ACTOR Bodiless_Swarm
{
	//$Category Critters (Wolf3D)
	//$Title Swarm
	//$Color 0
	//$Sprite SFLYA0
	Radius 1
	Height 64
	+CLIENTSIDEONLY
	+MISSILE
	+NOBLOCKMAP
	+NOGRAVITY
	+NOSECTOR
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_PlaySound("SFX/Buzz", CHAN_BODY, frandom(0.6, 0.8), TRUE)
		"####" AAAAA 1 A_SpawnItemEx("SwarmFly",frandom(-16,16),frandom(-16,16),frandom(-16,16))
		"####" A -1
		Stop
	}
}

ACTOR Bodiless_Swarm2 : Bodiless_Swarm
{
	//$Title FireFly Swarm
	//$Sprite SFLYC0
	States
	{
	Spawn:
		TNT1 AAAAA 1 A_SpawnItemEx("FireFly",frandom(-16,16),frandom(-16,16),frandom(-16,16))
		"####" A -1
		Stop
	}
}

ACTOR SwarmFly
{
	Radius 1
	Height 1
	Speed 5
	Projectile
	ReactionTime 4
	Scale 0.25
	+NOCLIP
	States
	{
	Spawn:
		SFLY A 1 ThrustThingZ(0,random(-1,1),random(1,0),1)
		TNT1 A 0 A_ChangeVelocity(frandom(-2,2),frandom(-1,1),frandom(-2,2),1)
		SFLY B 1 ThrustThingZ(0,random(-1,1),random(1,0),1)
		TNT1 A 0 A_ChangeVelocity(frandom(-1,1),frandom(-2,2),frandom(-1,1),1)
		SFLY A 0 A_CountDown
		SFLY C 1 ThrustThingZ(0,random(-1,1),random(1,0),1)
		TNT1 A 0 A_ChangeVelocity(frandom(-2,2),frandom(-1,1),frandom(-2,2),1)
		SFLY B 1 ThrustThingZ(0,random(-1,1),random(1,0),1)
		TNT1 A 0 A_ChangeVelocity(frandom(-1,1),frandom(-2,2),frandom(-1,1),1)
		SFLY A 0 A_CountDown
		Loop
	Death:
		SFLY A 1 A_SpawnProjectile("SwarmFly",0,0,frandom(-20,20))
		Stop
	}
}

ACTOR FireFly : SwitchableDecoration
{
	Radius 2
	Height 2
	Speed 0
	Mass 5
	Scale 0.05
	+BLOCKEDBYSOLIDACTORS
	+CANNOTPUSH
	+CLIENTSIDEONLY
	+FLOATBOB
	+NOGRAVITY
	+NOLIFTDROP
	+NOTARGET
	+NOTELEPORT
	+NOTELESTOMP
	+RANDOMIZE
	+SPAWNFLOAT
	RenderStyle "Shaded"
	StencilColor "FFB100"
	States
	{
	Spawn:
		STLI C 0 NODELAY A_SetScale(frandom(0.05, 0.15))
	SpawnLoop:
		STLI C 1 LIGHT("FIREFLY") A_CheckProximity("Inactive", "BoAPlayer", 128)
	Active:
		TNT1 A 0 A_ChangeVelocity(frandom(-1,1),frandom(-1,1),frandom(-1,1),1)
		STLI C 1 LIGHT("FIREFLY") ThrustThingZ(0,random(-1,1),random(1,0),1)
		TNT1 A 0 A_ChangeVelocity(frandom(-1,1),frandom(-1,1),frandom(-1,1),1)
		STLI C 1 LIGHT("FIREFLY") A_Wander
		STLI C 1 LIGHT("FIREFLY") ThrustThingZ(0,random(-1,1),random(1,0),1)
		TNT1 A 0 A_ChangeVelocity(frandom(-1,1),frandom(-1,1),frandom(-1,1),1)
		STLI C 1 LIGHT("FIREFLY") ThrustThingZ(0,random(-1,1),random(1,0),1)
		TNT1 A 0 A_ChangeVelocity(frandom(-1,1),frandom(-1,1),frandom(-1,1),1)
		STLI C 1 LIGHT("FIREFLY") A_Wander
		Goto SpawnLoop
	Inactive:
		STLI C 1 LIGHT("FIREFLY") A_Fadeout(0.04)
	Death:
		TNT1 A 1 A_SetTics(random(525,1050))
		TNT1 A 5 A_Respawn(FALSE)
		TNT1 A -1
		Stop
	}
}