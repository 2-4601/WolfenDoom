ACTOR Cows
{
	//$Category Critters (Wolf3D)
	//$Title Cows (random)
	//$Color 0
	Height 32
	Radius 32
	+RANDOMIZE
	States
	{
	Spawn:
		COWS A 0 NODELAY A_SetScale(ScaleX + frandom(0.1,0.2),ScaleY + frandom(0.1,0.2))
		Randomize:
		COWS A 0 A_Jump(128,"Cow2")
		Cow1:
		"####" BCDA 1 A_SetTics(random(4,10))
		"####" AA 1 A_SetTics(random(10,40)) 
		Loop
		Cow2:
		"####" FGHE 1 A_SetTics(random(4,10))
		"####" EE 1 A_SetTics(random(10,40)) 
		Loop
	}
}

ACTOR BarkDog : SwitchableDecoration
{
	//$Category Critters (Wolf3D)
	//$Title Barking Dog (Interactive)
	//$Color 0
	Radius 8
	Height 24
	Scale 0.9
	+DONTTHRUST
	+NOTAUTOAIMED
	+RANDOMIZE
	+SOLID
	+USESPECIAL
	Activation THINGSPEC_Switch
	States
	{
	Active:
		BRDG E 0 A_PlaySound("dog/attack", CHAN_AUTO, frandom(0.7,1.0), FALSE, ATTN_NORM)
	ActiveSet:
		"####" F 1 A_SetTics(random(4,8))
		"####" GE 1 A_SetTics(random(20,40)) 
		Goto SpawnSet
	Spawn:
		BRDG A 0 NODELAY A_SetScale(ScaleX + frandom(0.1,0.2),ScaleY + frandom(0.1,0.2))
	SpawnSet:
		BRDG DCBA 1 A_SetTics(random(4,10))
		"####" AA 1 A_SetTics(random(10,40)) 
		Loop
	Inactive:
		"####" A 0 A_StopSound(CHAN_AUTO)
		Goto SpawnSet
	}
}

ACTOR BlackCat : BarkDog
{
	//$Title Black Cat (Interactive)
	Height 16
	Scale 0.87
	States
	{
	Active:
		BCAT D 0 A_StopSound(CHAN_ITEM)
		"####" D 0 A_PlaySound("cat/meows", CHAN_AUTO, frandom(0.7,1.0), FALSE, ATTN_NORM) //cat will meow here
	ActiveSet:
		BCAT D 0 A_Jump(128,"Hiss2")
		Hiss1:
		"####" D 1 A_SetTics(random(10,40))
		"####" EF 1 A_SetTics(random(20,40)) 
		Goto Purr
		Hiss2:
		"####" D 1 A_SetTics(random(10,40))
		"####" EF 1 A_SetTics(random(4,8))
		"####" G 0 A_PlaySound("cat/hiss", CHAN_AUTO, frandom(0.7,1.0), FALSE, ATTN_NORM) //cat will hiss here
		"####" G 20
		Goto Purr
	Spawn:
		BCAT A 0 NODELAY A_SetScale(ScaleX + frandom(0.1,0.2),ScaleY + frandom(0.1,0.2))
	Purr:
		BCAT A 0 A_PlaySound("cat/purr", CHAN_ITEM, frandom(0.5,0.7), TRUE, ATTN_NORM)
	SpawnSet:
		BCAT A 0 A_CheckRange(512,"SpawnSilent")
		BCAT BCA 1 A_SetTics(random(4,10))
		"####" AA 1 A_SetTics(random(10,40)) 
		Loop
	SpawnSilent:
		BCAT A 0 A_StopSound(CHAN_ITEM)
		"####" BCA 1 A_SetTics(random(4,10))
		"####" AA 1 A_SetTics(random(10,40)) 
		Goto Purr
	Inactive:
		"####" A 0 A_StopSound(CHAN_AUTO)
		"####" A 0 A_StopSound(CHAN_ITEM)
		Goto Purr
	}
}

ACTOR Camel : Cows
{
	//$Title Desert Camel (Randomly Chewing)
	Scale 0.8
	States
	{
	Spawn:
		CAML A 0 NODELAY A_SetScale(ScaleX + frandom(0.1,0.2),ScaleY + frandom(0.1,0.2))
		SpawnSet:
		"####" BA 1 A_SetTics(random(40,80)) 
		"####" CDEFDEF 1 A_SetTics(random(8,16))
		Loop
	}
}