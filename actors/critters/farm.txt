ACTOR Cows
{
	//$Category Fauna (BoA)
	//$Title Cows (random)
	//$Color 0
	Height 32
	Radius 32
	Scale 0.8
	+RANDOMIZE
	States
	{
	Spawn:
		COWS A 0 NODELAY { A_SetScale(ScaleX + frandom(0.1,0.2),ScaleY + frandom(0.1,0.2)); A_GiveInventory("Z_ShadeMe",0); }
	Randomize:
		COWS A 0 A_Jump(128,"Cow2")
	Cow1:
		"####" BCDA 1 A_SetTics(random(4,10))
		"####" AA 1 A_SetTics(random(10,40))
		Loop
	Cow2:
		"####" FGHE 1 A_SetTics(random(4,10))
		"####" EE 1 A_SetTics(random(10,40))
		Loop
	}
}

ACTOR Camel : Cows
{
	//$Title Desert Camel (Randomly Chewing)
	Scale 0.9
	States
	{
	Spawn:
		CAML A 0 NODELAY { A_SetScale(ScaleX + frandom(0.1,0.2),ScaleY + frandom(0.1,0.2)); A_GiveInventory("Z_ShadeMe",0); }
	SpawnSet:
		"####" BA 1 A_SetTics(random(40,80))
		"####" CDEFDEF 1 A_SetTics(random(8,16))
		Loop
	}
}

ACTOR BarkDog : SwitchableDecoration
{
	//$Category Fauna (BoA)
	//$Title Barking Dog (Interactive)
	//$Color 0
	Radius 8
	Height 24
	Scale 0.9
	+DONTTHRUST
	+NONSHOOTABLE
	+NOTAUTOAIMED
	+RANDOMIZE
	+SOLID
	+USESPECIAL
	Activation THINGSPEC_Switch
	States
	{
	Active:
		BRDG E 0 A_StartSound("dog/attack", CHAN_ITEM, 0, frandom(0.7,1.0), ATTN_NORM)
	ActiveSet:
		"####" F 1 A_SetTics(random(4,8))
		"####" GE 1 A_SetTics(random(20,40))
		Goto SpawnSet
	Spawn:
		BRDG A 0 NODELAY { A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY); A_GiveInventory("Z_ShadeMe",0); }
	SpawnSet:
		BRDG DCBA 1 A_SetTics(random(4,10))
		"####" AA 1 A_SetTics(random(10,40))
		Loop
	Inactive:
		"####" A 0 A_StopSound(CHAN_ITEM)
		Goto SpawnSet
	}
}

ACTOR BlackCat : BarkDog
{
	//$Title Black Cat (Interactive)
	Height 16
	Scale 0.87
	+CANPASS
	+NOGRAVITY
	States
	{
	Active:
		BCAT D 0 A_StopSound(CHAN_ITEM)
		"####" D 0 A_JumpIfInventory("HissCounter", 2, "Hiss")
		"####" D 0 A_StartSound("cat/meows", CHAN_AUTO, 0, frandom(0.7,1.0), ATTN_NORM)
	ActiveSet:
		"####" D 0 A_GiveInventory("HissCounter", 1)
		"####" D 1 A_SetTics(random(10,40))
		"####" D 0 A_StartSound("cat/meows", CHAN_AUTO, 0, 1.0, ATTN_NORM)
		"####" EF 1 A_SetTics(random(20,40))
		Goto Purr
	Hiss:
		"####" D 1 A_SetTics(random(10,40))
		"####" EF 1 A_SetTics(random(4,8))
		"####" G 0 A_StartSound("cat/hiss", CHAN_AUTO, 0, frandom(0.7,1.0), ATTN_NORM)
		"####" G 1 A_SetTics(random(20,30))
		Goto Purr
	Spawn:
		BCAT A 0 NODELAY { A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY); A_GiveInventory("Z_ShadeMe",0); }
	Purr:
		BCAT A 0 A_StartSound("cat/purr", CHAN_ITEM, CHANF_LOOPING, frandom(0.1,0.2), ATTN_STATIC)
	SpawnSet:
		BCAT A 0 A_CheckRange(512,"SpawnSilent")
		BCAT BCA 1 A_SetTics(random(4,10))
		"####" AA 1 A_SetTics(random(10,40))
		Loop
	SpawnSilent:
		BCAT A 0 A_StopSound(CHAN_ITEM)
		"####" BCA 1 A_SetTics(random(4,10))
		"####" AA 1 A_SetTics(random(10,40))
		Goto Purr
	Inactive:
		"####" A 0 A_StopSound(CHAN_ITEM)
		Goto Purr
	}
}

ACTOR HissCounter : Inventory {Inventory.MaxAmount 2}

ACTOR OcherCat
{
	Radius 4
	Height 8
	Scale 0.9
	+CANPASS
	+DONTTHRUST
	+NOGRAVITY
	+NONSHOOTABLE
	+NOTAUTOAIMED
	+RANDOMIZE
	+SOLID
	States
	{
	Spawn:
		OCAT A 0 NODELAY { A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY); A_GiveInventory("Z_ShadeMe",0); }
	Randomize:
		OCAT A 0 A_Jump(128,"Cat2")
	Cat1:
		OCAT BCAD 1 A_SetTics(random(4,10))
		"####" AA 1 A_SetTics(random(10,40))
		Loop
	Cat2:
		OCAT FGEH 1 A_SetTics(random(4,10))
		"####" EE 1 A_SetTics(random(10,40))
		Loop
	}
}

ACTOR GrayCat : OcherCat
{
	States
	{
	Spawn:
		GCAT A 0 NODELAY { A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY); A_GiveInventory("Z_ShadeMe",0); }
	Randomize:
		GCAT A 0 A_Jump(128,"Cat2")
	Cat1:
		GCAT BCAD 1 A_SetTics(random(4,10))
		"####" AA 1 A_SetTics(random(10,40))
		Loop
	Cat2:
		GCAT FGEH 1 A_SetTics(random(4,10))
		"####" EE 1 A_SetTics(random(10,40))
		Loop
	}
}

ACTOR WhiteCat : OcherCat
{
	States
	{
	Spawn:
		WCAT A 0 NODELAY { A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY); A_GiveInventory("Z_ShadeMe",0); }
	Randomize:
		WCAT A 0 A_Jump(128,"Cat2")
	Cat1:
		WCAT BCAD 1 A_SetTics(random(4,10))
		"####" AA 1 A_SetTics(random(10,40))
		Loop
	Cat2:
		WCAT FGEH 1 A_SetTics(random(4,10))
		"####" EE 1 A_SetTics(random(10,40))
		Loop
	}
}

ACTOR CatSpawner : RandomSpawner
{
	//$Category Fauna (BoA)
	//$Title Cat Spawner (Random Ocher, White & Gray Non-Interactive Cats)
	//$Color 0
	//$Sprite OCATA0
	Radius 4
	Height 8
	DropItem "OcherCat"
	DropItem "GrayCat"
	DropItem "WhiteCat"
}

ACTOR DogsBodies
{
	//$Category Gore (BoA)
	//$Title Dead Dogs (random)
	//$Sprite ZARKA0
	Scale 0.65
	+NOBLOCKMAP
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(256,1,2,3,4,5,6,7,8,9,10)
		ZARK A -1 A_SetScale(0.9)
		ZARK B -1 A_SetScale(0.9)
		ZARK C -1 A_SetScale(0.9)
		ZYDO A -1 A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY)
		ZYDO B -1 A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY)
		ZYDO C -1 A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY)
		DOGY K -1 A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY)
		DOG2 K -1 A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY)
		DOG3 K -1 A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY)
		ZYDO J -1 A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY)
		Stop
	}
}

ACTOR CatsBodies : DogsBodies //recolors not included yet - ozy81
{
	//$Title Dead Cats (random)
	//$Sprite ZCATA0
	Scale 0.9
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(256,1,2,3)
		ZCAT A -1 A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY)
		ZCAT B -1 A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY)
		ZCAT C -1 A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY)
		Stop
	}
}

ACTOR CowsBodies : DogsBodies
{
	//$Title Dead Cows (random)
	//$Sprite ZCOWA0
	Scale 0.9
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(256,1,2,3,4,5,6,7)
		ZCOW A -1 A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY)
		ZCOW B -1 A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY)
		ZCOW C -1 A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY)
		ZCOW D -1 A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY)
		ZCOW E -1 A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY)
		ZCOW F -1 A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY)
		ZCOW G -1 A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY)
		Stop
	}
}

ACTOR PigsBodies : DogsBodies
{
	//$Title Dead Pigs (random)
	//$Sprite ZPIGA0
	Scale 0.8
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(256,1,2,3,4,5,6,7,8,9)
		ZPIG A -1 A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY)
		ZPIG B -1 A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY)
		ZPIG C -1 A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY)
		ZPIG D -1 A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY)
		ZPIG E -1 A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY)
		ZPIG F -1 A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY)
		ZPIG G -1 A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY)
		ZPIG H -1 A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY)
		ZPIG I -1 A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY)
		Stop
	}
}

ACTOR DeadBear : DogsBodies
{
	//$Title Dead Bear (random sides)
	Scale 0.75
	States
	{
	Spawn:
		ZBER A -1 NODELAY A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY)
		Stop
	}
}