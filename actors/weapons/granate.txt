ACTOR GrenadePickup : CustomInventory 15001
{
//$Category Ammo (Wolf3D)
//$Title Grenade
//$Color 6
	inventory.pickupmessage "Picked up a grenade."
	Tag "Frag Grenade"
	inventory.maxamount 9
	inventory.icon THRPA0
	inventory.usesound "grenade/throw"
	inventory.pickupsound "grenade/pickup"
	+Inventory.invbar
	+inventory.keepdepleted
	Inventory.InterHubamount 9
	-INVBAR
	ConversationID 181
	states
	{
	Spawn:
		THRP A -1
		stop
	Use:
		TNT1 A 0 A_Jump(168,2)
		TNT1 A 0 A_PlaySound("*grenadecall", CHAN_VOICE)
		TNT1 A 0 A_FireCustomMissile("HandGrenade",0,0)
		stop
	}
}

ACTOR HandGrenade
{
	Radius 4
	Height 3
	Speed 40
	Damage (1)
	DamageType "Frag"
	+NOBLOCKMAP 
	+DROPOFF 
	+MISSILE 
	+ACTIVATEIMPACT 
	+ACTIVATEPCROSS
	+CANBOUNCEWATER
	obituary "%o failed to field %k's fragmenting fastball."
	BounceType Doom
	BounceFactor 0.2
	WallBounceFactor 0.6
	bouncesound "grenade/bounce"
	states
	{
	Spawn:
		THRW A 2 A_Countdown
		THRW BCDEFGH 2
		loop
	Death:
		THRW B 35
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_SetScale(1.75,1.75)
		TNT1 A 0 A_SetTranslucent(0.75,1)
		TNT1 A 0 A_PlaySound("grenade/explode",0,1.0,0,0.1)
		TNT1 A 1 BRIGHT A_SpawnItemEx("GeneralExplosion_Medium", 0, 0, 0)
		TNT1 B 1 Radius_Quake(10,10,0,16,0)
		/*FRME CDEFGHIHJKLMNOPQRS 1 bright*/
		stop
	}
}