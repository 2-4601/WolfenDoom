const int TESLA_FLAGS = RGF_SILENT | RGF_NOPIERCING | RGF_FULLBRIGHT;

ACTOR TeslaLoaded : Ammo
{
	Tag "$TAGLOADD"
	+IGNORESKILL
	Inventory.MaxAmount 30
	Inventory.Icon "TESL01"
}

ACTOR TeslaCannon : NaziAstroWeapon
{
	//$Category Weapons (Wolf3D)
	//$Title (8) Tesla Cannon
	//$Color 14
	Scale 1.0 //for pickup
	Weapon.SelectionOrder 10105
	Weapon.AmmoType "TeslaLoaded"
	Weapon.AmmoUse 1
	Weapon.AmmoType2 "TeslaCell"
	Weapon.AmmoUse2 1
	Weapon.AmmoGive2 20
	Weapon.UpSound "weapons/rezup" //mxd
	Inventory.PickupMessage "$TESLAC"
	Obituary "$OBTESLAC"
	Tag "$TAGTESLA"
	States
	{
	Select:
		PULS B 0 A_Raise
		PULS B 1 A_Raise
		Loop
	Deselect:
		PULS B 0 A_Lower
		PULS B 1 A_Lower
		Loop
	Ready:
		PULS B 0 A_PlaySound("Tesla/Loop", CHAN_WEAPON, TRUE)
		PULS B 0 A_JumpIfInventory("TeslaLoaded",0,2)
		PULS B 0 A_JumpIfInventory("TeslaCell",1,7)
		PULS B 1 A_WeaponReady
		PULS B 1 A_WeaponReady
		PULS C 1 A_WeaponReady
		PULS C 1 A_WeaponReady
		PULS D 1 A_WeaponReady
		PULS D 1 A_WeaponReady
		Loop
		PULS B 1 A_WeaponReady (WRF_ALLOWRELOAD)
		PULS B 1 A_WeaponReady (WRF_ALLOWRELOAD)
		PULS C 1 A_WeaponReady (WRF_ALLOWRELOAD)
		PULS C 1 A_WeaponReady (WRF_ALLOWRELOAD)
		PULS D 1 A_WeaponReady (WRF_ALLOWRELOAD)
		PULS D 1 A_WeaponReady (WRF_ALLOWRELOAD)
		Loop
	Fire:
		PULS B 0 A_JumpIfInventory("TeslaLoaded",1,1)
		Goto Dryfire
		PULS B 0 A_AlertMonsters
		//PULS B 0 A_StopSound(CHAN_WEAPON)
		PULS B 0 A_PlaySound("electricplasma/shoot", CHAN_WEAPON)
		PULS BC 1
		PULS C 0 A_SetBlend("Purple",0.050,5)
//		PULS D 1 BRIGHT { A_GunFlash("Flash"); A_FireCustomMissile("TeslaBeam", 0, 1, 0, 0); }
		PULS DC 1 Bright {
			A_GunFlash("Flash");
			A_SpawnLightning("LightningBeamPri");
			A_SpawnLightning("LightningBeamPri");
			TakeInventory("TeslaLoaded", 1);
		}
//		PULS C 1
		PULS B 0 A_ReFire
		Goto Ready
	AltFire:
		PULS B 0 A_StopSound(CHAN_WEAPON)
		PULS B 0 A_JumpIfInventory("TeslaLoaded",1,1)
		Goto Dryfire
		PULS B 0 A_AlertMonsters
		PULS B 0 A_CheckReload
	AltFire.Hold1:
		PULS B 0 A_JumpIfInventory("TeslaLoaded",1,1)
		Goto Dryfire
		PULS B 0 A_SetBlend("Purple",0.050,5)
		PULS BCD 1 Bright {
			A_GunFlash("Flash");
			A_SpawnLightning("LightningBeamAlt");
			TakeInventory("TeslaLoaded", 1);
		}
		PULS CDB 1 Bright { 
			A_GunFlash("Flash");
			A_SpawnLightning("LightningBeamAlt");
		}
/*
		PULS BCD 1 Bright { A_GunFlash("Flash"); A_RailAttack(random(2,4),0,1,none,none,TESLA_FLAGS,0,"TeslaPuff",0,0,radius+192,1,1,1,"TeslaTracer",2); }
		PULS CDB 1 Bright { A_GunFlash("Flash"); A_RailAttack(random(2,4),0,1,none,none,TESLA_FLAGS,0,"TeslaPuff",0,0,radius+192,1,1,1,"TeslaTracer",2); }
*/
		PULS B 0 A_Refire ("AltFire.Hold2")
		Goto Ready
	AltFire.Hold2:
		PULS B 0 A_JumpIfInventory("TeslaLoaded",1,1)
		Goto Dryfire
		PULS B 0 A_SetBlend("Purple",0.050,5)
		PULS CDB 1 Bright {
			A_GunFlash("Flash");
			A_SpawnLightning("LightningBeamAlt");
			TakeInventory("TeslaLoaded", 1);
		}
		PULS BCD 1 Bright { 
			A_GunFlash("Flash");
			A_SpawnLightning("LightningBeamAlt");
		}
/*
		PULS CDB 1 Bright { A_GunFlash("Flash"); A_RailAttack(random(2,4),0,1,none,none,TESLA_FLAGS,0,"TeslaPuff",0,0,radius+192,1,1,1,"TeslaTracer",2); }
		PULS BCD 1 Bright { A_GunFlash("Flash"); A_RailAttack(random(2,4),0,1,none,none,TESLA_FLAGS,0,"TeslaPuff",0,0,radius+192,1,1,1,"TeslaTracer",2); }
*/
		PULS B 0 A_Refire("AltFire.Hold1")
		Goto Ready
	Reload:
		TNT1 A 0 A_JumpIfInventory("TeslaCell",1,2)
		PULS H 1 Offset(0,35) A_PlaySound("weapon/dryfire", CHAN_WEAPON)
		Goto Ready
		PULS H 1 Offset(0,35) A_PlaySound("mp40/reload", CHAN_WEAPON)
		PULS H 1 Offset(0,38)
		PULS H 1 Offset(0,44)
		PULS H 1 Offset(0,52)
		PULS H 1 Offset(-1,54)
		PULS H 1 Offset(-2,56)
		PULS H 1 Offset(-3,58)
		PULS H 1 Offset(-4,58)
		PULS H 1 Offset(-4,57)
		PULS H 1 Offset(-3,54)
		PULS H 1 Offset(-3,51)
		PULS H 1 Offset(-3,53)
		PULS H 1 Offset(-3,55)
		PULS H 7 Offset(-3,56)
	ReloadLoop:
		TNT1 A 0 A_TakeInventory("TeslaCell",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GiveInventory("TeslaLoaded")
		TNT1 A 0 A_JumpIfInventory("TeslaLoaded",0,"ReloadFinish")
		TNT1 A 0 A_JumpIfInventory("TeslaCell",1,"ReloadLoop")
	ReloadFinish:
		PULS H 1 Offset(-3,57)
		PULS H 1 Offset(-3,59)
		PULS H 1 Offset(-3,63)
		PULS H 1 Offset(-3,67)
		PULS H 1 Offset(-3,65)
		PULS H 1 Offset(-3,62)
		PULS H 1 Offset(-3,58)
		PULS H 1 Offset(-3,55)
		PULS H 1 Offset(-2,53)
		PULS H 1 Offset(-2,49)
		PULS H 1 Offset(-2,46)
		PULS H 1 Offset(-2,45)
		PULS H 1 Offset(-1,44)
		PULS H 1 Offset(-1,46)
		PULS H 1 Offset(-1,47)
		PULS B 1 Offset(0,49) A_WeaponReady(WRF_NOBOB)
		PULS C 1 Offset(0,44) A_WeaponReady(WRF_NOBOB)
		PULS D 1 Offset(0,38) A_WeaponReady(WRF_NOBOB)
		PULS B 1 Offset(0,35) A_WeaponReady(WRF_NOBOB)
		PULS C 1 Offset(0,32) A_WeaponReady(WRF_NOBOB)
		Goto Ready
	Flash:
//		PULS I 1 BRIGHT A_Light2
		TNT1 A 1 BRIGHT A_Light2
		TNT1 A 1
		TNT1 A 2 A_Light1
		Goto LightDone
	Spawn:
		PULS A -1 LIGHT(BOAPKEY)
		Stop
	}
}