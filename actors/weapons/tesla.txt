const int TESLA_FLAGS = RGF_SILENT | RGF_NOPIERCING | RGF_FULLBRIGHT;

ACTOR TeslaCannon : NaziAstroWeapon
{
	//$Category Weapons (Wolf3D)
	//$Title (8) Tesla Cannon
	//$Color 14
	Weapon.SelectionOrder 10105
	Weapon.AmmoType "AstroChaingunLoaded"
	Weapon.AmmoUse 1
	Weapon.AmmoType2 "AstroClipAmmo"
	Weapon.AmmoUse2 1
	Weapon.AmmoGive2 20
	Weapon.UpSound "weapons/rezup" //mxd
	Inventory.PickupMessage "$TESLAC"
	Obituary "$OBTESLAC"
	Tag "$TAGTESLA"
	States
	{
	Select:
		PULS B 0 A_Raise
		PULS B 1 A_Raise
		Loop
	Deselect:
		PULS B 0 A_Lower
		PULS B 1 A_Lower
		Loop
	Ready:
		PULS B 0 A_JumpIfInventory("AstroChaingunLoaded",0,2)
		PULS B 0 A_JumpIfInventory("AstroClipAmmo",1,2)
		PULS BBCCDD 1 A_WeaponReady
		Loop
		PULS BBCCDD 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Fire: //this is just placeholder yet, need to implement new sprites
		PULS B 0 A_JumpIfInventory("AstroChaingunLoaded",1,1)
		Goto Dryfire
		PULS E 0 A_AlertMonsters
		PULS E 0 A_PlaySound("astrochaingun/fire", CHAN_WEAPON)
		PULS E 2
		PULS FG 2 BRIGHT A_FireCustomMissile("TeslaTracerPlayer",frandom(-1.0,1.0),1,0,0,0,frandom(-1.0,1.0))
		PULS E 0 A_CheckReload
		PULS E 0 A_ReFire
		Goto Ready
	AltFire:
		PULS B 0 A_JumpIfInventory("AstroChaingunLoaded",1,1)
		Goto Dryfire
		PULS B 0 A_AlertMonsters
		PULS B 0 A_CheckReload
	AltFire.Hold1:
		PULS B 0 A_JumpIfInventory("AstroChaingunLoaded",1,1)
		Goto Dryfire
		PULS B 0 A_GunFlash("TeslaFlash")
		PULS BCD 1 Bright A_RailAttack(random(2,4),0,1,none,none,TESLA_FLAGS,0,"TeslaPuff",0,0,radius+192,1,1,1,"TeslaTracer",2)
		PULS CDB 1 Bright A_RailAttack(random(2,4),0,0,none,none,TESLA_FLAGS,0,"TeslaPuff",0,0,radius+192,1,1,1,"TeslaTracer",2)
		PULS B 0 A_CheckReload
		PULS B 0 A_Refire("AltFire.Hold2")
		Goto Ready
	AltFire.Hold2:
		PULS B 0 A_JumpIfInventory("AstroChaingunLoaded",1,1)
		Goto Dryfire
		PULS B 0 A_GunFlash("TeslaFlash")
		PULS CDB 1 Bright A_RailAttack(random(2,4),0,1,none,none,TESLA_FLAGS,0,"TeslaPuff",0,0,radius+192,1,1,1,"TeslaTracer",2)
		PULS BCD 1 Bright A_RailAttack(random(2,4),0,0,none,none,TESLA_FLAGS,0,"TeslaPuff",0,0,radius+192,1,1,1,"TeslaTracer",2)
		PULS B 0 A_CheckReload
		PULS B 0 A_Refire("AltFire.Hold1")
		Goto Ready
	Reload:
		TNT1 A 0 A_JumpIfInventory("AstroClipAmmo",1,2)
		PULS H 1 Offset(0,35) A_PlaySound("weapon/dryfire", CHAN_WEAPON)
		Goto Ready
		PULS H 1 Offset(0,35) A_PlaySound("mp40/reload", CHAN_WEAPON)
		PULS H 1 Offset(0,38)
		PULS H 1 Offset(0,44)
		PULS H 1 Offset(0,52)
		PULS H 1 Offset(-1,54)
		PULS H 1 Offset(-2,56)
		PULS H 1 Offset(-3,58)
		PULS H 1 Offset(-4,58)
		PULS H 1 Offset(-4,57)
		PULS H 1 Offset(-3,54)
		PULS H 1 Offset(-3,51)
		PULS H 1 Offset(-3,53)
		PULS H 1 Offset(-3,55)
		PULS H 7 Offset(-3,56)
	ReloadLoop:
		TNT1 A 0 A_TakeInventory("AstroClipAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GiveInventory("AstroChaingunLoaded")
		TNT1 A 0 A_JumpIfInventory("AstroChaingunLoaded",0,"ReloadFinish")
		TNT1 A 0 A_JumpIfInventory("AstroClipAmmo",1,"ReloadLoop")
	ReloadFinish:
		PULS H 1 Offset(-3,57)
		PULS H 1 Offset(-3,59)
		PULS H 1 Offset(-3,63)
		PULS H 1 Offset(-3,67)
		PULS H 1 Offset(-3,65)
		PULS H 1 Offset(-3,62)
		PULS H 1 Offset(-3,58)
		PULS H 1 Offset(-3,55)
		PULS H 1 Offset(-2,53)
		PULS H 1 Offset(-2,49)
		PULS H 1 Offset(-2,46)
		PULS H 1 Offset(-2,45)
		PULS H 1 Offset(-1,44)
		PULS H 1 Offset(-1,46)
		PULS H 1 Offset(-1,47)
		PULS B 1 Offset(0,49) A_WeaponReady(WRF_NOBOB)
		PULS C 1 Offset(0,44) A_WeaponReady(WRF_NOBOB)
		PULS D 1 Offset(0,38) A_WeaponReady(WRF_NOBOB)
		PULS B 1 Offset(0,35) A_WeaponReady(WRF_NOBOB)
		PULS C 1 Offset(0,32) A_WeaponReady(WRF_NOBOB)
		Goto Ready
	TeslaFlash:
		PULS I 1 BRIGHT A_Light2
		TNT1 A 1
		TNT1 A 2 A_Light1
		Goto LightDone
	Spawn:
		PULS A -1 LIGHT(LAZERPUR)
		Stop
	}
}