ACTOR Shovel : NaziWeapon
{
	//$Category Weapons (Wolf3D)
	//$Title (1) Shovel
	//$Color 14
	Scale .5
	Weapon.SelectionOrder 9999
	Weapon.UpSound "shovel/select"
	Tag "The Shovel!"
	Inventory.PickupMessage "$SHOVEL"
	+WEAPON.NOAUTOFIRE
	-WEAPON.AMMO_CHECKBOTH
	States
	{
	Ready:
		SHUV A 1 A_WeaponReady
		Loop
	Deselect:
		SHUV A 0 A_Lower
		SHUV A 1 A_Lower
		Loop
	Select:
		SHUV A 0 A_Raise
		SHUV A 1 A_Raise
		Loop
	Fire:
		SHUV B 2 A_JumpIfInventory("PowerStrength",1,"PowerShovel")
		SHUV B 1 Offset(15,32)
		SHUV B 1 Offset(30,33) A_PlaySound("shovel/miss",CHAN_WEAPON)
		SHUV B 1 Offset(52,35)
		SHUV B 1 Offset(78,38)
		SHUV B 1 Offset(92,44)
		SHUV B 1 Offset(64,50)
		SHUV C 1 Offset(30,38) A_CustomPunch(60,1,CPF_DAGGER,"ShovelPuff",100)
		SHUV C 1 Offset(1,44)
		SHUV D 1 Offset(-32,24)
		SHUV D 1 Offset(-50,28)
		SHUV E 1 Offset(-74,32)
		SHUV E 1 Offset(-100,36)
	FireFinish:
		TNT1 A 10
		SHUV A 1 Offset(60,60)
		SHUV A 1 Offset(50,55)
		SHUV A 1 Offset(40,50) A_WeaponReady(WRF_NOBOB)
		SHUV A 1 Offset(30,45) A_WeaponReady(WRF_NOBOB)
		SHUV A 1 Offset(20,40) A_WeaponReady(WRF_NOBOB)
		SHUV A 1 Offset(10,35) A_WeaponReady(WRF_NOBOB)
		Goto Ready
	PowerShovel:
		SHUV B 1 Offset(5,32)
		SHUV B 1 Offset(10,33) A_PlaySound("shovel/miss",CHAN_WEAPON)
		SHUV B 1 Offset(16,35)
		SHUV B 1 Offset(24,38)
		SHUV B 1 Offset(26,39)
		SHUV B 1 Offset(27,40)
		SHUV C 1 Offset(20,38) A_CustomPunch(200,1,CPF_DAGGER,"ShovelPuff",100)
		SHUV C 1 Offset(10,39)
		SHUV C 1 Offset(-3,40)
		SHUV D 1 Offset(-19,32)
		SHUV D 1 Offset(-40,36)
		SHUV E 1 Offset(-64,40)
		SHUV E 1 Offset(-90,42)
		Goto FireFinish
	Spawn:
		SHUV Z -1
		Stop
	}
}

ACTOR ShovelPuff
{
	+NOBLOCKMAP
	+NOGRAVITY
	+PUFFONACTORS
	+NOEXTREMEDEATH
	RenderStyle "Add"
	ActiveSound ""
	AttackSound "shovel/wall"
	SeeSound "shovel/impact"
	Obituary "$OBSHOVEL"
	ProjectileKickback 100
	States
	{
	Spawn:
	Melee:
	Crash:
		POOF ABCDE 3
		Stop
	}
}

ACTOR Prod : ArtiBlastRadius
{
	+AUTOACTIVATE
	States
	{
	Use:
		TNT1 A 0 A_Blast(BF_DONTWARN, 96, random(16,24), random(8,16))
	}
}

ACTOR KnifeSilent : Weapon
{
	//$Category Weapons (Wolf3D)
	//$Title (1) Knife
	//$Color 14
	+FORCEPAIN
	+WEAPON.MELEEWEAPON
	+WEAPON.NOALERT
	+WEAPON.WIMPY_WEAPON
	Tag "Combat Knife"
	Weapon.BobStyle Smooth
	Weapon.BobSpeed 1.5
	Weapon.BobRangeX 1.5
	Weapon.UpSound "knife/select"
	Inventory.PickupMessage "$KNIFE"
	Weapon.SelectionOrder 10000
	
	States
	{
	Spawn:
		KNFE A -1
		Loop
	Ready:
		KNFG A 1 A_WeaponReady
		Loop
	Deselect:
		KNFG A 0 A_Lower
		KNFG A 1 A_Lower
		Loop
	Select:
		KNFG A 0 A_Raise
		KNFG A 1 A_Raise
		Loop
	Fire:
		KNFG A 1 A_PlaySound("knife/swing",CHAN_WEAPON)
		KNFG B 1 A_CustomPunch(10,1,CPF_DAGGER,"KnifePuff",64)
		KNFG CDEFGHJKLMN 1
		Goto Ready
	AltFire:
		KNFG A 1 A_PlaySound("kick/swing")
		KNFG B 1
		KNFG C 0 A_TakeInventory("Stamina",25)
		KNFG C 0 A_GiveInventory("Prod",1)
		KNFG C 1 A_CustomPunch(2, FALSE, CPF_DAGGER, "KnifePuff", 80)
		KNFG BA 2
		Goto Ready
	}
}

ACTOR KnifePuff : ShovelPuff
{
	AttackSound ""
	SeeSound ""
	Scale 0.4
	Obituary "$OBKNIFE"
	PainType "SilentKnifeAttack" //mxd. Used to invoke Pain.SilentKnifeAttack exclusively on stealth monsters
	
	States
	{
	Spawn:
	Melee:
	Crash:
		POOF A 3 A_PlaySound("knife/stone")
		POOF BCDE 3
		Stop
	XDeath:
		POOF A 3 A_PlaySound("knife/hit")
		Goto Crash+1
	}
}