// Base Tracer actors moved to ZScript: scripts/actors/tracers.txt
ACTOR TankSpark : TracerSpark //use this as blood for vehicles et similae
{
	Speed 0.2
	Scale 0.4
	Mass 125
	States
	{
	Spawn:
		PUFF A 2
		"####" AEFGHI 4 BRIGHT A_SetTranslucent(.8,1)
		"####" J 1 BRIGHT A_SetTranslucent(.8,1)
		"####" K 2 BRIGHT A_SetTranslucent(.7,1)
		"####" K 1 BRIGHT A_SetTranslucent(.6,1)
		"####" K 2 BRIGHT A_SetTranslucent(.5,1)
		"####" K 1 BRIGHT A_SetTranslucent(.4,1)
		"####" K 2 BRIGHT A_SetTranslucent(.3,1)
		"####" K 1 BRIGHT A_SetTranslucent(.2,1)
		"####" K 0 {bWindThrust = FALSE;}
		Stop
	}
}

//////////////////////////////////////////////////////////////////
////////////////// PLAYER WEAPON TRACERS /////////////////////////
//////////////////////////////////////////////////////////////////

//The Player's variants on the tracers, so they have different speeds and damages based on which weapon is being used.
// Base class defined in ZScript

//Muzzle velocity: 1,148-1,312 ft/s (350-400 m/s) - Wikipedia
ACTOR LugerTracer : PlayerTracer
{
	ProjectileKickback 60
	Damage (random(3,17))
	Speed 100
}

//Muzzle velocity: 1,200 ft/s (365 m/s) - Wikipedia
ACTOR WaltherTracer : LugerTracer
{
	Damage (random(6,18))
	Speed 90
}

ACTOR ShotgunTracer : PlayerTracer
{
	Scale .3
	Damage (random(5,15))
	States
	{
		Death:
		Crash:
		TNT1 AAA 0 A_SpawnItemEx("TracerSpark", 0, 0, 0, random(-2,2), random(-2,2), random(-2,2), random(0,359)) //T667 improvements
		TNT1 A 0 {bWindThrust = FALSE;}
		PUFF BCD 3 BRIGHT LIGHT(BPUFF1)
		Stop
	}
}

ACTOR AutoShotgunTracer : ShotgunTracer	{Obituary "$OBAUTOSH"}

//Muzzle velocity: 1,312 ft/s (400 m/s) - Wikipedia
ACTOR MP40Tracer : PlayerTracer
{
	Damage (random(9,14))
	Speed 90
}

//Muzzle velocity: 1,001 ft/s (305 m/s) - Wikipedia
ACTOR StenTracer : PlayerTracer { Damage (random(9,14)) }

//Muzzle velocity: 935 ft/s (285 m/s) - Wikipedia
ACTOR ThompsonTracer : PlayerTracer
{
	ProjectileKickback 100
	Damage (random(14,18))
	Speed 120
}

//Muzzle velocity: 2,822 ft/s (860 m/s) - Wikipedia
ACTOR Kar98kTracer : PlayerTracer
{
	ProjectileKickback 100
	Damage (random(40,65))
	Speed 180
	Obituary "$OBMAUSER"
}

ACTOR Kar98kTracer2 : Kar98kTracer { Damage (random(60,85)) }

//Muzzle velocity: 2,448-2,546 ft/s (746-776 m/s) - Wikipedia
ACTOR G43Tracer : Kar98kTracer
{
	Damage (random(26,55))
	Speed 150
	Obituary "$OBG43"
}

ACTOR ChaingunTracer : PlayerTracer
{
	Damage (random(11,15))
	Speed 90
}

//////////////////////////////////////////////////////////////////
//////////////// SHELLS & CASINGS ////////////////////////////////
//////////////////////////////////////////////////////////////////
ACTOR 9MMCasing
{
	Scale .075
	Projectile
	-NOBLOCKMAP
	-NOGRAVITY
	+BOUNCEONACTORS
	+FORCEXYBILLBOARD
	+MTHRUSPECIES //let's avoid these to be blocked by shark blockers - ozy81
	+THRUSPECIES //let's avoid these to be blocked by shark blockers - ozy81
	+WINDTHRUST
	BounceType Doom
	BounceSound "bulletcasing/bounce"
	BounceFactor .5
	WallBounceFactor .2
	Mass 5
	Radius 3
	Height 3
	Species "Player" //let's avoid these to be blocked by shark blockers - ozy81
	States
	{
	Spawn:
		CAS1 ABCDEFGH 2 NODELAY A_JumpIf(waterlevel == 3, "AdjustMass") //mxd
		Loop
	AdjustMass: //mxd
		"####" "#" 0 A_SetMass(400)
		Goto Swim
	Swim: //mxd. This presumes all cases have the same ammout of frames...
		"####" A 2 A_ScaleVelocity(0.7)
		"####" B 2 A_SpawnItemEx("Bubble", 0, 0, 0, 0, 0, 2, random (0, 360), 0, 128)
		"####" C 2 A_ScaleVelocity(0.7)
		"####" D 2 A_SpawnItemEx("Bubble", 0, 0, 0, 0, 0, 2, random (0, 360), 0, 128)
		"####" E 2 A_ScaleVelocity(0.7)
		"####" F 2 A_SpawnItemEx("Bubble", 0, 0, 0, 0, 0, 2, random (0, 360), 0, 128)
		"####" G 2 A_ScaleVelocity(0.7)
		"####" H 2 A_SpawnItemEx("Bubble", 0, 0, 0, 0, 0, 2, random (0, 360), 0, 128)
	SwimLoop: // Don't spawn more bubbles after the first batch
		"####" ABCDEFGH 2
		Loop
	Death:
		"####" A 0 A_UnSetSolid
		"####" A 0 A_Jump(256,1,2,3)
		"####" ACG 0 A_Jump(256,"Death1")
		Stop
	Death1:
		"####" "#" 0 {bBounceOnActors = FALSE;}
		"####" "#" 1 A_SetTics(35*CallACS("ShellLifetime")) //try to run tics into seconds for a player-friendly setting
	Death1Wait:
		"####" "#" 1 A_FadeOut(0.1)
		Wait
	}
}

ACTOR 9MMCasing2 : 9MMCasing { Scale 0.75}

ACTOR 45ACPCasing : 9MMCasing
{
	Scale .1
	States
	{
	Spawn:
		CAS3 ABCDEFGH 2 NODELAY A_JumpIf(waterlevel == 3, "AdjustMass") //mxd
		Loop
	}
}

ACTOR ShotgunCasing : 9MMCasing
{
	WallBounceSound "shellcasing/bounce"
	BounceSound "shellcasing/bounce"
	DeathSound "shellcasing/stop"
	Scale .175
	States
	{
	Spawn:
		CAS2 ABCDEFGH 2 NODELAY A_JumpIf(waterlevel == 3, "AdjustMass") //mxd
		Loop
	}
}

ACTOR MauserRifleCasing : 9MMCasing
{
	Scale .15
	States
	{
	Spawn:
		CAS4 ABCDEFGH 2 NODELAY A_JumpIf(waterlevel == 3, "AdjustMass") //mxd
		Loop
	}
}

ACTOR PyroCasing : ShotgunCasing
{
	Scale 0.48
	States
	{
	Spawn:
		PYCS ABCDEFGH 2 NODELAY A_JumpIf(waterlevel == 3, "AdjustMass") //mxd
		Loop
	}
}

ACTOR EnfieldRifleCasing : MauserRifleCasing {}
ACTOR GarandRifleCasing : MauserRifleCasing {}
ACTOR RevolverCasing : 9MMCasing {}
ACTOR STGCasing : MauserRifleCasing {}
ACTOR TokarevCasing : 9MMCasing {}

//Variant for Tesla
ACTOR TeslaRays
{
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	+NOINTERACTION
	+ROLLSPRITE
	RenderStyle Add
	Alpha 0.95
	Scale 0.1
	Translation "0:255=%[0.0,0.0,0.0]:[0.8,0.4,1.0]"
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(256,"Ray1","Ray2","Ray3","Ray4","Ray5")
	Ray1:
		TSL1 A 0
		Goto PlayOff
	Ray2:
		TSL2 A 0
		Goto PlayOff
	Ray3:
		TSL3 A 0
		Goto PlayOff
	Ray4:
		TSL4 A 0
		Goto PlayOff
	Ray5:
		TSL5 A 0
		Goto PlayOff
	PlayOff:
		"####" A 0 A_Jump(128,"FromL")
		"####" A 0 A_Jump(128,"FromAV")
	FromA:
		"####" ABCDEFGHIJK 2 BRIGHT { A_SetRoll(roll+10, SPF_INTERPOLATE); A_FadeOut(0.4); }
		Loop
	FromL:
		"####" LMNOPQRSTUV 2 BRIGHT { A_SetRoll(roll+10, SPF_INTERPOLATE); A_FadeOut(0.4); }
		Loop
	FromAV:
		"####" ABCDEFGHIJKLMNOPQRSTUV 2 BRIGHT { A_SetRoll(roll+10, SPF_INTERPOLATE); A_FadeOut(0.4); }
		Loop
	}
}

ACTOR TeslaTracer : BulletTracer
{
	+BLOODLESSIMPACT
	DamageType "Electric"
	Decal "LazScorch"
	States
	{
	Spawn:
		TNT1 A 1 BRIGHT A_JumpIfTracerCloser(96,"Whiz")
		TNT1 A 0 { A_SpawnItemEx("TeslaRays", 0, 0, 0, velx, vely, velz, 0, SXF_CLIENTSIDE); A_SpawnItemEx("TeslaTrail", 0, 0, 0, velx, vely, velz, 0, SXF_CLIENTSIDE); }
		Stop
	Whiz:
		TNT1 A -1 BRIGHT A_SpawnItemEx("TeslaWhizzer")
		Stop
	Death:
	Crash:
		TNT1 AAAAA 0 A_SpawnItemEx("Spark_P1", 0, 0, 0, random(1,2), random(1,2), random(1,2), random(1,360), SXF_CLIENTSIDE)
		TNT1 A 0 A_SpawnItemEx("SparkFlare_P1")
		TNT1 A 8 A_PlaySound("astroricochet")
		Stop
	XDeath:
		TNT1 AAAAA 0 A_SpawnItemEx("Spark_P1", 0, 0, 0, random(1,2), random(1,2), random(1,2), random(1,360), SXF_CLIENTSIDE)
		TNT1 A 0 A_SpawnItemEx("SparkFlare_P1")
		TNT1 A 1 A_PlaySound("astrohitflesh")
		Stop
	}
}

ACTOR TeslaTracer2 : TeslaTracer //this loops, needed for primary fire
{
	States
	{
	Spawn:
		TNT1 A 1 BRIGHT A_JumpIfTracerCloser(96,"Whiz")
		TNT1 A 0 { A_SpawnItemEx("TeslaRays", 0, 0, 0, velx, vely, velz, 0, SXF_CLIENTSIDE); A_SpawnItemEx("TeslaTrail2", 0, 0, 0, velx, vely, velz, 0, SXF_CLIENTSIDE); }
		Loop
	}
}

ACTOR EnemyTeslaTracer : TeslaTracer { Damage (random(2,4)) }

ACTOR TeslaWhizzer
{
	States
	{
	Spawn:
		TNT1 A 1 A_PlaySound("tesla/attack")
		Stop
	}
}

ACTOR TeslaTrail : TracerTrail { Scale 0.01 Decal "LazScorch" }
ACTOR TeslaTrail2 : TracerTrail { Decal "LazScorch" }

Actor TeslaBeam : FastProjectile //PillowBlaster Gamma-Ray gun code [taken straight & adjusted from Ghoul School 3D]
{
	Speed 250
	Radius 13
	Height 8
	Damage 5
	DamageType "Electric"
	Scale 0.15
	Decal LazScorch
	Renderstyle Add
	Alpha 0.95
	SeeSound ""
	-CANNOTPUSH
	-NODAMAGETHRUST
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	+NOEXTREMEDEATH
	+RANDOMIZE
	+RIPPER
	+ROLLSPRITE
	DeathSound "electricplasma/hit"
	MissileType "TeslaTrailSpawner"
	Translation "0:255=%[0.0,0.0,0.0]:[0.8,0.4,1.0]"
	States
	{
	Death:
		TSL1 A 0 A_CustomMissile("TeslaBeam2",0,0,random(0,360),4,random(0,360))
		"####" ABCDEFGHIJKLMNOPQRSTUV 1 BRIGHT { A_SetRoll(roll+random(100,-100), SPF_INTERPOLATE); A_Fadeout(0.04); }
		Stop
	}
}

ACTOR TeslaBeam2 : TeslaBeam
{
	Alpha 0.93
	States
	{
	Death:
		TSL2 A 0 A_CustomMissile("TeslaBeam3",0,0,random(0,360),4,random(0,360))
		"####" ABCDEFGHIJKLMNOPQRSTUV 1 BRIGHT { A_SetRoll(roll-random(100,-100), SPF_INTERPOLATE); A_Fadeout(0.04); }
		Stop
	}
}

ACTOR TeslaBeam3 : TeslaBeam
{
	Alpha 0.91
	States
		{
		Death:
			TSL3 A 0 A_CustomMissile("TeslaBeam4",0,0,random(0,360),4,random(0,360))
			"####" ABCDEFGHIJKLMNOPQRSTUV 1 BRIGHT { A_SetRoll(roll+random(100,-100), SPF_INTERPOLATE); A_Fadeout(0.04); }
			Stop
		}
}

ACTOR TeslaBeam4 : TeslaBeam
{
	Alpha 0.89
	States
	{
	Death:
		TSL4 A 0
		"####" ABCDEFGHIJKLMNOPQRSTUV 1 BRIGHT { A_SetRoll(roll-random(100,-100), SPF_INTERPOLATE); A_Fadeout(0.04); }
		Stop
	}
}

ACTOR TeslaSeeker : TeslaBeam
{
	Alpha 0.98
	States
	{
	Death:
		TSL5 A 0 A_CustomMissile("TeslaKiller",0,0,0,4,0)
		"####" ABCDEFGHIJKLMNOPQRSTUV 1 BRIGHT { A_SetRoll(roll+random(100,-100), SPF_INTERPOLATE); A_Fadeout(0.04); }
		Stop
	}
}

ACTOR TeslaKiller
{
	Radius 0
	Height 0
	+FRIENDLY
	+ISMONSTER
	+LOOKALLAROUND
	+NOBLOCKMONST
	+NOGRAVITY
	+NOINTERACTION
	-COUNTKILL
	Obituary "$OBTESKIL"
	RenderStyle None
	States
	{
	Spawn:
		TNT1 A 0 A_Look
	See:
		TNT1 A 0 A_Chase
	Missile:
		TNT1 A 0 A_FaceTarget
		TNT1 A 1 A_CustomMissile("TeslaBeam4",0,0,0)
		Stop
	}
}

ACTOR TeslaTrailSpawner
{
	+NOCLIP
	+NOGRAVITY
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 AAAAA 1 A_SpawnItemEx("TeslaTrails", random (-3, 3), 0, random (-3, 3), 0, 0, 0, 0, 160)
		Stop
	}
}

ACTOR TeslaTrails
{
	Radius 2
	Height 2
	Scale 0.1
	RenderStyle Add
	Alpha 0.95
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	+NOINTERACTION
	+ROLLSPRITE
	Translation "0:255=%[0.0,0.0,0.0]:[0.8,0.4,1.0]"
	States
	{
	Spawn:
		TSL1 ABCDEFGHIJKLMNOPQRSTUV 1 { A_SetRoll(roll+random(100,-100), SPF_INTERPOLATE); A_FadeOut(0.04); }
		Stop
	}
}

//Variant for Players
ACTOR TeslaTracerPlayer : TeslaTracer
{
	ProjectileKickback 60
	Damage (random(3,17))
	Speed 100
}

ACTOR TeslaTracer2Player : TeslaTracer2
{
	ProjectileKickback 60
	Damage (random(3,17))
	Speed 100
}

/////////////////////////////////////////////////////////////
/////////////////// MUTANT PROJECTILES //////////////////////
/////////////////////////////////////////////////////////////

//Nifty new mutants variations for tracers
ACTOR MutantTracer : BulletTracer
{
	DamageType "MutantPoison"
	BulletTracer.Trail "MutantTrail"
	States
	{
	Spawn:
		TNT1 A 1 BRIGHT A_JumpIfTracerCloser(96,"Whiz")
		Loop
	}
}

ACTOR MutantTrail: TracerTrail {}

/////////////////////////////////////////////////////////////
/////////////////// ASTROSTEIN PROJECTILES //////////////////
/////////////////////////////////////////////////////////////

//Variant for Enemies
ACTOR AstroTracer : BulletTracer
{
	+BLOODLESSIMPACT
	DamageType "AstroPoison"
	DeathType Disintegrate
	BulletTracer.Trail "AstroTrail"
	States
	{
	Spawn:
		TNT1 A 1 BRIGHT A_JumpIfTracerCloser(96,"Whiz")
		Loop
	Whiz:
		TNT1 A -1 BRIGHT A_SpawnItemEx("AstroWhizzer")
		Stop
	Death:
	Crash:
		TNT1 AAAAA 0 A_SpawnItemEx("Spark_G1", 0, 0, 0, random(1,2), random(1,2), random(1,2), random(1,360), SXF_CLIENTSIDE)
		TNT1 A 0 A_SpawnItemEx("SparkFlare_G1")
		TNT1 A 8 A_PlaySound("astroricochet")
		Stop
	XDeath:
		TNT1 AAAAA 0 A_SpawnItemEx("Spark_G1", 0, 0, 0, random(1,2), random(1,2), random(1,2), random(1,360), SXF_CLIENTSIDE)
		TNT1 A 0 A_SpawnItemEx("SparkFlare_G1")
		TNT1 A 1 A_PlaySound("astrohitflesh")
		Stop
	}
}

ACTOR EnemyAstroTracer : AstroTracer {Damage (random(2,4))}

ACTOR AstroWhizzer
{
	States
	{
	Spawn:
		TNT1 A 1 A_PlaySound("astrowhiz")
		Stop
	}
}

ACTOR AstroTrail: TracerTrail { TracerTrail.FadeRate 0.0 } // Don't fade the laser beams

//Variant for Players
ACTOR AstroTracerPlayer : AstroTracer
{
	ProjectileKickback 60
	Damage (random(3,17))
	Speed 100
}

//Robots tracers
ACTOR ProtoTracer : BulletTracer
{
	+BLOODLESSIMPACT
	DamageType "AstroPoison"
	DeathType Disintegrate
	BulletTracer.Trail "ProtoTrail"
	States
	{
	Spawn:
		TNT1 A 1 BRIGHT A_JumpIfTracerCloser(96,"Whiz")
		Loop
	Whiz:
		TNT1 A -1 BRIGHT A_SpawnItemEx("AstroWhizzer")
		Stop
	Death:
	Crash:
		TNT1 AAAAA 0 A_SpawnItemEx("Spark_R1", 0, 0, 0, random(1,2), random(1,2), random(1,2), random(1,360), SXF_CLIENTSIDE)
		TNT1 A 0 A_SpawnItemEx("SparkFlare_R1")
		TNT1 A 8 A_PlaySound("astroricochet")
		Stop
	XDeath:
		TNT1 AAAAA 0 A_SpawnItemEx("Spark_R1", 0, 0, 0, random(1,2), random(1,2), random(1,2), random(1,360), SXF_CLIENTSIDE)
		TNT1 A 0 A_SpawnItemEx("SparkFlare_R1")
		TNT1 A 1 A_PlaySound("astrohitflesh")
		Stop
	}
}

ACTOR EnemyProtoTracer : ProtoTracer { Damage (random(1,3)) }
ACTOR ProtoTrail: TracerTrail { TracerTrail.FadeRate 0.0 } // Don't fade the laser beams

/////////////////////////////////////////////////////////////
/////////////////// SKILL SETTINGS //////////////////////////
/////////////////////////////////////////////////////////////

// Enemy tracer variants for Normal mode.
ACTOR EnemyChaingunTracer : BulletTracer 			{Damage (random(6,8))}
ACTOR EnemyChaingunTracer2 : BulletTracer 			{Scale 2.0 Damage (random(6,8))}
ACTOR EnemyMutantTracer : MutantTracer				{Damage (random(2,4))}
ACTOR EnemyPistolTracer : BulletTracer				{Damage (random(4,8))}
ACTOR EnemyRifleTracer : BulletTracer				{Damage (random(15,30))}
ACTOR EnemySMGTracer : BulletTracer					{Damage (random(6,7))}
ACTOR EnemyShotgunTracer : BulletTracer 			{Damage (random(3,7))}
ACTOR EnemyStGTracer : BulletTracer					{Damage (random(7,8))}

// Enemy tracer variants for Baby mode
ACTOR BabyEnemyAstroTracer : EnemyAstroTracer		{Speed 30}
ACTOR BabyEnemyChaingunTracer : EnemyChaingunTracer	{Speed 30}
ACTOR BabyEnemyChaingunTracer2 : EnemyChaingunTracer2	{Scale 2.0 Speed 30}
ACTOR BabyEnemyMutantTracer : EnemyMutantTracer		{Speed 30}
ACTOR BabyEnemyPistolTracer : EnemyPistolTracer		{Speed 30}
ACTOR BabyEnemyProtoTracer : EnemyProtoTracer		{Speed 30}
ACTOR BabyEnemyRifleTracer : EnemyRifleTracer		{Speed 30}
ACTOR BabyEnemySMGTracer : EnemySMGTracer			{Speed 30}
ACTOR BabyEnemyShotgunTracer : EnemyShotgunTracer	{Speed 30}
ACTOR BabyEnemyStGTracer : EnemyStGTracer			{Speed 30}

// Enemy tracer variants for Easy mode
ACTOR EasyEnemyAstroTracer : EnemyAstroTracer		{Speed 50}
ACTOR EasyEnemyChaingunTracer : EnemyChaingunTracer	{Speed 50}
ACTOR EasyEnemyChaingunTracer2 : EnemyChaingunTracer2	{Scale 2.0 Speed 50}
ACTOR EasyEnemyMutantTracer : EnemyMutantTracer		{Speed 50}
ACTOR EasyEnemyPistolTracer : EnemyPistolTracer		{Speed 50}
ACTOR EasyEnemyProtoTracer : EnemyProtoTracer		{Speed 50}
ACTOR EasyEnemyRifleTracer : EnemyRifleTracer		{Speed 50}
ACTOR EasyEnemySMGTracer : EnemySMGTracer			{Speed 50}
ACTOR EasyyEnemyShotgunTracer : EnemyShotgunTracer	{Speed 50}
ACTOR EasyEnemyStGTracer : EnemyStGTracer			{Speed 50}

// Enemy tracer variants for Hard and Nightmare mode
ACTOR HardEnemyAstroTracer : EnemyAstroTracer		{+MTHRUSPECIES Speed 90}
ACTOR HardEnemyChaingunTracer : EnemyChaingunTracer	{+MTHRUSPECIES Speed 90}
ACTOR HardEnemyChaingunTracer2 : EnemyChaingunTracer2	{+MTHRUSPECIES Scale 2.0 Speed 90}
ACTOR HardEnemyMutantTracer : EnemyMutantTracer		{+MTHRUSPECIES Speed 90}
ACTOR HardEnemyPistolTracer : EnemyPistolTracer		{+MTHRUSPECIES Speed 90}
ACTOR HardEnemyProtoTracer : EnemyProtoTracer		{+MTHRUSPECIES Speed 90}
ACTOR HardEnemyRifleTracer : EnemyRifleTracer		{+MTHRUSPECIES Speed 90}
ACTOR HardEnemySMGTracer : EnemySMGTracer			{+MTHRUSPECIES Speed 90}
ACTOR HardEnemyShotgunTracer : EnemyShotgunTracer	{+MTHRUSPECIES Speed 90}
ACTOR HardEnemyStGTracer : EnemyStGTracer			{+MTHRUSPECIES Speed 90}

// SmokeSpawners for weapons //
ACTOR PistolSmokeSpawner : GunSmokeSpawner { GunSmokeSpawner.SmokeClass "PistolSmoke" }
ACTOR PistolSmoke : GunSmoke { Alpha 0.2 Scale 3 }
ACTOR ShotSmokeSpawner : GunSmokeSpawner { GunSmokeSpawner.SmokeClass "ShotSmoke" }
ACTOR ShotSmoke : GunSmoke { Alpha 0.75 Scale 3 }
ACTOR ChainSmokeSpawner : GunSmokeSpawner { GunSmokeSpawner.SmokeClass "ChainSmoke" }
ACTOR ChainSmoke : PistolSmoke { Alpha 0.5 Scale 2 }
ACTOR TurrSmokeSpawner : GunSmokeSpawner { GunSmokeSpawner.SmokeClass "TurrSmoke" }
ACTOR TurrCoolSmoke : GunSmoke { Alpha 0.3 Scale 4 }
ACTOR TurrSmoke : GunSmoke { Alpha 0.5 Scale 7 }