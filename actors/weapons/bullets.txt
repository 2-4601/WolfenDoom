// Base Tracer actors moved to ZScript: scripts/actors/tracers.txt 

ACTOR TankSpark : TracerSpark //use this as blood for vehicles et similae
{
	Speed 0.2
	Scale 0.4
	Mass 125
	States
	{
	Spawn:
		PUFF A 2
		"####" AEFGHI 4 BRIGHT A_SetTranslucent(.8,1)
		"####" J 1 BRIGHT A_SetTranslucent(.8,1)
		"####" K 2 BRIGHT A_SetTranslucent(.7,1)
		"####" K 1 BRIGHT A_SetTranslucent(.6,1)
		"####" K 2 BRIGHT A_SetTranslucent(.5,1)
		"####" K 1 BRIGHT A_SetTranslucent(.4,1)
		"####" K 2 BRIGHT A_SetTranslucent(.3,1)
		"####" K 1 BRIGHT A_SetTranslucent(.2,1)
		"####" K 0 A_ChangeFlag("WINDTHRUST", 0)
		Stop
	}
}

//////////////////////////////////////////////////////////////////
////////////////// PLAYER WEAPON TRACERS /////////////////////////
//////////////////////////////////////////////////////////////////

//The Player's variants on the tracers, so they have different speeds and damages based on which weapon is being used.
// Base class defined in ZScript

//Muzzle velocity: 1,148-1,312 ft/s (350-400 m/s) - Wikipedia
ACTOR LugerTracer : PlayerTracer
{
	ProjectileKickback 60
	Damage (random(3,17))
	Speed 100
}

//Muzzle velocity: 1,200 ft/s (365 m/s) - Wikipedia
ACTOR WaltherTracer : LugerTracer
{
	Damage (random(6,18))
	Speed 90
}

ACTOR ShotgunTracer : PlayerTracer
{
	Scale .3
	Damage (random(5,15))
	States
	{
		Death:
		Crash:
		TNT1 AAA 0 A_SpawnItemEx("TracerSpark", 0, 0, 0, random(-2,2), random(-2,2), random(-2,2), random(0,359)) //T667 improvements
		TNT1 A 0 A_ChangeFlag("WINDTHRUST", 0)
		PUFF BCD 3 BRIGHT LIGHT(BPUFF1)
		Stop
	}
}

ACTOR AutoShotgunTracer : ShotgunTracer	{Obituary "$OBAUTOSH"}

//Muzzle velocity: 1,312 ft/s (400 m/s) - Wikipedia
ACTOR MP40Tracer : PlayerTracer
{
	Damage (random(9,14))
	Speed 90
}

//Muzzle velocity: 1,001 ft/s (305 m/s) - Wikipedia
ACTOR StenTracer : PlayerTracer {Damage (random(9,14))}

//Muzzle velocity: 935 ft/s (285 m/s) - Wikipedia
ACTOR ThompsonTracer : PlayerTracer
{
	ProjectileKickback 100
	Damage (random(14,18))
	Speed 120
}

//Muzzle velocity: 2,822 ft/s (860 m/s) - Wikipedia
ACTOR Kar98kTracer : PlayerTracer
{
	ProjectileKickback 100
	Damage (random(40,65))
	Speed 180
	Obituary "$OBMAUSER"
}

ACTOR Kar98kTracer2 : Kar98kTracer
{
	Damage (random(60,85))
} 

//Muzzle velocity: 2,448-2,546 ft/s (746-776 m/s) - Wikipedia
ACTOR G43Tracer : Kar98kTracer
{
	Damage (random(26,55))
	Speed 150
	Obituary "$OBG43"
}

ACTOR ChaingunTracer : PlayerTracer
{
	Damage (random(11,15))
	Speed 90
}

//////////////////////////////////////////////////////////////////
//////////////// SHELLS & CASINGS ////////////////////////////////
//////////////////////////////////////////////////////////////////
ACTOR 9MMCasing
{
	Scale .075
	Projectile
	-NOBLOCKMAP
	-NOGRAVITY
	+BOUNCEONACTORS
	+FORCEXYBILLBOARD
	+MTHRUSPECIES //let's avoid these to be blocked by shark blockers - ozy81
	+THRUSPECIES //let's avoid these to be blocked by shark blockers - ozy81
	+WINDTHRUST
	BounceType Doom
	BounceSound "bulletcasing/bounce"
	BounceFactor .5
	WallBounceFactor .2
	Mass 5
	Radius 3
	Height 3
	Species "Player" //let's avoid these to be blocked by shark blockers - ozy81
	States
	{
	Spawn:
		CAS1 ABCDEFGH 2 NoDelay A_JumpIf(waterlevel == 3, "AdjustMass") //mxd
		Loop
	AdjustMass: //mxd
		"####" "#" 0 A_SetMass(400)
		Goto Swim
	Swim: //mxd. This presumes all cases have the same ammout of frames...
		"####" A 2 A_ScaleVelocity(0.7)
		"####" B 2 A_SpawnItemEx("Bubble", 0, 0, 0, 0, 0, 2, random (0, 360), 0, 128)
		"####" C 2 A_ScaleVelocity(0.7)
		"####" D 2 A_SpawnItemEx("Bubble", 0, 0, 0, 0, 0, 2, random (0, 360), 0, 128)
		"####" E 2 A_ScaleVelocity(0.7)
		"####" F 2 A_SpawnItemEx("Bubble", 0, 0, 0, 0, 0, 2, random (0, 360), 0, 128)
		"####" G 2 A_ScaleVelocity(0.7)
		"####" H 2 A_SpawnItemEx("Bubble", 0, 0, 0, 0, 0, 2, random (0, 360), 0, 128)
		Loop
	Death:
		"####" A 0 A_UnSetSolid
		"####" A 0 A_Jump(256,1,2,3)
		"####" ACG 0 A_Jump(256,"Death1")
		Stop
	Death1:
		"####" "#" 0 A_ChangeFlag("BOUNCEONACTORS", FALSE)
		Death1Wait:
		"####" "#" 1 A_SetTics(35*CallACS("ShellLifetime")) //try to run tics into seconds for a player-friendly setting
		"####" "#" 1 A_FadeOut(0.1)
		Wait
	}
}

ACTOR RevolverCasing : 9MMCasing{}

ACTOR 45ACPCasing : 9MMCasing
{
	Scale .1
	States
	{
	Spawn:
		CAS3 ABCDEFGH 2 NoDelay A_JumpIf(waterlevel == 3, "AdjustMass") //mxd
		Loop
	}
}

ACTOR ShotgunCasing : 9MMCasing
{
	WallBounceSound "shellcasing/bounce"
	BounceSound "shellcasing/bounce"
	DeathSound "shellcasing/stop"
	Scale .175
	States
	{
	Spawn:
		CAS2 ABCDEFGH 2 NoDelay A_JumpIf(waterlevel == 3, "AdjustMass") //mxd
		Loop
	}
}

ACTOR TokarevCasing : 9MMCasing{}

ACTOR MauserRifleCasing : 9MMCasing
{
	Scale .15
	States
	{
	Spawn:
		CAS4 ABCDEFGH 2 NoDelay A_JumpIf(waterlevel == 3, "AdjustMass") //mxd
		Loop
	}
}

ACTOR EnfieldRifleCasing : MauserRifleCasing{}
ACTOR GarandRifleCasing : MauserRifleCasing{}
ACTOR STGCasing : MauserRifleCasing{}

ACTOR PyroCasing : ShotgunCasing
{
	Scale 0.48
	States
	{
	Spawn:
		PYCS ABCDEFGH 2 NoDelay A_JumpIf(waterlevel == 3, "AdjustMass") //mxd
		Loop
	}
}

/////////////////////////////////////////////////////////////
/////////////////// MUTANT PROJECTILES //////////////////////
/////////////////////////////////////////////////////////////

//Nifty new mutants variations for tracers
ACTOR MutantTracer : Tracer
{
	DamageType MutantPoison

	States
	{
	Spawn:
		TNT1 A 1 BRIGHT A_JumpIfTracerCloser(96,"Whiz")
		TNT1 A 0 A_SpawnItemEx("MutantTrail", 0, 0, 0, velx, vely, velz, 0, SXF_ABSOLUTEVELOCITY) //mxd
		Loop
	}
}

ACTOR MutantTrail: TracerTrail {}

/////////////////////////////////////////////////////////////
/////////////////// ASTROSTEIN PROJECTILES //////////////////
/////////////////////////////////////////////////////////////

//Variant for Enemies
ACTOR AstroTracer : Tracer
{
	+BLOODLESSIMPACT
	DamageType AstroPoison
	DeathType Disintegrate
	States
	{
	Spawn:
		TNT1 A 1 BRIGHT A_JumpIfTracerCloser(96,"Whiz")
		TNT1 A 0 A_SpawnItemEx("AstroTrail", 0, 0, 0, velx, vely, velz, 0, SXF_ABSOLUTEVELOCITY) //mxd
		Loop
	Whiz:
		TNT1 A -1 BRIGHT A_SpawnItemEx("AstroWhizzer")
		Stop
	Death:
	Crash:
		TNT1 AAAAA 0 A_SpawnItemEx("Spark_G1", 0, 0, 0, random(1,2), random(1,2), random(1,2), random(1,360), SXF_CLIENTSIDE)
		TNT1 A 0 A_SpawnItemEx("SparkFlare_G1")
		TNT1 A 8 A_PlaySound("astroricochet")
		Stop
	XDeath:
		TNT1 AAAAA 0 A_SpawnItemEx("Spark_G1", 0, 0, 0, random(1,2), random(1,2), random(1,2), random(1,360), SXF_CLIENTSIDE)
		TNT1 A 0 A_SpawnItemEx("SparkFlare_G1")
		TNT1 A 1 A_PlaySound("astrohitflesh")
		Stop
	}
}

ACTOR EnemyAstroTracer : AstroTracer	{Damage (random(2,4))}

ACTOR AstroWhizzer
{
	States
	{
	Spawn:
		TNT1 A 1 A_PlaySound("astrowhiz")
		Stop
	}
}

ACTOR AstroTrail: TracerTrail{}

//Variant for Players
ACTOR AstroTracerPlayer : AstroTracer
{
	ProjectileKickback 60
	Damage (random(3,17))
	Speed 100
}

//Robots tracers
ACTOR ProtoTracer : Tracer
{
	+BLOODLESSIMPACT
	DamageType AstroPoison
	DeathType Disintegrate
	States
	{
	Spawn:
		TNT1 A 1 BRIGHT A_JumpIfTracerCloser(96,"Whiz")
		TNT1 A 0 A_SpawnItemEx("ProtoTrail", 0, 0, 0, velx, vely, velz, 0, SXF_ABSOLUTEVELOCITY) //mxd
		Loop
	Whiz:
		TNT1 A -1 BRIGHT A_SpawnItemEx("AstroWhizzer")
		Stop
	Death:
	Crash:
		TNT1 AAAAA 0 A_SpawnItemEx("Spark_R1", 0, 0, 0, random(1,2), random(1,2), random(1,2), random(1,360), SXF_CLIENTSIDE)
		TNT1 A 0 A_SpawnItemEx("SparkFlare_R1")
		TNT1 A 8 A_PlaySound("astroricochet")
		Stop
	XDeath:
		TNT1 AAAAA 0 A_SpawnItemEx("Spark_R1", 0, 0, 0, random(1,2), random(1,2), random(1,2), random(1,360), SXF_CLIENTSIDE)
		TNT1 A 0 A_SpawnItemEx("SparkFlare_R1")
		TNT1 A 1 A_PlaySound("astrohitflesh")
		Stop
	}
}

ACTOR EnemyProtoTracer : ProtoTracer	{Damage (random(1,3))}
ACTOR ProtoTrail: TracerTrail{}

/////////////////////////////////////////////////////////////
/////////////////// SKILL SETTINGS //////////////////////////
/////////////////////////////////////////////////////////////

// Enemy tracer variants for Normal mode.
ACTOR EnemyChaingunTracer : Tracer 			{Damage (random(6,8))}
ACTOR EnemyMutantTracer : MutantTracer	{Damage (random(2,4))}
ACTOR EnemyPistolTracer : Tracer				{Damage (random(4,8))}
ACTOR EnemyRifleTracer : Tracer					{Damage (random(15,30))}
ACTOR EnemySMGTracer : Tracer						{Damage (random(6,7))}
ACTOR EnemyShotgunTracer : Tracer 			{Damage (random(3,7))}
ACTOR EnemyStGTracer : Tracer						{Damage (random(7,8))}

// Enemy tracer variants for Baby mode
ACTOR BabyEnemyAstroTracer : EnemyAstroTracer				{Speed 30}
ACTOR BabyEnemyChaingunTracer : EnemyChaingunTracer	{Speed 30}
ACTOR BabyEnemyMutantTracer : EnemyMutantTracer			{Speed 30}
ACTOR BabyEnemyPistolTracer : EnemyPistolTracer			{Speed 30}
ACTOR BabyEnemyProtoTracer : EnemyProtoTracer				{Speed 30}
ACTOR BabyEnemyRifleTracer : EnemyRifleTracer				{Speed 30}
ACTOR BabyEnemySMGTracer : EnemySMGTracer						{Speed 30}
ACTOR BabyEnemyShotgunTracer : EnemyShotgunTracer		{Speed 30}
ACTOR BabyEnemyStGTracer : EnemyStGTracer						{Speed 30}

// Enemy tracer variants for Easy mode
ACTOR EasyEnemyAstroTracer : EnemyAstroTracer				{Speed 50}
ACTOR EasyEnemyChaingunTracer : EnemyChaingunTracer	{Speed 50}
ACTOR EasyEnemyMutantTracer : EnemyMutantTracer			{Speed 50}
ACTOR EasyEnemyPistolTracer : EnemyPistolTracer			{Speed 50}
ACTOR EasyEnemyProtoTracer : EnemyProtoTracer				{Speed 50}
ACTOR EasyEnemyRifleTracer : EnemyRifleTracer				{Speed 50}
ACTOR EasyEnemySMGTracer : EnemySMGTracer						{Speed 50}
ACTOR EasyyEnemyShotgunTracer : EnemyShotgunTracer	{Speed 50}
ACTOR EasyEnemyStGTracer : EnemyStGTracer						{Speed 50}

// Enemy tracer variants for Hard and Nightmare mode
ACTOR HardEnemyAstroTracer : EnemyAstroTracer				{+MTHRUSPECIES Speed 90}
ACTOR HardEnemyChaingunTracer : EnemyChaingunTracer	{+MTHRUSPECIES Speed 90}
ACTOR HardEnemyMutantTracer : EnemyMutantTracer			{+MTHRUSPECIES Speed 90}
ACTOR HardEnemyPistolTracer : EnemyPistolTracer			{+MTHRUSPECIES Speed 90}
ACTOR HardEnemyProtoTracer : EnemyProtoTracer				{+MTHRUSPECIES Speed 90}
ACTOR HardEnemyRifleTracer : EnemyRifleTracer				{+MTHRUSPECIES Speed 90}
ACTOR HardEnemySMGTracer : EnemySMGTracer						{+MTHRUSPECIES Speed 90}
ACTOR HardEnemyShotgunTracer : EnemyShotgunTracer		{+MTHRUSPECIES Speed 90}
ACTOR HardEnemyStGTracer : EnemyStGTracer						{+MTHRUSPECIES Speed 90}

// SmokeSpawners for weapons //
ACTOR PistolSmokeSpawner
{
	Radius 1
	Height 1
	Speed 25
	PROJECTILE
	States
		{
		Spawn:
			TNT1 A 1
			TNT1 A 0 A_Die
			Goto Death
		Death:
			TNT1 A 0 A_SpawnProjectile("PistolSmoke",random(-2,2),0,0)
			Stop
		}
}

ACTOR PistolSmoke
{
	Radius 1
	Height 1
	RenderStyle ADD
	Alpha 0.6
	Scale 0.1
	+NOCLIP
	+NOGRAVITY
	+NOBLOCKMAP
	+DONTSPLASH
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	States
		{
		Spawn:
			SMSP ABCDEFGHIJKLMNOPQ 1
			Stop
		}
}

ACTOR ShotSmokeSpawner : PistolSmokeSpawner
{
	Speed 20
	States
		{
		Death:
			TNT1 A 0 A_SpawnProjectile("ShotSmoke",random(-2,2),0,0)
			Stop
		}
}

ACTOR ShotSmoke : PistolSmoke { Scale 0.15 }

ACTOR ChainSmokeSpawner : PistolSmokeSpawner
{
	Speed 28
	States
		{
		Death:
			TNT1 A 0 A_SpawnProjectile("ChainSmoke",random(-2,2),0,0)
			Stop
		}
}

ACTOR ChainSmoke : PistolSmoke { Alpha 0.5 Scale 0.20 }