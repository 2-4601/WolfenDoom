ACTOR AstroShotgun : NaziWeapon
{
	//$Category Weapons (Wolf3D)/Astrostein
	//$Title (3) Astrostein Shotgun
	//$Color 14
	Weapon.SelectionOrder 400
	Weapon.AmmoUse 1
	Weapon.AmmoGive 8
	Weapon.AmmoType "AstroShotgunShell"
 	Weapon.UpSound "shotgun/select"
 	Inventory.PickupMessage "You got the Protonenflinte S79"
	Obituary "%o was mowed down by %k's protonenflinte."
	+WEAPON.NOAUTOFIRE
	Scale 1.0
	Tag "Protonenflinte S79"
	
	States
  	{
  	Spawn:
  	  ASSG A -1
  	  Stop
  	Ready:
  	  ASSG B 1 A_WeaponReady
  	  Loop
  	Deselect:
  	  ASSG B 1 A_Lower
  	  Loop
  	Select:
  	 ASSG B 1 A_Raise
  	  Loop
  	Fire:
	  TNT1 A 0 A_PlaySound("astroshotgun/fire")
  	  ASSG C 0 A_FireCustomMissile ("AstroTracerPlayer", -1.5, 1,  0, 0, 0)
  	  ASSG C 0 A_FireCustomMissile ("AstroTracerPlayer", -3.0, 0,  0, 0, 0)
  	  ASSG C 0 A_FireCustomMissile ("AstroTracerPlayer", -4.5, 0,  0, 0, 0)
  	  ASSG C 0 A_FireCustomMissile ("AstroTracerPlayer",  1.5, 0,  0, 0, 0)
  	  ASSG C 0 A_FireCustomMissile ("AstroTracerPlayer",  3.0, 0,  0, 0, 0)
  	  ASSG C 3 A_FireCustomMissile ("AstroTracerPlayer",  4.5, 0,  0, 0, 0)
  	  ASSG DEF 5
	  ASSG B 15
  	  ASSG B 0 A_ReFire
  	  Goto Ready
  	}
}


actor AstroShotgunShell : Ammo
{
  //$Category Ammo (Wolf3D)/Astrostein	
  //$Title Protonenflinte (ammo)
  //$Color 6
  Inventory.PickupMessage "Protonencell"
  Inventory.Amount 5
  Inventory.MaxAmount 50
  Inventory.Icon "ASSLA0"
  Ammo.BackpackAmount 10
  Ammo.BackpackMaxAmount 100
  states
  {
  Spawn:
    ASSL A -1
    stop
  }
}

///////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////


ACTOR AstroRocketlauncher : NaziWeapon
{
  //$Category Weapons (Wolf3D)/Astrostein
  //$Title (5) Astrostein RocketLauncher
  //$Color 14
  Weapon.SelectionOrder 1400
  Weapon.AmmoUse 1
  Weapon.AmmoGive 5
  Weapon.AmmoType "AstroRocketAmmo"
  Inventory.PickupMessage "You got the Energiegewehr B95"
  Obituary "%o was mowed down by %k's energiegewehr."
  Tag "Energiegewehr B95"

  States
  {
  Spawn:
    ASRL A -1
    Stop
  Ready:
    ASRL B 1 A_WeaponReady
    Loop
  Deselect:
    ASRL B 1 A_Lower
    Loop
  Select:
    ASRL B 1 A_Raise
    Loop
  Fire:
    ASRL C 4
  Hold:
    ASRL D 4
    ASRL E 4 A_FireCustomMissile ("AstroRocket", 0, 1,  0, 0, 0, 0)
    ASRL D 4
    ASRL C 25 
    ASRL C 0 A_ReFire
    Goto Ready
  }
}

ACTOR AstroRocket
{
  Radius 8
  Height 16
  Projectile
  Speed 12
  Damage 6
  Gravity 0.125
  -NOGRAVITY
  BounceType "Grenade"
  BounceFactor 0.9
  WallBounceFactor 0.9
  BounceCount 5
  SeeSound "astrolauncher/fire"
  DeathSound "astrolauncher/explode"
  BounceSound "astrolauncher/bounce"
  Obituary "$OB_MPMACE"

  States
  {
  Spawn:
    ASRK BC 4 Bright
    Loop
  Death:
    ASRK DEF 4 Bright
    Stop
  }
}

actor AstroRocketAmmo : Ammo
{
  //$Category Ammo (Wolf3D)/Astrostein	
  //$Title Energiegewehr (ammo)
  //$Color 6
  Inventory.PickupMessage "Energy unit"
  Inventory.Amount 1
  Inventory.MaxAmount 50
  Inventory.Icon "ASRKA0"
  Ammo.BackpackAmount 5
  Ammo.BackpackMaxAmount 100
  states
  {
  Spawn:
    ASRK A -1
    stop
  }
}

///////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////


ACTOR AstroChaingun : NaziWeapon
{
   //$Category Weapons (Wolf3D)/Astrostein
   //$Title (4) Astrostein Chaingun
   //$Color 14
   Weapon.SelectionOrder 700
   Weapon.AmmoUse 1
   Weapon.AmmoGive 20
   Weapon.AmmoType "AstroClipAmmo"
   Inventory.PickupMessage "You got the Repetierlaser 0815"
   Obituary "%o was mowed down by %k's repetierlaser."
   Tag "Repetierlaser 0815"
   States
   {
   Ready:
     ASCG B 1 A_WeaponReady
     Loop
   Deselect:
     ASCG B 1 A_Lower
     Loop
   Select:
     ASCG C 1 A_Raise
     Loop
   Fire:
     TNT1 A 0 A_PlaySound("astrochaingun/fire")
     ASCG DE 4 A_FireCustomMissile ("AstroTracerPlayer", random(-1.0,1.0), 1, 0, 0, 0, random(-1.0,1.0))
     ASCG D 0 A_ReFire
     Goto Ready
   Spawn:
     ASCG A -1
     Stop
   }
}

actor AstroClipAmmo : Ammo
{
  //$Category Ammo (Wolf3D)/Astrostein	
  //$Title Repetierlaser (ammo)
  //$Color 6
  Inventory.PickupMessage "Laser rounds"
  Inventory.Amount 10
  Inventory.MaxAmount 100
  Inventory.Icon "ASRKA0"
  Ammo.BackpackAmount 20
  Ammo.BackpackMaxAmount 200
  states
  {
  Spawn:
    ASCL A -1
    stop
  }
}