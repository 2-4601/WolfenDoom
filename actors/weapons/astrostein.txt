ACTOR AstroShotgun : NaziWeapon
{
	//$Category Weapons (Wolf3D)/Astrostein
	//$Title (3) Astrostein Shotgun
	//$Color 14
	Weapon.SelectionOrder 400
	Weapon.AmmoUse 1
	Weapon.AmmoGive 8
	Weapon.AmmoType "AstroShotgunShell"
 	Weapon.UpSound "shotgun/select"
 	Inventory.PickupMessage "$PROTON"
	Obituary "$OBPROTON"
	Tag "Protonenflinte S79"
	-WEAPON.AMMO_CHECKBOTH
	-WEAPON.NOALERT
	States
	{
  	Select:
		ASSG B 0 A_Raise
		ASSG B 1 A_Raise
		Loop
  	Deselect:
		ASSG B 0 A_Lower
		ASSG B 1 A_Lower
		Loop
  	Ready:
		ASSG B 1 A_WeaponReady
		Loop
	Fire:
		TNT1 A 0 A_PlaySound("astroshotgun/fire",CHAN_WEAPON)
		ASSG C 0 A_FireCustomMissile("AstroTracerPlayer",-4.5,0)
		ASSG C 0 A_FireCustomMissile("AstroTracerPlayer",-3.0,0)
		ASSG C 0 A_FireCustomMissile("AstroTracerPlayer",-1.5,0)
		ASSG C 0 A_FireCustomMissile("AstroTracerPlayer")
		ASSG C 0 A_FireCustomMissile("AstroTracerPlayer",1.5,0)
		ASSG C 0 A_FireCustomMissile("AstroTracerPlayer",3.0,0)
		ASSG C 3 A_FireCustomMissile("AstroTracerPlayer",4.5,0)
		ASSG DEF 5
		ASSG B 15
		ASSG B 0 A_ReFire
		Goto Ready
	Spawn:
		ASSG A -1
		Stop
	}
}

actor AstroShotgunShell : Ammo
{
	//$Category Ammo (Wolf3D)/Astrostein
	//$Title Protonenflinte (ammo)
	//$Color 6
	Inventory.PickupMessage "Protonencell"
	Inventory.Amount 5
	Inventory.MaxAmount 50
	Inventory.Icon "ASSLA0"
	Ammo.BackpackAmount 10
	Ammo.BackpackMaxAmount 100
	States
	{
	Spawn:
		ASSL A -1
		stop
	}
}

ACTOR AstroRocketlauncher : NaziWeapon
{
	//$Category Weapons (Wolf3D)/Astrostein
	//$Title (5) Astrostein RocketLauncher
	//$Color 14
	Weapon.SelectionOrder 1400
	Weapon.AmmoUse 1
	Weapon.AmmoGive 5
	Weapon.AmmoType "AstroRocketAmmo"
	Inventory.PickupMessage "$ENERGIE"
	Obituary "$OBENERGIE"
	Tag "Energiegewehr B95"
	+WEAPON.NOAUTOFIRE
	-WEAPON.AMMO_CHECKBOTH
	-WEAPON.NOALERT
	States
	{
	Select:
		ASRL B 0 A_Raise
		ASRL B 1 A_Raise
		Loop
	Deselect:
		ASRL B 0 A_Lower
		ASRL B 1 A_Lower
		Loop
	Ready:
		ASRL B 1 A_WeaponReady
		Loop
	Fire:
		ASRL CD 4
		ASRL E 4 A_FireCustomMissile("AstroRocket")
		ASRL D 4
		ASRL C 21
		ASRL C 0 A_ReFire
		Goto Ready
	Spawn:
		ASRL A -1
		Stop
  }
}

ACTOR AstroRocket
{
	Radius 8
	Height 16
	Projectile
	Speed 12
	Damage 20
	Gravity 0.125
	Renderstyle "Add"
	-NOGRAVITY
	BounceType "Grenade"
	BounceFactor 0.9
	WallBounceFactor 0.9
	BounceCount 5
	SeeSound "astrolauncher/fire"
	DeathSound "astrolauncher/explode"
	BounceSound "astrolauncher/bounce"
	Obituary "$OBASTROR"
	States
	{
	Spawn:
		ASRK BC 4 Bright A_SpawnItemEx("Spark_G1",0,0,0,random(1,2),random(1,2),random(1,2),random(1,360))
		Loop
	Death:
		TNT1 AAAAA 0 A_SpawnItemEx("Spark_G1",0,0,0,random(1,2),random(1,2),random(1,2),random(1,360))
		TNT1 A 0 A_SpawnItemEx("SparkFlare_G1")
		ASRK DEF 4 Bright
		Stop
	}
}

actor AstroRocketAmmo : Ammo
{
	//$Category Ammo (Wolf3D)/Astrostein
	//$Title Energiegewehr (ammo)
	//$Color 6
	Inventory.PickupMessage "Energy unit"
	Inventory.MaxAmount 50
	Inventory.Icon "ASRKA0"
	Ammo.BackpackAmount 5
	Ammo.BackpackMaxAmount 100
	States
	{
	Spawn:
		ASRK A -1
		stop
	}
}

ACTOR AstroChaingun : NaziWeapon
{
	//$Category Weapons (Wolf3D)/Astrostein
	//$Title (4) Astrostein Chaingun
	//$Color 14
	Weapon.SelectionOrder 700
	Weapon.AmmoUse 1
	Weapon.AmmoGive 20
	Weapon.AmmoType "AstroClipAmmo"
	Inventory.PickupMessage "$REPETIE"
	Obituary "$OBREPETIE"
	Tag "Repetierlaser 0815"
	-WEAPON.AMMO_CHECKBOTH
	-WEAPON.NOALERT
	States
	{
	Spawn:
		ASCG A -1
		Stop
	Ready:
		ASCG B 1 A_WeaponReady
		Loop
	Deselect:
		ASCG B 0 A_Lower
		ASCG B 1 A_Lower
		Loop
	Select:
		ASCG C 0 A_Raise
		ASCG C 1 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_PlaySound("astrochaingun/fire",CHAN_WEAPON)
		ASCG DE 4 A_FireCustomMissile("AstroTracerPlayer",random(-1.0,1.0),1,0,0,0,random(-1.0,1.0))
		ASCG D 0 A_ReFire
		Goto Ready
	}
}

actor AstroClipAmmo : Ammo
{
	//$Category Ammo (Wolf3D)/Astrostein
	//$Title Repetierlaser (ammo)
	//$Color 6
	Inventory.PickupMessage "Laser rounds"
	Inventory.Amount 10
	Inventory.MaxAmount 100
	Inventory.Icon "ASRKA0"
	Ammo.BackpackAmount 20
	Ammo.BackpackMaxAmount 200
	states
	{
	Spawn:
		ASCL A -1
		stop
	}
}

actor AstrosteinMelee : Naziweapon
{
	//$Category Weapons (Wolf3D)/Astrostein
	//$Title (1) Astrostein Melee
	//$Color 14
	Weapon.Kickback 0
	Weapon.SelectionOrder 2200
	Weapon.UpSound "weapons/rezup"
	Weapon.ReadySound "weapons/rezidle"
	Inventory.PickupMessage "$ELEKTRO"
	Obituary "$OBELEKTRO"
	Tag "Elektroschocker"
	+WEAPON.MELEEWEAPON
	-WEAPON.AMMO_CHECKBOTH
	-WEAPON.NOALERT
	States
	{
	Select:
		REZG C 0 A_Raise
		REZG C 1 A_Raise
		Loop
	Deselect:
		REZG C 0 A_Lower
		REZG C 1 A_Lower
		Loop
	Ready:
		REZG CD 4 A_WeaponReady
		Loop
	Fire:
		REZG AB 3 A_Saw("weapons/rezfull","weapons/rezhit",2,"ElektroshockerPuff")
		REZG B 0 A_ReFire
		goto Ready
	Spawn:
		REZN A -1
		Stop
	}
}

Actor ElektroshockerPuff : BulletPuff
{
	+PUFFONACTORS
	DamageType Reznator
	RenderStyle Add
	Alpha 0.8
	VSpeed 0
	States
	{
	Spawn:
	Melee:
		TNT1 AAAAA 0 A_SpawnItemEx("Spark_G1", 0, 0, 0, random(1,2), random(1,2), random(1,2), random(1,360), SXF_CLIENTSIDE)
		TNT1 A 0 A_SpawnItemEx("SparkFlare_G1")
		RZAP ACEGH 2 bright
		Stop
	}
}

///////////////////////////////////////////////////////////////////////////////
/////// ASTROSTEIN HANDGRANATE ////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////

ACTOR AstroGrenadePickup : CustomInventory
{
	//$Category Weapons (Wolf3D)/Astrostein
	//$Title Grenade (Astrostein)
	//$Color 6
	Inventory.PickupMessage "$GRENADE"
	Tag "Astro Grenade"
	Inventory.PickupSound "grenade/pickup"
	Scale 0.25
	+INVENTORY.AUTOACTIVATE
	States
	{
	Spawn:
		ASGN A -1
		Stop
	Use:
		TNT1 A 0 A_GiveInventory("GrenadePickup")
		Stop
	}
}

ACTOR AstroHandGrenade : HandGrenade
{
	BounceFactor 0.4
	WallBounceFactor 0.8
	Scale 0.25
	states
	{
	Spawn:
		ASGR A 2 A_Countdown
		ASGR ACDEFGH 2
		Loop
	Death:
		ASGR I 35
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_SetScale(1.75,1.75)
		TNT1 A 0 A_SetTranslucent(0.75,1)
		TNT1 A 0 A_PlaySound("astrostein/explosion",0,1.0,0,0.1)
		TNT1 A 1 A_SpawnItemEx("AstrosteinExplosion_Medium")
		TNT1 A 1 Radius_Quake(10,10,0,16,0)
		stop
	}
}