ACTOR BigSpider
{
	//$Category Monsters (Wolf3D)/Critters
	//$Title Spider (small)
	//$Color 4
	Health 15
	Radius 8
	Height 16
	Speed 8
	Mass 100
	PainChance 150
	Scale 0.33
	Translation "176:191=164:167" , "32:47=164:167"
	BloodType "ScorpionBlood"
	BloodColor "DarkOrange1"
	MaxDropOffHeight 128
	MaxStepHeight 16
	MONSTER 
	+FLOORCLIP
	+NEVERFAST
	+DONTGIB
	MeleeSound "spider1/melee"
	SeeSound "spider1/sight"
	PainSound "spider1/pain"
	DeathSound "spider1/death"
	ActiveSound "spider1/active"
	HitObituary "%o was bitten by a big spider."
	MeleeDamage 1

	States
	{
		Spawn:
			GAYS A 0 NoDelay A_SetScale(ScaleX + frandom(-0.1, 0.05))
			goto Idle
			
		Idle:
			GAYS A 10 A_Look
			Loop	
			
		See:
			GAYS A 0 A_PlaySound("spider1/walk", CHAN_AUTO, 0.4)
			GAYS AABB 2 A_Chase
			GAYS B 0 A_PlaySound("spider1/walk", CHAN_AUTO, 0.4)
			GAYS CCDD 2 A_Chase
			GAYS A 0 A_JumpIf(Random(1, 24) == 1, "TryJump")
			Loop
			
		TryJump:
			GAYS A 0 A_CheckFloor("Jump") //don't jump if mid-air
			goto See

		Jump:
			GAYS A 0 A_FaceTarget
			GAYS A 0 ThrustThing(angle*256/360, Random(8, 12), 0, 0)
			GAYS A 0 ThrustThingZ(0, Random(30, 50), 0, 1)
			GAYS A 0 A_PlaySound("spider1/jump", CHAN_AUTO, 0.2)
			GAYS E 15
			goto See	
			
		Melee:
			GAYS A 4 A_FaceTarget
			GAYS E 4 A_MeleeAttack
			GAYS E 9 A_FaceTarget
			Goto See
			
		Pain:
			GAYS C 5
			GAYS C 3 A_Pain
			Goto See
			
		Death:
			GAYR F 5
			GAYR E 5 A_Scream
			GAYR D 5 A_Fall
			GAYR C 5
			GAYR B 5
			GAYR A 5
			GAYR A -1
			Stop
	}
}