ACTOR NaziLoper : Guard
{
	//$Category Monsters (Wolf3D)/Occult
	//$Title Nazi Loper
	//$Color 4
	Health 550
	Height 64
	Radius 32
	Mass 300
	Speed 14
	PainChance 180
	MeleeDamage 7
	MaxStepHeight 16
	MaxDropOffHeight 128
	SeeSound "loper/wakeup"
	DeathSound "loper/death"
	HitObituary "%o was electrificated by a grinny Loper"
	Obituary "%o was zapped by a Loper"
	+SHORTMISSILERANGE
	+NORADIUSDMG //or the ZTracer will kill the Loper :D
	Scale 1.10
	States
	{
	Spawn:
		LOPR A 0 NoDelay A_CustomMissile("TPortLightningWaveSpawner", 8, 0, 0, 2, 0)
		"####" A 0 A_CustomMissile("TPortLightningWaveSpawner", 8, 0, 180, 2, 0)
		"####" AAAA 4 bright A_Look 
		Goto SkillCheck
	Idle:
		"####" BCDC 4 Bright A_Look
		Loop
	See:
		"####" BBBBCCCCDDDDCCCC 1 bright Light ("TPortNormal") A_Chase
		"####" B 0 bright Light("TPortNormal") A_SpawnItemEx("TPortLightningWaveSpawner",0,0,8,0,0,0,0,0,160)
		"####" B 0 bright Light("TPortNormal") A_Jump(128,"Missile")
		"####" B 0 bright Light("TPortNormal") A_Jump(96,"Missile2")
		Loop
	Melee:
		"####" E 0 A_CustomMissile("TPortLightningWaveSpawner", 8, 0, 0, 2, 0)
		"####" E 0 A_CustomMissile("TPortLightningWaveSpawner", 8, 0, 180, 2, 0)
		"####" E 8 bright Light("TPortNormal") A_FaceTarget
		"####" E 0 bright Light("TPortNormal") A_SpawnItemEx("TPortLightningWaveSpawner",0,0,8,0,0,0,0,0,160)
		"####" FG 8 bright Light("TPortNormal") A_CustomMeleeAttack(2*random(2,10),"loper/attack","loper/attack")
		Goto See
	Missile:
		"####" E 0 A_CustomMissile("TPortLightningWaveSpawner", 8, 0, 0, 2, 0)
		"####" E 0 A_CustomMissile("TPortLightningWaveSpawner", 8, 0, 180, 2, 0)
		"####" E 0 bright Light("TPortNormal") A_SpawnItemEx("TPortLightningWaveSpawner",0,0,8,0,0,0,0,0,160)
		"####" E 0 A_SpawnItemEx("TPortLightningWaveSpawner",0,0,8,0,0,0,0,0,160)
		"####" E 0 Bright A_PlaySound("tesla/attack")
		"####" E 8 BRIGHT LIGHT(LOPERLIT) A_CustomRailgun(random(10,20), 0, white, cyan, RGF_NOPIERCING, 1, 2, none, 0, 0, 1500, random(24,48), 8.0, 2.0, "LoperZap", -28)
		"####" EE 8 A_FaceTarget
		"####" E 0 A_CustomMissile("TPortLightningWaveSpawner", 8, 0, 0, 2, 0)
		"####" E 0 A_CustomMissile("TPortLightningWaveSpawner", 8, 0, 180, 2, 0)
		"####" E 0 Bright A_PlaySound("tesla/attack")
		"####" E 8 BRIGHT LIGHT(LOPERLIT) A_CustomRailgun(random(10,20), 0, cyan, white, RGF_NOPIERCING, 1, 2, none, 0, 0, 1500, random(24,48), 8.0, 2.0, "LoperZap", -28)
		"####" EE 8 A_FaceTarget
		Goto See
	Missile2:
		"####" A 0 A_CustomMissile("TPortLightningWaveSpawner", 8, 0, 0, 2, 0)
		"####" A 0 A_CustomMissile("TPortLightningWaveSpawner", 8, 0, 180, 2, 0)
		"####" A 0 bright Light("TPortNormal") A_SpawnItemEx("TPortLightningWaveSpawner",0,0,8,0,0,0,0,0,160)
		"####" A 0 A_SpawnItemEx("TPortLightningWaveSpawner",0,0,8,0,0,0,0,0,160)
		"####" A 0 ThrustThing(angle*256/360,Random(4,6),0,0)
		"####" AE 6 bright Light("TPortNormal") A_SpawnItemEx("TPortLightningWaveSpawner",0,0,8,0,0,0,0,0,160)
		"####" H 8 bright Light("TPortNormal") ThrustThingZ(0,Random(30,40),0,1)
		"####" E 6 bright Light("TPortNormal") A_SpawnItemEx("TPortLightningWaveSpawner",0,0,8,0,0,0,0,0,160)
		"####" FG 17 BRIGHT LIGHT(LOPERLIT) A_SpawnItemEx("TPortLightningWaveSpawner",0,0,8,0,0,0,0,0,160)
		"####" G 1 BRIGHT LIGHT(LOPERLIT) A_SpawnItemEx("GeneralExplosion_Medium")
		"####" G 1 BRIGHT LIGHT(LOPERLIT) Radius_Quake(10,10,0,16,0)
		//Explosion:
		"####" F 0 bright Light("TPortNormal") A_SpawnItem("TPortMistSpawner",0,32)
		"####" G 0 bright Light("TPortNormal") A_SpawnItemEx("TPortLightningWaveSpawner",0,0,8,0,0,0,0,0,160)
		"####" F 0 Bright LIGHT(LOPERLIT) A_custommissile("ZTracer",0,0,0,2,0)
		"####" G 0 Bright LIGHT(LOPERLIT) A_custommissile("ZTracer",0,0,20,2,0)
		"####" F 0 Bright LIGHT(LOPERLIT) A_custommissile("ZTracer",0,0,40,2,0)
		"####" G 0 Bright LIGHT(LOPERLIT) A_custommissile("ZTracer",0,0,60,2,0)
		"####" F 0 Bright LIGHT(LOPERLIT) A_custommissile("ZTracer",0,0,80,2,0)
		"####" G 0 Bright LIGHT(LOPERLIT) A_custommissile("ZTracer",0,0,100,2,0)
		"####" F 0 Bright LIGHT(LOPERLIT) A_custommissile("ZTracer",0,0,120,2,0)
		"####" G 0 Bright LIGHT(LOPERLIT) A_custommissile("ZTracer",0,0,140,2,0)
		"####" F 0 Bright LIGHT(LOPERLIT) A_custommissile("ZTracer",0,0,160,2,0)
		"####" G 0 Bright LIGHT(LOPERLIT) A_custommissile("ZTracer",0,0,180,2,0)
		"####" F 0 Bright LIGHT(LOPERLIT) A_custommissile("ZTracer",0,0,200,2,0)
		"####" G 0 Bright LIGHT(LOPERLIT) A_custommissile("ZTracer",0,0,220,2,0)
		"####" F 0 Bright LIGHT(LOPERLIT) A_custommissile("ZTracer",0,0,240,2,0)
		"####" G 0 Bright LIGHT(LOPERLIT) A_custommissile("ZTracer",0,0,260,2,0)
		"####" F 0 Bright LIGHT(LOPERLIT) A_custommissile("ZTracer",0,0,280,2,0)
		"####" G 0 Bright LIGHT(LOPERLIT) A_custommissile("ZTracer",0,0,300,2,0)
		"####" F 0 Bright LIGHT(LOPERLIT) A_custommissile("ZTracer",0,0,320,2,0)
		"####" G 0 Bright LIGHT(LOPERLIT) A_custommissile("ZTracer",0,0,340,2,0)
		goto See
	Pain:
		"####" I 0 A_CustomMissile("TPortLightningWaveSpawner", 8, 0, 0, 2, 0)
		"####" I 0 A_CustomMissile("TPortLightningWaveSpawner", 8, 0, 180, 2, 0)
		"####" I 0 bright Light("TPortNormal") A_SpawnItemEx("TPortLightningWaveSpawner",0,0,8,0,0,0,0,0,160)
		"####" I 5 bright Light("TPortNormal") A_Pain
		Goto See
	Death.Fire:
	Death:
	XDeath:
		"####" I 0 A_CustomMissile("TPortLightningWaveSpawner", 8, 0, 0, 2, 0)
		"####" I 0 A_CustomMissile("TPortLightningWaveSpawner", 8, 0, 180, 2, 0)
		"####" I 8 A_Scream
		"####" J 8 A_NoBlocking
		"####" K 8
		"####" L -1 A_Die
		Stop
	Raise:
		"####" LKJIEA 8
		"####" A 0 A_CustomMissile("TPortLightningWaveSpawner", 8, 0, 0, 2, 0)
		"####" A 0 A_CustomMissile("TPortLightningWaveSpawner", 8, 0, 180, 2, 0)
		Goto See
   }
}

ACTOR LoperZap : Blood
{
	+NOGRAVITY
	Mass 0
	RenderStyle Translucent
	Alpha 0.6
	Translation "176:191=194:197"
}

ACTOR ZTracer
{
   Radius 5
   Height 5
   Speed 15
   ReactionTime 175
   Damage 5
   ExplosionDamage 16
   ExplosionRadius 16
   DamageType fire
   RenderStyle ADD
   Alpha 0.67
   PROJECTILE
   +FLOORHUGGER
   +THRUGHOST
   -NOGRAVITY
   Obituary "%o got was vaporized by a Loper spark."
   States
   {
   Spawn:
      TNT1 A 1 Bright LIGHT(LOPERLIT)
      TNT1 A 0 Bright A_Countdown
      TNT1 A 0 Bright A_CustomMissile("ZTracerPuff",0,0,0,0)
      loop
   Death:
      ZTRA K 4 Bright LIGHT(LOPERLIT)
      ZTRA L 4 Bright LIGHT(LOPERLIT) A_Explode
      ZTRA MNO 3 Bright LIGHT(LOPERLIT)
      stop
   }
}

ACTOR ZTracerPuff
{
   Radius 1
   Height 1
   Speed 0
   RENDERSTYLE ADD
   DamageType fire
   ALPHA 0.67
   PROJECTILE
   +FLOORHUGGER
   -NOGRAVITY
   States
   {
   Spawn:
      ZTRA ABCDEFGHIJ 3 Bright LIGHT(LOPERLIT) A_Explode(2,4)
      stop
   }
}

//Support Actors from TPortal//
Actor VisualSpecialEffect
{
	+CLIENTSIDEONLY
	+NOINTERACTION
	+NOBLOCKMAP
	+NOGRAVITY
	+NOTELEPORT
	+FORCEXYBILLBOARD
}

Actor TPortMist : VisualSpecialEffect
{
	RenderStyle Add
	Alpha 0.25
	Scale 0.225
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(256,1,2,3,4,5,6,7,8,9,10)
	Loopy:
		MIST ABCDEFGHIJKLMOOPQR 1 bright A_FadeOut(0.01)
		loop
	}
}

Actor TPortMistSpawner : VisualSpecialEffect
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 AA 0 A_SpawnItemEx("TPortMist",Random(-14,14),Random(-14,14),Random(-14,14),0.012*Random(-50,50),0.012*Random(-50,50),0.012*Random(-50,50),0,0,24)
		stop
	}
}

Actor TPortLightning : VisualSpecialEffect
{
	RenderStyle Add
	Alpha 0.7
	States
	{
	Spawn:
		TNT1 A 0 // Huh, that's the jump...
		TNT1 A 0 A_Jump(256,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24,25,26,27,28,29,30,31,32,33,34,35,36,37,38,39,40,41,42,43,44,45,46,47,48,49,50,51,52,53,54,55,56,57,58,59,60,61,62,63,64,65,66,67,68,69,70,71,72)
	Select:
		BLL1 ABCDEFGHIJKLMNOPQR 0 A_Jump(256,"Fade")
		BLL2 ABCDEFGHIJKLMNOPQR 0 A_Jump(256,"Fade")
		BLL3 ABCDEFGHIJKLMNOPQR 0 A_Jump(256,"Fade")
		BLL4 ABCDEFGHIJKLMNOPQR 0 A_Jump(256,"Fade")
	Fade:
		"----" A 1 bright A_FadeOut(0.15)
		loop
	}
}

Actor TPortLightningSmall : TPortLightning {Scale 0.1}
Actor TPortLightningMedium : TPortLightning {Scale 0.2}
Actor TPortLightningLarge : TPortLightning {Scale 0.3}
Actor TPortLightningHuge : TPortLightning {Scale 0.4}

// A wave of lightning
Actor TPortLightningWave : VisualSpecialEffect
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("TPortLightningSmall",Random(-2,2),Random(-2,2),Random(-2,2),0,0,0,0,0,32)
		TNT1 A 0 A_SpawnItemEx("TPortLightningMedium",Random(-3,3),Random(-3,3),Random(-3,3),0,0,0,0,0,56)
		TNT1 A 0 A_SpawnItemEx("TPortLightningLarge",Random(-4,4),Random(-4,4),Random(-3,3),0,0,0,0,0,96)
		TNT1 A 0 A_SpawnItemEx("TPortLightningHuge",Random(-5,5),Random(-5,5),Random(-5,5),0,0,0,0,0,144)
		stop
	}
}

// Spawns lightning waves
Actor TPortLightningWaveSpawner : VisualSpecialEffect
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("TPortalZap")
		TNT1 A 2 Light("TPortZap") A_SpawnItem("TPortLightningWave")
		TNT1 A 1 Light("TPortZap") A_SpawnItem("TPortLightningWave")
		TNT1 A 3 Light("TPortZap") A_SpawnItem("TPortLightningWave")
		TNT1 A 5 Light("TPortZap") A_SpawnItem("TPortLightningWave")
		TNT1 A 1 Light("TPortZap") A_SpawnItem("TPortLightningWave")
		TNT1 A 2 Light("TPortZap") A_SpawnItem("TPortLightningWave")
		TNT1 A 4 A_SpawnItem("TPortLightningWave")
		stop
	}
}