ACTOR AstroGuard : Nazi
{
	//$Category Monsters (Wolf3D)/Astrostein
	//$Title Astrostein Guard
	//$Color 4
	Health 20
	SeeSound "Nazi1/Sighted"
	PainSound "Nazi1/Pain"
	DeathSound "Nazi1/Death"
	Obituary "$GUARD"
	DropItem AstroClipAmmo 192
	DamageFactor "AstroPoison", 0.0
	+NOBLOOD
	+NOINFIGHTING
	States
	{
	Spawn:
		ATR2 A 0
		Goto SkillCheck
	Missile:
		"####" E 5 A_FaceTarget
		"####" F 0 A_PlaySound("astrochaingun/fire")
		"####" F 8 BRIGHT LIGHT(ASTROFIRE) A_CustomMissile("EnemyAstroTracer",48,1,random(-8,8))
		"####" E 8 A_Jump(256,"See")
		Stop
	Pain:
		"####" G 3
		"####" G 3 A_Pain
		"####" "#" 0 A_AlertMonsters(256) //mxd
		"####" G 0 A_Jump(256,"See")
		Stop
	Death:
		"####" H 0 A_Playsound("astrostein/guard_death")
		"####" H 5 A_Scream
		"####" H 0 A_NoBlocking
		"####" H 0 A_SpawnItemEx("BaseLine", random(16, -16), random(16, -16), random(0, 8), 0, 0, random(1,3), 0, 129, 0)
		"####" H 1 A_FadeOut(0.02)
		Goto Death+3
	}
}

ACTOR AstroGuard2 : AstroGuard
{
	//$Category Monsters (Wolf3D)/Astrostein
	//$Title Astrostein Guard 2
	//$Color 4
	Health 30
	States
	{
	Spawn:
		ATR1 A 0
		Goto SkillCheck
	Missile:
		"####" E 5 A_FaceTarget
		"####" F 0 A_PlaySound("astrochaingun/fire")
		"####" F 8 BRIGHT LIGHT(ASTROFIRE) A_CustomMissile("EnemyAstroTracer",48,1,random(-8,8))
		"####" E 5 A_FaceTarget
		"####" F 0 A_PlaySound("astrochaingun/fire")
		"####" F 8 BRIGHT LIGHT(ASTROFIRE) A_CustomMissile("EnemyAstroTracer",48,1,random(-8,8))
		"####" E 5 A_FaceTarget
		"####" F 0 A_PlaySound("astrochaingun/fire")
		"####" F 8 BRIGHT LIGHT(ASTROFIRE) A_CustomMissile("EnemyAstroTracer",48,1,random(-8,8))
		"####" E 8 A_Jump(256,"See")
		Stop
	}
}

ACTOR AstroCommando : AstroGuard
{
	//$Category Monsters (Wolf3D)/Astrostein
	//$Title Astrostein Commando
	//$Color 4
	Health 80
	States
	{
	Spawn:
		ATR3 A 0
		Goto SkillCheck
	Missile:
		"####" EF 5 A_FaceTarget
		"####" G 0 A_PlaySound("astrochaingun/fire")
		"####" G 8 BRIGHT LIGHT(ASTROFIRE) A_CustomMissile("EnemyAstroTracer",48,1,random(-8,8))
		"####" F 5 A_MonsterRefire(30,"See")
		Goto Missile+1
	}
}

Actor AstroOfficer : AstroGuard
{
	//$Category Monsters (Wolf3D)/Astrostein
	//$Title Astrostein Officer
	//$Color 4
	Speed 5.33
	Health 30
	States
	{
	Spawn:
		ATR5 A 0
		Goto SkillCheck
	Missile:
		"####" G 5 A_FaceTarget
		"####" G 0 A_PlaySound("astrochaingun/fire")
		"####" E 8 BRIGHT LIGHT(ASTROFIRE) A_CustomMissile("EnemyAstroTracer",32,1,random(-8,8))
		"####" F 5 A_MonsterRefire(30,"See")
		"####" E 8 A_Jump(256,"See")
		Stop
	}
}

Actor AstroElite : AstroGuard
{
	//$Category Monsters (Wolf3D)/Astrostein
	//$Title Astrostein Elite
	//$Color 4
	Speed 5.0
	Health 30
	DropItem AstroShotgun 64
	DropItem AstroShotgunShell 192
	States
	{
	Spawn:
		ATR4 A 0
		Goto SkillCheck
	Missile:
		"####" F 5 A_FaceTarget
		"####" F 0 A_PlaySound("astrochaingun/fire")
		"####" GGGGGGGG 1 BRIGHT LIGHT(ASTROFIRE) A_CustomMissile("EnemyAstroTracer",48,1,random(-8,8))
		"####" F 5 A_MonsterRefire(30,"See")
		Goto Missile+1
	}
}

ACTOR AstroDrone
{
	//$Category Monsters (Wolf3D)/Astrostein
	//$Title Astrostein Drone
	//$Color 4
	Health 200
	Radius 31
	Height 56
	Mass 400
	Speed 2.66666667
	PainChance 128
	Monster
	+FLOAT
	+NOGRAVITY
	+NOBLOOD
	SeeSound "drone_sight"
	PainSound "drone_pain"
	DeathSound "astrostein/explosion"
	ActiveSound "drone_act"
	//Obituary "$OB_CACO"
	//HitObituary "$OB_CACOHIT"
	States
	{
	Spawn:
		DRON A 10 A_Look
		Loop
	See:
		DRON A 1 A_Chase
		DRON AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		DRON B 1 A_Chase
		DRON BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		Loop
	Missile:
		DRON EG 5 A_FaceTarget
		DRON E 5 Bright A_CustomMissile("AstroDroneBall",54,1,random(-8,8))
		Goto See
	Pain:
		DRON H 3
		DRON H 3 A_Pain
		DRON H 6
		Goto See
	Death:
		DRON M 0 A_Scream
		DRON M 0 A_SpawnItemEx("AstrosteinExplosion_Medium", 0, 0, 48)
		DRON M 0 A_NoBlocking
		DRON M -1 A_SetFloorClip
		Stop
	}
}

ACTOR AstroDroneBall
{
	Radius 6
	Height 8
	Speed 10
	FastSpeed 20
	Damage 5
	Projectile
	+RANDOMIZE
	RenderStyle "Add"
	Scale 0.5
	SeeSound "astrochaingun/fire"
	DeathSound "astroplasmaexplode"
	States
	{
	Spawn:
		ROBP AB 4 Bright A_SpawnItemEx("Spark_G1", 0, 0, 0, random(1,2), random(1,2), random(1,2), random(1,360), SXF_CLIENTSIDE)
		Loop
	Death:
		TNT1 AAAAA 0 A_SpawnItemEx("Spark_G1", 0, 0, 0, random(1,2), random(1,2), random(1,2), random(1,360), SXF_CLIENTSIDE)
    		TNT1 A 0 A_SpawnItemEx("SparkFlare_G1")
		ROBP CDEG 6 Bright
	Stop
	}
}

ACTOR AstroRobotSeekerBall : AstroDroneBall
{
	Radius 12
	Height 16
	+SEEKERMISSILE
	States
	{
	Spawn:
		ROBP AB 4 Bright A_Tracer2
		Loop
	}
}

ACTOR AstroCyborg1
{
	//$Category Monsters (Wolf3D)/Astrostein
	//$Title Astrostein Cyborg (1)
	//$Color 4
	Health 500
	Radius 31
	Height 56
	Mass 400
	Speed 2.66666667
	Scale 1.5
	PainChance 32
	+NOBLOOD
	DropItem AstroRocketAmmo 64
	Monster
	SeeSound "astrostein/cyborg_sight"
	PainSound "astrostein/cyborg_pain"
	DeathSound "astrostein/cyborg_death"
	ActiveSound "astrostein/cyborg_act"
	//Obituary "$OB_CACO"
	States
	{
	Spawn:
		CYB1 J 10 A_Look
		Loop
	See:
		"####" A 1 A_Chase
		"####" AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" A 1 A_Chase
		"####" AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" A 1 A_Chase
		"####" AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" B 1 A_Chase
		"####" BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" B 1 A_Chase
		"####" BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" B 1 A_Chase
		"####" BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" C 1 A_Chase
		"####" CC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" C 1 A_Chase
		"####" CC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" C 1 A_Chase
		"####" CC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" D 1 A_Chase
		"####" DD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" D 1 A_Chase
		"####" DD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" D 1 A_Chase
		"####" DD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		Loop
	Missile:
		CYB1 G 10 A_FaceTarget
		CYB1 H 0 A_CustomMissile("AstroRocket",54,-32,random(-8,8))
		CYB1 H 2 Bright A_CustomMissile("AstroRocket",54, 32,random(-8,8))
		Goto See
	Pain:
		CYB1 I 3
		CYB1 I 3 A_Pain
		CYB1 I 6
		Goto See
	Death:
		CYB1 J 8 A_Scream
		CYB1 J 0 A_NoBlocking
		CYB1 J 0 A_SpawnItemEx("BaseLine", random(32, -32), random(32, -32), random(0, 24), 0, 0, random(1,3), 0, 129, 0)
		CYB1 J 1 A_FadeOut(0.05)
		Goto Death+2
	}
}

ACTOR AstroCyborg2 : AstroCyborg1
{
	//$Category Monsters (Wolf3D)/Astrostein
	//$Title Astrostein Cyborg (2)
	//$Color 4
	Species "AstroCyborg2"
	//Obituary "$OB_CACO"
	DropItem "" //AstroRocketAmmo 64
	States
	{
	Spawn:
		CYB2 H 10 A_Look
		Loop
	See:
		CYB2 A 1 A_Chase
		CYB2 AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		CYB2 A 1 A_Chase
		CYB2 AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		CYB2 A 1 A_Chase
		CYB2 AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		CYB2 B 1 A_Chase
		CYB2 BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		CYB2 B 1 A_Chase
		CYB2 BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		CYB2 B 1 A_Chase
		CYB2 BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		CYB2 B 1 A_Chase
		CYB2 BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		CYB2 C 1 A_Chase
		CYB2 CC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		CYB2 C 1 A_Chase
		CYB2 CC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		CYB2 C 1 A_Chase
		CYB2 CC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		CYB2 C 1 A_Chase
		CYB2 CC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		CYB2 D 1 A_Chase
		CYB2 DD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		CYB2 D 1 A_Chase
		CYB2 DD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		CYB2 D 1 A_Chase
		CYB2 DD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		CYB2 D 1 A_Chase
		CYB2 DD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		Loop
	Missile:
		CYB2 E 10 A_FaceTarget
		CYB2 F 0 A_CustomMissile("AstroDroneBall",54,-32,random(-16,16), CMF_AIMDIRECTION, random(-8,8))
		CYB2 F 3 Bright A_CustomMissile("AstroDroneBall",54, 32,random(-16,16), CMF_AIMDIRECTION, random(-8,8))
		CYB2 E 3 A_FaceTarget
		CYB2 F 0 A_CustomMissile("AstroDroneBall",54,-32,random(-16,16), CMF_AIMDIRECTION, random(-8,8))
		CYB2 F 3 Bright A_CustomMissile("AstroDroneBall",54, 32,random(-16,16), CMF_AIMDIRECTION, random(-8,8))
		CYB2 E 3 A_FaceTarget
		CYB2 F 0 A_CustomMissile("AstroDroneBall",54,-32,random(-16,16), CMF_AIMDIRECTION, random(-8,8))
		CYB2 F 3 Bright A_CustomMissile("AstroDroneBall",54, 32,random(-16,16), CMF_AIMDIRECTION, random(-8,8))
		CYB2 E 3 A_FaceTarget
		CYB2 F 0 A_CustomMissile("AstroDroneBall",54,-32,random(-16,16), CMF_AIMDIRECTION, random(-8,8))
		CYB2 F 3 Bright A_CustomMissile("AstroDroneBall",54, 32,random(-16,16), CMF_AIMDIRECTION, random(-8,8))
		CYB2 E 3 A_FaceTarget
		CYB2 F 0 A_CustomMissile("AstroDroneBall",54,-32,random(-16,16), CMF_AIMDIRECTION, random(-8,8))
		CYB2 F 3 Bright A_CustomMissile("AstroDroneBall",54, 32,random(-16,16), CMF_AIMDIRECTION, random(-8,8))
		CYB2 E 3 A_FaceTarget
		CYB2 F 0 A_CustomMissile("AstroDroneBall",54,-32,random(-16,16), CMF_AIMDIRECTION, random(-8,8))
		CYB2 F 3 Bright A_CustomMissile("AstroDroneBall",54, 32,random(-16,16), CMF_AIMDIRECTION, random(-8,8))
		CYB2 E 3 A_FaceTarget
		Goto See
	Pain:
		CYB2 G 3
		CYB2 G 3 A_Pain
		CYB2 G 6
		Goto See
	Death:
		CYB2 H 8 A_Scream
		CYB2 H 0 A_NoBlocking
		CYB2 H 0 A_SpawnItemEx("BaseLine", random(32, -32), random(32, -32), random(0, 24), 0, 0, random(1,3), 0, 129, 0)
		CYB2 H 1 A_FadeOut(0.05)
		Goto Death+2
	}
}

Actor AstroSuiteGuard : AstroGuard
{
	//$Category Monsters (Wolf3D)/Astrostein
	//$Title Astrostein Suite Guard
	//$Color 4
	Speed 2
	Scale 1.5
	Health 200
	Painchance 16
	States
	{
	Spawn:
		ROB1 A 0
		Goto SkillCheck
	Missile:
		"####" EF 5 A_FaceTarget
		"####" F 0 A_PlaySound("astrochaingun/fire")
		"####" G 0 A_CustomMissile("EnemyAstroTracer",48,-10,random(-8,8))
		"####" G 0 A_CustomMissile("EnemyAstroTracer",40,-10,random(-8,8))
		"####" G 0 A_CustomMissile("EnemyAstroTracer",40,10,random(-8,8))
		"####" G 8 BRIGHT LIGHT(ASTROFIRE) A_CustomMissile("EnemyAstroTracer",40,10,random(-8,8))
		"####" E 8 A_Jump(256,"See")
		Stop
	Pain:
		"####" H 3
		"####" H 3 A_Pain
		"####" H 0 A_Jump(256,"See")
		Stop
	Death:
		"####" I 0 A_Playsound("astrostein/guard_death")
		"####" I 0 A_Scream
		"####" IIIIIJJJJJKKKKKLLLLLMMMMMNNNNNOOOOO 1 A_SpawnItemEx("BaseLine", random(16, -16), random(16, -16), random(0, 8), 0, 0, random(1,3), 0, 129, 0)
		"####" O -1 A_NoBlocking
		Stop
	}
}

ACTOR AstroRobot : AstroCyborg1
{
	//$Category Monsters (Wolf3D)/Astrostein
	//$Title Astrostein Robot
	//$Color 4
	Species "AstroRobot"
	Health 750
	Scale 1
	SeeSound "astrostein/robot_sight"
	PainSound "astrostein/robot_pain"
	DeathSound "astrostein/robot_death"
	ActiveSound "astrostein/robot_act"
	//Obituary "$OB_CACO"
	DropItem AstroRocketAmmo 160
	States
	{
	Spawn:
		ROB2 A 10 A_Look
		Loop
	Missile:
		ROB2 HG 5 A_FaceTarget
		ROB2 F 0 A_CustomMissile("AstroRobotSeekerBall",72,-36,random(-8,8))
		ROB2 F 5 Bright A_CustomMissile("AstroRobotSeekerBall",72, 36,random(-8,8))
		Goto See
	Pain:
		ROB2 H 3
		ROB2 H 3 A_Pain
		ROB2 H 6
		Goto See
	Death:
		ROB2 H 4 A_Scream
		ROB2 IJKLMN 10
		ROB2 O -1 A_NoBlocking
		Wait
	}
}

ACTOR AstroHitler : ExplosiveBarrel
{
	//$Category Monsters (Wolf3D)/Astrostein
	//$Title Hitler Cyborg
	//$Color 4
	Health 400
	Mass 99999999
	States
	{
	Spawn:
		ADHI ABC 6
		Loop
	Death:
		ADHI E 35 Bright A_Scream
		TNT1 A 0 A_PlaySound("astrostein/explosion",0,1.0,0,0.1)
		TNT1 A 0 A_SpawnItemEx("AstrosteinExplosion_Small")
		ADHI E 25 Bright
		TNT1 A 0 A_PlaySound("astrostein/explosion",0,1.0,0,0.1)
		TNT1 A 0 A_SpawnItemEx("AstrosteinExplosion_Medium")
		ADHI E 15 Bright
		TNT1 A 0 A_PlaySound("astrostein/explosion",0,1.0,0,0.1)
		TNT1 A 0 A_SpawnItemEx("AstrosteinExplosion_Medium")
		ADHI E 5 Bright
		TNT1 A 0 A_PlaySound("astrostein/explosion",0,1.0,0,0.1)
		TNT1 A 0 A_SpawnItemEx("AstrosteinExplosion_Large")
		ADHI FGHIJ 20 Bright
		ADHI J -1
		Stop
	}
}

ACTOR AstroScientist : AstroGuard
{
	//$Category Monsters (Wolf3D)/Astrostein//$Category Monsters (Wolf3D)
	//$Title Astro Scientist
	//$Color 4
	States
	{
	Spawn:
		SCN2 N 0
		Goto SkillCheck
	}
}

/////////////////////////////
// ASTROSTEIN DEATH EFFECT //
/////////////////////////////
Actor BaseLine
{
	+Missile
	+NoGravity
	+NoBlockmap
	+NoInteraction
	Radius 0
	Height 0
	RenderStyle Add
	Alpha 0.01
	States
	{
	Spawn:
		SPFX AAAAA 1 Bright Light("AstrosteinDeathLight") A_FadeIn(0.2)
		SPFX A 1 Bright Light("AstrosteinDeathLight") A_FadeOut(0.06)
		Wait
	}
}

/////////////////////////////
// TELEPORTFOG REPLACEMENT //
/////////////////////////////

Actor TeleportFog_Astro : TeleportFog replaces TeleportFog
{
	States
	{
	Spawn:
		TNT1 AAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("BaseLine", random(16, -16), random(16, -16), random(0, 16), 0, 0, random(1,3), 0, 129, 0)
		Stop
	}
}