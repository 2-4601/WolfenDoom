//OzyTanks here...
ACTOR TankSmoke : PuffSmoke //connections with GuardSoul smoke
{
Radius 1
Height 1
RenderStyle Translucent
Scale 5
Speed 3
Gravity 0
+MISSILE
+NOBLOCKMAP
+NOTELEPORT
+DONTSPLASH
+MISSILEMORE
+MISSILEEVENMORE
}

actor Boom
{
Radius 11
Height 8
Speed 8
Damage 0
Projectile
Renderstyle Translucent
Scale 0.6
Alpha 0.7
+MISSILE
+NOINTERACTION
States
	{
	Spawn:
	FBLX ABCDEFGHIJ 4 Bright A_FadeOut(0.005)
	stop
	}
}
//Some Sergeant_Mark_IV ArmageDoom stuff here + slight modifications of mine
ACTOR TankExplosion
{
Radius 8
Height 8
Speed 5
Mass 6
Scale 1.5
BounceFactor 0
+MISSILE
+DOOMBOUNCE
+NOBLOCKMAP
+NOGRAVITY
+NOTELEPORT
+DONTSPLASH
+MOVEWITHSECTOR
+CLIENTSIDEONLY
+FORCEXYBILLBOARD
Renderstyle Add
States
	{
	Spawn:
	EXPL A 0 Radius_Quake(4, 8, 0, 20, 0)
	TNT1 AAAA 0 A_CustomMissile("TankSmoke", 15, 0, random(0, 360), 2, random(0, 360))
	EXPL A 0 A_Explode(100, 150)
	BEXP DEFG 3 BRIGHT
	TNT1 AAAA 0 A_CustomMissile("TankSmoke", 30, 0, random(0, 360), 2, random(70, 130))
	BEXP HIJKLM 3 BRIGHT
	TNT1 AA 0 A_CustomMissile("TankSmoke", 40, 0, random(0, 360), 2, random(70, 130))
	BEXP NOPQ 3 BRIGHT
	TNT1 AA 0 A_CustomMissile("TankSmoke", 40, 0, random(0, 360), 2, random(70, 130))
	TNT1 A 0
	Stop
	}
}

ACTOR TankExplosion2: TankExplosion
{
Radius 8
Height 8
Speed 5
Mass 6
Scale 1.5
BounceFactor 0
+MISSILE
+NOGRAVITY
+DOOMBOUNCE
+NOBLOCKMAP
+NOTELEPORT
+DONTSPLASH
+MOVEWITHSECTOR
+CLIENTSIDEONLY
+FORCEXYBILLBOARD
Renderstyle Add
States
	{
	Spawn:
	EXPL A 0 Radius_Quake(4, 8, 0, 20, 0)
	TNT1 AAAA 0 A_CustomMissile("TankSmoke", 15, 0, random(0, 360), 2, random(0, 360))
	EXPL A 0 A_Explode(100, 150)
	FBLX A 0 Bright A_CustomMissile("Boom",56,0,0,2,0)
	FBLX A 0 Bright A_CustomMissile("Boom",56,0,20,2,0)
	FBLX A 0 Bright A_CustomMissile("Boom",56,0,40,2,0)
	FBLX A 0 Bright A_CustomMissile("Boom",56,0,60,2,0)
	FBLX A 0 Bright A_CustomMissile("Boom",56,0,80,2,0)
	FBLX A 0 Bright A_CustomMissile("Boom",56,0,100,2,0)
	FBLX A 0 Bright A_CustomMissile("Boom",56,0,120,2,0)
	FBLX A 0 Bright A_CustomMissile("Boom",56,0,140,2,0)
	FBLX A 0 Bright A_CustomMissile("Boom",56,0,160,2,0)
	FBLX A 0 Bright A_CustomMissile("Boom",56,0,180,2,0)
	FBLX A 0 Bright A_CustomMissile("Boom",56,0,200,2,0)
	FBLX A 0 Bright A_CustomMissile("Boom",56,0,220,2,0)
	FBLX A 0 Bright A_CustomMissile("Boom",56,0,240,2,0)
	FBLX A 0 Bright A_CustomMissile("Boom",56,0,260,2,0)
	FBLX A 0 Bright A_CustomMissile("Boom",56,0,280,2,0)
	FBLX A 0 Bright A_CustomMissile("Boom",56,0,300,2,0)
	FBLX A 0 Bright A_CustomMissile("Boom",56,0,320,2,0)
	FBLX A 0 Bright A_CustomMissile("Boom",56,0,340,2,0)
	EXPL A 4 Bright A_Mushroom("Boom", random(8,16))
	BFE1 ABCDEFG 3 BRIGHT
	TNT1 AAAA 0 A_CustomMissile("TankSmoke", 30, 0, random(0, 360), 2, random(70, 130))
	BFE1 HIJKL 3 BRIGHT
	Stop
	}
}

ACTOR TankPieces
{
Radius 5
Height 5
Speed 8
Mass 1
YScale 0.8
XScale 0.8
BounceFactor 0.5
+MISSILE
+DOOMBOUNCE
+NOBLOCKMAP
+NOTELEPORT
+BOUNCEONWALLS
+BOUNCEONACTORS
+CLIENTSIDEONLY
+BOUNCEONCEILINGS
States
	{
	Spawn:
	BRKP AB 2 A_SetScale(ScaleX+frandom(-0.1, 0.15), ScaleY+frandom(-0.1, 0.1))
	Loop
	Death:
	TNT1 A 0 A_Jump(90,2)
	BRKP A -1
	Stop
	BRKP B -1
	Stop
	}
}

ACTOR TankPieces2 : TankPieces
{
YScale 0.7
XScale 0.7
Speed 6
Mass 1
BounceFactor 0.7
States
	{
	Spawn:
	DBR1 ABCD 2 A_SetScale(ScaleX+frandom(-0.1, 0.15), ScaleY+frandom(-0.1, 0.1))
	Loop
	Death:
	TNT1 A 0 A_Jump(128,2)
	DBR1 A -1
	Stop
	TNT1 A 0 A_Jump(136,2)
	DBR1 B -1
	Stop
	TNT1 A 0 A_Jump(144,2)
	DBR1 C -1
	Stop
	DBR1 D -1
	Stop
	}
}

ACTOR TankGlassShards : TankPieces
{
Speed 7
BounceFactor 0.75
RENDERSTYLE ADD
Alpha 0.6
States
	{
	Spawn:
	PBIT ABCDEFGHIJ 1
	Loop
	Death:
	TNT1 A 0 A_Jump(90,2)
	PBIT G -1
	Stop
	TNT1 A 0 A_Jump(90,2)
	PBIT H -1
	Stop
	PBIT J -1
	Stop
	}
}
//End MarkIV///////////////////////////////////////////////
ACTOR SSTank1 11116
{
//$Category Monsters (Wolf3D)
//$Title Tank 1
//$Color 4
Game Doom
Health 3500
Radius 48
Height 96
Mass 0x7ffffff
Speed 8
MinMissileChance 160
+NOBLOODDECALS
+MISSILEMORE
+NORADIUSDMG
+RANDOMIZE
+FLOORCLIP
+DONTMORPH
+BOSSDEATH
+PAINLESS
+NOBLOOD
+SOLID
Monster
Obituary "%o was disintegrated by a Tank missile."
DamageFactor "Enemy", 0.0
MaxStepHeight 32
States
	{
	Spawn:
	PTN2 A 10 A_Look
	Loop
	See:
	TNT1 A 0 A_PlaySoundEx("tnk1loop", "SoundSlot7", 1)
	PTN2 A 3 A_Hoof
	PTN2 A 3 A_Chase
	PTN2 A 3 A_Metal
	PTN2 A 3 A_Chase
	Loop
	Missile:
	TNT1 A 0 A_PlaySoundEx("tnk1loop", "SoundSlot7", 1)
	PTN2 A 10 A_FaceTarget
	PTN2 A 0 A_Jump(96,"Missile2")
	PTN2 A 0 A_Jump(32,"Missile3")
	PTN2 A 12 A_CustomMissile("PanzerRocket",64,0)
	Goto See
	Missile2: //so it becomes a bit challengin'
	TNT1 A 0 A_PlaySoundEx("tnk1loop", "SoundSlot7", 1)
	PTN2 A 12 A_CustomMissile("PanzerRocket",64,0)
	PTN2 A 10 A_FaceTarget
	PTN2 A 20 A_CustomMissile("PanzerRocket",64,0)
	PTN2 A 10 A_FaceTarget
	Goto See
	Missile3: //so it becomes mooore challengin'
	TNT1 A 0 A_PlaySoundEx("tnk1loop", "SoundSlot7", 1)
	PTN2 A 12 A_CustomMissile("PanzerRocket",64,0)
	PTN2 A 10 A_FaceTarget
	PTN2 A 20 A_CustomMissile("PanzerRocket",64,0)
	PTN2 A 10 A_FaceTarget
	PTN2 A 28 A_CustomMissile("PanzerRocket",64,0)
	PTN2 A 10 A_FaceTarget
	Goto See
	Death:
	TNT1 A 0 A_StopSoundEx("SoundSlot7")
	TNT1 A 0 A_SpawnItem("TankExplosion2")
	TNT1 A 0 A_PlaySound("weapons/explode")
	TNT1 A 0 A_Scream
	TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("TankPieces", random(96,128), 0, random(0, 360), 2, random(0, 360))
	TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("TankGlassShards", random(96,128), 0, random(0, 360), 2, random(0, 360))
	WRK1 A 8 A_SpawnItemEx("GeneralExplosion_Large", 56, 0, 32)
	TNT1 A 0 A_SpawnItem("FuckedSSTank1")
	TNT1 A 20
	Stop
	}
}

ACTOR FuckedSSTank1 11117
{
//$Category Gore (Wolf3D)
//$Title Tank 1 (destroyed)
//$Color 4
Game Doom
+NOBLOODDECALS
+INVULNERABLE
+SHOOTABLE
+FLOORCLIP
-PUSHABLE
+NOBLOOD
+SOLID
Health -1
Height 48
Radius 48
Mass 0x7ffffff
States
	{
	Spawn:
	WRK1 A 20 A_CustomMissile("TankSmoke", 56, 0, random(0, 360), 2, random(70, 130))
	Loop
	}
}

ACTOR SSTank2 : SSTank1 11118
{
//$Category Monsters (Wolf3D)
//$Title Tank 2
//$Color 4
Health 4000
Speed 7
States
	{
	Spawn:
	PTN3 A 10 A_Look
	Loop
	See:
	TNT1 A 0 A_PlaySoundEx("tnk2loop", "SoundSlot7", 1)
	PTN3 A 3 A_Hoof
	PTN3 A 3 A_Chase
	PTN3 A 3 A_Metal
	PTN3 A 3 A_Chase
	Loop
	Missile:
	TNT1 A 0 A_PlaySoundEx("tnk2loop", "SoundSlot7", 1)
	PTN3 A 10 A_FaceTarget
	PTN3 A 0 A_Jump(128,"Turret")
	PTN3 A 12 A_CustomMissile("PanzerRocket",90,0)
	Goto See
	Turret:
	TNT1 A 0 A_PlaySoundEx("tnk2loop", "SoundSlot7", 1)
	PTN3 A 0 A_FaceTarget
	PTN3 A 0 BRIGHT A_PlayWeaponSound("weapons/chaingun")
	PTN3 AAAA 1 BRIGHT light("SUPAFIRE") A_CustomMissile("EnemyChaingunTracer",60,16,random(-4,4))
	PTN3 A 2 BRIGHT A_SentinelRefire
	goto Turret+1
	Death:
	TNT1 A 0 A_StopSoundEx("SoundSlot7")
	TNT1 A 0 A_SpawnItem("TankExplosion2")
	TNT1 A 0 A_PlaySound("weapons/explode")
	TNT1 A 0 A_Scream
	TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("TankPieces2", random(96,128), 0, random(0, 360), 2, random(0, 360))
	TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("TankGlassShards", random(96,128), 0, random(0, 360), 2, random(0, 360))
	WRK3 A 8 A_SpawnItemEx("GeneralExplosion_Large", 56, 0, 32)
	TNT1 A 0 A_SpawnItem("FuckedSSTank2")
	TNT1 A 20
	Stop
	}
}

ACTOR FuckedSSTank2 : FuckedSSTank1 11119
{
//$Category Gore (Wolf3D)
//$Title Tank 2 (destroyed)
//$Color 4
States
	{
	Spawn:
	WRK3 A 20 A_CustomMissile("TankSmoke", 58, 0, random(0, 360), 2, random(70, 130))
	Loop
	}
}