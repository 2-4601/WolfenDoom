//OzyTanks defs here...
ACTOR TankSmoke : PuffSmoke //connections with GuardSoul smoke
{
	Radius 1
	Height 1
	Scale 5
	Speed 3
	Projectile
	-DROPOFF
	-ACTIVATEIMPACT
	-ACTIVATEPCROSS
	+DONTSPLASH
}

//Ka-Boom from Russian Overkill by PillowBlaster - Tweaked by Ozymandias81
ACTOR KaBoom
{
	Scale 2
	Renderstyle Add
	PROJECTILE
	+NOINTERACTION
	States
	{
	Spawn:
		BFE1 ABCDE 2 Bright
		BFE1 FGHI 2 Bright A_Explode(75,128)
		BFE1 JKL 2 Bright
		Stop
	}
}

ACTOR KaBoomer
{
	+NOGRAVITY
	+NOINTERACTION
	States
	{
	Spawn:
		TNT1 AA 1 A_SpawnItemEx("KaBoom",0,0,0,random(-4,4),random(-4,4),random(-2,6),random(0,359),SXF_TRANSFERTRANSLATION|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION,40)
		Stop
	}
}

Actor Nuke
{
	+NoBlockmap
	+NoGravity
	+NoInteraction
	Radius 0
	Height 0
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_PlaySound("panzer/explode",CHAN_VOICE,1,0,ATTN_NONE)
		TNT1 A 0 Radius_Quake(7,30,0,96,0)
		TNT1 A 0 A_SpawnItemEx("TankBoom",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_SpawnItemEx("TankFlare")
		TNT1 A 25 A_SpawnItemEx("NukeFloor")
		TNT1 AA 0 A_SpawnItemEx("NukeSmokeFloor")
		TNT1 A 0 A_SpawnItemEx("TankPillar",0,0,0,0,0,2)
		TNT1 A 45 A_SpawnItemEx("TankSmokePillar",0,0,0,0,0,2)
		TNT1 A 0 A_SpawnItemEx("TankMushroom",0,0,56)
		TNT1 AA 0 A_SpawnItemEx("TankSmokeMushroom",0,0,56)
		Stop
	}
}

Actor TankBoom
{
	+NoBlockmap
	+NoGravity
	+ForceRadiusDmg
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 AAAAA 2 A_Explode(10,128,0)
		Stop
	}
}

Actor TankFire : Nuke
{
	RenderStyle Add
	+NoInteraction
	+DONTSPLASH
	+EXPLODEONWATER
	Scale 0.5
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(256,1,2,3,4)
		FLA1 A 2 Bright A_FadeOut(0.03)
		Wait
		FLA2 A 2 Bright A_FadeOut(0.02)
		Wait
		FLA3 A 2 Bright A_FadeOut(0.03)
		Wait
		FLA4 A 2 Bright A_FadeOut(0.02)
		Wait
	}
}

Actor NukeSmoke : Nuke
{
	Scale 1.7
	+NoInteraction
	+DONTSPLASH
	+EXPLODEONWATER
	Alpha 0.5
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(256,1,2,3,4)
		NSMK ABCD 0 A_Jump(256,"End")
	End:
		NSMK "#" 2 A_FadeOut(0.008)
		Wait
	}
}

Actor TankFlare : TankFire
{
	Scale 0.8
	+NoInteraction
	States
	{
	Spawn:
		FLAR B 1 Bright A_FadeOut(0.011)
		Wait
	}
}

Actor NukeFloor : Nuke
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("TankFire",0,0,0,0.1 * Random(0,30),0.1 * Random(0,30),0,Random(0,359),128)
		Stop
	}
}

Actor NukeSmokeFloor : Nuke
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("NukeSmoke",0,0,0,0.1 * Random(0,30),0.1 * Random(0,30),0,Random(0,359),128)
		Stop
	}
}

Actor TankPillar : Nuke
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("TankFire",0,0,0,0.1 * Random(0,10),0.1 * Random(0,10),Random(0,-3),Random(0,359),128)
		TNT1 A 0 A_SpawnItemEx("TankSmokeRing")
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("TankFire",0,0,0,0.1 * Random(0,10),0.1 * Random(0,10),Random(0,-3),Random(0,359),128)
		Stop
	}
}

Actor TankSmokePillar : Nuke
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("NukeSmoke",0,0,0,0.1 * Random(0,10),0.1 * Random(0,10),0.1 * Random(0,-40),Random(0,359),128)
		Stop
	}
}

Actor TankMushroom : Nuke
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("TankFire",0,0,0,0.1 * Random(0,25),0.01 * Random(0,25),0.1 * Random(-10,10),Random(0,359),128)
		Stop
	}
}

Actor TankSmokeMushroom : Nuke
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("NukeSmoke",0,0,0,0.1 * Random(0,25),0.1 * Random(0,25),0.1 * Random(-10,10),Random(0,359),128)
		Stop
	}
}

Actor TankSmokeRing : Nuke
{
	var int user_theta;
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SpawnItemEx("NukeSmoke",0,0,0,1,0,0,user_theta)
		UWMR A 0 A_SetUserVar("user_theta",user_theta+20)
		UWMR A 0 A_JumpIf(user_theta==360,1)
		Loop
		TNT1 A 0
		Stop
	}
}

//Some Sergeant_Mark_IV ArmageDoom stuff here + slight modifications of mine
ACTOR TankPieces
{
	Radius 5
	Height 5
	Speed 8
	Mass 1
	Scale 0.8
	BounceFactor 0.5
	+MISSILE
	BounceType Doom
	+NOBLOCKMAP
	+NOTELEPORT
	+BOUNCEONWALLS
	+BOUNCEONACTORS
	+CLIENTSIDEONLY
	+BOUNCEONCEILINGS
	States
	{
	Spawn:
		BRKP AB 2 A_SetScale(ScaleX+frandom(-0.1,0.15),ScaleY+frandom(-0.1,0.1))
		Loop
	Death:
		"####" "#" -1
		Stop
	}
}

ACTOR TankPieces2 : TankPieces
{
	Scale 0.7
	Speed 6
	Mass 1
	BounceFactor 0.7
	States
	{
	Spawn:
		DBR1 ABCD 2 A_SetScale(ScaleX+frandom(-0.1,0.15),ScaleY+frandom(-0.1,0.1))
		Loop
	}
}

ACTOR TrashPieces : TankPieces //this is here to avoid warnings
{
	Speed 4
	BounceFactor 0.3
	States
	{
	Spawn:
		SRCB BCD 2
		Loop
	}
}

ACTOR TankGlassShards : TankPieces
{
	Speed 7
	BounceFactor 0.75
	RENDERSTYLE ADD
	Alpha 0.6
	States
	{
	Spawn:
		PBIT ABCDEFGHIJ 1
		Loop
	Death:
		TNT1 A 0 A_Jump(256,1,2,3)
		PBIT GHJ 0 A_Jump(256,"End")
	End:
		PBIT "#" -1
		Stop
	}
}
//End MarkIV///////////////////////////////////////////////
ACTOR SSTank1
{
	//$Category Monsters (Wolf3D)
	//$Title Tank 1 (3500 HP)
	//$Color 4
	Health 3500
	Radius 48
	Height 96
	Mass 0x7ffffff
	Speed 2.66666667
	MinMissileChance 160
	+BOSS
	Tag "Tank"
	+NOBLOODDECALS
	+MISSILEMORE
	+NORADIUSDMG
	+RANDOMIZE
	+FLOORCLIP
	+DONTMORPH
	+BOSSDEATH
	+PAINLESS
	+NOBLOOD
	+SOLID
	Monster
	Obituary "%o was disintegrated by a tank shell."
	Species Nazi
	MaxStepHeight 32
	States
	{
	Spawn:
		TNK4 A 10 A_Look
		Loop
	See:
		"####" A 0 A_PlaySound("TKMOVE",7,1,1)
		"####" A 1 A_Chase
		Loop
	Missile:
		TNK4 A 10 A_FaceTarget
		TNK4 A 0 A_Jump(128,"Turret")
		TNK4 A 0 A_Jump(96,"Missile2")
		TNK4 BCDEFG 2 A_CustomMissile("PanzerRocket",64)
		Goto See
	Turret:
		TNK4 A 0 A_FaceTarget
		TNK4 H 0 A_PlaySound("weapons/chaingun",CHAN_WEAPON)
		TNK4 HHHH 1 BRIGHT light("SUPAFIRE") A_CustomMissile("EnemyChaingunTracer",98,-12,random(-4,4))
		TNK4 A 2 BRIGHT A_MonsterRefire(30,"See")
		Loop
	Missile2:
		TNK4 BCDEFG 2 A_CustomMissile("PanzerRocket",64)
		TNK4 A 10 A_FaceTarget
		TNK4 BCDEFG 4 A_CustomMissile("PanzerRocket",64)
		TNK4 A 10 A_FaceTarget
		Goto See
	Death:
		TNT1 A 0 A_StopSound(7)
		TNT1 A 0 A_PlaySound("weapons/explode")
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("TankPieces",random(96,128),0,random(0,360),2,random(0,360))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("TankGlassShards",random(96,128),0,random(0,360),2,random(0,360))
		TNT1 A 0 A_SpawnItemEx("Nuke",0,0,5,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION)
		TNT1 A 1 A_SpawnItemEx("KaBoomer",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
		WRK4 A 8 A_SpawnItemEx("GeneralExplosion_Large",56,0,32)
		TNT1 A 20 A_SpawnItemEx("FuckedSSTank1")
		Stop
	}
}

ACTOR FuckedSSTank1
{
	//$Category Gore (Wolf3D)
	//$Title Tank 1 (destroyed)
	//$Color 4
	+NOBLOODDECALS
	+INVULNERABLE
	+SHOOTABLE
	+FLOORCLIP
	+NOBLOOD
	+SOLID
	Health -1
	Height 48
	Radius 48
	Mass 0x7ffffff
	States
	{
	Spawn:
		WRK4 A 20 A_CustomMissile("TankSmoke",56,0,random(0,360),2,random(70,130))
		Loop
	}
}

ACTOR SSTank2 : SSTank1
{
	//$Category Monsters (Wolf3D)
	//$Title Tank 2 (4000 HP)
	//$Color 4
	Health 4000
	Speed 2.33333333
	+BOSS
	Tag "Tank"
	States
	{
	Spawn:
		TNK1 A 10 A_Look
		Loop
	Missile:
		TNK1 A 10 A_FaceTarget
		TNK1 A 0 A_Jump(128,"Turret")
		TNK1 BCD 2 BRIGHT
		TNK1 E 2 BRIGHT A_CustomMissile("PanzerRocket",90)
		TNK1 FG 2
		Goto See
	Turret:
		TNK1 A 0 A_FaceTarget
		TNK1 H 0 A_PlaySound("weapons/chaingun",CHAN_WEAPON)
		TNK1 HHHH 1 BRIGHT light("SUPAFIRE") A_CustomMissile("EnemyChaingunTracer",60,16,random(-4,4))
		TNK1 A 2 BRIGHT A_SentinelRefire
		Loop
	Death:
		TNT1 A 0 A_StopSound(7)
		TNT1 A 0 A_PlaySound("weapons/explode")
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("TankPieces2",random(96,128),0,random(0,360),2,random(0,360))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("TankGlassShards",random(96,128),0,random(0,360),2,random(0,360))
		TNT1 A 0 A_SpawnItemEx("Nuke",0,0,5,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION)
		TNT1 A 1 A_SpawnItemEx("KaBoomer",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
		WRK1 A 8 A_SpawnItemEx("GeneralExplosion_Large",56,0,32)
		TNT1 A 20 A_SpawnItemEx("FuckedSSTank2")
		Stop
	}
}

ACTOR FuckedSSTank2 : FuckedSSTank1
{
	//$Category Gore (Wolf3D)
	//$Title Tank 2 (destroyed)
	//$Color 4
	States
	{
	Spawn:
		WRK1 A 20 A_CustomMissile("TankSmoke",58,0,random(0,360),2,random(70,130))
		Loop
	}
}

ACTOR SSTank3 : SSTank1
{
	//$Category Monsters (Wolf3D)
	//$Title Tank 3 (3000 HP)
	//$Color 4
	Health 3000
	Speed 2.55555556
	+BOSS
	Tag "Tank"
	States
	{
	Spawn:
		TNK3 A 10 A_Look
		Loop
	Missile:
		TNK3 A 10 A_FaceTarget
		TNK3 A 0 A_Jump(128,"Turret")
		TNK3 BCD 2 BRIGHT
		TNK3 E 2 BRIGHT A_CustomMissile("PanzerRocket",54)
		TNK3 FG 2
		Goto See
	Turret:
		TNK3 A 0 A_FaceTarget
		TNK3 H 0 A_PlaySound("weapons/chaingun",CHAN_WEAPON)
		TNK3 HHHH 1 BRIGHT light("SUPAFIRE") A_CustomMissile("EnemyChaingunTracer",98,-16,random(-4,4))
		TNK3 A 2 BRIGHT A_SentinelRefire
		Loop
	Death:
		TNT1 A 0 A_StopSound(7)
		TNT1 A 0 A_PlaySound("weapons/explode")
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("TankPieces2",random(96,128),0,random(0,360),2,random(0,360))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("TankGlassShards",random(96,128),0,random(0,360),2,random(0,360))
		TNT1 A 0 A_SpawnItemEx("Nuke",0,0,5,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION)
		TNT1 A 1 A_SpawnItemEx("KaBoomer",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
		WRK1 A 8 A_SpawnItemEx("GeneralExplosion_Large",56,0,32)
		TNT1 A 20 A_SpawnItemEx("FuckedSSTank3")
		Stop
	}
}

ACTOR FuckedSSTank3 : FuckedSSTank1
{
	//$Category Gore (Wolf3D)
	//$Title Tank 3 (destroyed)
	//$Color 4
	States
	{
	Spawn:
		WRK1 A 20 A_CustomMissile("TankSmoke",58,0,random(0,360),2,random(70,130))
		Loop
	}
}