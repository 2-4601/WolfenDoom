actor PrisonerAgent : Nazi 12710
{
//$Category Monsters (Wolf3D)
//$Title Prisoner
//$Color 4
  scale 1.2
  obituary "%o was somehow killed by a prisoner."
  health 99999999
  radius 6
  height 52
  mass 100
  speed 15
  MaxStepHeight 57 //This is equal to the maximum distance a Player can jump. Modify to allow no jumps and stuff.
  MaxDropOffHeight 16383
  MaxTargetRange 65536
  painchance 255
  painsound "Nazi1/Pain"
  deathsound "Nazi1/Death"
  activesound ""
  DamageFactor "Enemy", 0.0
  MONSTER
  +FLOORCLIP
  +NOTELEPORT 
  -QUICKTORETALIATE
  +NOBLOCKMONST
  +FIXMAPTHINGPOS
  +LOOKALLAROUND
  +PUSHABLE
  +SLIDESONWALLS
  +JUMPDOWN
  +NOTELESTOMP
  +NOTAUTOAIMED
  +NOFEAR
  -DONTSPLASH
  -NORADIUSDMG
  -CANNOTPUSH
  -COUNTKILL

  states
  {
  		SPAWN:
			TORT A 1  NODELAY A_LookEx(0, 0, 0, 0, 360)
			LOOP
		
		SEE:
			TORT A 1 A_FaceTarget
			GOTO FOLLOWPLAYER
			
		FOLLOWPLAYER:
			TNT1 A 0 A_ChangeFlag("SHOOTABLE", 1)
			TNT1 A 0 A_ChangeFlag("NOBLOCKMAP", 0)
			TNT1 A 0 A_ChangeFlag("NOCLIP", 0)
			TNT1 A 0 A_ChangeFlag("SOLID", 1)
			TNT1 A 0 A_JumpIfCloser(128, "STAND")
			TORT A 0 A_FaceTarget
			
			TORT B 3 A_Chase(0,0)
			TNT1 A 0 A_Recoil(-1)
			TNT1 A 0 A_JumpIfCloser(128, "STAND")
			TNT1 A 0 A_CheckSight("TELEPORTING") 
			
			TORT C 3 A_Chase(0,0)
			TNT1 A 0 A_Recoil(-1)
			TNT1 A 0 A_JumpIfCloser(128, "STAND")
			TNT1 A 0 A_CheckSight("TELEPORTING") 
			
			TORT D 3 A_Chase(0,0)
			TNT1 A 0 A_Recoil(-1)
			TNT1 A 0 A_JumpIfCloser(128, "STAND")
			TNT1 A 0 A_CheckSight("TELEPORTING") 

			TORT E 3 A_Chase(0,0)
			TNT1 A 0 A_Recoil(-1)
			TNT1 A 0 A_JumpIfCloser(128, "STAND")
			TNT1 A 0 A_CheckSight("TELEPORTING") 
			GOTO SEE

		STAND:
			TNT1 A 0 A_ClearTarget
			
			TNT1 A 0 A_Jump(1, "LOOKAROUND")
			
			TORT AA 1 A_LookEx(0, 0, 0, 0, 360, "SEE")
			TNT1 A 0 A_LookEx(0, 0, 0, 0, 360, "FOLLOWPLAYER")
			GOTO SEE
			
		LOOKAROUND:
			TNT1 A 0 A_SetAngle(angle + 45)
			TORT EE 4 A_Look
			TNT1 A 0 A_SetAngle(angle + 45)
			TORT EE 8 A_Look
			TNT1 A 0 A_SetAngle(angle - 45)
			TORT EE 4 A_Look
			TNT1 A 0 A_SetAngle(angle - 45)
			TORT EE 4 A_Look
			TORT EEEE 8 A_Look
			TORT E 1 A_LookEx(0, 0, 0, 0, 360, "FOLLOWPLAYER")
			GOTO SEE
			
		PAIN:
			TORT G 5 A_Pain
			GOTO SEE

		DEATH:
			TORT H 10 A_Noblocking
			TORT I 10 A_PlayerScream
			TNT1 A 0 A_Log("Helper down!")
			TNT1 A 0 A_PlaySound("misc/chat2", CHAN_AUTO, 1.0, false, ATTN_NONE)
			TORT J 10 A_ChangeFLag("SOLID", 0)
			TORT KLM 10
			TORT N 9 
			TFOG ABABABABABABABAB 32 BRIGHT //Wait for about 30 seconds.
			TFOG ABCABCABCABCABCA 32 BRIGHT //Every one of these lines is about 15 seconds, so add more or less at your leisure.
			TNT1 A 0 A_PlaySound("misc/chat2", CHAN_AUTO, 1.0, false, ATTN_NONE)
			TNT1 A 1 A_Log("Respawning!")
			GOTO TELEPORTING

		TELEPORTING:
			TNT1 A 0 A_ChangeFlag("SHOOTABLE", 0)
			TNT1 A 0 A_ChangeFlag("NOBLOCKMAP", 1)
			TNT1 A 0 A_ChangeFlag("NOCLIP", 1)
			TNT1 A 0 A_ChangeFlag("SOLID", 0)
			TNT1 A 0 A_Noblocking
			TNT1 A 0 A_Chase(0,0)
			TNT1 A 1 A_JumpIfCloser(48, "FOLLOWPLAYER")
		GOTO TELEPORTING+5
			
  }
}