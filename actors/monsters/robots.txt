ACTOR ProtoDrone : Nazi //ozy81
{
	//$Category Monsters (Wolf3D)
	//$Title Prototype Drone
	//$Color 4
	Scale 0.87
	Health 250
	Radius 32
	Mass 400
	MeleeRange 256
	MeleeThreshold 256 //forces the melee attack
	MaxTargetRange 896 //same of Arch-Vile, guess it's enough
	Speed 2
	PainChance 96 //it is a drone, so...
	-FLOORCLIP
	+FLOAT
	+LOOKALLAROUND
	+NOBLOOD
	+NOGRAVITY
	+NOINFIGHTING
	Obituary "$PDRONE"
	SeeSound "proto/see"
	PainSound "drone_pain"
	DeathSound "astrostein/explosion"
	ActiveSound "proto/see"
	States
	{
	Spawn:
		PROT A 0
		"####" "#" 0 A_SetUserVar("user_incombat", TRUE) //mxd
		Goto Look
	Look:
		PROT A 1 A_Look
		Loop
	See:
		PROT A 1 A_Chase
		"####" AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		Loop
	Idle:
		PROT AAA 1 A_Wander
		"####" A 0 A_Look
		Loop
	Melee:
		PROT B 0 A_Jump(128,"Melee2")
		"####" B 5 A_FaceTarget
		"####" B 0 A_PlaySound("astrochaingun/fire", CHAN_WEAPON)
		"####" CDE 8 LIGHT(PROTOFIRE) A_SpawnProjectile("EnemyProtoTracer",ScaleX*2,ScaleY*-16,random(-12,12))
		"####" B 5 A_FaceTarget
		"####" B 0 A_PlaySound("astrochaingun/fire", CHAN_WEAPON)
		"####" CDE 8 LIGHT(PROTOFIRE) A_SpawnProjectile("EnemyProtoTracer",ScaleX*2,ScaleY*-16,random(-12,12))
		Goto See
	Melee2:
		"####" N 5 A_FaceTarget
		"####" N 0 A_Jump(128,2)
		"####" N 1 ThrustThing(Angle*256/360-192, 8, 1, 0)
		"####" N 0 A_PlaySound("chaingun/fire", CHAN_WEAPON)
		"####" FGHI 2 LIGHT(NAZIFIRE) A_SpawnProjectile("EnemyPistolTracer",ScaleX*2,ScaleY*16,random(-24,24))
		"####" I 0 A_SpawnItemEx("9MMCasing", ScaleX*2, ScaleY*16, random(-2,2), RandomPick(-3, 3), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		"####" N 5 A_FaceTarget
		"####" N 0 A_Jump(128,2)
		"####" N 1 ThrustThing(Angle*256/360-192, 8, 1, 0)
		"####" N 0 A_PlaySound("chaingun/fire", CHAN_WEAPON)
		"####" FGHI 2 LIGHT(NAZIFIRE) A_SpawnProjectile("EnemyPistolTracer",ScaleX*2,ScaleY*16,random(-24,24))
		"####" I 0 A_SpawnItemEx("9MMCasing", ScaleX*2, ScaleY*16, random(-2,2), RandomPick(-3, 3), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		Goto See
	Missile:
		PROT N 10 A_FaceTarget
		"####" N 8 A_VileTarget("ProtoDrone_Redpointer")
		"####" NNNNN 14 A_FaceTarget
		"####" N 8 A_VileAttack("astroshotgun/fire", 10, 10, 70, 0.5)
		"####" N 0 A_CustomRailgun (0, 0, "none", "FF0000", RGF_NOPIERCING | RGF_FULLBRIGHT, 1, 32, "MeasurePuff", 0, 0, 0, 20, 0.5, 0.5, NONE, ScaleY*14, 270, 1, 2.5)
		Goto See
	Pain:
	Pain.Dagger:
		PROT N 3
		"####" O 3 A_Pain
		"####" N 6
		Goto See
	Death:
		PROT OPQRS 3
		"####" T 1 {A_Scream; A_SpawnItemEx("ProtoExplosion_Medium", 0, 0, 48); A_NoBlocking;}
		"####" TTTTTTTTTTTT 0 A_SpawnItemEx("Debris_MetalJunk", random(0,8), random(0,16), random(0,48), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		"####" TTTTTTTTTTTT 0 A_SpawnItemEx("Debris_MetalJunk", random(8,16), random(16,32), random(48,64), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		"####" TTTTTTTTTTTT 0 A_SpawnItemEx("Debris_MetalJunk", random(16,32), random(32,48), random(64,80), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		"####" U 3
		Stop
	}
}

ACTOR ProtoDrone_Redpointer
{
	+BRIGHT
	+NOBLOCKMAP
	+NOGRAVITY
	RenderStyle Add
	Alpha 0.1
	Scale 0.5
	States
	{
	Spawn:
		RLPR A 0 Bright NODELAY A_Playsound("proto/marker", CHAN_AUTO, 0.75)
	Marker:
		"####" A 3 { A_Fire; A_FadeIn(0.03, FTF_REMOVE);}
		Loop
	}
}

ACTOR TeslaTurret : NaziStandard
{
	//$Category Monsters (Wolf3D)/Defensive Devices
	//$Title Tesla Turret (Invulnerable)
	//$Color 4
	Radius 16
	Height 96
	Speed 0
	MaxTargetRange 640
	-COUNTKILL
	+DONTTHRUST
	+INVULNERABLE
	+LOOKALLAROUND
	+NOBLOOD
	+NOTAUTOAIMED //because it's invulnerable, some ammo saved
	Obituary "$TESLAT"
	SeeSound ""
	PainSound ""
	DeathSound ""
	Base.Shadow 0  //needed for shadows
	States
	{
	Spawn:
		TSLT A 1 A_NaziLook
		"####" A 0 A_PlaySound("Tesla/Loop", CHAN_ITEM, TRUE, ATTN_STATIC)
		Loop
	See:
		"####" A 1 A_NaziChase
		"####" A 0 A_PlaySound("Tesla/Loop", CHAN_ITEM, TRUE, ATTN_STATIC)
		Loop
	Missile:
		"####" A 4 A_FaceTarget
		"####" A 0 BRIGHT A_PlaySound("Tesla/Attack")
		"####" AAAAAA 4 {
			A_PlaySound("tesla/kill");
			A_SpawnProjectile("TPortLightningWaveSpawner", 54, 0, 0, CMF_AIMDIRECTION, 0);
			A_SpawnProjectile("TPortLightningWaveSpawner", 54, 0, 180, CMF_AIMDIRECTION, 0);
			A_SpawnItemEx("TPortLightningWaveSpawner",0,0,54,0,0,0,0,0,160);
			A_SpawnItemEx("TPortLightningWaveSpawner",0,0,54,0,0,0,0,0,160);
			A_LightningAttack("LightningBeamPillarZap2", 0, 0, 54, 0, -90, true);
			A_LightningAttack("LightningBeamZap2", 0, 0, 54, 0, 0, false);
		}
		"####" AA 0 A_SpawnItemEx("Spark_B1",0,0,54,0,random(-1,1),random(-1,1),random(0,360),TESLA_FLAGS1,random(157,203))
		"####" A 0 A_SpawnItemEx("SparkFlare_B6",0,0,54,0,0,0,random(0,360),TESLA_FLAGS2)
		"####" A 5
		"####" A 0 A_JumpIfInTargetLOS("See", 0, JLOSF_CLOSENOJUMP, 0, 640)
		Loop
	//no Death states because it's invulnerable
	}
}