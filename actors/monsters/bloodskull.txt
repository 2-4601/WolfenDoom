ACTOR BloodSkull
{
	//$Category Monsters (Wolf3D)/Occult
	//$Title Bloodskull
	//$Color 4
	Health 150
	Radius 16
	Height 56
	Mass 9999
	Speed 2
	FloatSpeed 1
	PainChance 50
	Monster
	+DONTFALL
	+FLOAT
	+NEVERRESPAWN
	+NOGRAVITY
	SeeSound "BSkull/See"
	PainSound "BSkull/Pain"
	DeathSound "BSkull/Death"
	Obituary "$BLOSKULL"
	States
	{
	Spawn:
		SWBS AA 4 A_Look
		TNT1 A 0 A_SpawnItemEx("SkullBloodDrip",random(-2,2),random(-2,2),0,0,0,0,0,0)
		Loop
	See:
		SWBS A 1 A_Chase
		SWBS AAA 1 A_Chase("","")
		SWBS A 1 A_Chase
		SWBS AAA 1 A_Chase("","")
		TNT1 A 0 A_SpawnItemEx("SkullBloodDrip",random(0,4),random(-2,2),0,0,0,0,0,0)
		Loop
	Missile:
		SWBS A 2 A_FaceTarget
		SWBS B 2 A_SpawnProjectile("BloodSpit",12,0,0,0,0)
		SWBS BA 2
		TNT1 A 0 A_SpawnItemEx("SkullBloodDrip",random(-2,2),random(-2,2),0,0,0,0,0,0)
		Goto See
	Pain:
		SWBS A 2 A_Pain
		Goto See
	Death:
		SWBS C 6 A_NoBlocking
		SWBS D 5 BRIGHT A_Scream
		TNT1 A 0 A_SetTranslucent(0.8,0)
		SWBS EFGH 5 BRIGHT
		Stop
	}
}

ACTOR SkullBloodDrip
{
	Radius 1
	Height 4
	+DONTSPLASH
	+MISSILE
	Scale 0.4
	Gravity 0.7
	States
	{
	Spawn:
		BDR2 A 50
		Stop
	}
}

ACTOR BloodSpit
{
	Radius 6
	Height 6
	Speed 17
	Damage 3
	Scale 0.5
	SeeSound "Blood/Spit"
	DeathSound "Blood/Impact"
	Projectile
	Decal BloodSplat
	States
	{
	Spawn:
		BSHT AB 2 A_SpawnItemEx("BloodSpitTrail")
		Loop
	Death:
		BSHT CD 6
		Stop
	}
}

ACTOR BloodSpitTrail
{
	Radius 1
	Height 1
	Projectile
	RenderStyle "Translucent"
	Alpha 0.8
	Scale 0.3
	+NOCLIP
	States
	{
	Spawn:
		BSHT CD 4
		Stop
	}
}