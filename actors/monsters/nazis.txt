ACTOR Nazi
{
	Scale 1.2
	Height 56
	Mass 100
	PainChance 200
	Speed 2
	var int user_incombat;
	Monster
	+FLOORCLIP
	+DONTHARMSPECIES
	+BOSSDEATH
	+DONTGIB
	States
	{
	SkillCheck:
		"####" "#" 0 A_JumpIf(CallACS("WWNaziSkillCheck")>=2,3)
		"####" "#" 0 A_ChangeFlag("DOHARMSPECIES",1)
		"####" "#" 0 A_ChangeFlag("DONTHARMSPECIES",0)
		"####" "#" 0 A_JumpIf(CallACS("WWNaziSkillCheck")<=2,2)
		"####" "#" 0 A_ChangeFlag("GHOST",1)
		"####" "#" 0 A_Jump(256,"Look")
		Stop
	Look:
		"####" "#" 10 A_Look
		Loop
	See:
		"####" "#" 0 A_SetUserVar("user_incombat", true) //mxd
		"####" A 1 A_Chase
		"####" AAA 1 A_Chase("","")
		"####" A 1 A_Chase
		"####" AAA 1 A_Chase("","")
		"####" B 1 A_Chase
    "####" B 0 A_SpawnItemEx("PlayerStep",0,0,14,0,0,0,0,SXF_NoCheckPosition)
		"####" BBB 1 A_Chase("","")
		"####" B 1 A_Chase
		"####" BBB 1 A_Chase("","")
		"####" C 1 A_Chase
		"####" CCC 1 A_Chase("","")
		"####" C 1 A_Chase
		"####" CCC 1 A_Chase("","")
		"####" D 1 A_Chase
    "####" D 0 A_SpawnItemEx("PlayerStep",0,0,14,0,0,0,0,SXF_NoCheckPosition)
		"####" DDD 1 A_Chase("","")
		"####" D 1 A_Chase
		"####" DDD 1 A_Chase("","")
		Loop
	Idle:
		"####" AAAAAAAA 1 A_Wander
		"####" A 0 A_Look
		"####" BBBBBBBB 1 A_Wander
		"####" A 0 A_Look
		"####" CCCCCCCC 1 A_Wander
		"####" A 0 A_Look
		"####" DDDDDDDD 1 A_Wander
		"####" A 0 A_Look
		Loop
	Pain:
		"####" H 3
		"####" H 3 A_Pain
		"####" "#" 0 A_AlertMonsters(256) //mxd
		"####" H 0 A_Jump(256,"See")
		Stop
	Pain.Dagger: //mxd. Hacky critical hit system
		 "####" "#" 0 A_JumpIf(user_incombat, "Pain")
		 "####" "#" 0 A_SetUserVar("user_incombat", true)
		 "####" "#" 0 A_Log("Critical hit!")
		 "####" "#" 0 A_AlertMonsters(128) //mxd
		 "####" "#" 0 A_DamageSelf(health, "Melee")
		 goto Pain
	}
}

ACTOR Guard : Nazi
{
	//$Category Monsters (Wolf3D)
	//$Title SA Guard
	//$Color 4
	GibHealth 30
	Health 20
	SeeSound "Nazi1/Sighted"
	PainSound "Nazi1/Pain"
	DeathSound "Nazi1/Death"
	Obituary "$GUARD"
	DropItem 9mmAmmo 192
	DropItem Luger9mm 64
	States
	{
	Spawn:
		GARD N 0
		Goto SkillCheck
	Missile:
		"####" EF 5 A_FaceTarget
		"####" G 0 A_PlaySound("nazi/pistol",CHAN_WEAPON)
		"####" G 8 BRIGHT LIGHT(NAZIFIRE) A_CustomMissile("EnemyPistolTracer",54,1,random(-8,8))
		"####" F 8 A_SpawnItemEx("9MMCasing", 1, 0, 56, random(1,2), random(-1,1), random(1,2), random(-55,-80), SXF_NOCHECKPOSITION)
		"####" F 0 A_Jump(256,"See")
		Stop
	Death:
		"####" I 5
		"####" J 5 A_Scream
		"####" K 5 A_NoBlocking
		"####" L 5
		"####" M -1
		Stop
	Death.Fire:
		BURN A 5 BRIGHT LIGHT(ITBURNS1) A_SetScale(1.0)
		BURN BC 5 BRIGHT LIGHT(ITBURNS1)
		BURN D 5 BRIGHT LIGHT(ITBURNS1) A_PlaySound("death/burning")
		BURN E 5 BRIGHT LIGHT(ITBURNS1)
		BURN FGHIJ 5 BRIGHT LIGHT(ITBURNS2)
		BURN KLMN 5 BRIGHT LIGHT(ITBURNS3)
		BURN O 5 BRIGHT LIGHT(ITBURNS3) A_NoBlocking
		BURN PQR 5 BRIGHT LIGHT(ITBURNS2)
		BURN STU 5 BRIGHT LIGHT(ITBURNS1)
		BURN V -1
		Stop
	Raise:
		"####" MLKJI 5
		"####" I 0 A_Jump(256,"See")
		Stop
	XDeath:
		SLOP ABCDEF 5 A_SpawnItemEx("NashGore_FlyingBlood", 0, 0, 8, random(-4, 4), random(-4, 4), random(2, 5), 0, 143, 176)
		SLOP G 0 A_NoBlocking
		SLOP G -1
		Stop
	}
}

ACTOR SSGuard : Guard
{
	//$Category Monsters (Wolf3D)
	//$Title SS Guard
	//$Color 4
	Speed 2.67
	Scale 1.24
	Obituary "SSGUARD"
	Health 40
	States
	{
	Spawn:
		SSPG N 0
		Goto SkillCheck
	See:
		"####" "#" 0 A_SetUserVar("user_incombat", true) //mxd
		"####" A 1 A_Chase
		"####" AA 1 A_Chase("","")
		"####" A 1 A_Chase
		"####" AA 1 A_Chase("","")
		"####" B 1 A_Chase
		"####" BB 1 A_Chase("","")
		"####" B 1 A_Chase
		"####" BB 1 A_Chase("","")
		"####" C 1 A_Chase
		"####" CC 1 A_Chase("","")
		"####" C 1 A_Chase
		"####" CC 1 A_Chase("","")
		"####" D 1 A_Chase
		"####" DD 1 A_Chase("","")
		"####" D 1 A_Chase
		"####" DD 1 A_Chase("","")
		Loop
	Idle:
		"####" AAAAAA 1 A_Wander
		"####" A 0 A_Look
		"####" BBBBBB 1 A_Wander
		"####" A 0 A_Look
		"####" CCCCCC 1 A_Wander
		"####" A 0 A_Look
		"####" DDDDDD 1 A_Wander
		"####" A 0 A_Look
		Loop
	}
}

ACTOR WGuard : Guard
{
	//$Category Monsters (Wolf3D)
	//$Title Wehrmacht Guard
	//$Color 4
	Speed 2.5
	Scale 1.24
	Obituary "$WGUARD"
	Health 30
	PainChance 185
	+MISSILEMORE
	+MISSILEEVENMORE
	States
	{
	Spawn:
		GRD2 N 0
		Goto SkillCheck
	Missile:
		GRD2 EF 2 A_FaceTarget
		GRD2 G 0 A_PlaySound("nazi/pistol",CHAN_WEAPON)
		GRD2 G 4 BRIGHT LIGHT(NAZIFIRE) A_CustomMissile("EnemyPistolTracer",54,1,random(-6,6))
		GRD2 G 0 A_SpawnItemEx("9MMCasing", 1,0,56, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		GRD2 F 4 A_FaceTarget
		GRD2 G 0 A_PlaySound("nazi/pistol",CHAN_WEAPON)
		GRD2 G 8 BRIGHT LIGHT(NAZIFIRE) A_CustomMissile("EnemyPistolTracer",54,1,random(-10,10))
		GRD2 G 0 A_SpawnItemEx("9MMCasing", 1,0,56, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		GRD2 F 8
		Goto See
	}
}

ACTOR MP40Guard : Guard
{
	//$Category Monsters (Wolf3D)
	//$Title SA Guard (MP40)
	//$Color 4
	Obituary "$MPGUARD"
	Scale 1.24
	SeeSound "Nazi2/Sighted"
	PainSound "Nazi2/Pain"
	DeathSound "Nazi2/Death"
	dropitem 9mmAmmo 192
	dropitem MP40 64
	dropitem GrenadePickup 16
	States
	{
	Spawn:
		MGRD N 0
		Goto SkillCheck
	Missile:
		MGRD EF 5 A_FaceTarget
		MGRD G 0 A_PlaySound("nazi/mp40",CHAN_WEAPON)
		MGRD G 2 BRIGHT LIGHT(NAZIFIRE) A_CustomMissile("EnemySMGTracer",40,4,random(-8,8))
		MGRD G 0 A_SpawnItemEx("9MMCasing", 8,0,40, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		MGRD F 2 A_FaceTarget
		MGRD G 0 A_PlaySound("nazi/mp40",CHAN_WEAPON)
		MGRD G 2 BRIGHT LIGHT(NAZIFIRE) A_CustomMissile("EnemySMGTracer",40,4,random(-8,8))
		MGRD G 0 A_SpawnItemEx("9MMCasing", 8,0,40, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		MGRD F 2 A_FaceTarget
		MGRD G 0 A_PlaySound("nazi/mp40",CHAN_WEAPON)
		MGRD G 2 BRIGHT LIGHT(NAZIFIRE) A_CustomMissile("EnemySMGTracer",40,4,random(-8,8))
		MGRD G 0 A_SpawnItemEx("9MMCasing", 8,0,40, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		MGRD F 2 A_FaceTarget
		MGRD G 0 A_PlaySound("nazi/mp40",CHAN_WEAPON)
		MGRD G 2 BRIGHT LIGHT(NAZIFIRE) A_CustomMissile("EnemySMGTracer",40,4,random(-8,8))
		MGRD G 0 A_SpawnItemEx("9MMCasing", 8,0,40, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		MGRD F 2 A_FaceTarget
		MGRD G 0 A_PlaySound("nazi/mp40",CHAN_WEAPON)
		MGRD G 2 BRIGHT LIGHT(NAZIFIRE) A_CustomMissile("EnemySMGTracer",40,4,random(-8,8))
		MGRD F 2 A_SpawnItemEx("9MMCasing", 8,0,40, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		Goto See
	}
}

ACTOR SSMP40Guard : SSGuard
{
	//$Category Monsters (Wolf3D)
	//$Title SS Guard (MP40)
	//$Color 4
	SeeSound "Nazi2/Sighted"
	PainSound "Nazi2/Pain"
	DeathSound "Nazi2/Death"
	dropitem 9mmAmmo 192
	dropitem MP40 64
	dropitem GrenadePickup 16
	Obituary "$SSMPGURD"
	States
	{
	Spawn:
		SSMG N 0
		Goto SkillCheck
	Missile:
		SSMG EF 5 A_FaceTarget
		SSMG G 0 A_PlaySound("nazi/mp40",CHAN_WEAPON)
		SSMG G 2 BRIGHT LIGHT(NAZIFIRE) A_CustomMissile("EnemySMGTracer",40,4,random(-8,8))
		SSMG G 0 A_SpawnItemEx("9MMCasing", 6,0,42, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		SSMG F 2 A_MonsterRefire(30,"See")
		SSMG F 0 A_FaceTarget
		Goto Missile+2
	}
}

ACTOR SSMP40GuardBlind : SSMP40Guard
{
	//$Category Monsters (Wolf3D)
	//$Title SS Guard (MP40, blind, alert)
	//$Color 4
	States
	{
	Look:
		SSMG A 10 A_LookEx(0, 0, 256, 0, 90, "See")
		Loop
	Missile:
		SSMG EF 5 A_FaceTarget
		SSMG G 0 A_AlertMonsters(512)
		SSMG G 0 A_PlaySound("nazi/mp40",CHAN_WEAPON)
		SSMG G 2 BRIGHT LIGHT(NAZIFIRE) A_CustomMissile("EnemySMGTracer",40,4,random(-8,8))
		SSMG G 0 A_SpawnItemEx("9MMCasing", 6,0,42, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		SSMG F 2 A_MonsterRefire(30,"See")
		SSMG F 0 A_FaceTarget
		Goto Missile+2
	}
}

ACTOR WMP40Guard : MP40Guard
{
	//$Category Monsters (Wolf3D)
	//$Title Wehrmacht Guard (MP40)
	//$Color 4
	Obituary "$WMPGUARD"
	Health 30
	Speed 2.5
	+MISSILEMORE
	+MISSILEEVENMORE
	States
	{
	Spawn:
		MGR2 N 0
		Goto SkillCheck
	Missile:
		MGR2 EF 2 A_FaceTarget
		MGR2 G 0 A_PlaySound("nazi/mp40",CHAN_WEAPON)
		MGR2 G 2 BRIGHT LIGHT(NAZIFIRE) A_CustomMissile("EnemySMGTracer",40,4,random(-8,8))
		MGR2 G 0 A_SpawnItemEx("9MMCasing", 6,0,42, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		MGR2 F 1 A_FaceTarget
		MGR2 G 0 A_PlaySound("nazi/mp40",CHAN_WEAPON)
		MGR2 G 2 BRIGHT LIGHT(NAZIFIRE) A_CustomMissile("EnemySMGTracer",40,4,random(-12,12))
		MGR2 G 0 A_SpawnItemEx("9MMCasing", 6,0,42, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		MGR2 F 2 A_MonsterRefire(30,"See")
		MGR2 F 0 A_FaceTarget
		Goto Missile+2
	}
}

ACTOR Officer : Guard
{
	//$Category Monsters (Wolf3D)
	//$Title SA Officer
	//$Color 4
	Speed 4
	Obituary "$OFFICER"
	SeeSound "Nazi4/Sighted"
	PainSound "Nazi4/Pain"
	DeathSound "Nazi4/Death"
	States
	{
	Spawn:
		OFFI N 0
		Goto SkillCheck
	See:
		"####" "#" 0 A_SetUserVar("user_incombat", true) //mxd
		"####" A 1 A_Chase
		"####" A 1 A_Chase("","")
		"####" A 1 A_Chase
		"####" A 1 A_Chase("","")
		"####" B 1 A_Chase
		"####" B 1 A_Chase("","")
		"####" B 1 A_Chase
		"####" B 1 A_Chase("","")
		"####" C 1 A_Chase
		"####" C 1 A_Chase("","")
		"####" C 1 A_Chase
		"####" C 1 A_Chase("","")
		"####" D 1 A_Chase
		"####" D 1 A_Chase("","")
		"####" D 1 A_Chase
		"####" D 1 A_Chase("","")
		Loop
	Missile:
		"####" EF 2 A_FaceTarget
		"####" G 0 A_PlaySound("nazi/pistol",CHAN_WEAPON)
		"####" G 5 BRIGHT LIGHT(NAZIFIRE) A_CustomMissile("EnemyPistolTracer",52,1,random(-4,4))
		"####" F 4 A_SpawnItemEx("9MMCasing", 1,0,54, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		"####" F 0 A_Jump(256,"See")
		Stop
	Idle:
		"####" AAAA 1 A_Wander
		"####" A 0 A_Look
		"####" BBBB 1 A_Wander
		"####" A 0 A_Look
		"####" CCCC 1 A_Wander
		"####" A 0 A_Look
		"####" DDDD 1 A_Wander
		"####" A 0 A_Look
		Loop
	}
}

ACTOR SSOfficer : Officer
{
	//$Category Monsters (Wolf3D)
	//$Title SS Officer
	//$Color 4
	Speed 5.33
	Scale 1.24
	Obituary "$SSOFFICE"
	Health 40
	States
	{
	Spawn:
		SSOF N 0
		Goto SkillCheck
	}
}

ACTOR WOfficer : Officer
{
	//$Category Monsters (Wolf3D)
	//$Title Wehrmacht Officer
	//$Color 4
	Obituary "$WOFFICER"
	Health 30
	Speed 5
	PainChance 180
	+MISSILEMORE
	+MISSILEEVENMORE
	States
	{
	Spawn:
		OFR2 N 0
		Goto SkillCheck
	Missile:
		OFR2 EF 2 A_FaceTarget
		OFR2 G 0 A_PlaySound("nazi/pistol",CHAN_WEAPON)
		OFR2 G 4 BRIGHT LIGHT(NAZIFIRE) A_CustomMissile("EnemyPistolTracer",52,1,random(-4,4))
		OFR2 G 0 A_SpawnItemEx("9MMCasing", 1,0,54, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		OFR2 F 3 A_FaceTarget
		OFR2 G 0 A_PlaySound("nazi/pistol",CHAN_WEAPON)
		OFR2 G 5 BRIGHT LIGHT(NAZIFIRE) A_CustomMissile("EnemyPistolTracer",52,1,random(-6,6))
		OFR2 G 0 A_SpawnItemEx("9MMCasing", 1,0,54, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		OFR2 F 3 A_FaceTarget
		OFR2 G 0 A_PlaySound("nazi/pistol",CHAN_WEAPON)
		OFR2 G 5 BRIGHT LIGHT(NAZIFIRE) A_CustomMissile("EnemyPistolTracer",52,1,random(-8,8))
		OFR2 G 0 A_SpawnItemEx("9MMCasing", 1,0,54, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		OFR2 F 3 A_FaceTarget
		OFR2 G 0 A_PlaySound("nazi/pistol",CHAN_WEAPON)
		OFR2 G 5 BRIGHT LIGHT(NAZIFIRE) A_CustomMissile("EnemyPistolTracer",52,1,random(-8,8))
		OFR2 G 0 A_SpawnItemEx("9MMCasing", 1,0,54, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		OFR2 F 4
		Goto See
	}
}

ACTOR RifleGuard : MP40Guard
{
	//$Category Monsters (Wolf3D)
	//$Title SA Guard (Rifle)
	//$Color 4
	Obituary "$RIFLSOLD"
	SeeSound "Nazi3/Sighted"
	dropitem MauserAmmo
	dropitem G43 64
	States
	{
	Spawn:
		RGRD N 0
		Goto SkillCheck
	Missile:
		RGRD EF 10 A_FaceTarget
		RGRD G 0 A_PlaySound("mauser/fire",CHAN_WEAPON)
		RGRD G 8 BRIGHT LIGHT(NAZIFIRE) A_CustomMissile("EnemyRifleTracer",40,8,random(-1,1))
		RGRD G 0 A_SpawnItemEx("MauserRifleCasing", 8,0,42, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		RGRD F 8
		RGRD F 0 A_PlaySound("mauser/cock",CHAN_AUTO,0.25)
		Goto See
	}
}

ACTOR WRifleGuard : RifleGuard
{
	//$Category Monsters (Wolf3D)
	//$Title Wehrmacht Guard (Rifle)
	//$Color 4
	Obituary "$WRIFSOLD"
	Health 30
	Speed 2.5
	+MISSILEMORE
	+MISSILEEVENMORE
	States
	{
	Spawn:
		RGR2 N 0
		Goto SkillCheck
	Missile:
		RGR2 EF 10 A_FaceTarget
		RGR2 G 0 A_PlaySound("mauser/fire",CHAN_WEAPON)
		RGR2 G 8 BRIGHT LIGHT(NAZIFIRE) A_CustomMissile("EnemyRifleTracer",40,8,random(-1,1))
		RGR2 G 0 A_SpawnItemEx("MauserRifleCasing", 8,0,42, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		RGR2 F 4 A_FaceTarget
		RGR2 G 0 A_PlaySound("mauser/fire",CHAN_WEAPON)
		RGR2 G 8 BRIGHT LIGHT(NAZIFIRE) A_CustomMissile("EnemyRifleTracer",40,8,random(-5,5))
		RGR2 G 0 A_SpawnItemEx("MauserRifleCasing", 8,0,42, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		RGR2 F 4
		RGR2 F 4 A_PlaySound("mauser/cock",CHAN_AUTO,0.25)
		Goto See
	}
}

ACTOR WaffenSS : SSMP40Guard
{
	//$Category Monsters (Wolf3D)
	//$Title Waffen SS
	//$Color 4
	SeeSound "Nazi3/Sighted"
	Obituary "$SSWAFFEN"
	Health 80
	dropitem "9mmAmmo" 192
	dropitem "MP40" 64
	dropitem "GrenadePickup" 32
	States
	{
	Spawn:
		WAFF N 0
		Goto SkillCheck
	Missile:
		"####" EF 5 A_FaceTarget
		"####" G 0 A_PlaySound("nazi/stg44",CHAN_WEAPON)
		"####" G 2 BRIGHT LIGHT(NAZIFIRE) A_CustomMissile("EnemyStGTracer",38,6,random(-4,4))
		"####" G 0 A_SpawnItemEx("MauserRifleCasing", 8,0,40, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		"####" F 4 A_MonsterRefire(30,"See")
		"####" F 0 A_FaceTarget
		Goto Missile+2
	}
}

ACTOR Paratrooper : WaffenSS
{
	//$Category Monsters (Wolf3D)
	//$Title Paratrooper
	//$Color 4
	Obituary "$PARATROP"
	Health 90
	dropitem "9mmAmmo" 192
	dropitem "MP40" 64
	States
	{
	Spawn:
		PARA N 0
		Goto SkillCheck
	}
}

ACTOR Mutant : Guard
{
	//$Category Monsters (Wolf3D)/Mutants
	//$Title Mutant (Projectile)
	//$Color 4
	Obituary "$MUTANT"
	Health 90
	Scale 1.4
	SeeSound "mutant/see"
	PainSound "mutant/pain"
	DeathSound "mutant/Death"
	BloodColor "Purple"
	BloodType "MutantBlood"
	DamageFactor MutantPoison, 0
	DamageFactor Poison, 0
	+MISSILEMORE
	+MISSILEEVENMORE
	dropitem "9mmAmmo" 128
	States
	{
	Spawn:
		MTNT A 0 NoDelay ACS_NamedExecuteAlways("DisableMutants",0) //mxd
		Goto SkillCheck
	See:
		"####" "#" 0 A_SetUserVar("user_incombat", true) //mxd
		"####" A 0 A_SetSpeed(4)
		"####" B 1 A_Chase
		"####" B 1 A_Chase("","")
		"####" B 1 A_Chase
		"####" B 1 A_Chase("","")
		"####" C 1 A_Chase
		"####" C 1 A_Chase("","")
		"####" C 1 A_Chase
		"####" C 1 A_Chase("","")
		"####" D 1 A_Chase
		"####" D 1 A_Chase("","")
		"####" D 1 A_Chase
		"####" D 1 A_Chase("","")
		"####" E 1 A_Chase
		"####" E 1 A_Chase("","")
		"####" E 1 A_Chase
		"####" E 1 A_Chase("","")
		Loop
	Missile:
		MTNT F 6 A_FaceTarget
		MTNT G 0 A_PlaySound("walther/fire",CHAN_WEAPON)
		MTNT G 5 BRIGHT LIGHT(MUTNFIRE) A_CustomMissile("EnemyMutantTracer",45,0,random(-1,1))
		MTNT G 0 A_SpawnItemEx("9MMCasing", 0,0,47, RandomPick(-3, 3), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		MTNT F 6 A_FaceTarget
		MTNT H 0 A_PlaySound("walther/fire",CHAN_WEAPON)
		MTNT H 5 BRIGHT LIGHT(MUTNFIRE) A_CustomMissile("EnemyMutantTracer",45,0,random(-1,1))
		MTNT H 0 A_SpawnItemEx("9MMCasing", 0,0,47, RandomPick(-3, 3), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		MTNT F 6 A_MonsterRefire(30,"See")
		MTNT F 0 A_FaceTarget
		Goto Missile+1
	Pain:
		"####" I 6 A_Pain
		"####" "#" 0 A_AlertMonsters(256) //mxd
		Goto See
	Death:
		"####" J 3
		"####" K 3 A_Scream
		"####" L 3 A_NoBlocking
		"####" MN 3
		"####" O -1
		Stop
	Idle:
		"####" A 0 A_SetSpeed(1.5)
		"####" BBBBBBBB 1 A_Wander
		"####" A 0 A_Look
		"####" CCCCCCCC 1 A_Wander
		"####" A 0 A_Look
		"####" DDDDDDDD 1 A_Wander
		"####" A 0 A_Look
		"####" EEEEEEEE 1 A_Wander
		"####" A 0 A_Look
		Loop
	Raise:
		"####" ONMLKJ 4
		Goto See
	XDeath:
		SLOP ABCDEF 5 A_SpawnItemEx("MutantFlyingBlood", 0, 0, 8, random(-4, 4), random(-4, 4), random(2, 5), 0, 143, 176)
		SLOP G 0 A_NoBlocking
		SLOP G -1
		Stop
	}
}

ACTOR MutantMelee : Mutant
{
	//$Category Monsters (Wolf3D)/Mutants
	//$Title Mutant (Melee)
	//$Color 4
	dropitem ""
	+LOOKALLAROUND
	states
	{
	Spawn:
		MLMT A 0 NoDelay ACS_NamedExecuteAlways("DisableMutants",0) //mxd
		Goto SkillCheck
	See:
		"####" "#" 0 A_SetUserVar("user_incombat", true) //mxd
		"####" A 0 A_SetSpeed(6)
		"####" B 1 A_Chase
		"####" B 1 A_Chase("","")
		"####" B 1 A_Chase
		"####" B 1 A_Chase("","")
		"####" C 1 A_Chase
		"####" C 1 A_Chase("","")
		"####" C 1 A_Chase
		"####" C 1 A_Chase("","")
		"####" D 1 A_Chase
		"####" D 1 A_Chase("","")
		"####" D 1 A_Chase
		"####" D 1 A_Chase("","")
		"####" E 1 A_Chase
		"####" E 1 A_Chase("","")
		"####" E 1 A_Chase
		"####" E 1 A_Chase("","")
		Loop
	Melee:
		MLMT F 4 A_FaceTarget
		MLMT G 5 A_CustomMeleeAttack(5*random(1,8))
		MLMT F 4 A_FaceTarget
		MLMT H 5 A_CustomMeleeAttack(5*random(1,8))
		goto See
	Missile:
		Stop
	}
}

ACTOR BigMutant1 : Mutant
{
	//$Category Monsters (Wolf3D)/Mutants
	//$Title Giant Mutant (UMG43)
	//$Color 4
	//$Sprite MTB1A0C0
	Obituary "$GMUTANT1"
	Health 400
	Scale 1.0
	States
	{
	Spawn:
		MTB1 A 0 NoDelay ACS_NamedExecuteAlways("DisableMutants",0) //mxd
		Goto SkillCheck
	See:
		"####" "#" 0 A_SetUserVar("user_incombat", true) //mxd
		"####" A 0 A_SetSpeed(5)
		"####" B 1 A_Chase
		"####" B 1 A_Chase("","")
		"####" B 1 A_Chase
		"####" B 1 A_Chase("","")
		"####" C 1 A_Chase
		"####" C 1 A_Chase("","")
		"####" C 1 A_Chase
		"####" C 1 A_Chase("","")
		"####" D 1 A_Chase
		"####" D 1 A_Chase("","")
		"####" D 1 A_Chase
		"####" D 1 A_Chase("","")
		"####" E 1 A_Chase
		"####" E 1 A_Chase("","")
		"####" E 1 A_Chase
		"####" E 1 A_Chase("","")
		Loop
	Melee:
		MTB1 F 4 A_FaceTarget
		MTB1 G 5 A_CustomMeleeAttack(8*random(1,8))
		MTB1 F 4 A_FaceTarget
		MTB1 H 5 A_CustomMeleeAttack(8*random(1,8))
		goto See
	Missile:
		MTB1 E 0 A_Jump(128, 22)
		MTB2 E 0 A_Jump(96,"Missile2")
		MTB1 E 3
		MTB1 E 7 A_FaceTarget
		MTB1 F 0 A_PlaySound("chaingun/fire",CHAN_WEAPON)
		MTB1 F 2 BRIGHT A_CustomMissile("EnemyMutantTracer", 62, 0, -4, 0)
		MTB1 F 2 A_SpawnItemEx("9MMCasing", 0,0,54, RandomPick(-3, 3), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		MTB1 F 0 A_PlaySound("chaingun/fire",CHAN_WEAPON)
		MTB1 F 2 BRIGHT A_CustomMissile("EnemyMutantTracer", 62, 0, 0, 0)
		MTB1 F 2 A_SpawnItemEx("9MMCasing", 0,0,54, RandomPick(-3, 3), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		MTB1 F 0 A_PlaySound("chaingun/fire",CHAN_WEAPON)
		MTB1 F 2 BRIGHT A_CustomMissile("EnemyMutantTracer", 62, 0, 4, 0)
		MTB1 F 2 A_SpawnItemEx("9MMCasing", 0,0,54, RandomPick(-3, 3), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		MTB1 F 0 A_PlaySound("chaingun/fire",CHAN_WEAPON)
		MTB1 F 2 BRIGHT A_CustomMissile("EnemyMutantTracer", 62, 0, 8, 0)
		MTB1 F 2 A_SpawnItemEx("9MMCasing", 0,0,54, RandomPick(-3, 3), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		MTB1 F 0 A_PlaySound("chaingun/fire",CHAN_WEAPON)
		MTB1 F 2 BRIGHT A_CustomMissile("EnemyMutantTracer", 62, 0, 12, 0)
		MTB1 F 2 A_SpawnItemEx("9MMCasing", 0,0,54, RandomPick(-3, 3), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		MTB1 F 0 A_PlaySound("chaingun/fire",CHAN_WEAPON)
		MTB1 F 2 BRIGHT A_CustomMissile("EnemyMutantTracer", 62, 0, 16, 0)
		MTB1 F 2 A_SpawnItemEx("9MMCasing", 0,0,54, RandomPick(-3, 3), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		Goto See
		MTB1 E 3
		MTB1 E 7 A_FaceTarget
		MTB1 G 0 A_PlaySound("chaingun/fire",CHAN_WEAPON)
		MTB1 G 2 BRIGHT A_CustomMissile("EnemyMutantTracer", 62, 0, 4, 0)
		MTB1 G 2 A_SpawnItemEx("9MMCasing", 0,0,54, RandomPick(-3, 3), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		MTB1 G 0 A_PlaySound("chaingun/fire",CHAN_WEAPON)
		MTB1 G 2 BRIGHT A_CustomMissile("EnemyMutantTracer", 62, 0, 0, 0)
		MTB1 G 2 A_SpawnItemEx("9MMCasing", 0,0,54, RandomPick(-3, 3), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		MTB1 G 0 A_PlaySound("chaingun/fire",CHAN_WEAPON)
		MTB1 G 2 BRIGHT A_CustomMissile("EnemyMutantTracer", 62, 0, -4, 0)
		MTB1 G 2 A_SpawnItemEx("9MMCasing", 0,0,54, RandomPick(-3, 3), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		MTB1 G 0 A_PlaySound("chaingun/fire",CHAN_WEAPON)
		MTB1 G 2 BRIGHT A_CustomMissile("EnemyMutantTracer", 62, 0, -8, 0)
		MTB1 G 2 A_SpawnItemEx("9MMCasing", 0,0,54, RandomPick(-3, 3), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		MTB1 G 0 A_PlaySound("chaingun/fire",CHAN_WEAPON)
		MTB1 G 2 BRIGHT A_CustomMissile("EnemyMutantTracer", 62, 0, -12, 0)
		MTB1 G 2 A_SpawnItemEx("9MMCasing", 0,0,54, RandomPick(-3, 3), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		MTB1 G 0 A_PlaySound("chaingun/fire",CHAN_WEAPON)
		MTB1 G 2 BRIGHT A_CustomMissile("EnemyMutantTracer", 62, 0, -16, 0)
		MTB1 G 2 A_SpawnItemEx("9MMCasing", 0,0,54, RandomPick(-3, 3), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		Goto See
	Missile2:
		MTB1 E 4
		MTB1 E 10 A_FaceTarget
		MTB1 F 0 A_PlaySound("chaingun/fire",CHAN_WEAPON)
		MTB1 FFF 0 A_CustomMissile("EnemyMutantTracer",64,0,frandom(-4,4))
		MTB1 FFF 0 A_SpawnItemEx("9MMCasing", 0,0,54, RandomPick(-3, 3), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		MTB1 EG 3 BRIGHT LIGHT(SUPAFIRE) A_FaceTarget
		MTB1 G 0 A_PlaySound("chaingun/fire",CHAN_WEAPON)
		MTB1 GGG 0 A_CustomMissile("EnemyMutantTracer",64,0,frandom(-4,4))
		MTB1 GGG 0 A_SpawnItemEx("9MMCasing", 0,0,54, RandomPick(-3, 3), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		MTB1 EF 3 BRIGHT LIGHT(SUPAFIRE) A_FaceTarget
		Goto See
	Pain:
		"####" H 6
		"####" "#" 0 A_AlertMonsters(256) //mxd
		Goto See
	Death:
		"####" I 5
		"####" J 5
		"####" K 5 A_Scream
		"####" L 5 A_NoBlocking
		"####" M -1
		Stop
	Idle:
		"####" A 0 A_SetSpeed(2.5)
		"####" BBBBBBBB 1 A_Wander
		"####" A 0 A_Look
		"####" CCCCCCCC 1 A_Wander
		"####" A 0 A_Look
		"####" DDDDDDDD 1 A_Wander
		"####" A 0 A_Look
		"####" EEEEEEEE 1 A_Wander
		"####" A 0 A_Look
		Loop
	Raise:
		"####" MMMLKJI 4
		Goto See
	Death.Fire:
		BURN A 5 BRIGHT LIGHT(ITBURNS1) A_SetScale(1.4)
		BURN BC 5 BRIGHT LIGHT(ITBURNS1)
		BURN D 5 BRIGHT LIGHT(ITBURNS1) A_PlaySound("death/burning")
		BURN E 5 BRIGHT LIGHT(ITBURNS1)
		BURN FGHIJ 5 BRIGHT LIGHT(ITBURNS2)
		BURN KLMN 5 BRIGHT LIGHT(ITBURNS3)
		BURN O 5 BRIGHT LIGHT(ITBURNS3) A_NoBlocking
		BURN PQR 5 BRIGHT LIGHT(ITBURNS2)
		BURN STU 5 BRIGHT LIGHT(ITBURNS1)
		BURN V -1
		Stop
	XDeath:
		SLOP A 0 A_SetScale(1.4)
		SLOP ABCDEF 5 A_SpawnItemEx("MutantFlyingBlood", 0, 0, 8, random(-4, 4), random(-4, 4), random(2, 5), 0, 143, 176)
		SLOP G 0 A_NoBlocking
		SLOP G -1
		Stop
	}
}

ACTOR BigMutant2 : BigMutant1
{
	//$Category Monsters (Wolf3D)/Mutants
	//$Title Giant Mutant (NebRockets)
	//$Color 4
	Obituary "$GMUTANT2"
	Health 450
	+MISSILEEVENMORE
	dropitem NebAmmoSmall, 128, 2
	States
	{
	Spawn:
		MTB2 A 0 NoDelay ACS_NamedExecuteAlways("DisableMutants",0) //mxd
		Goto SkillCheck
	Melee:
		MTB2 E 4 A_FaceTarget
		MTB2 F 5 A_CustomMeleeAttack(8*random(1,8))
		MTB2 E 4 A_FaceTarget
		MTB2 G 5 A_CustomMeleeAttack(8*random(1,8))
	Missile:
		MTB2 E 5 A_FaceTarget
		MTB2 E 0 A_Jump(96,"Missile2")
		MTB2 E 0 A_Jump(192,"Missile3")
		MTB2 F 4 BRIGHT LIGHT(MUTNROCK) A_CustomMissile("MutantRocket",62,0,random(-8,-16))
		MTB2 E 5 A_FaceTarget
		MTB2 G 4 BRIGHT LIGHT(MUTNROCK) A_CustomMissile("MutantRocket",62,0,0)
		Goto See
	Missile2:
		MTB2 G 4 BRIGHT LIGHT(MUTNROCK) A_CustomMissile("MutantRocket",62,0,random(8,16))
		MTB2 E 5 A_FaceTarget
		MTB2 G 4 BRIGHT LIGHT(MUTNROCK) A_CustomMissile("MutantRocket",62,0,0)
		Goto See
	Missile3:
		MTB2 F 0 BRIGHT LIGHT(MUTNROCK) A_CustomMissile("MutantRocket", 62,0,random(-8,-16))
		MTB2 F 0 BRIGHT LIGHT(MUTNROCK) A_CustomMissile("MutantRocket", 62,0, 0)
		MTB2 G 5 BRIGHT LIGHT(MUTNROCK) A_CustomMissile("MutantRocket", 62,0,random(8,16))
		MTB2 E 5 A_FaceTarget
		Goto See
	Pain:
		"####" H 5
		"####" "#" 0 A_AlertMonsters(256) //mxd
		Goto See
	}
}

ACTOR Rottweiler : Nazi
{
	//$Category Monsters (Wolf3D)/Critters
	//$Title Dog (Rottweiler)
	//$Color 4
	Scale 1.1
	HitObituary "$DOGS"
	Health 25
	PainChance 180
	Speed 4
	Height 48
	Mass 70
	SeeSound "dog/sight"
	PainSound "dog/pain"
	DeathSound "dog/death"
	ActiveSound "dog/active"
	-CANUSEWALLS
	States
	{
	Spawn:
		DOGY A 0
		Goto SkillCheck
	Idle:
		"####" A 0 A_SetSpeed(4)
		"####" AAAAAAAA 1 A_Wander
		"####" A 0 A_Look
		"####" BBBBBBBB 1 A_Wander
		"####" A 0 A_Look
		"####" CCCCCCCC 1 A_Wander
		"####" A 0 A_Look
		"####" DDDDDDDD 1 A_Wander
		"####" A 0 A_Look
		Loop
	See:
		"####" "#" 0 A_SetUserVar("user_incombat", true) //mxd
		"####" A 0 A_SetSpeed(8)
		"####" A 1 A_Chase
		"####" AAA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" B 1 A_Chase
		"####" BBB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" C 1 A_Chase
		"####" CCC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" D 1 A_Chase
		"####" DDD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		Loop
	Melee:
		"####" EF 2 Fast A_FaceTarget
		"####" G 8 Fast A_CustomMeleeAttack(random(1,8)*3,"dog/attack","dog/attack")
		"####" FE 4 Fast
		Goto See
	Pain:
		"####" M 2
		"####" M 2 A_Pain
		"####" "#" 0 A_AlertMonsters(256) //mxd
		Goto See
	Death:
		"####" H 8
		"####" I 8 A_Scream
		"####" J 6
		"####" K -1 A_NoBlocking
		Stop
	Raise:
		"####" KJIH 6
		Goto See
	}
}

ACTOR GermanShepherd : Rottweiler
{
	//$Category Monsters (Wolf3D)/Critters
	//$Title Dog (Shepherd)
	//$Color 4
	States
	{
	Spawn:
		DOG2 A 0
		Goto SkillCheck
	}
}

ACTOR Doberman : Rottweiler
{
	//$Category Monsters (Wolf3D)/Critters
	//$Title Dog (Doberman)
	//$Color 4
	States
	{
	Spawn:
		DOG3 A 0
		Goto SkillCheck
	}
}

ACTOR RocketMan : Nazi
{
	//$Category Monsters (Wolf3D)/Bosses
	//$Title Otto Giftmacher (Boss)
	//$Color 4
	Health 300
	PainChance 110
	Mass 200
	Obituary "$OTTOBOSS"
	SeeSound "Boss2/Sighted"
	PainSound "boss/pain"
	DeathSound "Boss2/Death"
	dropitem NebAmmoSmall
	+LOOKALLAROUND
	+BOSS
	Tag "Otto Giftmacher"
	States
	{
	Spawn:
		OTTO A 0
		Goto SkillCheck
	Missile:
		OTTO E 10 A_FaceTarget
		OTTO F 8 BRIGHT LIGHT(OTTOFIRE) A_CustomMissile("MiniRocket",32,12)
		OTTO E 8
		Goto See
	Pain:
		OTTO K 3
		OTTO K 3 A_Pain
		"####" "#" 0 A_AlertMonsters(256) //mxd
		Goto See
	Death:
		"####" G 5
		"####" H 5 A_Scream
		"####" I 5
		"####" J -1 A_NoBlocking
		Stop
	Raise:
		"####" JIHG 5
		Goto See
	}
}

ACTOR MiniRocket
{
	Radius 11
	Height 8
	Speed 15
	Damage 5
	Projectile
	+RANDOMIZE
	+THRUGHOST
	SeeSound "nazi/missilef"
	DeathSound "world/barrelboom"
	States
	{
	Spawn:
		NMIS A 1 BRIGHT LIGHT(OTTOFIRE) A_SpawnItemEx("RocketFlame",random(-1,1),0,random(-1,1))
		Loop
	Death:
		FRME A 1 BRIGHT LIGHT(OTTOFIRE) A_Explode(20,40)
		FRME BCDEFGHIHJKLMNOPQRS 1 BRIGHT LIGHT(OTTOFIRE)
		Stop
	}
}
//Mutants stuff
ACTOR MutantFartCloud
{
Game Doom
Radius 2
Height 2
DamageType MutantPoison
+NOINTERACTION
+WINDTHRUST
RenderStyle Translucent
Alpha 0.3
States
	{
	Spawn:
	FUGS ABCDE 2
	Fade:
	FUGS ABCDE 2 A_FadeOut (0.05)
	FUGS ABCDE 2 A_FadeIn (0.05)
	FUGS ABCDE 2 A_FadeOut (0.05)
	FUGS ABCDE 2 A_FadeIn (0.05)
	FUGS ABCDE 2 A_FadeOut (0.05)
	FUGS ABCDE 2 A_FadeIn (0.05)
	FUGS ABCDE 2 A_FadeOut (0.05)
	FUGS ABCDE 2 A_FadeIn (0.05)
	FUGS ABCDE 2 A_FadeOut (0.05)
	Stop
	}
}

ACTOR MutantSmoke
{
Game Doom
Radius 2
Height 2
Scale 0.3
Gravity 0.3
Alpha 0.5
VSpeed 2.5
+FORCEXYBILLBOARD
+MOVEWITHSECTOR
+WINDTHRUST
+DONTSPLASH
+NOBLOCKMAP
+NOGRAVITY
+MISSILE
RenderStyle Shaded
StencilColor "E0 00 E0"
States
	{
	Spawn:
	TNT1 A 0
	CLFX A 8 A_FadeOut(0.05)
	Wait
	}
}

ACTOR MutantRocket
{
	Radius 8
	Height 8
	Speed 12
	FastSpeed 14
	Damage (Random(4,8))
	DamageType MutantPoison
	Projectile
	Scale 0.9
	+RANDOMIZE
	+THRUGHOST
	SeeSound "nazi/missilef"
	DeathSound "world/barrelboom"
	States
	{
	Spawn:
		NMIS A 1 BRIGHT LIGHT(MUTNROCK) A_SpawnItemEx("MutantFlame",random(-1,1),0,random(-1,1))
		Loop
	Death:
		FUGS A 1 BRIGHT LIGHT(MUTNROCK) A_Explode(10,30)
		FUGS A 0 BRIGHT A_CustomMissile("PoisonCloudMutant",16,0,0,2,0)
		FUGS AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx ("MutantFartCloud",random(-64,64),random(-64,64),random(-48,48),0,0,0.1,0,128)
		FUGS BBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBB 0 A_SpawnItemEx ("MutantSmoke",random(-64,64),random(-64,64),random(-48,48),momx,0,random(0.5,1),0,SXF_TRANSFERTRANSLATION,160)
		FUGS ABCDEDCBABCDE 1 BRIGHT LIGHT(MUTNROCK)
		Stop
	}
}
//End of Mutants Stuff

Actor PoisonCloudMutant
{
  Radius 20
  Height 30
  Mass 0x7fffffff
  Reactiontime 5
  +NOBLOCKMAP
  +NOGRAVITY
  +DROPOFF
  +NODAMAGETHRUST
  +DONTSPLASH
  +FOILINVUL
  +CANBLAST
  +BLOODLESSIMPACT
  +BLOCKEDBYSOLIDACTORS
  RenderStyle Translucent
  Alpha 0.6
  DamageType MutantPoison

  States
  {
  Spawn:
    TNT1 A 1
    "####" A 2 A_Explode(10,128,0,0,128)
    "####" A 20 A_Countdown
    Loop
  Death:
    "####" A 7
    "####" A 6
    Stop
  }
}

ACTOR MadDoctor : RocketMan
{
	//$Category Monsters (Wolf3D)/Bosses
	//$Title Dr Schabbs (Boss)
	//$Color 4
	Obituary "$SCHABBS"
	dropitem "Medikit_Medium"
	Tag "Dr. Schabbs Jr."
	Health 400
	+BOSS
	states
	{
	Spawn:
		SCHB A 0
		Goto SkillCheck
	Missile:
		SCHB E 10 A_FaceTarget
		SCHB F 8 A_CustomMissile("FlyingNeedle",32,12)
		SCHB A 8
		Goto See
	Pain:
		"####" G 3
		"####" A 3 A_Pain
		"####" "#" 0 A_AlertMonsters(256) //mxd
		Goto See
	}
}

ACTOR FlyingNeedle : MiniRocket
{
	Speed 20
	Damage (random(1,4)*8)
	SeeSound ""
	DeathSound ""
	States
	{
	Spawn:
		NEDL ABCD 3
		Loop
	Death:
		TNT1 A 1
		Stop
	}
}

ACTOR SuperSoldier : Nazi
{
	//$Category Monsters (Wolf3D)/Bosses
	//$Title Hans Grosse (Boss)
	//$Color 4
	Obituary "$SUPASOLD"
	Tag "Ubersoldat" //"Hans Grosse"
	Health 500
	Radius 24
	Height 64
	Mass 500
	PainChance 150
	SeeSound "Boss1/Sighted"
	PainSound "boss/pain"
	DeathSound "Boss1/Death"
	+LOOKALLAROUND
	+NOBLOOD
	+BOSS
	States
	{
	Spawn:
		HANS A 0
		Goto SkillCheck
	Missile:
		HANS E 4 A_FaceTarget
		HANS E 5 A_PlaySound("chaingun/start",CHAN_AUTO)
		HANS E 1 A_PlaySound("chaingun/loop",CHAN_AUTO)
		HANS E 10 A_FaceTarget
		HANS F 0 A_PlaySound("chaingun/fire",CHAN_WEAPON)
		HANS F 1 BRIGHT LIGHT(SUPAFIRE) A_CustomMissile("EnemyChaingunTracer",44,20,random(-11,11))
		HANS F 1 BRIGHT LIGHT(SUPAFIRE) A_CustomMissile("EnemyChaingunTracer",44,-20,random(-11,11))
		HANS F 0 A_SpawnItemEx("9MMCasing", 12,0,36, random(3,4), random(-1,1), random(4,6), random(-55,-80),SXF_NOCHECKPOSITION)
		HANS F 0 A_SpawnItemEx("9MMCasing", 12,0,36, random(3,4), random(-1,1), random(4,6), random(55,80),SXF_NOCHECKPOSITION)
		HANS G 2 BRIGHT A_FaceTarget
		HANS F 0 A_PlaySound("chaingun/fire",CHAN_WEAPON)
		HANS F 1 BRIGHT LIGHT(SUPAFIRE) A_CustomMissile("EnemyChaingunTracer",44,20,random(-11,11))
		HANS F 1 BRIGHT LIGHT(SUPAFIRE) A_CustomMissile("EnemyChaingunTracer",44,-20,random(-11,11))
		HANS F 0 A_SpawnItemEx("9MMCasing", 12,0,36, random(3,4), random(-1,1), random(4,6), random(-55,-80),SXF_NOCHECKPOSITION)
		HANS F 0 A_SpawnItemEx("9MMCasing", 12,0,36, random(3,4), random(-1,1), random(4,6), random(55,80),SXF_NOCHECKPOSITION)
		HANS G 2 BRIGHT A_FaceTarget
		HANS F 0 A_PlaySound("chaingun/fire",CHAN_WEAPON)
		HANS F 1 BRIGHT LIGHT(SUPAFIRE) A_CustomMissile("EnemyChaingunTracer",44,20,random(-11,11))
		HANS F 1 BRIGHT LIGHT(SUPAFIRE) A_CustomMissile("EnemyChaingunTracer",44,-20,random(-11,11))
		HANS F 0 A_SpawnItemEx("9MMCasing", 12,0,36, random(3,4), random(-1,1), random(4,6), random(-55,-80),SXF_NOCHECKPOSITION)
		HANS F 0 A_SpawnItemEx("9MMCasing", 12,0,36, random(3,4), random(-1,1), random(4,6), random(55,80),SXF_NOCHECKPOSITION)
		HANS G 2 BRIGHT A_MonsterRefire(30,"See")
		HANS G 0 A_FaceTarget
 		Goto Missile+4
	Death:
		"####" I 5
		"####" J 5 A_Scream
		"####" K 5
		"####" L -1 A_NoBlocking
		Stop
	Raise:
		"####" LKJI 5
		Goto See
	}
}

ACTOR SuperSoldierGirl : SuperSoldier
{
	//$Category Monsters (Wolf3D)/Bosses
	//$Title Gretel Grosse (Boss)
	//$Color 4
	Obituary "$SUPAGIRL"
	Tag "Female Ubersoldat" //"Gretel Grosse"
	SeeSound "BossGirl/Sighted"
	PainSound "bossGirl/pain"
	DeathSound "BossGirl/Death"
	DropItem "9mmAmmoBox" 128
	DropItem "9mmAmmo" 128
	DropItem "9mmAmmo" 128
	DropItem "9mmAmmo" 128
	+MISSILEMORE
	+MISSILEEVENMORE
	+BOSS
	States
	{
	Spawn:
		WBO4 A 0
		Goto SkillCheck
	Missile:
		WBO4 E 20 A_FaceTarget
		WBO4 F 0 A_PlaySound("browning/fire",CHAN_WEAPON)
		WBO4 FFFF 0 A_CustomMissile("EnemyPistolTracer",44,20,frandom(-6,6))
		WBO4 FFFF 0 A_CustomMissile("EnemyPistolTracer",44,-20,frandom(-6,6))
		WBO4 FFFF 0 A_SpawnItemEx("9MMCasing", 12,0,36, random(3,4), random(-1,1), random(4,6), random(-55,-80),SXF_NOCHECKPOSITION)
		WBO4 FFFF 0 A_SpawnItemEx("9MMCasing", 12,0,36, random(3,4), random(-1,1), random(4,6), random(55,80),SXF_NOCHECKPOSITION)
		WBO4 FG 2 BRIGHT LIGHT(SUPAFIRE) A_FaceTarget
		WBO4 F 0 A_PlaySound("browning/fire",CHAN_WEAPON)
		WBO4 FFFF 0 A_CustomMissile("EnemyPistolTracer",44,20,frandom(-6,6))
		WBO4 FFFF 0 A_CustomMissile("EnemyPistolTracer",44,-20,frandom(-6,6))
		WBO4 FFFF 0 A_SpawnItemEx("9MMCasing", 12,0,36, random(3,4), random(-1,1), random(4,6), random(-55,-80),SXF_NOCHECKPOSITION)
		WBO4 FFFF 0 A_SpawnItemEx("9MMCasing", 12,0,36, random(3,4), random(-1,1), random(4,6), random(55,80),SXF_NOCHECKPOSITION)
		WBO4 FG 2 BRIGHT LIGHT(SUPAFIRE) A_FaceTarget
		WBO4 F 0 A_PlaySound("browning/fire",CHAN_WEAPON)
		WBO4 FFFF 0 A_CustomMissile("EnemyPistolTracer",44,20,frandom(-6,6))
		WBO4 FFFF 0 A_CustomMissile("EnemyPistolTracer",44,-20,frandom(-6,6))
		WBO4 FFFF 0 A_SpawnItemEx("9MMCasing", 12,0,36, random(3,4), random(-1,1), random(4,6), random(-55,-80),SXF_NOCHECKPOSITION)
		WBO4 FFFF 0 A_SpawnItemEx("9MMCasing", 12,0,36, random(3,4), random(-1,1), random(4,6), random(55,80),SXF_NOCHECKPOSITION)
		WBO4 FG 2 BRIGHT LIGHT(SUPAFIRE)
		Goto See
	}
}

ACTOR SuperSoldierElite : SuperSoldier replaces BaronOfHell
{
	//$Category Monsters (Wolf3D)
	//$Title Ubersoldat
	//$Color 4
	+BOSS
	Tag "Elite Ubersoldat" //"Hptm. Furzknall"
	Obituary "$SUPAELIT"
	Health 850
	DropItem "9mmAmmoBox" 128
	DropItem "9mmAmmo" 128
	DropItem "NebAmmoSmall" 256
	DropItem "9mmAmmo" 128
	States
	{
	Spawn:
		TRAN A 0
		Goto SkillCheck
	Missile:
		TRAN E 6 A_FaceTarget
		TRAN E 4 A_PlaySound("chaingun/start",CHAN_AUTO)
		TRAN E 0 A_PlaySound("chaingun/loop",CHAN_AUTO)
		TRAN E 10 A_FaceTarget
		TRAN F 0 A_PlaySound("chaingun/fire",CHAN_WEAPON)
		TRAN F 1 BRIGHT LIGHT(SUPAFIRE) A_CustomMissile("EnemyChaingunTracer",44,20,random(-11,11))
		TRAN F 1 BRIGHT LIGHT(SUPAFIRE) A_CustomMissile("EnemyChaingunTracer",44,-20,random(-11,11))
		TRAN F 0 A_SpawnItemEx("9MMCasing", 12,0,46, random(3,4), random(-1,1), random(4,6), random(-55,-80),SXF_NOCHECKPOSITION)
		TRAN F 0 A_SpawnItemEx("9MMCasing", 12,0,46, random(3,4), random(-1,1), random(4,6), random(55,80),SXF_NOCHECKPOSITION)
		TRAN G 2 A_FaceTarget
		TRAN F 0 A_PlaySound("chaingun/fire",CHAN_WEAPON)
		TRAN F 1 BRIGHT LIGHT(SUPAFIRE) A_CustomMissile("EnemyChaingunTracer",44,20,random(-11,11))
		TRAN F 1 BRIGHT LIGHT(SUPAFIRE) A_CustomMissile("EnemyChaingunTracer",44,-20,random(-11,11))
		TRAN F 0 A_SpawnItemEx("9MMCasing", 12,0,46, random(3,4), random(-1,1), random(4,6), random(-55,-80),SXF_NOCHECKPOSITION)
		TRAN F 0 A_SpawnItemEx("9MMCasing", 12,0,46, random(3,4), random(-1,1), random(4,6), random(55,80),SXF_NOCHECKPOSITION)
		TRAN G 2 A_FaceTarget
		TRAN F 0 A_PlaySound("chaingun/fire",CHAN_WEAPON)
		TRAN F 1 BRIGHT LIGHT(SUPAFIRE) A_CustomMissile("EnemyChaingunTracer",44,20,random(-11,11))
		TRAN F 1 BRIGHT LIGHT(SUPAFIRE) A_CustomMissile("EnemyChaingunTracer",44,-20,random(-11,11))
		TRAN F 0 A_SpawnItemEx("9MMCasing", 12,0,46, random(3,4), random(-1,1), random(4,6), random(-55,-80),SXF_NOCHECKPOSITION)
		TRAN F 0 A_SpawnItemEx("9MMCasing", 12,0,46, random(3,4), random(-1,1), random(4,6), random(55,80),SXF_NOCHECKPOSITION)
		TRAN G 2 A_FaceTarget
		TRAN F 0 A_PlaySound("chaingun/fire",CHAN_WEAPON)
		TRAN F 1 BRIGHT LIGHT(SUPAFIRE) A_CustomMissile("EnemyChaingunTracer",44,20,random(-11,11))
		TRAN F 1 BRIGHT LIGHT(SUPAFIRE) A_CustomMissile("EnemyChaingunTracer",44,-20,random(-11,11))
		TRAN F 0 A_SpawnItemEx("9MMCasing", 12,0,46, random(3,4), random(-1,1), random(4,6), random(-55,-80),SXF_NOCHECKPOSITION)
		TRAN F 0 A_SpawnItemEx("9MMCasing", 12,0,46, random(3,4), random(-1,1), random(4,6), random(55,80),SXF_NOCHECKPOSITION)
		TRAN G 2 BRIGHT A_MonsterRefire(30,"See")
		TRAN G 0 A_FaceTarget
		Goto Missile+4
	}
}

ACTOR General : RocketMan replaces Archvile
{
	//$Category Monsters (Wolf3D)/Bosses
	//$Title General Fettgesicht (Boss)
	//$Color 4
	Obituary "$GENERAL"
	Health 800
	Mass 1000
	SeeSound "Boss3/Sighted"
	DeathSound "Boss3/Death"
	DropItem "NebAmmoSmall" 128
	DropItem "NebAmmoSmall" 128
	DropItem "NebAmmoSmall" 128
	+MISSILEEVENMORE
	+BOSS
	Tag "Gen. Fettgesicht"
	States
	{
	Spawn:
		FETT A 0
		Goto SkillCheck
	Missile:
		FETT E 10 A_FaceTarget
		FETT E 0 A_Jump(128,"Chaingun")
		FETT F 8 BRIGHT LIGHT(OTTOFIRE) A_CustomMissile("MiniRocket",43,24)
		FETT E 8 A_FaceTarget
		FETT F 8 BRIGHT LIGHT(OTTOFIRE) A_CustomMissile("MiniRocket",43,24)
		FETT E 8 A_FaceTarget
		FETT F 8 BRIGHT LIGHT(OTTOFIRE) A_CustomMissile("MiniRocket",43,24)
		FETT E 8
		Goto See
	Chaingun:
		FETT H 0 A_FaceTarget
		FETT H 0 A_PlaySound("chaingun/fire",CHAN_WEAPON)
		FETT H 2 BRIGHT LIGHT(SUPAFIRE) A_CustomMissile("EnemyChaingunTracer",40,-20,random(-11,11))
		FETT F 0 A_SpawnItemEx("9MMCasing", -12,0,36, random(-3,-4), random(-1,1), random(4,6), random(-55,-80),SXF_NOCHECKPOSITION)
		FETT G 2 BRIGHT A_MonsterRefire(30,"See")
		Loop
	Pain:
		FETT I 3
		FETT I 3 A_Pain
		"####" "#" 0 A_AlertMonsters(256) //mxd
		Goto See
	Death:
		FETT J 5
		FETT K 5 A_Scream
		FETT L 5
		FETT M -1 A_NoBlocking
		Stop
	Raise:
		FETT MLKJ 5
		Goto See
	}
}

ACTOR FlamerMan : RocketMan replaces Arachnotron
{
	//$Category Monsters (Wolf3D)/Bosses
	//$Title Flamer (Boss)
	//$Color 4
	Speed 2.67
	Obituary "$FLAMEBOY"
	Tag "Johan Feuermann"
	Health 500
	SeeSound "Boss1/Sighted"
	DeathSound "Boss1/Death"
	dropitem "FlameAmmo"
	dropitem "Pyrolight" 64
	States
	{
	Spawn:
		FLAM A 0
		Goto SkillCheck
	See:
		"####" "#" 0 A_SetUserVar("user_incombat", true) //mxd
		FLAM A 1 A_Chase
		FLAM AA 1 A_Chase("","")
		FLAM A 1 A_Chase
		FLAM AA 1 A_Chase("","")
		FLAM B 1 A_Chase
		FLAM BB 1 A_Chase("","")
		FLAM B 1 A_Chase
		FLAM BB 1 A_Chase("","")
		FLAM C 1 A_Chase
		FLAM CC 1 A_Chase("","")
		FLAM C 1 A_Chase
		FLAM CC 1 A_Chase("","")
		FLAM D 1 A_Chase
		FLAM DD 1 A_Chase("","")
		FLAM D 1 A_Chase
		FLAM DD 1 A_Chase("","")
		Loop
	Missile:
		FLAM E 10 A_FaceTarget
		FLAM E 0 A_JumpIfCloser(384,"ProperMissile")
		FLAM F 4 BRIGHT LIGHT(OTTOFIRE) A_CustomMissile("EnemyFlamebolt",44,12)
		FLAM E 6
		Goto See
	ProperMissile:
		FLAM FFFFFFFF 1 BRIGHT LIGHT(OTTOFIRE) A_CustomMissile("EnemyFlamerShot",44,12)
		FLAM E 6 A_MonsterRefire(10,"See")
		Goto Missile
	Pain:
		FLAM A 3
		FLAM A 3 A_Pain
		"####" "#" 0 A_AlertMonsters(256) //mxd
		Goto See
	Death:
		FLAM G 5
		FLAM H 5 A_Scream
		FLAM I 5
		FLAM J -1 A_NoBlocking
		Stop
	Idle:
		FLAM AAAAAA 1 A_Wander
		FLAM A 0 A_Look
		FLAM BBBBBB 1 A_Wander
		FLAM A 0 A_Look
		FLAM CCCCCC 1 A_Wander
		FLAM A 0 A_Look
		FLAM DDDDDD 1 A_Wander
		FLAM A 0 A_Look
		Loop
	}
}

ACTOR EnemyFlamerShot : Flamebolt
{
	+THRUGHOST
	-RIPPER
}

ACTOR EnemyFlamebolt : Flameball
{
	+THRUGHOST
	Damage 8
	Scale .5
	States
	{
	Death:
		FBLX A 2 BRIGHT LIGHT(BOAFLMW2) A_Explode(8,64)
		FBLX BCDEFGHIJK 2 BRIGHT LIGHT(BOAFLMW2)
		Stop
	}
}

ACTOR HitlerGhost : Nazi
{
	//$Category Monsters (Wolf3D)
	//$Title Hitler Ghost
	//$Color 4
	Scale 1.25
	Obituary "$SSGHOST"
	Health 250
	Mass 1000
	Speed 2.167
	RenderStyle Translucent
	Alpha 0.5
	Floatspeed .67
	PainChance 24
	DeathSound "hitlerghost/die"
	SeeSound "hitlerghost/see"
	+FLOAT
	+NOGRAVITY
	+NOBLOOD
	-FLOORCLIP
	States
	{
	Spawn:
		HGST A 0
		"####" "#" 0 A_SetUserVar("user_incombat", true) //mxd
		Goto SkillCheck
	See:
		"####" A 1 A_Chase("Missile","Missile",CHF_FASTCHASE)
		"####" AAAAA 1 A_Chase("","")
		"####" B 1 A_Chase
		"####" BBBBB 1 A_Chase("","")
		"####" C 1 A_Chase
		"####" CCCCC 1 A_Chase("","")
		"####" B 1 A_Chase
		"####" BBBBB 1 A_Chase("","")
		Loop
	Missile:
		"####" A 4 A_FaceTarget
		"####" D 4 BRIGHT LIGHT(OTTOFIRE) A_CustomMissile("EnemyFlamebolt2",36)
		"####" A 4
		Goto See
	Pain:
		"####" A 4 A_Pain
		"####" "#" 0 A_AlertMonsters(256) //mxd
		Goto See
	Death:
		"####" E 6 A_Scream
		"####" FGHI 6
		"####" J -1 A_NoBlocking
		Stop
	Idle:
		Stop
	}
}

ACTOR EnemyFlamebolt2 : EnemyFlamebolt
{
	Damage (random(1,4)*5)
	Scale .35
	States
	{
	Death:
		FBLX ABCDEFGHIJK 2 BRIGHT LIGHT(BOAFLMW2)
		Stop
	}
}

ACTOR Geist : Nazi
{
	//$Category Monsters (Wolf3D)
	//$Title Geist
	//$Color 4
	+LOOKALLAROUND
	Scale .9
	Obituary "$GEIST"
	Health 100
	Radius 16
	Mass 1000
	Speed 5
	PainChance 255
	RenderStyle Translucent
	Alpha .5
	DeathSound "geist/die"
	PainSound "geist/pain"
	Floatspeed 1.33
	-FLOORCLIP
	+FLOAT
	+NOGRAVITY
	+DONTFALL
	+NOBLOOD
	States
	{
	Spawn:
		GEST A 0
		"####" "#" 0 A_SetUserVar("user_incombat", true) //mxd
		Goto SkillCheck
	Look:
		GEST ABCD 4 A_Look
		Loop
	See:
		GEST A 0 A_PlaySound("geist/active",CHAN_VOICE,1,1)
		GEST A 0 A_SetTranslucent(0.1)
		GEST AAABBBCCCDDD 1 A_Chase
		GEST A 0 A_SetTranslucent(0.2)
		GEST AAABBBCCCDDD 1 A_Chase
		GEST A 0 A_SetTranslucent(0.3)
		GEST AAABBBCCCDDD 1 A_Chase
		GEST A 0 A_SetTranslucent(0.4)
		GEST AAABBBCCCDDD 1 A_Chase
		GEST A 0 A_SetTranslucent(0.5)
		GEST AAABBBCCCDDD 1 A_Chase
		GEST A 0 A_SetTranslucent(0.4)
		GEST AAABBBCCCDDD 1 A_Chase
		GEST A 0 A_SetTranslucent(0.3)
		GEST AAABBBCCCDDD 1 A_Chase
		GEST A 0 A_SetTranslucent(0.2)
		GEST AAABBBCCCDDD 1 A_Chase
		Loop
	Melee:
		"####" A 1 A_SetTranslucent(0.1)
		"####" AA 1 A_FadeIn(0.2)
		"####" B 0 A_FaceTarget
		"####" BCDABCD 3 BRIGHT A_CustomMeleeAttack(4)
		"####" AAAA 1 A_Fadeout(0.1)
		TNT1 A 0 A_Jump(256,"See")
		Stop
	Pain:
		"####" E 1 A_Pain
		"####" E 1 A_SetTranslucent(0.4)
		"####" EEE 1 A_Fadeout(0.1)
		"####" "#" 0 A_AlertMonsters(256) //mxd
		TNT1 A 0 A_Jump(256,"See")
		Stop
	Death:
		"####" E 5 A_SetTranslucent(0.5)
		"####" E 6 A_Scream
		"####" FG 6
		"####" H 6 A_NoBlocking
		Stop
	Idle:
		Stop
	}
}

ACTOR NaziGeist : Geist
{
	Scale 1.25
	Health 400
	Radius 31
	Speed 3.75
	PainChance 128
	States
	{
	Spawn:
		GES2 A 0
		"####" "#" 0 A_SetUserVar("user_incombat", true) //mxd
		Goto SkillCheck
	Look:
		GES2 ABCD 6 A_Look
		Loop
	See:
		GES2 A 0 A_PlaySound("geist/active",CHAN_VOICE,1,1)
		GES2 A 0 A_SetTranslucent(0.1)
		GES2 A 1 A_Chase
		GES2 AAAAA 1 A_Chase("","")
		GES2 B 1 A_Chase
		GES2 BBBBB 1 A_Chase("","")
		GES2 C 1 A_Chase
		GES2 CCCCC 1 A_Chase("","")
		GES2 D 1 A_Chase
		GES2 DDDDD 1 A_Chase("","")
		GES2 A 0 A_SetTranslucent(0.2)
		GES2 A 1 A_Chase
		GES2 AAAAA 1 A_Chase("","")
		GES2 B 1 A_Chase
		GES2 BBBBB 1 A_Chase("","")
		GES2 C 1 A_Chase
		GES2 CCCCC 1 A_Chase("","")
		GES2 D 1 A_Chase
		GES2 DDDDD 1 A_Chase("","")
		GES2 A 0 A_SetTranslucent(0.3)
		GES2 A 1 A_Chase
		GES2 AAAAA 1 A_Chase("","")
		GES2 B 1 A_Chase
		GES2 BBBBB 1 A_Chase("","")
		GES2 C 1 A_Chase
		GES2 CCCCC 1 A_Chase("","")
		GES2 D 1 A_Chase
		GES2 DDDDD 1 A_Chase("","")
		GES2 A 0 A_SetTranslucent(0.4)
		GES2 A 1 A_Chase
		GES2 AAAAA 1 A_Chase("","")
		GES2 B 1 A_Chase
		GES2 BBBBB 1 A_Chase("","")
		GES2 C 1 A_Chase
		GES2 CCCCC 1 A_Chase("","")
		GES2 D 1 A_Chase
		GES2 DDDDD 1 A_Chase("","")
		GES2 A 0 A_SetTranslucent(0.5)
		GES2 A 1 A_Chase
		GES2 AAAAA 1 A_Chase("","")
		GES2 B 1 A_Chase
		GES2 BBBBB 1 A_Chase("","")
		GES2 C 1 A_Chase
		GES2 CCCCC 1 A_Chase("","")
		GES2 D 1 A_Chase
		GES2 DDDDD 1 A_Chase("","")
		GES2 A 0 A_SetTranslucent(0.4)
		GES2 A 1 A_Chase
		GES2 AAAAA 1 A_Chase("","")
		GES2 B 1 A_Chase
		GES2 BBBBB 1 A_Chase("","")
		GES2 C 1 A_Chase
		GES2 CCCCC 1 A_Chase("","")
		GES2 D 1 A_Chase
		GES2 DDDDD 1 A_Chase("","")
		GES2 A 0 A_SetTranslucent(0.3)
		GES2 A 1 A_Chase
		GES2 AAAAA 1 A_Chase("","")
		GES2 B 1 A_Chase
		GES2 BBBBB 1 A_Chase("","")
		GES2 C 1 A_Chase
		GES2 CCCCC 1 A_Chase("","")
		GES2 D 1 A_Chase
		GES2 DDDDD 1 A_Chase("","")
		GES2 A 0 A_SetTranslucent(0.2)
		GES2 A 1 A_Chase
		GES2 AAAAA 1 A_Chase("","")
		GES2 B 1 A_Chase
		GES2 BBBBB 1 A_Chase("","")
		GES2 C 1 A_Chase
		GES2 CCCCC 1 A_Chase("","")
		GES2 D 1 A_Chase
		GES2 DDDDD 1 A_Chase("","")
		Loop
	Missile:
		GES2 A 1 BRIGHT A_SetTranslucent(0.1)
		GES2 AAABBB 1 BRIGHT A_FadeIn(0.1)
		GES2 B 1 BRIGHT A_SetTranslucent(1.0)
		GES2 CD 4 BRIGHT A_FaceTarget
		GES2 A 0 A_Jump(128,3)
		GES2 A 4 BRIGHT A_PainAttack("Geist",0,PAF_NOSKULLATTACK,0)
		GES2 A 0 A_Jump(256,3)
		GES2 A 0 A_PainAttack("Geist",-45,PAF_NOSKULLATTACK,0)
		GES2 A 4 BRIGHT A_PainAttack("Geist",45,PAF_NOSKULLATTACK,0)
		GES2 B 3 BRIGHT
		GES2 BCCCC 1 BRIGHT A_Fadeout(0.1)
		GES2 D 4 BRIGHT
		Goto See
	}
}

ACTOR MiniShip : Nazi
{
	//$Category Monsters (Wolf3D)
	//$Title Miniship
	//$Color 4
	+LOOKALLAROUND
	Obituary "$NAZISHIP"
	Health 375
	Speed 3.25
	Mass 2000
	PainSound "metal/pain"
	DeathSound "world/barrelboom"
	Floatspeed .67
	-FLOORCLIP
	+FLOAT
	+NOGRAVITY
	+NOBLOOD
	States
	{
	Spawn:
		SHIP A 0
		"####" "#" 0 A_SetUserVar("user_incombat", true) //mxd
		Goto SkillCheck
	See:
		"####" A 1 A_Chase
		"####" AAA 1 A_Chase("","")
		Loop
	Missile:
		SHIP A 10 A_FaceTarget
		SHIP A 0 A_PlaySound("miniship/fire", CHAN_WEAPON)
		SHIP B 8 BRIGHT LIGHT(SUPAFIRE) A_CustomMissile("EnemyChaingunTracer",26,7,random(-11,11))
		SHIP A 8 A_FaceTarget
		SHIP A 0 A_PlaySound("miniship/fire", CHAN_WEAPON)
		SHIP C 8 BRIGHT LIGHT(SUPAFIRE) A_CustomMissile("EnemyChaingunTracer",26,-7,random(-11,11))
		SHIP A 8
		Goto See
	Pain:
		"####" A 3
		"####" A 3 A_Pain
		"####" "#" 0 A_AlertMonsters(256) //mxd
		Goto See
	Death:
		SHIP A -1
		Wait
	Crash:
		TNT1 A 0 A_SetFloorClip
		TNT1 A 0 A_Scream
		TNT1 A 0 A_SpawnItemEx("GeneralExplosion_Small",0,0,32)
		Stop
	Idle:
		SHIP AAAAAAAA 1 A_Wander
		SHIP A 0 A_Look
		Loop
	}
}

ACTOR MGTurret : MiniShip
{
	//$Category Monsters (Wolf3D)
	//$Title MGTurret
	//$Color 4
	Scale 1.1
	Speed 0
	Obituary "$MGTURRET"
	Health 100
	Radius 24
	Height 24
	mass 0x7fffffff
	PainChance 100
	+MISSILEEVENMORE
	-FLOAT
	-NOGRAVITY
	States
	{
	Spawn:
		GTUR A 0
		"####" "#" 0 A_SetUserVar("user_incombat", true) //mxd
		Goto SkillCheck
	Missile:
		"####" A 4 A_PlaySound("chaingun/start",CHAN_WEAPON)
		"####" A 1 A_PlaySound("chaingun/loop",CHAN_WEAPON)
		"####" A 5 A_FaceTarget
		"####" B 0 A_PlaySound("chaingun/fire",CHAN_WEAPON)
		"####" C 3 BRIGHT LIGHT(NAZIFIRE) A_CustomMissile("EnemyChaingunTracer",20,0,random(-11,11))
		"####" B 3 BRIGHT A_MonsterRefire(40,"See")
		"####" B 0 A_FaceTarget
		Goto Missile+3
	Death:
		Goto Crash
	Idle:
		Goto Look
	}
}

ACTOR DeathKnight : Nazi
{
	//$Category Monsters (Wolf3D)/Bosses
	//$Title Todesritter (Boss)
	//$Color 4
	+LOOKALLAROUND
	Scale 1.5
	Obituary "$DKNIGHT"
	Tag "Todesritter"
	Health 3000
	Radius 64
	Height 96
	Mass 1000
	Speed 3.5
	PainChance 20
	SeeSound "deathknight/sight"
	PainSound "boss/pain"
	DeathSound "deathknight/death"
	DropItem "9mmAmmoBox" 128
	DropItem "9mmAmmo" 128
	DropItem "NebAmmoSmall" 256
	DropItem "9mmAmmo" 128
	+MISSILEEVENMORE
	+BOSS
	+NORADIUSDMG
	states
	{
	Spawn:
		DKGT A 0
		"####" "#" 0 A_SetUserVar("user_incombat", true) //mxd
		Goto SkillCheck
	See:
		"####" A 1 A_Chase
		"####" AAA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" A 1 A_Chase
		"####" AAA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" B 1 A_Chase
		"####" BBB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" B 1 A_Chase
		"####" BBB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" A 0 A_PlaySound("hitler/walk")
		"####" C 1 A_Chase
		"####" CCC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" C 1 A_Chase
		"####" CCC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" D 1 A_Chase
		"####" DDD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" D 1 A_Chase
		"####" DDD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" A 0 A_PlaySound("hitler/walk")
		Loop
	Missile:
		DKGT E 10 A_FaceTarget
		DKGT E 0 A_Jump(40,"AlphaStrike")
		DKGT E 0 A_Jump(128,"Chaingun")
		DKGT F 3 BRIGHT LIGHT(OTTOFIRE) A_CustomMissile("NebRocketEnemy",80,24)
		DKGT E 3 A_MonsterRefire(40,"See")
		DKGT G 3 BRIGHT LIGHT(OTT2FIRE) A_CustomMissile("NebRocketEnemy",80,-24)
		DKGT E 3 A_MonsterRefire(40,"See")
		DKGT F 3 BRIGHT LIGHT(OTTOFIRE) A_CustomMissile("NebRocketEnemy",80,24)
		DKGT E 3 A_MonsterRefire(40,"See")
		DKGT G 3 BRIGHT LIGHT(OTT2FIRE) A_CustomMissile("NebRocketEnemy",80,-24)
		DKGT E 3
		Goto See
	Chaingun:
		DKGT I 0 A_FaceTarget
		DKGT I 0 A_PlaySound("chaingun/fire",CHAN_WEAPON)
		DKGT II 0 A_CustomMissile("EnemyChaingunTracer",54,30,random(-11,11))
		DKGT II 0 A_CustomMissile("EnemyChaingunTracer",54,-30,random(-11,11))
		DKGT II 0 A_SpawnItemEx("9MMCasing", 32,0,58, random(3,4), random(-1,1), random(4,6), random(-55,-80),SXF_NOCHECKPOSITION)
		DKGT II 0 A_SpawnItemEx("9MMCasing", 32,0,58, random(3,4), random(-1,1), random(4,6), random(55,80),SXF_NOCHECKPOSITION)
		DKGT I 2 BRIGHT LIGHT(DEKNBFIR)
		DKGT H 2 BRIGHT A_MonsterRefire(30,"See")
		Loop
	AlphaStrike:
		DKGT K 0 A_FaceTarget
		DKGT K 0 A_CustomMissile("NebRocketEnemy",80,24)
		DKGT K 0 A_PlaySound("chaingun/fire",CHAN_WEAPON)
		DKGT KK 0 A_CustomMissile("EnemyChaingunTracer",54,30,random(-11,11))
		DKGT KK 0 A_CustomMissile("EnemyChaingunTracer",54,-30,random(-11,11))
		DKGT KK 0 A_SpawnItemEx("9MMCasing", 32,0,58, random(3,4), random(-1,1), random(4,6), random(-55,-80),SXF_NOCHECKPOSITION)
		DKGT KK 0 A_SpawnItemEx("9MMCasing", 32,0,58, random(3,4), random(-1,1), random(4,6), random(55,80),SXF_NOCHECKPOSITION)
		DKGT K 2 BRIGHT LIGHT(DEKNBFIR)
		DKGT L 0 A_CustomMissile("NebRocketEnemy",80,-24)
		DKGT L 2 BRIGHT A_MonsterRefire(30,"See")
		Loop
	Pain:
		DKGT E 3
		DKGT E 3 A_Pain
		"####" "#" 0 A_AlertMonsters(256) //mxd
		Goto See
	Death:
		DKGT M 5 A_Scream
		DKGT NOP 5
		DKGT Q 5 A_NoBlocking
		DKGT R 5
		DKGT S -1
		Stop
	Idle:
		Stop
	}
}

ACTOR NebRocketEnemy : NebRocket
{
	Speed 15
	+THRUGHOST
	-FORCERADIUSDMG
	SeeSound "nebelwerfer/fire"
	States
	{
	Death:
		FRME A 1 BRIGHT LIGHT(NEBEXPLO) A_Explode(40)
		FRME BCDEFGHIHJKLMNOPQRS 1 BRIGHT LIGHT(NEBEXPLO)
		Stop
	}
}

ACTOR Hitler : DeathKnight
{
	//$Category Monsters (Wolf3D)/Bosses
	//$Title Adolf Hitler (Boss)
	//$Color 4
	Scale 1.2
	Obituary "$HITLER"
	Tag "Adolf Hitler"
	Health 2500
	Radius 32
	Height 64
	Speed 2.5
	SeeSound "hitler/sight"
	PainSound "hitler/pain"
	DeathSound "hitler/death"
	ActiveSound "hitler/active"
	States
	{
	Spawn:
		HITL A 0
		"####" "#" 0 A_SetUserVar("user_incombat", true) //mxd
		Goto SkillCheck
	Missile:
		HITL E 6 A_FaceTarget
		HITL EEEE 1 A_PlaySound("chaingun/start",CHAN_AUTO)
		HITL E 0 A_PlaySound("chaingun/loop",CHAN_AUTO)
		HITL F 0 A_FaceTarget
		HITL F 0 A_PlaySound("chaingun/fire",CHAN_WEAPON)
		HITL F 0 A_CustomMissile("EnemyChaingunTracer",32,28,random(-3,3))
		HITL F 1 BRIGHT LIGHT(HAIL3FIR) A_CustomMissile("EnemyChaingunTracer",32,-28,random(-3,3))
		HITL F 0 A_SpawnItemEx("9MMCasing", 32,0,36, random(3,4), random(-1,1), random(4,6), random(-55,-80),SXF_NOCHECKPOSITION)
		HITL F 0 A_SpawnItemEx("9MMCasing", 32,0,36, random(3,4), random(-1,1), random(4,6), random(55,80),SXF_NOCHECKPOSITION)
		HITL F 0 A_CustomMissile("EnemyChaingunTracer",48,28,random(-3,3))
		HITL F 1 BRIGHT LIGHT(HAIL4FIR) A_CustomMissile("EnemyChaingunTracer",48,-28,random(-3,3))
		HITL F 0 A_SpawnItemEx("9MMCasing", 32,0,52, random(3,4), random(-1,1), random(4,6), random(-55,-80),SXF_NOCHECKPOSITION)
		HITL F 0 A_SpawnItemEx("9MMCasing", 32,0,52, random(3,4), random(-1,1), random(4,6), random(55,80),SXF_NOCHECKPOSITION)
		HITL G 2 BRIGHT A_MonsterRefire(30,"See")
		Goto Missile+5
	Pain:
		HITL Q 3
		HITL Q 3 A_Pain
		"####" "#" 0 A_AlertMonsters(256) //mxd
		Goto See
	Death:
		HITL H 10 A_Scream
		HITL I 10 A_SpawnItemEx("HitlerSuitScrap",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		HITL JK 10
		HITL L 10 A_PlaySound("hitler/gib")
		HITL M 10 A_NoBlocking
		HITL NO 10
		HITL P -1
		Stop
	}
}

ACTOR HitlerSuitScrap
{
	Scale 1.2
	+FLOORCLIP
	States
	{
	Spawn:
		HIT2 AB 20
		HIT2 C -1
		Stop
	}
}

ACTOR ChairGuy : Nazi
{
	//$Category Monsters (Wolf3D)/Bosses
	//$Title Chairman (Boss)
	//$Color 4
	Obituary "$CHAIRMAN"
	Health 600
	Mass 1000
	PainChance 110
	SeeSound "krist/sight"
	PainSound "boss/pain"
	DeathSound "krist/death"
	Tag "Chairman"
	DropItem GrenadePickup,128
	DropItem GrenadePickup,128
	DropItem GrenadePickup,128
	+LOOKALLAROUND
	+MISSILEEVENMORE
	+BOSS
	states
	{
	Spawn:
		KRIS A 0
		"####" "#" 0 A_SetUserVar("user_incombat", true) //mxd
		Goto SkillCheck
	See:
		KRIS A 1 A_Chase
		KRIS AAA 1 A_Chase("","")
		KRIS A 1 A_Chase
		KRIS AAA 1 A_Chase("","")
		KRIS B 1 A_Chase
		KRIS BBB 1 A_Chase("","")
		KRIS B 1 A_Chase
		KRIS BBB 1 A_Chase("","")
		Loop
	Missile:
		KRIS E 10 A_FaceTarget
		KRIS F 6 BRIGHT LIGHT(OTTOFIRE) A_CustomMissile("MiniMiniRocket",66,22)
		KRIS E 6 A_FaceTarget
		KRIS G 6 BRIGHT LIGHT(OTT2FIRE) A_CustomMissile("MiniMiniRocket",66,-22)
		KRIS E 6 A_FaceTarget
		KRIS F 6 BRIGHT LIGHT(OTTOFIRE) A_CustomMissile("MiniMiniRocket",66,22)
		KRIS E 6 A_FaceTarget
		KRIS G 6 BRIGHT LIGHT(OTT2FIRE) A_CustomMissile("MiniMiniRocket",66,-22)
		Goto See
	Death:
		KRIS H 8 A_Scream
		KRIS HHH 8 A_SetAngle(angle+45)
		KRIS HHHHHH 4 A_SetAngle(angle+45)
		KRIS HHHHHHHHHHHH 2 A_SetAngle(angle+45)
		KRIS HHHHHHHHHHHH 1 A_SetAngle(angle+45)
		BARX A 0 A_NoBlocking
		BARX A 0 A_PlaySound("world/barrelboom")
		BARX A 2 BRIGHT A_Explode(40)
		BARX BCDEFGHIJKLM 2 BRIGHT
		Stop
	Idle:
		KRIS AAAAAAAA 1 A_Wander
		KRIS A 0 A_Look
		KRIS BBBBBBBB 1 A_Wander
		KRIS A 0 A_Look
		Loop
	}
}

ACTOR MiniMiniRocket : MiniRocket
{
	Scale .8
	Damage (random(1,4)*4)
	DeathSound "nebelwerfer/xplode"
}

ACTOR FatMan : ChairGuy
{
	//$Category Monsters (Wolf3D)/Bosses
	//$Title FatMan (Boss)
	//$Color 4
	+BOSS
	Tag "Gen. Schwabbelhals"
	Obituary "$FATMAN"
	seesound "fatman/sight"
	deathsound "fatman/death"
	States
	{
	Spawn:
		GOER A 0
		"####" "#" 0 A_SetUserVar("user_incombat", true) //mxd
		Goto SkillCheck
	See:
		GOER A 1 A_Chase
		GOER AAA 1 A_Chase("","")
		GOER A 1 A_Chase
		GOER AAA 1 A_Chase("","")
		GOER B 1 A_Chase
		GOER BBB 1 A_Chase("","")
		GOER B 1 A_Chase
		GOER BBB 1 A_Chase("","")
		GOER C 1 A_Chase
		GOER CCC 1 A_Chase("","")
		GOER C 1 A_Chase
		GOER CCC 1 A_Chase("","")
		GOER D 1 A_Chase
		GOER DDD 1 A_Chase("","")
		GOER D 1 A_Chase
		GOER DDD 1 A_Chase("","")
		Loop
	Missile:
		GOER E 10 A_FaceTarget
		GOER F 6 BRIGHT LIGHT(OTTOFIRE) A_CustomMissile("MiniMiniRocket",40,22)
		GOER E 6 A_FaceTarget
		GOER G 6 BRIGHT LIGHT(OTT2FIRE) A_CustomMissile("MiniMiniRocket",40,-18)
		GOER E 6 A_FaceTarget
		GOER F 6 BRIGHT LIGHT(OTTOFIRE) A_CustomMissile("MiniMiniRocket",40,22)
		GOER E 6 A_FaceTarget
		GOER G 6 BRIGHT LIGHT(OTT2FIRE) A_CustomMissile("MiniMiniRocket",40,-18)
		Goto See
	Pain:
		GOER D 3
		GOER D 3 A_Pain
		"####" "#" 0 A_AlertMonsters(256) //mxd
		goto See
	Death:
		GOER H 5
		GOER I 5 A_Scream
		GOER J 5
		GOER K 0 A_NoBlocking
		GOER K -1 A_BossDeath
		stop
	Raise:
		GOER KJIH 5
		goto See
	Idle:
		GOER AAAAAAAA 1 A_Wander
		GOER A 0 A_Look
		GOER BBBBBBBB 1 A_Wander
		GOER A 0 A_Look
		GOER CCCCCCCC 1 A_Wander
		GOER A 0 A_Look
		GOER DDDDDDDD 1 A_Wander
		GOER A 0 A_Look
		Loop
	}
}

ACTOR Scientist : Guard
{
	//$Category Monsters (Wolf3D)
	//$Title Scientist
	//$Color 4
	States
	{
	Spawn:
		SCN2 N 0
		Goto SkillCheck
	}
}

ACTOR Scientist2 : Guard
{
	//$Category Monsters (Wolf3D)
	//$Title Scientist (old)
	//$Color 4
	States
	{
	Spawn:
		SCNT N 0
		Goto SkillCheck
	}
}

ACTOR UberMutant : Mutant
{
	//$Category Monsters (Wolf3D)/Bosses
	//$Title Ubermutant (Boss)
	//$Color 4
	Tag "Ubermutant"
	Health 1000
	Radius 24
	Height 64
	Mass 1000
	Speed 2.66666667
	PainChance 50
	SeeSound "baron/sight"
	DeathSound "baron/death"
	Obituary "$OB_BARON"
	HitObituary "$UBERMUTN"
	+LOOKALLAROUND
	+BOSS
	States
	{
	Spawn:
		UBMU A 0
		"####" "#" 0 A_SetUserVar("user_incombat", true) //mxd
		Goto SkillCheck
	Look:
		UBMU AB 10 A_Look
		Loop
	See:
		UBMU A 1 A_SetSpeed(6)
		UBMU AA 1 A_Chase("","")
		UBMU A 1 A_Chase
		UBMU AA 1 A_Chase("","")
		UBMU B 1 A_Chase
		UBMU BB 1 A_Chase("","")
		UBMU B 1 A_Chase
		UBMU BB 1 A_Chase("","")
		UBMU C 1 A_Chase
		UBMU CC 1 A_Chase("","")
		UBMU C 1 A_Chase
		UBMU CC 1 A_Chase("","")
		UBMU D 1 A_Chase
		UBMU DD 1 A_Chase("","")
		UBMU D 1 A_Chase
		UBMU DD 1 A_Chase("","")
		Loop
	Melee:
	Missile:
		UBMU EF 8 A_FaceTarget
		UBMU G 0 A_Jump(64,"Chaingun")
		UBMU G 0 A_Jump(128,"Missile2")
		UBMU G 0 A_Jump(192,"Missile3")
		UBMU G 4 BRIGHT LIGHT(MUTNROCK) A_CustomMissile("MutantRocket",44,0,0)
		Goto See
	Missile2:
		UBMU G 4 BRIGHT LIGHT(MUTNROCK) A_CustomMissile("MutantRocket",44,0,random(8,16))
		UBMU EF 5 A_FaceTarget
		UBMU G 4 BRIGHT LIGHT(MUTNROCK) A_CustomMissile("MutantRocket",44,0,0)
		Goto See
	Missile3:
		UBMU G 0 BRIGHT LIGHT(MUTNROCK) A_CustomMissile("MutantRocket",44,0,random(-8,-16))
		UBMU G 0 BRIGHT LIGHT(MUTNROCK) A_CustomMissile("MutantRocket",44,0, 0)
		UBMU G 5 BRIGHT LIGHT(MUTNROCK) A_CustomMissile("MutantRocket",44,0,random(8,16))
		UBMU EF 5 A_FaceTarget
		Goto See
	Chaingun:
		UBMU G 0 A_FaceTarget
		UBMU G 0 A_PlaySound("chaingun/fire",CHAN_WEAPON)
		UBMU GGGG 0 A_CustomMissile("EnemyMutantTracer",44,0,random(-1,1))
		UBMU GGGG 0 A_CustomMissile("EnemyMutantTracer",44,0,random(-4,4))
		UBMU GGGGGGG 0 A_SpawnItemEx("9MMCasing", 0,0,46, RandomPick(-8, 8), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		UBMU G 2 BRIGHT LIGHT(MUTNROCK)
		UBMU G 2 BRIGHT A_MonsterRefire(200,"See")
		Loop
	Pain:
		UBMU H 2
		UBMU H 2 A_Pain
		"####" "#" 0 A_AlertMonsters(256) //mxd
		Goto See
	Death:
		UBMU I 6
		UBMU I 6 A_Scream
		UBMU J 6
		UBMU J 6 A_NoBlocking
		UBMU KL 6
		UBMU M -1 A_BossDeath
		Stop
	Raise:
		UBMU MLKJJII 6
		Goto See
	/*Idle:
		Stop*/
	}
}

ACTOR Sniper : Guard
{
	//$Category Monsters (Wolf3D)
	//$Title SA Sniper
	//$Color 4
	Obituary "$SNIPER"
	Health 50
	speed 0
	dropitem "MauserAmmo" 192
	dropitem "G43" 64
	+LOOKALLAROUND
	States
	{
	Spawn:
		SNIP A 0
		Goto SkillCheck
	See:
		"####" "#" 0 A_SetUserVar("user_incombat", true) //mxd
		"####" A 1 A_Chase
		Loop
	Pain:
		"####" A 3
		"####" A 3 A_Pain
		"####" "#" 0 A_AlertMonsters(256) //mxd
		goto See
	Missile:
		"####" A 32 A_FaceTarget
		"####" B 0 A_PlaySound("browning/fire",CHAN_WEAPON)
		"####" B 8 BRIGHT LIGHT(NAZIFIRE)A_CustomMissile("EnemyRifleTracer",44,5)
		"####" B 0 A_SpawnItemEx("EnfieldRifleCasing", 1, 0, 56, random(1,2), random(-1,1), random(1,2), random(-55,-80), SXF_NOCHECKPOSITION)
		"####" A 32
		"####" A 0 A_PlaySound("browning/cock",CHAN_AUTO,0.25)
		goto See
	Death:
		"####" E 5
		"####" F 5 A_Scream
		"####" G 5 A_NoBlocking
		"####" H 5
		"####" I -1
		stop
	Raise:
		"####" IHGFE 5
		goto See
	Idle:
		Goto Look
	}
}

ACTOR WSniper : Sniper
{
	//$Category Monsters (Wolf3D)
	//$Title Wehrmacht Sniper
	//$Color 4
	States
	{
	Spawn:
		SNI2 A 0
		Goto SkillCheck
	}
}

ACTOR UndeadMonk : HitlerGhost
{
	//$Category Monsters (Wolf3D)
	//$Title Undead Monk
	//$Color 4
	+LOOKALLAROUND
	Obituary "$UNDEADM"
	DeathSound "monk/death"
	PainSound "monk/pain"
	SeeSound "monk/see"
	States
	{
	Spawn:
		MONK A 0
		"####" "#" 0 A_SetUserVar("user_incombat", true) //mxd
		Goto SkillCheck
	Missile: //mxd. Added cause firing offset
		"####" A 4 A_FaceTarget
		"####" D 4 BRIGHT LIGHT(OTTOFIRE) A_CustomMissile("EnemyFlamebolt2",68)
		"####" A 4
		Goto See
	}
}

ACTOR SuperMutant : Mutant
{
	//$Category Monsters (Wolf3D)/Mutants
	//$Title Mutant (super)
	//$Color 4
	Obituary "$SUPAMUTN"
	Health 180
	Scale 1.45
	states
	{
	Spawn:
		SUPM A 0 NoDelay ACS_NamedExecuteAlways("DisableMutants",0) //mxd
		Goto SkillCheck
	See:
		"####" "#" 0 A_SetUserVar("user_incombat", true) //mxd
		"####" A 0 A_SetSpeed(5)
		"####" B 1 A_Chase
		"####" B 1 A_Chase("","")
		"####" B 1 A_Chase
		"####" B 1 A_Chase("","")
		"####" C 1 A_Chase
		"####" C 1 A_Chase("","")
		"####" C 1 A_Chase
		"####" C 1 A_Chase("","")
		"####" D 1 A_Chase
		"####" D 1 A_Chase("","")
		"####" D 1 A_Chase
		"####" D 1 A_Chase("","")
		"####" E 1 A_Chase
		"####" E 1 A_Chase("","")
		"####" E 1 A_Chase
		"####" E 1 A_Chase("","")
		Loop
	Missile:
		SUPM F 6 A_FaceTarget
		SUPM G 0 A_PlaySound("walther/fire",CHAN_WEAPON)
		SUPM G 4 BRIGHT LIGHT(MUTNFIRE) A_CustomMissile("EnemyMutantTracer",40,random(-22,-20),random(-1,1))
		SUPM G 0 A_SpawnItemEx("9MMCasing", 12,0,46, random(3,4), random(-1,1), random(4,6), random(-55,-80),SXF_NOCHECKPOSITION)
		SUPM F 5 A_FaceTarget
		SUPM P 0 A_PlaySound("walther/fire",CHAN_WEAPON)
		SUPM P 4 BRIGHT LIGHT(MUTNFIRE) A_CustomMissile("EnemyMutantTracer",40,random(18,20),random(-1,1))
		SUPM P 0 A_SpawnItemEx("9MMCasing", 12,0,46, random(3,4), random(-1,1), random(4,6), random(55,80),SXF_NOCHECKPOSITION)
		SUPM H 5 A_MonsterRefire(30,"See")
		SUPM H 0 A_FaceTarget
		Goto Missile+1
	}
}

ACTOR FlamerSoldier : Guard
{
	//$Category Monsters (Wolf3D)
	//$Title SS Flamer (Soldier)
	//$Color 4
	Obituary "$FLAMEBOY"
	Health 100
	dropitem "FlameAmmo" 192
	dropitem "Pyrolight" 32
	MaxTargetRange 384
	States
	{
	Spawn:
		FLSL N 0
		Goto SkillCheck
	Missile:
		FLSL E 3
		FLSL F 10 A_FaceTarget
		FLSL GGGGGGGG 1 BRIGHT LIGHT(NAZIFIRE) A_CustomMissile("EnemyFlamerShot",38,4)
		FLSL F 6 A_MonsterRefire(10,"See")
		Goto Missile+1
	Death:
	Death.Fire:
	XDeath:
		"####" I 5 A_SpawnItemEx("GeneralExplosion_Medium")
		Goto Super::Death+1
		"####" J 5 A_Scream
		"####" K 5 A_NoBlocking
		"####" L 5
		"####" M -1
		Stop
	}
}

ACTOR MGTurretInvisible : MGTurret
{
	//$Category Monsters (Wolf3D)
	//$Title MGTurret (hidden/nokillcount)
	//$Color 4
	Obituary "$MGTHIDE"
	Health 0
	-SHOOTABLE
	-SOLID
	-COUNTKILL
	RenderStyle None
}

ACTOR MGTurretSoldier : MGTurret
{
	//$Category Monsters (Wolf3D)
	//$Title Stationary Turret Soldier
	//$Color 4
	Height 56
	seesound "Nazi1/Sighted"
	painsound "Nazi1/Pain"
	deathsound "Nazi1/Death"
	States
	{
	Spawn:
		SMGS A 0
		"####" "#" 0 A_SetUserVar("user_incombat", true) //mxd
		Goto SkillCheck
	Missile: //mxd. Copied from parent cause diffetent firing offset
		"####" A 4 A_PlaySound("chaingun/start",CHAN_WEAPON)
		"####" A 1 A_PlaySound("chaingun/loop",CHAN_AUTO)
		"####" A 5 A_FaceTarget
		"####" B 0 A_PlaySound("chaingun/fire",CHAN_WEAPON)
		"####" C 3 BRIGHT LIGHT(NAZIFIRE) A_CustomMissile("EnemyChaingunTracer",34,-2,random(-11,11))
		"####" C 0 A_SpawnItemEx("9MMCasing", 0,0,36, random(2,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		"####" B 3 BRIGHT A_MonsterRefire(40,"See")
		"####" B 0 A_FaceTarget
		Goto Missile+3
	Death:
	Crash:
		TNT1 A 0 A_Scream
		SMGS D 6 A_SpawnItemEx("GeneralExplosion_Small",0,0,32)
		SMGS EFG 6
		SMGS H -1 A_NoBlocking
		Stop
	}
}

ACTOR EliteFlamer : Guard
{
	//$Category Monsters (Wolf3D)
	//$Title Elite Soldier (Flamer)
	//$Color 4
	Obituary "$FLAMEBOY"
	health 450
	Speed 4
	seesound "eliteflamer/sight"
	painsound "eliteflamer/pain"
	deathsound "eliteflamer/death"
	dropitem "FlameAmmo"
	dropitem "Pyrolight" 32
	+MISSILEMORE
	MaxTargetRange 400
	var int user_count;
	var int user_offset; //mxd
	States
	{
	Spawn:
		ELTF A 0 A_Look
		Goto SkillCheck
	See:
		"####" "#" 0 A_SetUserVar("user_incombat", true) //mxd
		ELTF A 1 A_Chase
		ELTF AA 1 A_Chase("","")
		ELTF A 1 A_Chase
		ELTF AA 1 A_Chase("","")
		ELTF B 1 A_Chase
		ELTF BB 1 A_Chase("","")
		ELTF B 1 A_Chase
		ELTF BB 1 A_Chase("","")
		ELTF C 1 A_Chase
		ELTF CC 1 A_Chase("","")
		ELTF C 1 A_Chase
		ELTF CC 1 A_Chase("","")
		ELTF D 1 A_Chase
		ELTF DD 1 A_Chase("","")
		ELTF D 1 A_Chase("","",CHF_FASTCHASE)
		ELTF DD 1 A_Chase("","")
		Loop
	Missile:
		ELTF E 0 A_PlaySound("ammo/cell1")
		ELTF E 15 A_FaceTarget
		ELTF E 0 A_Jump(60,"ProperMissile2")
	ProperMissileStart:
		ELTF F 0 A_FaceTarget
		ELTF FG 2 BRIGHT LIGHT(BOAFLAMW) A_CustomMissile("EnemyFlamerShot",42,6)
		ELTF G 0 A_MonsterRefire(40,"See")
		FLAM E 0 A_JumpIfCloser(400,"ProperMissileStart")
		Goto See
	ProperMissile2:
		ELTF F 0 A_FaceTarget
		ELTF F 0 A_SetUserVar("user_count",0)
		ELTF F 0 A_SetUserVar("user_offset", RandomPick(-3, 3)) //mxd
		ELTF F 2 Bright LIGHT(BOAFLAMW) A_CustomMissile("EnemyFlamerShot",42,6)
	Wave1:
		ELTF F 0 A_SetAngle(angle + user_offset)
		ELTF G 2 Bright LIGHT(BOAFLAMW) A_CustomMissile("EnemyFlamerShot",42,6,angle,CMF_ABSOLUTEANGLE)
		ELTF F 0 A_SetAngle(angle + user_offset)
		ELTF F 2 Bright LIGHT(BOAFLAMW) A_CustomMissile("EnemyFlamerShot",42,6,angle,CMF_ABSOLUTEANGLE)
		ELTF F 0 A_SetUserVar("user_count",user_count+1)
		ELTF F 0 A_JumpIf(user_count>=5,1)
		Loop
	Wave2:
		ELTF F 0 A_SetAngle(angle - user_offset)
		ELTF G 2 Bright LIGHT(BOAFLAMW) A_CustomMissile("EnemyFlamerShot",42,6,angle,CMF_ABSOLUTEANGLE)
		ELTF F 0 A_SetAngle(angle - user_offset)
		ELTF F 2 Bright LIGHT(BOAFLAMW) A_CustomMissile("EnemyFlamerShot",42,6,angle,CMF_ABSOLUTEANGLE)
		ELTF F 0 A_SetUserVar("user_count",user_count+1)
		ELTF F 0 A_JumpIf(user_count>=15,1)
		Loop
	Wave3:
		ELTF F 0 A_SetAngle(angle + user_offset)
		ELTF G 2 Bright LIGHT(BOAFLAMW) A_CustomMissile("EnemyFlamerShot",42,6,angle,CMF_ABSOLUTEANGLE)
		ELTF F 0 A_SetAngle(angle + user_offset)
		ELTF F 2 Bright LIGHT(BOAFLAMW) A_CustomMissile("EnemyFlamerShot",42,6,angle,CMF_ABSOLUTEANGLE)
		ELTF F 0 A_SetUserVar("user_count",user_count+1)
		ELTF F 0 A_JumpIf(user_count>=20,1)
		Loop
		ELTF E 6
		Goto See
	Death:
		ELTF I 5
		ELTF J 5 A_Scream
		ELTF K 5 A_NoBlocking
		ELTF L -1
		stop
	Raise:
		ELTF LKJI 5
		goto See
	}
}

ACTOR EliteAssaulter : Guard
{
	//$Category Monsters (Wolf3D)
	//$Title Elite Soldier (MP40)
	//$Color 4
	Obituary "$ELITASS"
	health 350
	Speed 4.66666667
	seesound "eliteassaulter/sight"
	painsound "eliteassaulter/pain"
	deathsound "eliteassaulter/death"
	dropitem "9mmAmmo" 192
	dropitem "MP40" 64
	dropitem "GrenadePickup" 16
	+MISSILEMORE
	States
	{
	Spawn:
		ELTA N 0
		Goto SkillCheck
	See:
		"####" "#" 0 A_SetUserVar("user_incombat", true) //mxd
		ELTA A 0 A_SetSpeed(4.66666667)
		ELTA A 0 A_ChangeFlag("FRIGHTENED",0)
		ELTA A 1 A_Chase
		ELTA AA 1 A_Chase("","")
		ELTA A 1 A_Chase
		ELTA AA 1 A_Chase("","")
		ELTA B 1 A_Chase
		ELTA BB 1 A_Chase("","")
		ELTA B 1 A_Chase
		ELTA BB 1 A_Chase("","")
		ELTA C 1 A_Chase
		ELTA CC 1 A_Chase("","")
		ELTA C 1 A_Chase
		ELTA CC 1 A_Chase("","")
		ELTA D 1 A_Chase
		ELTA DD 1 A_Chase("","")
		ELTA D 1 A_Chase("","",CHF_FASTCHASE)
		ELTA DD 1 A_Chase("","")
		Loop
	Missile:
		ELTA E 0 A_Jump(50,"Grenada")
		ELTA E 0 A_PlaySound("ammo/cell2")
		ELTA EF 12 A_FaceTarget
		ELTA G 0 A_PlaySound("weapons/stg44",CHAN_WEAPON)
		ELTA G 2 BRIGHT LIGHT(NAZIFIRE) A_CustomMissile("EnemyStGTracer",39,5,random(-6,6))
		ELTA G 0 A_SpawnItemEx("9MMCasing", 6,0,40, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		ELTA F 4 A_FaceTarget
		ELTA G 0 A_PlaySound("weapons/stg44",CHAN_WEAPON)
		ELTA G 2 BRIGHT LIGHT(NAZIFIRE) A_CustomMissile("EnemyStGTracer",39,5,random(-5,5))
		ELTA G 0 A_SpawnItemEx("9MMCasing", 6,0,40, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		ELTA F 4 A_FaceTarget
		ELTA G 0 A_PlaySound("weapons/stg44",CHAN_WEAPON)
		ELTA G 2 BRIGHT LIGHT(NAZIFIRE) A_CustomMissile("EnemyStGTracer",39,5,random(-6,6))
		ELTA G 0 A_SpawnItemEx("9MMCasing", 6,0,40, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		ELTA F 10 A_FaceTarget
		ELTA F 0 A_MonsterRefire(30,"See")
		goto Missile+4
	Grenada:
		ELTA EF 7 A_FaceTarget
		ELTA E 4 A_CustomMissile("HandGrenade")
	Retreat:
		ELTA A 0 A_SetSpeed(14)
		ELTA A 0 A_ChangeFlag("FRIGHTENED",1)
		ELTA AABBCCDDAABBCCDDAABBCCDD 1 A_Chase
		ELTA A 0 A_ChangeFlag("FRIGHTENED",0)
		Goto Missile
	}
}

ACTOR EliteSoldatRifler : Guard
{
	//$Category Monsters (Wolf3D)
	//$Title Elite Soldier (Rifle)
	//$Color 4
	Obituary "$ELITRIFL"
	health 65
	Speed 4.66666667
	painchance 80
	seesound "eliterifler/Sight"
	painsound "eliteassaulter/pain"
	deathsound "eliterifler/death"
	dropitem "MauserAmmo" 192
	dropitem "GrenadePickup" 16
	dropitem "G43" 64
	+MISSILEEVENMORE
	states
	{
	Spawn:
		ELTR N 0
		Goto SkillCheck
	See:
		"####" "#" 0 A_SetUserVar("user_incombat", true) //mxd
		ELTR A 1 A_Chase
		ELTR AA 1 A_Chase("","")
		ELTR A 1 A_Chase
		ELTR AA 1 A_Chase("","")
		ELTR B 1 A_Chase
		ELTR BB 1 A_Chase("","")
		ELTR B 1 A_Chase
		ELTR BB 1 A_Chase("","")
		ELTR C 1 A_Chase
		ELTR CC 1 A_Chase("","")
		ELTR C 1 A_Chase
		ELTR CC 1 A_Chase("","")
		ELTR D 1 A_Chase
		ELTR DD 1 A_Chase("","")
		ELTR D 1 A_Chase("","",CHF_FASTCHASE)
		ELTR DD 1 A_Chase("","")
		Loop
	Missile:
		ELTR E 0 A_JumpIfCloser(1000,"ProperMissile")
		Goto Camping
	ProperMissile:
		ELTR EF 9 A_FaceTarget
		ELTR G 0 A_PlaySound("mauser/fire",CHAN_WEAPON)
		ELTR G 2 BRIGHT LIGHT(NAZIFIRE) A_CustomMissile("EnemyRifleTracer",39,7,random(-4,4))
		ELTR G 0 A_SpawnItemEx("MauserRifleCasing", 7,0,42, random(3,4), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		ELTR F 4 A_PlaySound("mauser/cock",CHAN_AUTO,0.25)
		ELTR E 14
		Goto See
	Camping:
		ELTR O 4 A_JumpIfCloser(540,"CampingDone")
		ELTR P 6
	CampingContinue:
		ELTR Q 0 A_JumpIfCloser(540,"CampingDone")
		ELTR Q 0 A_Jump(6,"CampingDone")
		ELTR Q 0 A_JumpIfCloser(8000,"CampingFire")
		ELTR Q 6 A_FaceTarget
		Loop
	CampingFire:
		ELTR Q 0 A_JumpIfCloser(540,"CampingDone")
		ELTR Q 10 A_FaceTarget
		ELTR R 0 A_PlaySound("mauser/fire",CHAN_WEAPON)
		ELTR R 2 BRIGHT LIGHT(NAZIFIRE) A_CustomMissile("EnemyRifleTracer",32,5,random(-1,1))
		ELTR Q 20
		Goto CampingContinue
	CampingDone:
		ELTR QPO 2
		Goto See
	}
}

ACTOR MadCook : MadDoctor
{
	//$Category Monsters (Wolf3D)/Bosses
	//$Title Cook (Boss)
	//$Color 4
	Obituary "$MADCOOK"
	Health 400
	+BOSS
	Tag "Heinrich Gulasch"
	States
	{
	Spawn:
		COOK A 0
		"####" "#" 0 A_SetUserVar("user_incombat", true) //mxd
		Goto SkillCheck
	Missile:
		COOK E 10 A_FaceTarget
		COOK F 8 A_CustomMissile("FlyingHack",32,12)
		COOK A 8
		Goto See
	}
}

Actor MadCook2 : MadCook
{
	//$Category Monsters (Wolf3D)/Bosses
	//$Title Cook 2 (Boss)
	//$Color 4
	Health 400
	+BOSS
	Tag "Max Gulasch"
}

ACTOR FlyingHack : FlyingNeedle
{
	States
	{
	Spawn:
		COPR ABCD 3
		loop
	}
}

actor UndeadPriest : Guard
{
	//$Category Monsters (Wolf3D)
	//$Title Priest
	//$Color 4
	obituary "$UNDEADP"
	health 200
	mass 125
	Speed 2.66666667
	Scale 1.1
	painchance 30
	dropitem "12GaugeAmmo" 96
	dropitem "12GaugeAmmo" 128
	dropitem "TrenchShotgun" 32
	painsound "UndeadPriest/Pain"
	deathsound "UndeadPriest/Die"
	SeeSound ""
	BloodColor "Black"
	BloodType "UndeadBlood"
	States
	{
	Spawn:
		UNDP A 0
		Goto SkillCheck
	See:
		"####" "#" 0 A_SetUserVar("user_incombat", true) //mxd
		UNDP A 0 A_Jump (32, "Run")
		UNDP A 0 A_SetSpeed(2.66666667)
		UNDP A 1 A_Chase
		UNDP AA 1 A_Chase("","")
		UNDP A 1 A_Chase
		UNDP AA 1 A_Chase("","")
		UNDP B 1 A_Chase
		UNDP BB 1 A_Chase("","")
		UNDP B 1 A_Chase
		UNDP BB 1 A_Chase("","")
		UNDP C 1 A_Chase
		UNDP CC 1 A_Chase("","")
		UNDP C 1 A_Chase
		UNDP CC 1 A_Chase("","")
		UNDP D 1 A_Chase
		UNDP DD 1 A_Chase("","")
		UNDP D 1 A_Chase
		UNDP DD 1 A_Chase("","")
		Loop
	Run:
		UNDP A 0 A_Jump (24, "See")
		UNDP A 0 A_SetSpeed(4)
		UNDP A 1 A_Chase
		UNDP A 1 A_Chase("","")
		UNDP A 1 A_Chase
		UNDP A 1 A_Chase("","")
		UNDP B 1 A_Chase
		UNDP B 1 A_Chase("","")
		UNDP B 1 A_Chase
		UNDP B 1 A_Chase("","")
		UNDP C 1 A_Chase
		UNDP C 1 A_Chase("","")
		UNDP C 1 A_Chase
		UNDP C 1 A_Chase("","")
		UNDP D 1 A_Chase
		UNDP D 1 A_Chase("","")
		UNDP D 1 A_Chase
		UNDP D 1 A_Chase("","")
		Loop
	Missile:
		UNDP E 10 A_FaceTarget
		UNDP F 0 A_PlaySound("UndeadPriest/Shoot",6)
		UNDP FFF 0 A_CustomMissile("EnemyChaingunTracer",58,14,random(-11,11))
		UNDP FF 0 A_SpawnItemEx("ShotgunCasing", 16,0,60, random(3,4), random(-1,1), random(4,6), random(-55,-80),SXF_NOCHECKPOSITION)
		UNDP F 6 BRIGHT LIGHT(ZOMBIEATK)
		UNDP E 2 A_MonsterRefire(30,"See")
		UNDP E 10 A_FaceTarget
		UNDP F 0 A_PlaySound ("UndeadPriest/Shoot2",7)
		UNDP FFF 0 A_CustomMissile("EnemyChaingunTracer",52,-10,random(-11,11))
		UNDP FF 0 A_SpawnItemEx("ShotgunCasing", 12,0,54, random(3,4), random(-1,1), random(4,6), random(55,80),SXF_NOCHECKPOSITION)
		UNDP F 6 BRIGHT LIGHT(ZOMBIEATK)
		UNDP E 10
		Goto See
	Pain:
		UNDP G 3
		UNDP G 3 A_Pain
		"####" "#" 0 A_AlertMonsters(256) //mxd
		goto See
	Death:
		UNDP H 6
		UNDP I 6 A_Scream
		UNDP JK 6
		UNDP L 6 A_NoBlocking
		UNDP M 6
		UNDP N -1
		Stop
	Raise:
		UNDP NMLKJIH 6
		goto See
	Idle:
		UNDP AAAAAA 1 A_Wander
		UNDP A 0 A_Look
		UNDP BBBBBB 1 A_Wander
		UNDP A 0 A_Look
		UNDP CCCCCC 1 A_Wander
		UNDP A 0 A_Look
		UNDP DDDDDD 1 A_Wander
		UNDP A 0 A_Look
		Loop
	}
}

ACTOR Hostage : Guard
{
	//$Category Monsters (Wolf3D)/NPCs
	//$Title Hostage
	//$Color 4
	Obituary "$HOSTAGE"
	painchance 255
	Scale 1.25 //mxd
	DropItem ""
	SeeSound ""
	+FRIENDLY
	States
	{
	Spawn:
		HOST A 0
		Goto SkillCheck
	Missile:
		Stop
	Pain:
		HOST E 3
		HOST E 3 A_Pain
		"####" "#" 0 A_AlertMonsters(256) //mxd
		goto See
	Death:
		HOST E 5
		HOST F 5 A_Scream
		HOST G 5 A_NoBlocking
		HOST H 5
		HOST I -1
		stop
	Raise:
		HOST IHGFE 5
		Goto See
	}
}

ACTOR DirtyDarren : Guard
{
	//$Category Monsters (Wolf3D)/NPCs
	//$Title Dirty Darren
	//$Color 4
	Obituary "$DARREN"
	Translation 1
	painchance 255
	Scale 1.23 //mxd
	DropItem ""
	SeeSound ""
	+FRIENDLY
	+USESPECIAL
	states
	{
	Spawn:
		DARR N 0
		Goto SkillCheck
	Missile:
		"####" EF 5 A_FaceTarget
		"####" G 0 A_PlaySound("nazi/pistol",CHAN_WEAPON)
		"####" G 8 BRIGHT LIGHT(NAZIFIRE) A_CustomMissile("ThompsonTracer",32,0,random(-8,8))
		"####" F 8
		"####" F 0 A_Jump(256,"See")
		Stop
	}
}

ACTOR GeneralAllied : DirtyDarren
{
	//$Category Monsters (Wolf3D)/NPCs
	//$Title General Miller
	//$Color 4
	Obituary "$MILLER"
	Translation "0:0=0:0"
	Scale 1.35 //mxd
	States
	{
	Spawn:
		GENR O 0
		Goto SkillCheck
	Death:
		"####" I 5
		"####" J 5 A_Scream
		"####" K 5 A_NoBlocking
		"####" LM 5
		"####" N -1
		stop
	Raise:
		"####" NMLKJI 5
		Goto See
	}
}

ACTOR Marine1 : DirtyDarren
{
	//$Category Monsters (Wolf3D)/NPCs
	//$Title Marine (1)
	//$Color 4
	Obituary "$MARINE"
	Scale 1.3 //mxd
	states
	{
	Spawn:
		PARS A 0
		Goto SkillCheck
	Death:
		"####" J 5
		"####" K 5 A_Scream
		"####" L 5 A_NoBlocking
		"####" MN 5
		"####" O -1
		stop
	Pain:
		"####" I 3
		"####" I 3 A_Pain
		"####" I 0 A_Jump(256,"See")
		Stop
	Raise:
		"####" ONMLKJ 5
		Goto See
	}
}

ACTOR Marine2 : RandomSpawner
{
	//$Category Monsters (Wolf3D)/NPCs
	//$Title Marine (2)
	//$Color 4
	DropItem Marine2A
	DropItem Marine2B
}

ACTOR Marine2A : Marine1
{
	states
	{
	Spawn:
		ARMH A 0
		Goto SkillCheck
	}
}

ACTOR Marine2B : Marine1
{
	states
	{
	Spawn:
		MARH A 0
		Goto SkillCheck
	}
}

ACTOR Marine_Talk1 : Marine2A
{
	//$Category Conversion Stuff (Wolf3D)
	//$Title Talking Marine 1
	//$Color 5
}

ACTOR Marine_Talk2 : Marine2B
{
	//$Category Conversion Stuff (Wolf3D)
	//$Title Talking Marine 2
	//$Color 5
}

ACTOR Dummy_Talk1
{
	//$Category Conversion Stuff (Wolf3D)
	//$Title Talking Dummy (1)
	//$Color 5
	Radius 8
	Height 64
	+SOLID
}

ACTOR Dummy_Talk2
{
	//$Category Conversion Stuff (Wolf3D)
	//$Title Talking Dummy (2)
	//$Color 5
	Radius 8
	Height 64
	+SOLID
}

ACTOR BlackGuy : DirtyDarren
{
	//$Category Monsters (Wolf3D)/NPCs
	//$Title Black Guy
	//$Color 4
	Obituary "$NIGGAGUY"
	Height 75 //mxd. Players shouldn't be able to jump on this guy
	Scale 1.35 //mxd
	states
	{
	Spawn:
		BLCK N 0
		Goto SkillCheck
	}
}