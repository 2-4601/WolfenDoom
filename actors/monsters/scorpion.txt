actor Scorpion 11333
{
//$Category Monsters (Wolf3D)
//$Title Scorpion (normal)
//$Color 4
Radius 10
Height 12
Scale 0.6
Mass 20
Speed 1.2
Health 20
MeleeRange 16
MaxStepHeight 16
MaxDropOffHeight 128
MeleeDamage 3
Damage 1 //jumpattacks
DeathSound "rat/death"
Obituary "%o was stinged by a scorpion"
BloodType "ScorpionBlood"
Monster
+FLOORCLIP
+LOOKALLAROUND
+DONTMORPH
+NOTRIGGER
-COUNTKILL
States
	{
	Spawn:
	SCRP A 0 NoDelay A_SetScale(ScaleX + frandom(-0.05, 0.05), ScaleY + frandom(-0.05, 0.05))
	goto Idle
	Idle:
	SCRP A 4 A_LookEx(0, 0, 0, 512)
	loop
	See:
	SCRP AAAA 1 A_Chase
	SCRP A 0 A_JumpIf(Random(1, 16) == 5, "Procrastinate")
	SCRP BBBB 1 A_Chase
	SCRP B 0 A_JumpIf(Random(1, 16) == 5, "Procrastinate")
	NULL A 0 A_JumpIf(Random(1, 24) == 5, "TryJump")
	loop
	Melee:
	NULL A 0 A_FaceTarget
	NULL A 0 A_PlaySound("spider/see", CHAN_AUTO, 0.5)
	SCRP AB 6 A_MeleeAttack
	goto See
	TryJump:
	NULL A 0 A_CheckFloor("Jump") //don't jump if mid-air
	goto See
	Jump:
	NULL A 0 ThrustThing(angle*256/360, Random(4, 6), 0, 0)
	NULL A 0 ThrustThingZ(0, Random(30, 40), 0, 1)
	SCRP C 15
	goto See
	Missile:
	SCRP A 0 A_Jumpifcloser(112, 2)
	Goto See
	SCRP AB 4 A_FaceTarget
	SCRP A 8 A_SkullAttack
	SCRP B 5 A_Gravity
	Goto See
	TryJump:
	NULL A 0 A_CheckFloor("Jump") //don't jump if mid-air
	goto See
	Procrastinate:
	"----" "#" 35
	"----" "#" 0 A_JumpIf(Random(1, 2) == 1, "Procrastinate")
	goto Idle
	Death:
	NULL A 0 A_ScreamAndUnblock
	NULL AAAAAAAAAAAAAAA 0 A_SpawnItemEx("ScorpionChunk", 0, 0, 4, Random(-2, 2), Random(-2, 2), Random(5, 10), Random(0, 256), 0, 100)
	stop
	}
}

actor ScorpionBig 11334
{
//$Category Monsters (Wolf3D)
//$Title Scorpion (big)
//$Color 4
Radius 32
Height 48
Scale 1.0
Mass 150
Speed 1.2
Health 250
MeleeRange 32
MaxStepHeight 32
MaxDropOffHeight 256
MeleeDamage 10
Damage 2 //jumpattacks
SeeSound "scorpion/see"
PainSound "scorpion/pains"
MeleeSound "scorpion/attacks"
DeathSound "scorpion/death"
Obituary "%o was nailed by a big scorpion"
BloodType "BScorpionBlood"
Monster
+FLOORCLIP
+LOOKALLAROUND
+DONTMORPH
+NOTRIGGER
-COUNTKILL
States
	{
	Spawn:
	SCR2 A 0 NoDelay A_SetScale(ScaleX + frandom(-0.05, 0.05), ScaleY + frandom(-0.05, 0.05))
	goto Idle
	Idle:
	SCR2 A 4 A_LookEx(0, 0, 0, 512)
	loop
	See:
	SCR2 AAAA 1 A_Chase
	SCR2 A 0 A_JumpIf(Random(1, 16) == 5, "Procrastinate")
	SCR2 BBBB 1 A_Chase
	SCR2 B 0 A_JumpIf(Random(1, 16) == 5, "Procrastinate")
	NULL A 0 A_JumpIf(Random(1, 24) == 5, "TryJump")
	loop
	Melee:
	NULL A 0 A_FaceTarget
	//NULL A 0 A_PlaySound("scorpion/attacks", CHAN_AUTO, 0.5)
	SCR2 AB 6 A_MeleeAttack
	goto See
	TryJump:
	NULL A 0 A_CheckFloor("Jump") //don't jump if mid-air
	goto See
	Jump:
	NULL A 0 ThrustThing(angle*256/360, Random(4, 6), 0, 0)
	NULL A 0 ThrustThingZ(0, Random(30, 40), 0, 1)
	NULL A 0 A_PlaySound("scorpion/fall", CHAN_AUTO, 1.5)
	SCR2 C 15
	goto See
	Missile:
	SCR2 A 0 A_Jump(256,"Spit","Sting")
	Spit:
	SCR2 AB 4 A_FaceTarget
	SCR2 B 8 A_CustomMissile("ScorpionSpit",8,0,0,2,3)
	Goto See
	Sting:
	SCR2 AB 4 A_FaceTarget
	SCR2 B 8 A_CustomMissile("ScorpionSting",24,0,0,2,3)
	Goto See
	Procrastinate:
	"----" "#" 35
	"----" "#" 0 A_JumpIf(Random(1, 2) == 1, "Procrastinate")
	goto Idle
	Death:
	NULL A 0 A_ScreamAndUnblock
	NULL AAAAAAAAAAAAAAA 0 A_SpawnItemEx("BigScorpionChunk", 0, 0, 4, Random(-2, 2), Random(-2, 2), Random(5, 10), Random(0, 256), 0, 100)
	stop
	}
}

ACTOR ScorpionSpit
{
Scale 0.6
Radius 13
Height 8
Speed 28
VSpeed 20
Damage 10
PROJECTILE
-NOGRAVITY
Gravity 0.4
DamageType PoisonCloud
PoisonDamage 20
SeeSound "scorpion/spit"
States
	{
	Spawn:
	SPIT A 0 NoDelay A_SetScale(ScaleX + frandom(-0.05, 0.05), ScaleY + frandom(-0.05, 0.05))
	Goto See
	See:
	SPIT A 3 A_SpawnItemEx("BScorpionFlyingBlood", 0, 0, 0, 0, 0, 0, 0, 128)
	Loop
	}
}

ACTOR ScorpionSting
{
Scale 0.7
Radius 13
Height 8
Speed 34
VSpeed 20
Damage 10
PROJECTILE
-NOGRAVITY
Gravity 0.4
DamageType PoisonCloud
PoisonDamage 20
SeeSound "scorpion/spit"
States
	{
	Spawn:
	SPIT A 0 NoDelay A_SetScale(ScaleX + frandom(-0.05, 0.05), ScaleY + frandom(-0.05, 0.05))
	Goto See
	See:
	SPIT A 3 A_SpawnItemEx("BScorpionFlyingBlood", 0, 0, 0, 0, 0, 0, 0, 128)
	Loop
	}
}

Actor ScorpionChunk
{
Radius 8
Height 8
Scale 0.3
Mass 50
Damage 0
BounceType "Doom"
BounceCount 1
Gravity 0.6
DeathSound "gore/splat"
Projectile
-NOGRAVITY
-SOLID
+CANBOUNCEWATER
+BOUNCEAUTOOFF
+MOVEWITHSECTOR
+FORCEXYBILLBOARD
+DONTSPLASH
States
	{
	Spawn:
	NULL A 0 NoDelay A_SetScale(ScaleX + frandom(-0.1, 0.1), ScaleY + frandom(-0.1, 0.1))
	NULL A 0 A_Jump(256, "Chunk1", "Chunk2", "Chunk3", "Chunk4")
	stop
	Death:
	"----" "#" 0 A_SpawnItemEx("ScorpionBlood", 0, 0)
	Goto Lay
	Chunk1:
	SCRG A 3 A_SpawnItemEx("ScorpionFlyingBlood", Random(-1, 1), Random(-1, 1))
	loop
	Chunk2:
	SCRG B 3 A_SpawnItemEx("ScorpionFlyingBlood", Random(-1, 1), Random(-1, 1))
	loop
	Chunk3:
	SCRG C 3 A_SpawnItemEx("ScorpionFlyingBlood", Random(-1, 1), Random(-1, 1))
	loop
	Chunk4:
	SCRG D 3 A_SpawnItemEx("ScorpionFlyingBlood", Random(-1, 1), Random(-1, 1))
	loop
	Lay:
	"----" "#" 250 A_CheckSight("Vanish")
	loop
	Vanish:
	NULL A 0
	stop
	}
}

Actor BigScorpionChunk : ScorpionChunk
{
Scale 1.0
Mass 60
Gravity 0.7
States
	{
	Spawn:
	NULL A 0 NoDelay A_SetScale(ScaleX + frandom(-0.1, 0.1), ScaleY + frandom(-0.1, 0.1))
	NULL A 0 A_Jump(256, "Chunk1", "Chunk2", "Chunk3", "Chunk4")
	stop
	Death:
	"----" "#" 0 A_SpawnItemEx("BScorpionBlood", 0, 0)
	Goto Lay
	Chunk1:
	SC2G A 9 A_SpawnItemEx("BScorpionFlyingBlood", Random(-1, 1), Random(-1, 1))
	loop
	Chunk2:
	SC2G B 9 A_SpawnItemEx("BScorpionFlyingBlood", Random(-1, 1), Random(-1, 1))
	loop
	Chunk3:
	SC2G C 9 A_SpawnItemEx("BScorpionFlyingBlood", Random(-1, 1), Random(-1, 1))
	loop
	Chunk4:
	SC2G D 9 A_SpawnItemEx("BScorpionFlyingBlood", Random(-1, 1), Random(-1, 1))
	loop
	Lay:
	"----" "#" 250 A_CheckSight("Vanish")
	loop
	Vanish:
	NULL A 0
	stop
	}
}