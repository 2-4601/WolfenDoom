Actor BatFamiliar
{
	//$Category Monsters (Wolf3D)
	//$Title Bat
	//$Color 4
	Health 10
	Radius 12
	Height 24
	FloatSpeed 1.33333333
	Speed 4.66666667
	PainChance 200
	MONSTER
	+NOGRAVITY
	+FLOAT
	SeeSound "batfam/idle"
	PainSound "batfam/pain"
	DeathSound "batfam/death"
	ActiveSound "batfam/idle"
	HitObituary "$BAT"
	States
	{
	Spawn:
		BFAM ABCB 3 A_Look
		Loop
	See:
		BFAM A 1 A_Chase
		BFAM AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		BFAM B 1 A_Chase
		BFAM BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		BFAM C 1 A_Chase
		BFAM CC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		BFAM D 1 A_Chase
		BFAM DD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		Loop
	Melee:
		BFAM A 3 A_FaceTarget
		BFAM B 3 A_CustomMeleeAttack(Random(1,8)*1,"batfam/idle")
		BFAM CB 3 A_FaceTarget
		Goto See
	Pain:
		BFAM A 2
		BFAM A 2 A_Pain
		Goto See
	Death:
		BFAM D 5 A_ScreamAndUnblock
		BFAM EFD 5
		Goto Death+1
	Crash:
		TNT1 A 0 A_SetFloorClip
		BFAM G 6 A_PlaySound("batfam/crash")
		BFAM H 7
		BFAM I 8
		BFAM J -1
		Stop
	}
}