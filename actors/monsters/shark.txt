ACTOR SharkBlocker
{
	//$Category Monsters (Wolf3D)/Critters
	//$Title Shark (blocker)
	//$Color 4
	Height 1
	Radius 256
	Mass 99999999
	Species "Player"
	+SOLID
	+NOGRAVITY
	+THRUSPECIES
	+DONTSPLASH
}

//we need it smaller for narrow places/situations, not only the big one for sharks (check map c1m6)
ACTOR SharkBlockerMini : SharkBlocker
{
	//$Category Monsters (Wolf3D)/Critters
	//$Title Shark (blocker, mini)
	//$Color 4
	Radius 32
}

ACTOR Shark
{
	//$Category Monsters (Wolf3D)/Critters
	//$Title Shark
	//$Color 4
	Obituary "$SHARK"
	Health 100
	Mass 120
	Speed 2.5
	FloatSpeed 1
	Radius 30
	Height 30
	SeeSound "shark/sight"
	DeathSound "shark/death"
	ActiveSound "shark/active"
	Monster
	+FLOORCLIP
	+NOGRAVITY
	+FLOAT
	+FORCEXYBILLBOARD
	+NOINFIGHTING
	States
	{
	Spawn:
		SHRK BC 10 A_Look
		Loop
	See:
		SHRK A 0 A_JumpIf(waterlevel>=3,2)
		SHRK A 0 ThrustThingZ(0,4,1,1)
		SHRK A 1 A_Chase
		SHRK AAA 1 A_Chase("","")
		SHRK B 0 A_JumpIf(waterlevel>=3,2)
		SHRK B 0 ThrustThingZ(0,4,1,1)
		SHRK B 1 A_Chase
		SHRK BBB 1 A_Chase("","")
		SHRK C 0 A_JumpIf(waterlevel>=3,2)
		SHRK C 0 ThrustThingZ(0,4,1,1)
		SHRK C 1 A_Chase
		SHRK CCC 1 A_Chase("","")
		SHRK D 0 A_JumpIf(waterlevel>=3,2)
		SHRK D 0 ThrustThingZ(0,4,1,1)
		SHRK D 1 A_Chase
		SHRK DDD 1 A_Chase("","")
		Loop
	Missile:
		SHRK A 10 A_FaceTarget
		SHRK A 8 A_SkullAttack
		Wait
	Melee:
		SHRK A 10 A_FaceTarget
		SHRK F 8 A_CustomMeleeAttack(random(1,5)*5,"Chomp","Swipe")
		SHRK A 10
		Goto See
	Death:
		SHRK A 0 A_Scream
		SHRK AAAAAAAA 0 A_CustomMissile("Gibs1",random(20,40),0,random(0,360),2,random(-100,100))
		stop
	}
}

//from IOP
ACTOR Gibs1
{
	Radius 10
	Height 10
	Mass 6
	Speed 14
	BounceType Doom
	+CLIENTSIDEONLY
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(256,1,2,3,4,5,6,7,8,9,10,11,12,13)
		GIB1 AB 0 A_Jump(256,"End")
		GIB2 ABC 0 A_Jump(256,"End")
		GIB3 BC 0 A_Jump(256,"End")
		GIB4 ABC 0 A_Jump(256,"End")
		GIB5 ABC 0 A_Jump(256,"End")
	End:
		"####" "#######" 4 A_CustomMissile("Bloodflysm",0,0,random(0,360),2,random(-100,100))
		"####" "#" 700 A_CustomMissile("BloodFlySM",0,0,random(0,360),2,random(-100,100))
		Stop
	}
}

ACTOR BloodTrail
{
	Radius 1
	Height 1
	Projectile
	+NOCLIP
	+CLIENTSIDEONLY
	States
	{
	Spawn:
		BLUD CBA 3
		Stop
	}
}

ACTOR BloodTrailSM : BloodTrail
{
	Scale 0.5
}

ACTOR BloodFly
{
	Radius 0
	Height 0
	Mass 50
	Speed 6
	Projectile
	-NOGRAVITY
	+CLIENTSIDEONLY
	States
	{
	Spawn:
		BLUD C 2 A_SpawnItemEx("BloodTrail")
		loop
	Death:
		BLUD CBA 8
		Stop
	}
}

ACTOR BloodFly2 : BloodFly
{
	Scale 0.6
	Speed 8
	States
	{
	Spawn:
		BLUD C 1
		loop
	Death:
		BLUD CBA 4
		Stop
	}
}

ACTOR BloodFlySM : BloodFly
{
	Scale 0.5
	Mass 100
	Speed 8
	States
	{
	Spawn:
		BLUD C 1 A_SpawnItemEx("BloodTrailSM")
		loop
	}
}