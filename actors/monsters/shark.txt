ACTOR SharkBlocker 7999
{
	//$Category Monsters (Wolf3D)
	//$Title Shark (blocker)
	//$Color 4
	Height 1
	Radius 256
	Mass 99999999
	Species "Player"
	+SOLID
	+NOGRAVITY
	+THRUSPECIES
}

ACTOR Shark 8000
{
	//$Category Monsters (Wolf3D)
	//$Title Shark
	//$Color 4
	obituary "%o was shredded by a shark."
	health 100
	mass 120
	speed 10
	Radius 30
	Height 30
	seesound "shark/sight"
	deathsound "shark/death"
	activesound "shark/active"
	MONSTER
	+FLOORCLIP
	+NOGRAVITY
	+FLOAT
	States
	{
	Spawn:
		SHRK BC 10 A_Look
		loop
	See:
		SHRK ABCB 4 A_GiveInventory("Chase_Water")
		loop
	Missile:
		SHRK A 10 A_FaceTarget
		SHRK A 4 A_SkullAttack
		SHRK A 4
		Goto Missile+2
	Melee:
		SHRK A 10 A_FaceTarget
		SHRK F 8 A_CustomMeleeAttack (random (1, 5) *5, "Chomp", "Swipe")
		SHRK A 10
		goto See
	Death:
		SHRK A 0
		SHRK A 0 A_Scream
		SHRK AAAAAAAA 0 A_CustomMissile("Gibs1",random(20,40),0,random(0,360),2,random(-100,100))
		stop
	}
}

Actor Chase_Water : CustomInventory
{
	Inventory.MaxAmount 0
	+Inventory.AutoActivate
	States
	{
	Pickup:
		TNT1 A 0 A_jumpif(waterlevel < 3, "Waterlimit")
		TNT1 A 0 A_Chase
		Stop
	Waterlimit:
		TNT1 A 0 ThrustThingZ(0, 4, 1, 1)
		TNT1 A 0 A_Chase
		Stop
	}
}

//from IOP
ACTOR Gibs1
{
	Radius 10
	Height 10
	Mass 6
	Speed 14
	BounceType Doom
	+CLIENTSIDEONLY
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(256,1,2,3,4,5,6,7,8,9,10,11,12,13)
		GIB1 AB 0 A_Jump(256,"End")
		GIB2 ABC 0 A_Jump(256,"End")
		GIB3 BC 0 A_Jump(256,"End")
		GIB4 ABC 0 A_Jump(256,"End")
		GIB5 ABC 0 A_Jump(256,"End")
	End:
		"####" "#######" 4 A_CustomMissile("Bloodflysm",0,0,random(0,360),2,random(-100,100))
		"####" "#" 700 A_CustomMissile("BloodFlySM",0,0,random(0,360),2,random(-100,100))
		Stop
	}
}

actor BloodTrail
{
	Radius 1
	Height 1
	Projectile
	+NOCLIP
	+CLIENTSIDEONLY
	States
	{
	Spawn:
		BLUD CBA 3
		Stop
	}
}

actor BloodTrailSM : BloodTrail
{
	Scale 0.5
}

ACTOR BloodFly
{
	Radius 0
	Height 0
	Mass 50
	Speed 6
	Projectile
	-NOGRAVITY
	+CLIENTSIDEONLY
	States
	{
	Spawn:
		BLUD C 2 A_SpawnItemEx("BloodTrail")
		loop
	Death:
		BLUD CBA 8
		Stop
	}
}

ACTOR BloodFly2 : BloodFly
{
	Scale 0.6
	Speed 8
	States
	{
	Spawn:
		BLUD C 1
		loop
	Death:
		BLUD CBA 4
		Stop
	}
}

ACTOR BloodFlySM : BloodFly
{
	Scale 0.5
	Mass 100
	Speed 8
	States
	{
	Spawn:
		BLUD C 1 A_SpawnItemEx("BloodTrailSM")
		loop
	}
}