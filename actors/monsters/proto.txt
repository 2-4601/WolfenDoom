ACTOR ProtoDrone : Nazi //ozy81
{
	//$Category Monsters (Wolf3D)
	//$Title Prototype Drone
	//$Color 4
	Scale 0.87
	Health 300
	Radius 32
	Mass 400
	Speed 2.66666667
	PainChance 128
	-FLOORCLIP
	+FLOAT
	+LOOKALLAROUND
	+NOBLOOD
	+NOGRAVITY
	+NOINFIGHTING
	SeeSound "proto/see"
	PainSound "drone_pain"
	DeathSound "astrostein/explosion"
	ActiveSound "proto/see"
	Obituary "$PDRONE"
	States
	{
	Spawn:
		PROT A 0
		Goto Look
	See:
		PROT A 1 A_Chase
		"####" AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		Loop
	Idle:
		PROT AAA 1 A_Wander
		"####" A 0 A_Look
		Loop
	Missile:
		PROT B 0 A_Jump(192,"Missile2")
		"####" B 5 A_FaceTarget
		"####" B 0 A_PlaySound("astrochaingun/fire", CHAN_WEAPON)
		"####" CDE 8 LIGHT(PROTOFIRE) A_CustomMissile("EnemyProtoTracer",ScaleX*2,ScaleY*-16,random(-2,2))
		"####" B 5 A_FaceTarget
		"####" B 0 A_PlaySound("astrochaingun/fire", CHAN_WEAPON)
		"####" CDE 8 LIGHT(PROTOFIRE) A_CustomMissile("EnemyProtoTracer",ScaleX*2,ScaleY*-16,random(-2,2))
		Goto See
	Missile2:
		PROT N 0 A_Jump(192,"Missile3")
		"####" N 5 A_FaceTarget
		"####" N 0 A_PlaySound("chaingun/fire", CHAN_WEAPON)
		"####" FGHI 2 LIGHT(NAZIFIRE) A_CustomMissile("EnemyPistolTracer",ScaleX*2,ScaleY*16,random(-2,2))
		"####" I 0 A_SpawnItemEx("9MMCasing", ScaleX*2, ScaleY*16, random(-2,2), RandomPick(-3, 3), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		"####" N 5 A_FaceTarget
		"####" N 0 A_PlaySound("chaingun/fire", CHAN_WEAPON)
		"####" FGHI 2 LIGHT(NAZIFIRE) A_CustomMissile("EnemyPistolTracer",ScaleX*2,ScaleY*16,random(-2,2))
		"####" I 0 A_SpawnItemEx("9MMCasing", ScaleX*2, ScaleY*16, random(-2,2), RandomPick(-3, 3), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION)
		Goto See
	Missile3:
		"####" N 5 A_FaceTarget
		"####" N 0 {A_PlaySound("chaingun/fire", CHAN_WEAPON);
								A_PlaySound("astrochaingun/fire", CHAN_WEAPON);}
		"####" JKLM 5 LIGHT(PROTOFIRE) {A_CustomMissile("EnemyPistolTracer",ScaleX*2,ScaleY*16,random(-2,2));
																		A_SpawnItemEx("9MMCasing", ScaleX*2, ScaleY*16, random(-2,2), RandomPick(-3, 3), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION);
																		A_CustomMissile("EnemyProtoTracer",ScaleX*2,ScaleY*-16,random(-2,2));} 
		
		"####" N 5 A_FaceTarget
		"####" N 0 {A_PlaySound("chaingun/fire", CHAN_WEAPON);
								A_PlaySound("astrochaingun/fire", CHAN_WEAPON);}
		"####" JKLM 5 LIGHT(PROTOFIRE) {A_CustomMissile("EnemyPistolTracer",ScaleX*2,ScaleY*16,random(-2,2));
																		A_SpawnItemEx("9MMCasing", ScaleX*2, ScaleY*16, random(-2,2), RandomPick(-3, 3), random(-1,1), random(2,4), random(-55,-80),SXF_NOCHECKPOSITION);
																		A_CustomMissile("EnemyProtoTracer",ScaleX*2,ScaleY*-16,random(-2,2));}
		Goto See
	Melee: //uses the same attack of LaserShark
		PROT B 10 A_FaceTarget 
		"####" N 5 A_CustomRailgun (random(5,10), 0, "990000", "FF0000", RGF_NOPIERCING | RGF_FULLBRIGHT, 1, 0, "MeasurePuff", 0, 0, 768, 35*3, 0.5, 0.8, NONE, ScaleY*14)
		"####" B 4 A_FaceTarget 
		Goto See 
	Pain:
	Pain.Dagger:
		PROT N 3
		"####" O 3 A_Pain
		"####" N 6
		Goto See
	Death:
		PROT OPQRS 3
		"####" T 1 {A_Scream;
								A_SpawnItemEx("ProtoExplosion_Medium", 0, 0, 48);
								A_NoBlocking;}
		"####" TTTTTTTTTTTT 0 A_SpawnItemEx("Debris_MetalJunk", random(0,8), random(0,16), random(0,48), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		"####" TTTTTTTTTTTT 0 A_SpawnItemEx("Debris_MetalJunk", random(8,16), random(16,32), random(48,64), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		"####" TTTTTTTTTTTT 0 A_SpawnItemEx("Debris_MetalJunk", random(16,32), random(32,48), random(64,80), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		"####" U 3
		Stop
	}
}