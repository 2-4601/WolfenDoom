ACTOR Meal : Health
{
	//$Category Health (Wolf3D)
	//$Title Meals (2 variants+5)
	//$Color 6
	Inventory.Amount 5
	Inventory.PickupMessage "$MEAL"
	States
	{
	Spawn:
		MEAL A -1 NoDelay A_Jump(128,1)
		"####" B -1
		Stop
	}
}

ACTOR StackedMeal : Meal
{
	Radius 1
	Height 1
	+CANPASS
	+NOGRAVITY
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_Jump(256,"MealA","MealB")
		MealA:
		MEAL A 1
		Loop
		MealB:
		MEAL B 1
		Loop
	}
}

ACTOR Dogfood : Health
{
	//$Category Health (Wolf3D)
	//$Title Dog Food (+2)
	//$Color 6
	Inventory.Amount 2
	Inventory.PickupMessage "$DOGFOOD"
	States
	{
	Spawn:
		DGFD A -1
		Stop
	}
}

ACTOR Medikit_Small : Health
{
	//$Category Health (Wolf3D)
	//$Title Medikit (small+10)
	//$Color 6
	Inventory.Amount 10
	Inventory.PickupMessage "$MEDSML"
	States
	{
	Spawn:
		MEDI A -1
		Stop
	}
}

ACTOR Medikit_Medium : Health
{
	//$Category Health (Wolf3D)
	//$Title Medikit (medium+20)
	//$Color 6
	Inventory.Amount 20
	Inventory.PickupMessage "$MEDMID"
	States
	{
	Spawn:
		MEDI B -1
		Stop
	}
}

ACTOR Medikit_Large : Health
{
	//$Category Health (Wolf3D)
	//$Title Medikit (large+40)
	//$Color 6
	Inventory.Amount 40
	Inventory.PickupMessage "$MEDBIG"
	States
	{
	Spawn:
		MEDI C -1
		Stop
	}
}

ACTOR Medikit_Fullhealth : Health
{
	//$Category Health (Wolf3D)
	//$Title Medikit (large+40)
	//$Color 6
	Inventory.Amount 200
	Inventory.PickupMessage "$MEDBIG"
	States
	{
	Spawn:
		TNT1 A -1
		Stop
	}
}

// Used to limit the amount of Vitality Serums the player can buy. - Talon1024
ACTOR VitalityToken : CustomInventory {
	Radius 20
	Scale 0.9
	Inventory.MaxAmount 10
	Inventory.PickupMessage "$SERUM"
	+INVENTORY.IGNORESKILL
	+INVENTORY.UNTOSSABLE
	+COUNTITEM
	States
	{
		Spawn:
			VSRM ABC 6 BRIGHT
			Loop
		Pickup:
			VSRM A 0 A_GiveInventory("VitalitySerum")
		Use:
			// Does nothing except prevent the item from disappearing from the player's inventory.
			VSRM A 0
			Fail
	}
}

ACTOR VitalitySerum : CustomInventory {
	//$Category Health (Wolf3D)
	//$Title Vitality Serum (max+5)
	//$Color 6
	//$Category Powerups
	//$Title "Vitality Serum"
	Radius 20
	Scale 0.9
	Inventory.PickupMessage "$SERUM"

	+INVENTORY.IGNORESKILL
	+INVENTORY.UNTOSSABLE
	+COUNTITEM
	States
	{
		Spawn:
			VSRM ABC 6 BRIGHT
			Loop
		Pickup:
			VSRM A 0 {
				if (GetSpawnHealth() < 150) {
					ACS_NamedExecuteAlways("VSerum Max Health Boost");
				}
				A_GiveInventory("Meal");
			}
			Stop
	}
}

ACTOR FieldKit : HealthPickup
{
	//$Category Health (Wolf3D)
	//$Title Field Kit (max +15)
	//$Color 6
	Health 25
	HealthPickup.AutoUse 1
	Inventory.Icon "FKITC0"
	Inventory.MaxAmount 15
	Inventory.PickupMessage "$FKIT"
	Inventory.PickupSound "Misc/I_PkUp"
	Inventory.UseSound "Misc/I_PkUp"
	+COUNTITEM
	States
	{
	Spawn:
		FKIT AB 10
		Loop
	}
}

ACTOR BandagesPowerup : PowerRegeneration
{
	Powerup.Duration -10
	Powerup.Strength 1
}

ACTOR Bandages : PowerupGiver
{
	//$Category Health (Wolf3D)
	//$Title Bandages (+10)
	//$Color 6
	Inventory.Icon "BAIDB0"
	Inventory.MaxAmount 5
	Inventory.PickupMessage "$BAID"
	Powerup.Type "BandagesPowerup"
	+COUNTITEM
	States
	{
	Spawn:
		BAID A -1
		Stop
	}
}
