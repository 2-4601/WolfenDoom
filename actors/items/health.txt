ACTOR Meal : Health
{
	//$Category Health (BoA)
	//$Title Meals (2 variants+5)
	//$Color 6
	Scale 0.5
	Inventory.Amount 5
	Inventory.PickupMessage "$MEAL"
	Inventory.PickupSound "pickup/mealfood"
	States
	{
	Spawn:
		MEAL A -1 NoDelay A_Jump(128,1)
		"####" B -1
		Stop
	}
}

ACTOR StackedMeal : Meal
{
	Radius 1
	Height 1
	+CANPASS
	+NOGRAVITY
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_Jump(256,"MealA","MealB")
	MealA:
		MEAL A 1
		Loop
	MealB:
		MEAL B 1
		Loop
	}
}

ACTOR CupCake1 : Meal
{
	//$Title Cupcake, Pink (+1 health, shootable)
	Radius 4
	Height 8
	Health 1
	Mass 100
	Scale 0.08
	Inventory.Amount 1
	Inventory.PickupMessage "$CAKE"
	-DROPOFF
	+CANPASS
	+NOBLOOD
	+NOTAUTOAIMED
	+SHOOTABLE
	+SOLID
	States
	{
	Spawn:
		CUPK A 0 NODELAY A_Jump(256,"Set1","Set2","Set3")
	Set1:
		CUPK A -1 A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY)
		Stop
	Set2:
		CUPK B -1 A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY)
		Stop
	Set3:
		CUPK C -1 A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY)
		Stop
	Death:
		"####" "#" 0 A_UnSetSolid
		"####" "#" 0 A_PrintBold("$CAKEDIE", 3.0, "DBIGFONT")
		"####" "#" 0 A_PlaySound("bread/crumbs", CHAN_AUTO, frandom (0.5,0.8), FALSE, ATTN_NORM)
		"####" "########" 0 A_SpawnItemEx("Debris_Bread", random(0,16), random(0,16), random(4,16), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		Stop
	}
}

ACTOR CupCake2 : CupCake1
{
	//$Title Cupcake, White (+1 health, shootable)
	States
	{
	Spawn:
		CUPK D 0 NODELAY A_Jump(256,"Set1","Set2","Set3")
	Set1:
		CUPK D -1 A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY)
		Stop
	Set2:
		CUPK E -1 A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY)
		Stop
	Set3:
		CUPK F -1 A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY)
		Stop
	}
}

ACTOR CupCake3 : CupCake1
{
	//$Title Cupcake, Azure (+1 health, shootable)
	States
	{
	Spawn:
		CUPK G 0 NODELAY A_Jump(256,"Set1","Set2","Set3")
	Set1:
		CUPK G -1 A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY)
		Stop
	Set2:
		CUPK H -1 A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY)
		Stop
	Set3:
		CUPK I -1 A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY)
		Stop
	}
}

ACTOR CupCake4 : CupCake1
{
	//$Title Cupcake, Yellow (+1 health, shootable)
	States
	{
	Spawn:
		CUPK J 0 NODELAY A_Jump(256,"Set1","Set2","Set3")
	Set1:
		CUPK J -1 A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY)
		Stop
	Set2:
		CUPK K -1 A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY)
		Stop
	Set3:
		CUPK L -1 A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY)
		Stop
	}
}

ACTOR Dogfood : Health
{
	//$Category Health (BoA)
	//$Title Dog Food (+2)
	//$Color 6
	//$Sprite DOGFA0
	Scale 0.5
	Inventory.Amount 2
	Inventory.PickupMessage "$DOGFOOD"
	Inventory.PickupSound "pickup/mealfood"
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_Jump(256,1,2,3,4,5,6,7)
		DOGF A -1
		DOGF B -1
		DOGF C -1
		DOGF D -1
		DOGF E -1
		DOGF F -1
		DOGF G -1
		Stop
	}
}

ACTOR Medikit_Small : Health
{
	//$Category Health (BoA)
	//$Title Medikit (small+10)
	//$Color 6
	Scale 0.5
	Inventory.Amount 10
	Inventory.PickupMessage "$MEDSML"
	States
	{
	Spawn:
		MEDI A -1
		Stop
	}
}

ACTOR Medikit_Medium : Health
{
	//$Category Health (BoA)
	//$Title Medikit (medium+20)
	//$Color 6
	Scale 0.5
	Inventory.Amount 20
	Inventory.PickupMessage "$MEDMID"
	States
	{
	Spawn:
		MEDI B -1
		Stop
	}
}

ACTOR Medikit_Large : Health
{
	//$Category Health (BoA)
	//$Title Medikit (large+40)
	//$Color 6
	Scale 0.4
	Inventory.Amount 40
	Inventory.PickupMessage "$MEDBIG"
	States
	{
	Spawn:
		MEDI C -1 NoDelay A_SetScale(ScaleX * RandomPick(-1, 1), ScaleY)
		Stop
	}
}

ACTOR Medikit_Fullhealth : Health //needed by Halderman on HQs
{
	Inventory.Amount 200
	Inventory.PickupMessage "$MEDBIG"
	States
	{
	Spawn:
		TNT1 A -1
		Stop
	}
}

ACTOR FieldKit : HealthPickup
{
	//$Category Health (BoA)
	//$Title Field Kit (Item, +25)
	//$Color 6
	Health 25
	Scale 0.50
	HealthPickup.AutoUse 1
	Tag "$TAGFELDK"
	Inventory.Icon "FKITC0"
	Inventory.InterHubAmount 15
	Inventory.MaxAmount 15
	Inventory.PickupMessage "$FKIT"
	Inventory.PickupSound "Misc/I_PkUp"
	Inventory.UseSound "Misc/I_PkUp"
	States
	{
	Spawn:
		FKIT AB 10
		Loop
	}
}

ACTOR BandagesPowerup : PowerRegeneration
{
	Powerup.Duration -10
	Powerup.Strength 1
}

ACTOR Bandages : PowerupGiver
{
	//$Category Health (BoA)
	//$Title Bandages (+10)
	//$Color 6
	Scale 0.5
	Tag "$TAGBAIDS"
	Inventory.Icon "BAIDB0"
	Inventory.MaxAmount 5
	Inventory.PickupMessage "$BAID"
	Inventory.UseSound "pickup/bandage"
	Inventory.PickupSound "misc/health_pkup"
	Powerup.Type "BandagesPowerup"
	States
	{
	Spawn:
		BAID A -1
		Stop
	}
}