//////////////////////
// EXCLAMATION MARK //
//////////////////////
ACTOR Exclamation : SwitchableDecoration
{
	//$Category Misc (Wolf3D)
	//$Title Exclamation Mark
	//$Color 1
	//$Sprite EXCLA0
	Radius 32
	Height 64
	Scale 1.35
	RenderStyle "Translucent"
	Alpha 1.0
	+CLIENTSIDEONLY
	+NOGRAVITY
	States
	{
	Spawn:
		EXCL A 0
	Active:
		EXCL A 5 Bright
		EXCL A 0 A_CheckSight("Active") // Loop if there's no player in sight
		EXCL A 0 A_CheckRange(2048, "Active") // Loop if we're out of range of all players
		EXCL A 0 ACS_NamedExecuteAlways("BoA_CompassQueue",0) // If a player has seen us, add ourselves to the compass
	OnCompass:
		EXCL A -1 Bright
		Stop
	Inactive:
		TNT1 A -1
		Stop
	}
}

ACTOR ExclamationCompass : Exclamation
{
	//$Category Misc (Wolf3D)
	//$Title Objective Map Marker (Compass)
	//$Color 1
	//$Sprite EXCLC0
	Radius 20
	Height 8
	+INVISIBLE
	States
	{
	Spawn:
		EXCL C 5 NODELAY ACS_NamedExecuteAlways("BoA_CompassQueue", 0)
	Active:
		EXCL C -1 BRIGHT
		Stop
	Inactive:
		TNT1 A -1
		Stop
	}
}

ACTOR ExclamationTouchable : Exclamation
{
	//$Category Misc (Wolf3D)
	//$Title Exclamation Mark (inactive on touching)
	//$Color 1
	States
	{
	Spawn:
		EXCL A 0
	Active:
		EXCL A 5 Bright
		EXCL A 0 A_CheckSight("Active") // Loop if there's no player in sight
		EXCL A 0 A_CheckRange(2048, "Active") // Loop if we're out of range of all players
		EXCL A 0 ACS_NamedExecuteAlways("BoA_CompassQueue",0) // If a player has seen us, add ourselves to the compass
	OnCompass:
		EXCL # 5 Bright
		EXCL # 0 A_CheckRange(72.0, "OnCompass") // Only loop if there's no player within 72 units
		Goto OnCompassInactive
	Inactive:
		EXCL A 35 BRIGHT // Otherwise change to grey
	InactiveLoop:
		EXCL B 5
		EXCL B 0 A_CheckSight("InactiveLoop") // Loop if there's no player in sight
		EXCL B 0 A_CheckRange(2048, "InactiveLoop") // Loop if we're out of range of all players
		EXCL B 0 ACS_NamedExecuteAlways("BoA_CompassQueue",0) // If a player has seen us, add ourselves to the compass
	OnCompassInactive:
		EXCL B -1 Bright
		Stop
	}
}

ACTOR InteractionIcon
{
	Height 8
	Radius 64
	Scale 1.0
	RenderStyle "Translucent"
	Alpha 1.0
	-SOLID
	+CLIENTSIDEONLY
	+FLOATBOB
	+LOOKALLAROUND
	+NOBLOCKMAP
	+NOGRAVITY
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_CheckRange(128.0, "FadeAway", TRUE)
		ICNI A 1 BRIGHT
		Loop
	FadeAway:
		"####" A 10
		Goto Spawn
	}
}

ACTOR InteractionIcon25Health : InteractionIcon
{
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_CheckRange(128.0, "FadeAway", TRUE)
		TNT1 A 0 A_JumpIf(CallACS("PlayerHealthAbove25") == 1, "FadeAway")
		ICNI A 1 BRIGHT
		Loop
	FadeAway:
		TNT1 A 10
		Goto Spawn
	}
}

ACTOR ObjectiveIcon : MapMarker
{
	//$Category Misc (Wolf3D)
	//$Title Objective Map Marker (Automap)
	//$Color 1
	+NOBLOCKMAP
 	+NOGRAVITY
	+DONTSPLASH
	+INVISIBLE
	Scale 0.5
	States
	{
		Spawn:
			EXCL C -1 NoDelay ACS_NamedExecuteAlways("BoA_CompassQueue", 0)
			Stop
	}
}