//Player
Actor Stamina : Inventory
{
	Inventory.MaxAmount 100
}
Actor BoAPlayer : PlayerPawn
{
	Species "Player"
	+THRUSPECIES
	player.invulnerabilitymode "reflective"
	player.startitem "KnifeSilent"
	player.startitem "TrenchShotgunLoaded",8
	player.startitem "Browning5Loaded",4
	player.startitem "MP40Loaded",32
	player.startitem "G43Loaded",10
	player.startitem "Walther9mmLoaded",8
	player.startitem "Luger9mmLoaded",8
	player.startitem "StenLoaded",32
	player.startitem "Kar98kLoaded",5
	player.startitem "PyrolightLoaded",25
	player.startitem "PanzerschreckLoaded"
	player.startitem "GrenadePickup"
	Player.StartItem "Stamina", 100
	Player.ViewHeight 56
	Player.AttackZOffset 24
	Player.DisplayName "William Blazkowicz"
	Player.SideMove 1.0,0.5
	Player.ForwardMove 1.0,0.5
	Scale 1.2
	States
	{
	Spawn:
		PLAY A 0 NODELAY A_JumpIf(waterlevel<3,2)
		PLAY A 0 A_SpawnItemEx("Bubble",random(4,8),0,random(48,52),0,0,random(1,3),random(0,360),SXF_TRANSFERTRANSLATION,random(4,8))
		PLAY A 7
		Loop
	See:
		PLAY A 7
		PLAY A 0 A_JumpIf(waterlevel<3,2)
		PLAY A 0 A_SpawnItemEx("Bubble",random(4,8),0,random(48,52),0,0,random(1,3),random(0,360),SXF_TRANSFERTRANSLATION,random(4,8))
		PLAY B 7 A_CheckFloor("Step1")
	Otherleg:
		PLAY C 7
		PLAY C 0 A_JumpIf(waterlevel<3,2)
		PLAY C 0 A_SpawnItemEx("Bubble",random(4,8),0,random(48,52),0,0,random(1,3),random(0,360),SXF_TRANSFERTRANSLATION,random(4,8))
		PLAY D 7 A_CheckFloor("Step2")
		Goto See
	Step1:
		PLAY B 7 A_SpawnItemEx("PlayerStep", 0.0, 0.0, 11.1, 0.0, 0.0, 0.0, 0.0, SXF_NoCheckPosition | 128)
		Goto Otherleg
	Step2:
		PLAY D 7 A_SpawnItemEx("PlayerStep", 0.0, 0.0, 11.1, 0.0, 0.0, 0.0, 0.0, SXF_NoCheckPosition | 128)
		Goto See
	Pain:
		PLAY G 0 A_JumpIf(waterlevel<3,2)
		PLAY G 0 A_SpawnItemEx("Bubble",random(4,8),0,random(48,52),0,0,random(1,3),random(0,360),SXF_TRANSFERTRANSLATION,random(4,8))
		PLAY G 3 A_Pain
		Goto Spawn
	Missile:
		PLAY E 12
		Goto Spawn
	Melee:
		PLAY F 6 BRIGHT
		Goto Missile
	Death:
		PLAY H 0 A_JumpIf(waterlevel<3,2)
		PLAY H 0 A_SpawnItemEx("Bubble",random(4,8),0,random(48,52),0,0,random(1,3),random(0,360),SXF_TRANSFERTRANSLATION,random(4,8))
		PLAY H 5 A_PlayerScream
		PLAY I 5
		PLAY J 5 A_NoBlocking
		PLAY K 5
		PLAY L -1
		Stop
	Death.Fire:
		BURN A 5 BRIGHT LIGHT(ITBURNS1)
		BURN BC 5 BRIGHT LIGHT(ITBURNS1)
		BURN D 5 BRIGHT LIGHT(ITBURNS1) A_PlaySound("death/burning")
		BURN E 5 BRIGHT LIGHT(ITBURNS1)
		BURN FGHIJ 5 BRIGHT LIGHT(ITBURNS2)
		BURN KLMN 5 BRIGHT LIGHT(ITBURNS3)
		BURN O 5 BRIGHT LIGHT(ITBURNS3) A_NoBlocking
		BURN PQR 5 BRIGHT LIGHT(ITBURNS2)
		BURN STU 5 BRIGHT LIGHT(ITBURNS1)
		BURN V -1
		Stop
	}
}

ACTOR PlayerStep
{
	Radius 32
	Height 55
	Gravity 10
	//Mass 0
	+CORPSE
	+NOCLIP
	States
	{
	Spawn:
		TNT1 A 3
		Stop
	Crash:
		TNT1 A 1
		Stop
	}
}