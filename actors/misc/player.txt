//Player
Actor Stamina : Inventory
{
	Inventory.MaxAmount 100
}
Actor BoAPlayer : PlayerPawn
{
	Species "Player"
	+THRUSPECIES
	player.invulnerabilitymode "reflective"
	player.startitem "KnifeSilent"
	player.startitem "TrenchShotgunLoaded",8
	player.startitem "Browning5Loaded",4
	player.startitem "MP40Loaded",32
	player.startitem "G43Loaded",10
	player.startitem "Walther9mmLoaded",8
	player.startitem "Luger9mmLoaded",8
	player.startitem "StenLoaded",32
	player.startitem "Kar98kLoaded",5
	player.startitem "PyrolightLoaded",25
	player.startitem "PanzerschreckLoaded"
	player.startitem "AstroShotgunLoaded",8
	player.startitem "AstroRocketLauncherLoaded",5
	player.startitem "AstroChaingunLoaded",30
	player.startitem "GrenadePickup"
	Player.StartItem "Stamina", 100
	Player.ViewHeight 56
	Player.AttackZOffset 24
	Player.DisplayName "William Blazkowicz"
	Player.SideMove 1.0,0.5
	Player.ForwardMove 1.0,0.5
	Player.WeaponSlot 1,KnifeSilent,Shovel,Firebrand,AstrosteinMelee
	Player.WeaponSlot 2,Luger9mm,Walther9mm
	Player.WeaponSlot 3,TrenchShotgun,Browning5,AstroShotgun
	Player.WeaponSlot 4,MP40,Sten,AstroChaingun
	Player.WeaponSlot 5,Kar98k,G43,UMG43
	Player.WeaponSlot 6,Pyrolight,Nebelwerfer,AstroRocketlauncher
	Player.WeaponSlot 7,Panzerschreck
	Player.DamageScreenColor "ff ff ff", 1, "IceWater"
	Player.DamageScreenColor "64 00 C8", 1, "MutantPoison"
	Scale 1.2
	States
	{
	Spawn:
		PLAY A -1
		Stop
	See:
		PLAY ABCD 7
		Loop
	Pain:
		PLAY G 3 A_Pain
		Goto Spawn
	Missile:
		PLAY E 12
		Goto Spawn
	Melee:
		PLAY F 6 BRIGHT
		Goto Missile
	Death:
		PLAY H 5 A_PlayerScream
		PLAY I 5
		PLAY J 5 A_NoBlocking
		PLAY K 5
		PLAY L -1
		Stop
	Death.Fire:
		BURN A 5 BRIGHT LIGHT(ITBURNS1)
		BURN BC 5 BRIGHT LIGHT(ITBURNS1)
		BURN D 5 BRIGHT LIGHT(ITBURNS1) A_PlaySound("death/burning")
		BURN E 5 BRIGHT LIGHT(ITBURNS1)
		BURN FGHIJ 5 BRIGHT LIGHT(ITBURNS2)
		BURN KLMN 5 BRIGHT LIGHT(ITBURNS3)
		BURN O 5 BRIGHT LIGHT(ITBURNS3) A_NoBlocking
		BURN PQR 5 BRIGHT LIGHT(ITBURNS2)
		BURN STU 5 BRIGHT LIGHT(ITBURNS1)
		BURN V -1
		Stop
	}
}

ACTOR EnemyStep
{
	Radius 32
	Height 55
	Gravity 10
	+CORPSE
	+NOCLIP
	States
	{
	Spawn:
		TNT1 A 3
		Stop
	Crash:
		TNT1 A 1
		Stop
	}
}

actor PlayerStep
{
	Speed 0
	Gravity 10
	+CORPSE
	+LOOKALLAROUND
	States
	{
	Spawn:
		TNT1 A 1 NoDelay A_Look
		loop
	See:
		TNT1 A 0 A_AlertMonsters(256)
		TNT1 A 3
		goto See + 1
	Crash:
		TNT1 A 1
		Stop
	}
}

DamageType IceWater
{
    NoArmor
    ReplaceFactor
}

DamageType MutantPoison
{
    NoArmor
    ReplaceFactor
}