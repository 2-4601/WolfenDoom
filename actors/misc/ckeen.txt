//////////////////////
// ITEMS & POWERUPS //
//////////////////////
ACTOR CKShikadiSoda : CKTreasure
{
	//$Title Pickup (Shikadi Soda, 1pts)
	Radius 10
	Height 36
	Inventory.Amount 1
	Inventory.PickupMessage "$CKSODA"
	+BRIGHT
	States
	{
	Spawn:
		CKPU AB 15
		Loop
	Pickup:
		"####" C -1
		Stop
	}
}

ACTOR CKGum : CKTreasure
{
	//$Title Pickup (Three-Tooth Gum, 2pts)
	Radius 16
	Height 32
	Inventory.Amount 2
	Inventory.PickupMessage "$CKCANDY"
	+BRIGHT
	States
	{
	Spawn:
		CKPU ST 15
		Loop
	Pickup:
		"####" U -1
		Stop
	}
}

ACTOR CKShikkersCandyBar : CKTreasure
{
	//$Title Pickup (Shikkers Candy Bar, 5pts)
	Radius 14
	Height 36
	Inventory.Amount 5
	Inventory.PickupMessage "$CKCANDY"
	+BRIGHT
	States
	{
	Spawn:
		CKPU DE 15
		Loop
	Pickup:
		"####" F -1
		Stop
	}
}

ACTOR CKJawbreaker : CKTreasure
{
	//$Title Pickup (Jawbreaker, 10pts)
	Radius 16
	Height 30
	Inventory.Amount 10
	Inventory.PickupMessage "$CKJAW"
	+BRIGHT
	States
	{
	Spawn:
		CKPU PQ 15
		Loop
	Pickup:
		"####" R -1
		Stop
	}
}

ACTOR CKDoughnut : CKTreasure
{
	//$Title Pickup (Dougnut, 20pts)
	Radius 15
	Height 30
	Inventory.Amount 20
	Inventory.PickupMessage "$CKDNUT"
	+BRIGHT
	States
	{
	Spawn:
		CKPU GH 15
		Loop
	Pickup:
		"####" I -1
		Stop
	}
}

ACTOR CKIceCreamCone : CKTreasure
{
	//$Title Pickup (IceCreamCone, 50pts)
	Radius 10
	Height 32
	Inventory.Amount 50
	Inventory.PickupMessage "$CKCONE"
	Inventory.PickupSound "ckeen/pickup"
	+BRIGHT
	States
	{
	Spawn:
		CKPU JK 15
		Loop
	Pickup:
		"####" L -1
		Stop
	}
}

ACTOR CKRaindrop : CKHealth
{
	//$Title Health (Raindrop, +1)
	Radius 10
	Height 32
	+INVENTORY.ALWAYSPICKUP
	Inventory.Amount 1
	Inventory.MaxAmount 200
	Inventory.PickupMessage "$CKDROP"
	Inventory.PickupSound "ckeen/raindrop"
	CKHealth.VSpeed 0
	States
	{
	Spawn:
		CKDP ABCB 6
		Loop
	Pickup:
		"####" DEF 3
		Stop
	}
}

ACTOR CKOneUp : CKHealth
{
	//$Title Health (Lifewater Flask, +100)
	Radius 12
	Height 36
	+INVENTORY.ALWAYSPICKUP
	+INVENTORY.AUTOACTIVATE
	Inventory.Amount 100
	Inventory.MaxAmount 200
	Inventory.PickupMessage "$CKONEUP"
	Inventory.PickupSound "ckeen/life"
	+BRIGHT
	States
	{
	Spawn:
		CKPU MN 15
		Loop
	Pickup:
		"####" O -1
		Stop
	}
}

/////////////
// WEAPONS //
/////////////

ACTOR CKStunner : CKWeapon
{
	//$Title (2) Neural Stunner
	Radius 16
	Height 26
	Weapon.SelectionOrder 9998
	Weapon.AmmoType "CKStunnerAmmo"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 5
	Weapon.UpSound "ckeen/flag"
	Tag "$TAGNEURS"
	Inventory.PickupMessage "$NEUST"
	Inventory.Pickupsound "ckeen/gun"
	+WEAPON.NOAUTOFIRE
	States
	{
	Deselect: // Don't let the weapon be lowered
	Ready:
		CKBF A 1 A_WeaponReady
		Loop
	Select:
		CKBF A 1 A_Raise
		Loop
	Fire:
		CKBF B 2
		CKBF C 2 A_FireProjectile ("CKStunnerBall")
		CKBF D 1
		Goto Ready
	Spawn:
		CKBL AB 5
		Loop
	Pickup:
		CKBL C -1 BRIGHT
		Stop
	}
}

ACTOR CKStunnerBall
{
	Radius 11
	Height 8
	Speed 20
	Damage 20
	Projectile
	+BRIGHT
	+RANDOMIZE
	SeeSound "ckeen/shoot"
	DeathSound "ckeen/hit"
	Obituary "$OB_CKBLAST"
	States
	{
	Spawn:
		CKBA ABCD 3 { if (waterlevel > 2) { SetStateLabel("Death"); } }
		Loop
	Death:
		CKBA EF 4
		Stop
	}
}

ACTOR CKStunnerAmmo : Ammo
{
	Tag "$TAGECELL"
	+IGNORESKILL
	Inventory.MaxAmount 99
	Inventory.Icon CKNE01
}

////////////////////
// KEYS & KEYPOTS //
////////////////////
ACTOR CKBlueKey : CKPuzzleItem
{
	//$Title Gem, blue (Blue Card)
	Radius 15
	Height 24
	PuzzleItem.Number 101
	Inventory.PickupMessage "$CKBGEM"
	Inventory.Icon "CKKEYS1"
	Inventory.PickupSound "ckeen/gem"
	Species "BoABlueKey"
	+BRIGHT
	States
	{
	Spawn:
		CKKY CD 5
		Loop
	Pickup:
		"####" G -1
		Stop
	}
}

ACTOR CKYellowKey : CKPuzzleItem
{
	//$Title Gem, yellow (Yellow Card)
	Radius 15
	Height 24
	PuzzleItem.Number 102
	Inventory.PickupMessage "$CKYGEM"
	Inventory.Icon "CKKEYS0"
	Inventory.PickupSound "ckeen/gem"
	Species "BoAYellowKey"
	+BRIGHT
	States
	{
	Spawn:
		CKKY EF 5
		Loop
	Pickup:
		"####" G -1
		Stop
	}
}

ACTOR CKRedKey : CKPuzzleItem
{
	//$Title Gem, red (Red Card)
	Radius 15
	Height 24
	PuzzleItem.Number 103
	Inventory.PickupMessage "$CKRGEM"
	Inventory.Icon "CKKEYS2"
	Inventory.PickupSound "ckeen/gem"
	Species "BoARedKey"
	+BRIGHT
	States
	{
	Spawn:
		CKKY AB 5
		Loop
	Pickup:
		"####" G -1
		Stop
	}
}

ACTOR CKGreenKey : CKPuzzleItem
{
	//$Title Gem, green (Green Card)
	Radius 15
	Height 24
	PuzzleItem.Number 103
	Inventory.PickupMessage "$CKGGEM"
	Inventory.Icon "CKKEYS3"
	Inventory.PickupSound "ckeen/gem"
	Species "BoAGreenKey"
	+BRIGHT
	States
	{
	Spawn:
		CKKY HI 5
		Loop
	Pickup:
		"####" G -1
		Stop
	}
}

ACTOR CKRedKeyPod : SwitchableDecoration
{
	//$Category Commander Keen (BoA)/Items
	//$Title Gempod, red
	//$Color 13
	Radius 13
	Height 22
	Scale 2.0
	+DONTTHRUST
	+SOLID
	+USESPECIAL
	States
	{
	Spawn:
	Inactive:
		CKKS B -1
		Stop
	Active:
		TNT1 A 0 A_Playsound("ckeen/gemuse")
		CKKS A -1 BRIGHT
		Stop
	}
}

ACTOR CKBlueKeyPod : CKRedKeyPod
{
	//$Category Commander Keen (BoA)/Items
	//$Title Gempod, blue
	//$Color 13
	States
	{
	Spawn:
	Inactive:
		CKKS D -1
		Stop
	Active:
		TNT1 A 0 A_Playsound("ckeen/gemuse")
		CKKS C -1 BRIGHT
		Stop
	}
}

ACTOR CKYellowKeyPod : CKRedKeyPod
{
	//$Category Commander Keen (BoA)/Items
	//$Title Gempod, yellow
	//$Color 13
	States
	{
	Spawn:
	Inactive:
		CKKS F -1
		Stop
	Active:
		TNT1 A 0 A_Playsound("ckeen/gemuse")
		CKKS E -1 BRIGHT
		Stop
	}
}

ACTOR CKGreenKeyPod : CKRedKeyPod
{
	//$Category Commander Keen (BoA)/Items
	//$Title Gempod, green
	//$Color 13
	States
	{
	Spawn:
	Inactive:
		CKKS H -1
		Stop
	Active:
		TNT1 A 0 A_Playsound("ckeen/gemuse")
		CKKS G -1 BRIGHT
		Stop
	}
}

//////////////
// MONSTERS //
//////////////
ACTOR CKConfusion
{
	Radius 1
	Height 1
	Scale 2.0
	-SOLID
	States
	{
	Spawn:
		CKCF ABC 10
		Loop
	}
}

ACTOR CKArachnut : CKBaseEnemy
{
	//$Category Commander Keen (BoA)/Monsters
	//$Title Arachnut
	//$Color 4
	Speed 6
	Radius 30
	Height 56
	Mass 400
	+FLOORCLIP
	+LOOKALLAROUND
	Obituary "$CK_ARACH"
	CKBaseEnemy.StunTime 120

	States
	{
	Spawn:
		CKAC A 10 A_Look
		Loop
	See:
		"####" AAABBBCCCDDD 1 Fast A_Chase("", "")
		Loop
	Stunned:
		CKAC E -1
		Stop
	Revive:
		CKAC AEAE 10
		Goto See
	}
}

ACTOR CKMadMushroom : CKBaseEnemy
{
	//$Category Commander Keen (BoA)/Monsters
	//$Title Mad Mushroom
	//$Color 4
	Radius 30
	Height 56
	Obituary "$CK_MUSH"
	+NOTAUTOAIMED
	CKBaseEnemy.StunTime 0

	States
	{
	Spawn:
		CKMR AB 4 CK_MushroomBounce
		Loop
	}
}

ACTOR CKPoisonSlug : CKBaseEnemy
{
	//$Category Commander Keen (BoA)/Monsters
	//$Title Poison Slug
	//$Color 4
	Speed 16
	Radius 16
	Height 32
	Obituary "$CK_SLUG"
	CKBaseEnemy.StunFrames 2

	States
	{
	Spawn:
		CKPS A 10 A_Look
		Loop
	See:
		"####" AABB 4 A_Wander
		"####" A 0 A_Jump(16, "Pee")
		Loop
	Pee:
		TNT1 A 0 A_PlaySound("ckeen/slugpoo")
		CKPS C 30 A_SpawnItemEx("CKPoisonSlugPoison", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
		Goto See
	Stunned:
		CKPS E 1 A_ChangeVelocity(2, 0, 6, CVF_RELATIVE)
		CKPS E -1
		Stop
	}
}

ACTOR CKPoisonSlugPoison : CKBaseEnemy
{
	Radius 8
	Height 0.5
	Scale 3.0
	+FLATSPRITE
	+NOTAUTOAIMED
	-COUNTKILL
	CKBaseEnemy.StunTime 0

	States
	{
	Spawn:
		CKSL Z 150
		CKSL Y 30 { SetDamage(0); } // No damage during fading frame
		Stop
	}
}

ACTOR CKWallShooter : SwitchableDecoration
{
	//$Category Commander Keen (BoA)/Special
	//$Title Wall Shooter (3 types)
	//$Color 4
	//$Arg0 "Position"
	//$Arg0Type 11
	//$Arg0Enum { 0 = "Ceiling"; 1 = "Floor"; 2 = "Wall"; }
	Health 9999999
	Radius 8
	Height 16
	Mass 40000
	Obituary "$CK_WALSH"
	+NOGRAVITY
	+WALLSPRITE
	-SOLID
	States
	{
	Spawn:
	Active:
		TNT1 A 0 NODELAY A_JumpIf(Args[0] == 0,"APos_Ceiling")
		TNT1 A 0 A_JumpIf(Args[0] == 1,"APos_Floor")
		TNT1 A 0 A_JumpIf(Args[0] > 1,"APos_Wall")
		APos_Ceiling:
		CKWS C 70 A_SpawnItemEx("CKWallShooterArrow_Down", 0, 0, -10, 0, 0, -10, 0, SXF_NOCHECKPOSITION)
		Loop
		APos_Floor:
		CKWS A 70 A_SpawnItemEx("CKWallShooterArrow_Up", 0, 0, 0, 0, 0, 10, 0, SXF_NOCHECKPOSITION)
		Loop
		APos_Wall:
		CKWS B 70 A_SpawnItemEx("CKWallShooterArrow_Horizontal", 0, 0, 2, 10, 0, 0, 90, SXF_NOCHECKPOSITION)
		Loop
	Inactive:
		TNT1 A 0 A_JumpIf(Args[0] == 0,"IPos_Ceiling")
		TNT1 A 0 A_JumpIf(Args[0] == 1,"IPos_Floor")
		TNT1 A 0 A_JumpIf(Args[0] > 1,"IPos_Wall")
		IPos_Ceiling:
		CKWS C 1
		Loop
		IPos_Floor:
		CKWS A 1
		Loop
		IPos_Wall:
		CKWS B 1
		Loop
	}
}

ACTOR CKWallShooterArrow_Down
{
	Radius 4
	Height 32
	Damage 16
	Speed 16
	Seesound "ckeen/blowgun"
	Deathsound "ckeen/klick"
	PROJECTILE
	States
	{
	Spawn:
		CKTH A 0 NODELAY A_Playsound("ckeen/blowgun")
		CKAR CD 8
		Loop
	Death:
		TNT1 A 0
		Stop
	}
}

ACTOR CKWallShooterArrow_Up : CKWallShooterArrow_Down
{
	States
	{
	Spawn:
		CKAR AB 8
		Loop
	}
}

ACTOR CKWallShooterArrow_Horizontal : CKWallShooterArrow_Down
{
	Radius 8
	Height 8
	+NOGRAVITY
	+WALLSPRITE
	States
	{
	Spawn:
		CKAR E 0 NODELAY A_SetAngle(angle+90)
		CKAR EF 8
		Goto Spawn+1
	}
}

ACTOR CKOracle : CKBaseEnemy
{
	//$Category Commander Keen (BoA)/NPCs
	//$Title Oracle of Wisdom (Council Member)
	//$Color 11
	Speed 3
	Radius 16
	Height 64
	Damage 0
	Obituary "$CK_ORACLE"
	+FRIENDLY
	+NOTAUTOAIMED
	CKBaseEnemy.StunTime 0
	CKBaseEnemy.TouchSound "ckeen/message"

	States
	{
	Spawn:
		CKCC A 1
	See:
		"####" AAAAABBBBB 1 A_Wander
		"####" A 0 A_Jump(24, "Stand")
		Loop
	Stand:
		"####" C 60
		Goto See
	}
}

ACTOR CKBounder : CKBaseEnemy
{
	//$Category Commander Keen (BoA)/Monsters
	//$Title Bounder
	//$Color 4
	Speed 6
	Radius 18
	Height 36
	Damage 0
	Obituary "$CK_BOUND"

	States
	{
	Spawn:
		CKBB A 1
	See:
		CKBB AAAAAAAAAABBBBBBBBBB 1 CK_BounderBounce
		Loop
	Stunned:
		CKBB C -1
		Stop
	}
}

ACTOR CKThundercloud : CKBaseEnemy
{
	//$Category Commander Keen (BoA)/Monsters
	//$Title Thundercloud
	//$Color 4
	Speed 2
	Radius 64
	Height 32
	Damage 0
	MaxTargetRange 32
	+NOGRAVITY
	+NOTAUTOAIMED
	-FLOORCLIP
	-SOLID
	Obituary "$CK_TCLOUD"
	CKBaseEnemy.StunTime 0
	CKBaseEnemy.WakeOnTouch 1

	States
	{
	Spawn:
		CKCL A 10
		Loop
	See:
		CKCL B 50
	Chase:
		"####" BBBBB 2 CK_Glide
		Loop
	Missile:
		"####" B 40
		"####" CBC 10
		"####" B 10 A_SpawnItemEx("CKThunderbolt", -1, 0, -120, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
		"####" CBCB 10
		"####" C 24
		Goto Chase
	}
}

ACTOR CKCloud
{
	//$Category Commander Keen (BoA)/Props
	//$Title Cloud
	//$Color 3
	Radius 32
	Height 32
	Scale 2.0
	+NOGRAVITY
	-SOLID
	States
	{
	Spawn:
		CKCL A -1
		Stop
	}
}

ACTOR CKThunderbolt
{
	Radius 70
	Height 32
	Speed 0
	Scale 2.0
	+DONTTHRUST
	+NOGRAVITY
	-SOLID

	States
	{
	Spawn:
		CKTH A 0 NODELAY A_Playsound("ckeen/shothit")
		CKTH ABABAB 8 A_Explode
		Stop
	}
}

ACTOR CKCoral1
{
	//$Category Commander Keen (BoA)/Props
	//$Title Coral (large)
	//$Color 3
	Radius 32
	Height 32
	Scale 2.0
	+NOGRAVITY
	-SOLID
	States
	{
	Spawn:
		CKCR A -1
		Stop
	}
}

ACTOR CKCoral2: CKCoral1
{
	//$Category Commander Keen (BoA)/Props
	//$Title Coral (small)
	//$Color 3
	States
	{
	Spawn:
		CKCR B -1
		Stop
	}
}

ACTOR CKAlge1
{
	//$Category Commander Keen (BoA)/Props
	//$Title Alge (single)
	//$Color 3
	Radius 32
	Height 32
	Scale 2.0
	+NOGRAVITY
	-SOLID
	States
	{
	Spawn:
		CKAL ABCD 10
		Loop
	}
}

ACTOR CKAlge2 : CKAlge1
{
	//$Title Alge (triple)
	States
	{
	Spawn:
		CKAL EFGH 10
		Loop
	}
}

ACTOR CKFoot : CKAlge1
{
	//$Title Foot (secret access)
	Radius 32
	Height 64
	+SOLID
	States
	{
	Spawn:
		CKFT A -1
		Stop
	}
}

ACTOR CKFireplace
{
	//$Category Commander Keen (BoA)/Props
	//$Title Fireplace
	//$Color 3
	Radius 32
	Height 32
	Scale 2.0
	+NOGRAVITY
	-SOLID
	States
	{
	Spawn:
		CKFR ABCD 10
		Loop
	}
}

ACTOR CKGroundfire : CKBaseEnemy
{
	//$Category Commander Keen (BoA)/Props
	//$Title Groundfire
	//$Color 3
	Radius 32
	Height 0.5
	+NOTAUTOAIMED
	-COUNTKILL
	CKBaseEnemy.StunTime 0
	States
	{
	Spawn:
		CKFR EFGH 10
		Loop
	}
}

ACTOR CKWallSlug
{
	//$Category Commander Keen (BoA)/Props
	//$Title Wall Slug
	//$Color 3
	Radius 8
	Height 32
	Scale 2.0
	+NOGRAVITY
	+WALLSPRITE
	-SOLID
	States
	{
	Spawn:
		CKWS YZ 30
		Loop
	}
}

ACTOR CKWallFire : CKWallSlug
{
	//$Title Wall Fire
	States
	{
	Spawn:
		CKWF ABCD 10
		Loop
	}
}

ACTOR CKCaveEyes : CKWallSlug
{
	//$Title Wall Eyes
	States
	{
	Spawn:
		CKCE ABB 35
		Loop
	}
}

ACTOR CKFireBowl : CKWallSlug
{
	//$Title Fire Bowl
	Radius 32
	States
	{
	Spawn:
		CKFB A -1
		Stop
	}
}

ACTOR CKExitSign1
{
	//$Category Commander Keen (BoA)/Props
	//$Title ExitSign (1)
	//$Color 3
	Radius 32
	Height 32
	Scale 2.0
	+NOGRAVITY
	-SOLID
	States
	{
	Spawn:
		CKEX A -1
		Stop
	}
}

ACTOR CKExitSign2 : CKExitSign1
{
	//$Title ExitSign (2)
	States
	{
	Spawn:
		CKEX B -1
		Stop
	}
}

ACTOR CKFireColumn : CKExitSign1
{
	//$Title Fire Column
	States
	{
	Spawn:
		CKFC ABC 10
		Loop
	}
}

ACTOR CKAquaDrop
{
	//$Category Commander Keen (BoA)/Props
	//$Title Aqua Drop
	//$Color 3
	Radius 8
	Height 32
	Scale 2.0
	+FLATSPRITE
	+NOGRAVITY
	-SOLID
	States
	{
	Spawn:
		CKDR ABCDE 10
	Waiter:
		TNT1 A 10 A_Jump(80, "Spawn")
		Loop
	}
}

ACTOR CKGroundThruster : CKBaseEnemy
{
	//$Category Commander Keen (BoA)/Props
	//$Title Platform Thruster
	//$Color 3
	Radius 8
	Height 64
	+NOGRAVITY
	-SOLID
	States
	{
	Spawn:
		CKJT AB 10
		loop
	}
}

ACTOR CKSpike : CKBaseEnemy
{
	//$Category Commander Keen (BoA)/Props
	//$Title Spike
	//$Color 3
	Radius 16
	Height 48
	+NOTAUTOAIMED
	-COUNTKILL
	CKBaseEnemy.StunTime 0
	States
	{
	Spawn:
		CKSK A -1
		Stop
	}
}