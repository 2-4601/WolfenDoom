////////////////////////////////////////////////////
// ITEMS & POWERUPS ////////////////////////////////
////////////////////////////////////////////////////

actor CKShikadiSoda : SingleCoin
{
	//$Category Misc (Wolf3D)/Commander Keen
	//$Title Pickup (Shikadi Soda, 1pts)
	//$Color 17
	inventory.pickupmessage "Picked up a Shikadi Soda."
	inventory.pickupsound "ckeen/pickup"
	states
	{
	Spawn:
		CKPU AB 15 
		loop
	Pickup:
		CKPU C 5 A_GiveInventory("CoinItem",1)
		stop
	}
}

actor CKShikkersCandyBar : SingleCoin
{
	//$Category Misc (Wolf3D)/Commander Keen
	//$Title Pickup (Shikkers Candy Bar, 5pts)
	//$Color 17
	inventory.pickupmessage "Picked up a Shikkers Candy Bar."
	inventory.pickupsound "ckeen/pickup"
	states
	{
	Spawn:
		CKPU DE 15 
		loop
	Pickup:
		CKPU F 5 A_GiveInventory("CoinItem",2)
		stop
	}
}

actor CKDoughnut : SingleCoin
{
	//$Category Misc (Wolf3D)/Commander Keen
	//$Title Pickup (Dougnut, 20pts)
	//$Color 17
	inventory.pickupmessage "Picked up a Doughnut."
	inventory.pickupsound "ckeen/pickup"
	states
	{
	Spawn:
		CKPU GH 15 
		loop
	Pickup:
		CKPU I 5 A_GiveInventory("CoinItem",5)
		stop
	}
}

actor CKIceCreamCone : SingleCoin
{
	//$Category Misc (Wolf3D)/Commander Keen
	//$Title Pickup (IceCreamCone, 50pts)
	//$Color 17
	inventory.pickupmessage "Picked up an Ice Cream Cone."
	inventory.pickupsound "ckeen/pickup"
	states
	{
	Spawn:
		CKPU JK 15 
		loop
	Pickup:
		CKPU L 5 A_GiveInventory("CoinItem",10)
		stop
	}
}

ACTOR CKRaindrop : Health
{
	//$Category Misc (Wolf3D)/Commander Keen
	//$Title Health (Raindrop, +1)
	//$Color 17
  	+COUNTITEM
	+INVENTORY.ALWAYSPICKUP
  	Inventory.Amount 1
  	Inventory.MaxAmount 200
  	Inventory.PickupMessage "Picked up a Raindrop."
	inventory.pickupsound "ckeen/raindrop"
  	States
	  {
	  Spawn:
	    CKDP ABCB 6
	    Loop
	  Pickup:
	    CKPU DEF 3
            stop
	}
}

ACTOR CKOneUp : Health
{
  //$Category Misc (Wolf3D)/Commander Keen
  //$Title Health (Lifewater Flask, +100)
  //$Color 17
  +COUNTITEM
  +INVENTORY.AUTOACTIVATE
  +INVENTORY.ALWAYSPICKUP
  Inventory.Amount 100
  Inventory.MaxAmount 200
  Inventory.PickupMessage "Picked up a lifewater flask!"
  inventory.pickupsound "ckeen/life"
  States
  {
  Spawn:
    CKPU MN 15 Bright
    Loop
  Pickup:
    CKPU O 50
  Death:
    CKPU O 50
    stop
  }
}

//////////////////////////////////////////////////
// KEYS & KEYPOTS ////////////////////////////////
//////////////////////////////////////////////////

actor CKBlueKey : BoABlueKey
{
    //$Category Misc (Wolf3D)/Commander Keen
	//$Title Gem, blue (Blue Card)
	//$Color 13
	Inventory.PickupMessage "Picked up a blue gem."
	Inventory.Icon "CKKEYS1"
	inventory.pickupsound "ckeen/gem"
	Species "BoABlueKey"
	+BRIGHT
	states
	{
	Spawn:
		CKKY CD 5
		Loop
	Pickup:
    		CKKY G 5
    		stop
	}
}

actor CKYellowKey : BoAYellowKey
{
	//$Category Misc (Wolf3D)/Commander Keen
	//$Title Gem, yellow (Yellow Card)
	//$Color 13
	Inventory.PickupMessage "Picked up a yellow gem."
	Inventory.Icon "CKKEYS0"
	inventory.pickupsound "ckeen/gem"
	Species "BoAYellowKey"
	+BRIGHT	
	states
	{
	Spawn:
		CKKY EF 5
		Loop
	Pickup:
    		CKKY G 5
    		stop
	}
}

actor CKRedKey : BoARedKey
{
	//$Category Misc (Wolf3D)/Commander Keen
	//$Title Gem, red (Red Card)
	//$Color 13
	Inventory.PickupMessage "Picked up a red gem."
	Inventory.Icon "CKKEYS2"
	inventory.pickupsound "ckeen/gem"
	Species "BoARedKey"
	+BRIGHT
	states
	{
	Spawn:
		CKKY AB 5
		Loop
	Pickup:
    		CKKY G 5
    		stop
	}
}

actor CKRedKeyPod : SwitchableDecoration
{
    //$Category Misc (Wolf3D)/Commander Keen
	//$Title Gempod, red
	//$Color 13
	Radius 16
	Height 32
	Mass 999999999
	+SOLID
	+USESPECIAL
	states
	{
		Spawn:
		InActive:
			CKKS B -1
			Stop
		Active:
			TNT1 A 0 A_Playsound("ckeen/gemuse")
			CKKS A -1 BRIGHT
			Stop
	}			
}

actor CKBlueKeyPod : CKRedKeyPod
{
    //$Category Misc (Wolf3D)/Commander Keen
	//$Title Gempod, blue
	//$Color 13
	states
	{
		Spawn:
		InActive:
			CKKS D -1
			Stop
		Active:
			TNT1 A 0 A_Playsound("ckeen/gemuse")
			CKKS C -1 BRIGHT
			Stop
	}			
}

actor CKYellowKeyPod : CKRedKeyPod
{
    //$Category Misc (Wolf3D)/Commander Keen
	//$Title Gempod, yellow
	//$Color 13
	states
	{
		Spawn:
		InActive:
			CKKS F -1
			Stop
		Active:
			TNT1 A 0 A_Playsound("ckeen/gemuse")
			CKKS E -1 BRIGHT
			Stop
	}			
}

/////////////////////////////////////////////
// MONSTERS /////////////////////////////////
/////////////////////////////////////////////

ACTOR CKConfusion
{
  Radius 1
  Height 1
  -SOLID
  States
  {
  Spawn:
    CKCF ABC 12 Bright
    Loop
  }
}

ACTOR CKBaseEnemy
{
  Scale 2.0
  Monster
  PainSound "ckeen/kill"
  DeathSound "ckeen/kill"
  +BUMPSPECIAL
}

ACTOR CKArachnut : CKBaseEnemy
{
  //$Category Misc (Wolf3D)/Commander Keen
  //$Title Arachnut
  //$Color 4
  Health 999
  PainChance 128
  Speed 10
  Radius 30
  Height 56
  Mass 400
  MeleeDamage 10
  +FLOORCLIP
  +LOOKALLAROUND
  Obituary "%o was bit by an Arachnut."
  States
  {
  Spawn:
    CKAC A 10 A_Look
    Loop
  See:
    CKAC A 0 Thing_SetSpecial(0, 73, 15, 22, 0)
    CKAC AABBCCDD 2 Fast A_Chase
    Loop
  Melee:
    CKAC A 0 Fast A_FaceTarget
    CKAC A 1 Fast A_MeleeAttack
    Goto See
  Pain:
    CKAC E 0 Thing_SetSpecial(0, 0, 0, 0, 0)
    CKAC E 350 Fast
    CKAC AEAE 15
    Goto See
  Death:
    CKAC E 0 A_SpawnItemEx("CKConfusion", 0, 0, 48)
    CKAC E -1
    Stop
  }
}

ACTOR CKMadMushroom : CKBaseEnemy
{
  Health 999
  Painchance 0
  Speed 0
  Radius 30
  Height 56
  Mass 9999999
  Gravity 0.7
  Obituary "%o was crushed by a Mad Mushroom."
  States
  {
  Spawn:
  See: 
    CKAC A 0 NoDelay Thing_SetSpecial(0, 73, 15, 22, 0)
    CKMR A 1 A_CheckFloor("Jump")
    goto See
  Jump:
    TNT1 A 0 A_PlaySound("ckeen/madmushroombounce")
    CKMR B 12 ThrustThingZ(0, 32, 0, 1)
    CKMR A 12
    TNT1 A 0 A_PlaySound("ckeen/madmushroombounce")
    CKMR B 12 ThrustThingZ(0, 32, 0, 1)
    CKMR A 12
    TNT1 A 0 A_PlaySound("ckeen/madmushroombounce")
    CKMR B 20 ThrustThingZ(0, 64, 0, 1)
    CKMR A 20
    loop	
  }
}

ACTOR CKPoisonSlug : CKBaseEnemy
{
  //$Category Misc (Wolf3D)/Commander Keen
  //$Title Poison Slug
  //$Color 4
  Health 40
  PainChance 128
  Speed 10
  Radius 30
  Height 56
  Mass 400
  MeleeDamage 10
  +FLOORCLIP
  +LOOKALLAROUND
  Obituary "%o pooped to death by a Poison Slug."
  States
  {
  Spawn:
    CKPS A 10 A_Look
    Loop
  See:
    CKPS A 0 Thing_SetSpecial(0, 73, 15, 22, 0)
    CKPS AABB 4 A_Wander
    Loop
  Missile:
    TNT1 A 0 A_PlaySound("ckeen/slugpoo")
    CKPS C 5 
    CKPS C 30 A_SpawnItemEx("CKPoisonSlugPoison")
    Goto See
  Death:
    TNT1 A 0 A_Jump(128, 2)
    CKPS E 0 A_SpawnItemEx("CKConfusion", 0, 0, 48)
    CKPS E -1
    Stop
    CKPS F 0 A_SpawnItemEx("CKConfusion", 0, 0, 48)
    CKPS F -1
  }
}

ACTOR CKPoisonSlugPoison : CKBaseEnemy
{
  Radius 32
  Height 2
	States
	{
		Spawn:
		TNT1 A 0 NoDelay Thing_SetSpecial(0, 73, 15, 22, 0)
		CKSL Z 210
		CKSL Y 35
		Stop 
	}
}