//teleports player if he's in sight and below actor
actor ExTeleportIfPlayerBelow 5998
{
	//$Category Teleports
	//$Title Teleport (player below)
	//$Sprite TELED0
	//$arg0 Destination Tag
	//$arg0type 14

	Radius 16
	Height 16
	RenderStyle None

	+NOGRAVITY
	
	States
	{
		Spawn:
			TELE D 2
			TELE D 4 A_LookEx(LOF_NOSOUNDCHECK, 0, 152, 0, 360)
			loop

		See:
			TELE D 4 A_CheckSight("Spawn")
			TELE D 0 A_JumpIf(CallACS("ExGetPlayerZ") < z, "Teleport")
			loop

		Teleport:
			TELE D 0 ACS_NamedExecute("ExTeleportPlayer", 0, TID, args[0])
			TELE D 8
			goto Spawn
	}
}

//teleports player if he's in sight and above actor
actor ExTeleportIfPlayerAbove : ExTeleportIfPlayerBelow 5999
{
	//$Category Teleports
	//$Title Teleport (player above)
	//$Sprite TELEU0
	//$arg0 Destination Tag
	//$arg0type 14
	
	States
	{
		See:
			TELE U 4 A_CheckSight("Spawn")
			TELE U 0 A_JumpIf(CallACS("ExGetPlayerZ") > z, "Teleport")
			loop
	}
}

