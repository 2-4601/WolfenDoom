ACTOR PotKitchen1
{
	//$Category Props (Wolf3D)
	//$Title Pot (kitchen)
	//$Color 3
	Radius 16
	Height 32
	States
	{
	Spawn:
		TOPF A -1
		Stop
	}
}

ACTOR PotKitchen2
{
	//$Category Props (Wolf3D)
	//$Title Pot (kitchen,smoking)
	//$Color 3
	Radius 16
	Height 32
	States
	{
	Spawn:
		FCAN ABC 5
		Loop
	}
}

ACTOR BarSeat
{
	//$Category Props (Wolf3D)
	//$Title Bar Chair (Destroyable)
	//$Color 3
	Radius 12
	Height 24
	Health 20
	Mass 100
	+SOLID
	+PUSHABLE
	+NOBLOOD
	+SHOOTABLE
	States
	{
	Spawn:
		BARH A -1
		Stop
	Death: //need sounds and proper debrises
		TNT1 A 0 A_UnSetSolid
		TNT1 A 0 A_PlaySound("WOODBRK")
		//TNT1 AAAAAA 0 A_SpawnItemEx ("Debris_Trash", random(0,16), random(0,16), random(0,56), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE) //make sure to always adjust XYZ position depending on the size of the object and the amount of AAA
		TNT1 A 2 A_SpawnItemEx ("BarrelFrags")
		BARH B -1
		Stop
	}
}

ACTOR WineBottle
{
	//$Category Props (Wolf3D)
	//$Title Bottle (wine, random)
	//$Color 3
	Radius 8
	Height 8
	Scale 0.6
	States
	{
	Spawn:
		TNT1 A 0 NODELAY
		FLAS A -1 A_Jump(128,1)
		FLAS C -1 A_Jump(128,1)
		FLAS D -1 A_Jump(128,1)
		FLAS E -1 
		Stop
	}
}

ACTOR WineGlass
{
	//$Category Props (Wolf3D)
	//$Title Glass (wine, destroyable)
	//$Color 3
	Radius 8
	Height 8
	Scale 0.6
	+SHOOTABLE	//needs to be added for being destroyable
	+NOBLOOD	//needs to be added for being destroyable
	Health 1	//needs to be added for being destroyable
	States
	{
	Spawn:
		FLAS B -1
		Stop
	Death:		//needs to be added for being destroyable
		TNT1 A 0 A_PlaySound("GLASS5")
		TNT1 AAAAAAAA 0 A_SpawnItemEx ("Debris_GlassShard_Small", 0, 0, 0, random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		TNT1 A 0 A_UnsetSolid
		Stop
	}
}

ACTOR OvenHot : SwitchableDecoration
{
	//$Category Props (Wolf3D)
	//$Title Oven (Destroyable)
	//$Color 3
	Radius 16
	Height 64
	Health 35
	Mass 100
	+SOLID
	+NOBLOOD
	+PUSHABLE
	+SHOOTABLE
	+USESPECIAL
	Activation THINGSPEC_Switch | THINGSPEC_Deactivate
	States
	{
	Active:
		OVEN A 0 A_ChangeFlag("DORMANT", false) //mxd. DORMANT flag must be updated manually (because OF HORSE it must be! Why would the engine bother setting this for you?!)
	Spawn:
	  OVEN A 0 NoDelay A_PlaySound("SFX/FireLoop1", CHAN_BODY, 0.7, true, ATTN_STATIC)
		goto Frame1
	Frame1:
	  OVEN A 4 A_Jump(64, "Frame3")
		goto Frame2
	Frame2:
	  OVEN B 4 A_Jump(64, "Frame4")
		goto Frame3
	Frame3:
	  OVEN C 4 A_Jump(64, "Frame1")
		goto Frame4
	Frame4: 
		OVEN D 4 A_Jump(64, "Frame2")
		goto Frame1
	Inactive:
		OVEN E 0 A_ChangeFlag("DORMANT", true) //mxd. DORMANT flag must be updated manually
		OVEN E -1 A_StopSound(CHAN_BODY)
		Stop
	Death: //need sounds and proper debrises
		TNT1 A 0 A_UnSetSolid
		TNT1 A 0 A_StopSound(CHAN_BODY)
		TNT1 A 0 A_PlaySound("METALBRK")
		TNT1 AAAAAA 0 A_SpawnItemEx ("Debris_Trash", random(0,16), random(0,16), random(0,56), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE) //make sure to always adjust XYZ position depending on the size of the object and the amount of AAA 
		OVEN F -1
		Stop
	}
}