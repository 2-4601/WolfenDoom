ACTOR StreetBase
{
	Radius 16
	Height 56
	Mass 100
	+NOBLOOD
	+NOTAUTOAIMED
	+SHOOTABLE
	+SOLID
}

ACTOR Denkmal //not used - ozy81 14/10/2017
{
	//$Category Props (BoA)/Street
	//$Title Statue Horse (Street)
	//$Color 3
	Scale 0.85
	Radius 64
	Height 128
	+SOLID
	States
	{
	Spawn:
		DNML A -1
		Stop
	}
}

ACTOR Dome
{
	//$Category Props (BoA)/Street
	//$Title Dome
	//$Color 3
	Radius 2
	Height 2
	+NOGRAVITY
	States
	{
	Spawn:
		DOME A -1
		Stop
	}
}

ACTOR StreetBin : StreetBase
{
	//$Category Props (BoA)/Street
	//$Title Recycle Bin (Street)
	//$Color 3
	Scale 0.85
	Health 20
	+PUSHABLE
	States
	{
	Spawn:
		SRCB A -1
		Stop
	Death:
		TNT1 A 0 A_UnSetSolid
		"####" A 0 A_PlaySound("METALBRK")
		"####" AAAAAA 0 A_SpawnItemEx("Debris_Trash", random(0,16), random(0,16), random(0,56), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		SRCB E -1
		Stop
	}
}

ACTOR StreetBin2 : StreetBin
{
	//$Title Recycle Bin (Whermacht)
	Height 24
	States
	{
	Spawn:
		SSBN A -1
		Stop
	Death:
		TNT1 A 0 A_UnSetSolid
		"####" A 0 A_PlaySound("METALBRK")
		"####" AAAAAA 0 A_SpawnItemEx("Debris_Trash", random(0,16), random(0,16), random(0,56), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		SSBN B -1
		Stop
	}
}

ACTOR BurningBarrelBoA : StreetBase
{
	//$Category Props (BoA)/Street
	//$Title Burning Barrel (Destroyable)
	//$Color 3
	Height 64
	Health 35
	+DONTTHRUST
	States
	{
	Spawn:
		BBAR A 0 NoDelay A_PlaySound("SFX/FireLoop1", CHAN_BODY, frandom(0.4,0.8), TRUE, ATTN_STATIC)
		SpawnLoop:
		TNT1 A 0 A_CheckRange(768,"NoSound")
		TNT1 A 0 A_SpawnItemEx("FloatingCinder", 0, 0, random(48,54), 1, 0, random (1, 3), random (0, 360), SXF_TRANSFERPITCH | SXF_CLIENTSIDE)
		BBAR ABCD 3
		Loop
	NoSound:
		TNT1 A 0 A_StopSound(CHAN_BODY)
		TNT1 A 0 A_SpawnItemEx("FloatingCinder", 0, 0, random(48,54), 1, 0, random (1, 3), random (0, 360), SXF_TRANSFERPITCH | SXF_CLIENTSIDE)
		BBAR ABCD 3
		Goto Spawn
	Death:
		TNT1 A 0 A_UnSetSolid
		"####" A 0 A_StopSound(CHAN_BODY)
		"####" A 0 A_PlaySound("METALBRK")
		"####" AAAAAA 0 A_SpawnItemEx("Debris_Metal3Dark", random(0,16), random(0,16), random(0,56), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		"####" A 0 A_SpawnItemEx("KD_HL2SmokeGenerator", 0, 0, 0, 0, 0, 0, 0,SXF_CLIENTSIDE | SXF_TRANSFERSCALE)
		"####" AAAAAA 0 A_SpawnItemEx("BarrelFireSpawner", random(8,12), random(8,12), random(0,56), 0, 0, 0, random(0,360), SXF_TRANSFERPITCH | SXF_CLIENTSIDE)
		BBAR E -1
		Stop
	}
}

ACTOR ScareCrow : StreetBase
{
	//$Category Props (BoA)/Street
	//$Title Scarecrow (Destroyable)
	//$Color 3
	Height 48
	Health 25
	Mass 100000
	Scale 0.85
	+ROLLSPRITE
	States
	{
	Spawn:
		SCRW A -1
		Stop
	Death:
		TNT1 A 0 A_UnSetSolid
		"####" A 0 {A_PlaySound("WOODBRK", CHAN_AUTO, frandom (0.5,0.8), FALSE, ATTN_NORM);
								A_PlaySound("GRASBRKS", CHAN_AUTO, frandom (0.5,0.8), FALSE, ATTN_NORM);
								A_SpawnItemEx("GrassFrags_Dry");}
		"####" AAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Debris_Wood", random(20,40), random(0,16), random(0,48), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		"####" AAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Debris_Wood", random(40,60), random(0,16), random(0,48), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		SCRW B -1
		Stop
	}
}

//3D ACTORS//
ACTOR HydrantShootable : StreetBase
{
	//$Category Props (BoA)/Street
	//$Title Hydrant (shootable)
	//$Color 3
	DistanceCheck "scenelod"
	Health 1
	Radius 10
	Height 42
	Mass 999999
	+DONTGIB
	+NOICEDEATH
	DeathSound "hydrant/death"
	States
	{
	Spawn:
		MDLA A -1
		Stop
	Death:
		MDLA B 4 A_Scream
		"####" AAAAAAAA 0 A_SpawnItemEx("Debris_Hydrant", random(0,16), random(0,16), random(0,56), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
	Water:
		MDLA B 0 A_PlaySound("hydrant/broken", CHAN_7, frandom(0.8, 1.0), TRUE, ATTN_NORM)
	Broken:
		MDLA B 0 A_CheckRange(768,"BrokenSilent")
		"####" B 1 A_SpawnItemEx("WaterSmokePuffSmall", 0, 0, 1, (0.1)*random(0, 4), 0, (0.1)*random(8, 16), random(0, 360), 128)
		Loop
	BrokenSilent:
		MDLA B 0 A_StopSound(CHAN_7)
		"####" B 1 A_SpawnItemEx("WaterSmokePuffSmall", 0, 0, 1, (0.1)*random(0, 4), 0, (0.1)*random(8, 16), random(0, 360), 128)
		Goto Water
	}
}

ACTOR Tires
{
	//$Category Props (BoA)/Street
	//$Title Tires (Kubelwagon)
	//$Color 3
	DistanceCheck "scenelod"
	Radius 16
	Height 8
	+SOLID
	States
	{
	Spawn:
		MDLA A -1
		Stop
	}
}

ACTOR Single_Tire : Tires
{
	//$Title Single Tire (Kubelwagon)
	DistanceCheck "scenelod"
	+NOGRAVITY
}

ACTOR RoadBarrier : Tires //3d actor
{
	//$Title Road Barrier (Small)
	DistanceCheck "scenelod"
	Radius 32
	Height 64
}

ACTOR CafeStuff
{
	//$Category Props (BoA)/Street
	//$Title Table with Umbrella and Chairs (Cafe)
	//$Color 3
	Radius 28
	Height 32
	Scale 0.65
	+SOLID
	States
	{
	Spawn:
		MDLA A 0 NoDelay A_SpawnItemEx("Cup1", random(-24, -16), random(-24, -16), 32, 0, 0, 0, 0, 0, 128)
		"####" A 0 A_SpawnItemEx("Cup1", random(16, 24), random(-24, -16), 32, 0, 0, 0, 0, 0, 128)
		"####" A 0 A_SpawnItemEx("Cup1", random(-24, -16), random(16, 24), 32, 0, 0, 0, 0, 0, 128)
		"####" A 0 A_SpawnItemEx("Cup1", random(16, 24), random(16, 24), 32, 0, 0, 0, 0, 0, 128)
		"####" A -1
		Stop
	}
}

ACTOR NTomb1
{
	//$Category Props (BoA)/Street
	//$Title Tombstone 1
	//$Color 3
	DistanceCheck "scenelod"
	Radius 15
	Height 68
	+SOLID
	States
	{
	Spawn:
		MDLA A -1
		Stop
	}
}

ACTOR NTomb2 : NTomb1
{
	//$Title Tombstone 2
	DistanceCheck "scenelod"
	Height 62
}

ACTOR NTomb3 : NTomb1
{
	//$Title Tombstone 3
	DistanceCheck "scenelod"
	Height 48
}

ACTOR NTomb4 : NTomb1
{
	//$Title Tombstone 4
	DistanceCheck "scenelod"
	Height 43
}

ACTOR NTomb5 : NTomb1
{
	//$Title Tombstone 5
	DistanceCheck "scenelod"
	Radius 12
	Height 35
}

ACTOR Haycart
{
	//$Category Props (BoA)/Street
	//$Title Haycart
	//$Color 3
	//$Arg0 "Type"
	//$Arg0Tooltip "Pickup the desired type\nNormal: 0\nDestroyed: 1"
	DistanceCheck "scenelod"
	Radius 24 //reduced for map-wise placement - ozy
	Height 32
	+SOLID
	States
	{
	Spawn:
		MDLA A 0 NoDelay A_JumpIf(Args[0]==1, "Destroyed")
		MDLA A -1
		Stop
	Destroyed:
		MDLA B -1
		Stop
	}
}