//2d Actors//
ACTOR BarrelFuture
{
	//$Category Astrostein (BoA)/Props
	//$Title Barrel (modern, destroyable)
	//$Color 3
	Radius 8
	Height 56
	Health 10
	Mass 100
	-DROPOFF
	+NOBLOOD
	+NOTAUTOAIMED
	+PUSHABLE
	+SHOOTABLE
	+SOLID
	States
	{
	Spawn:
		CANF A -1
		Stop
	Death:
		"####" A 0 A_UnSetSolid
		"####" A 0 A_PlaySound("METALBRK", CHAN_AUTO, frandom (0.5,0.8), FALSE, ATTN_NORM)
		"####" B 0 A_SpawnItemEx("MetalFrags")
		"####" BBBBBB 0 A_SpawnItemEx("Debris_Astro", random(0,16), random(0,16), random(0,56), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		"####" BBBBBB 3 A_SpawnItemEx("Spark_B1", random(0,16), random(0,16), random(0,56), 0, frandom(-1.0,1.0), frandom(-1.0,1.0), random(0,360), SXF_CLIENTSIDE)
		"####" B -1
		Stop
	}
}

ACTOR BarrelFutureExploding : ExplosiveBarrel
{
	//$Category Astrostein (BoA)/Props
	//$Title Barrel (modern, explode)
	//$Color 3
	Radius 16
	Height 64
	States
	{
	Spawn:
		BARF ABCDCB 8
		Loop
	Death:
		"####" A 0 A_SpawnItemEx("AstrosteinExplosion_Medium",0,0,32)
		"####" AAAAAA 0 A_SpawnItemEx("Debris_Trash", random(0,16), random(0,16), random(0,56), random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		"####" A 6 A_Scream
		BARF E -1
		Stop
	}
}

ACTOR StreetLightAstro
{
	//$Category Astrostein (BoA)/Props
	//$Title Street Light (modern)
	//$Color 3
	Radius 8
	Height 128
	+SOLID
	States
	{
	Spawn:
		STRL X -1
		Stop
	}
}

ACTOR StripperGirl1
{
	//$Category Astrostein (BoA)/Props
	//$Title Stripper Girl, Purple
	//$Color 3
	Radius 16
	Height 56
	Scale 0.6
	+RANDOMIZE
	+SOLID
	States
	{
	Spawn:
		STRP A 0 A_Jump(64, 9)
		STRP A 0 A_Jump(64, 5)
		STRP A 0 A_Jump(64, 2)
		STRP ABCDEDCBA 3
		STRP FGHGHF 6
		Loop
	}
}

ACTOR StripperGirl1NH : StripperGirl1
{
	//$Title Stripper Girl, Purple (decorative)
	//$Color 3
	Radius 2
	Height 2
	Scale 0.6
	-SOLID
	+NOBLOCKMAP
}

ACTOR StripperGirl2 : StripperGirl1
{
	//$Title Stripper Girl, Black
	States
	{
	Spawn:
		STR2 A 0 A_Jump(64, 9)
		STR2 A 0 A_Jump(64, 5)
		STR2 A 0 A_Jump(64, 2)
		STR2 ABCDEDCBA 3
		STR2 FGHGHF 6
		Loop
	}
}

ACTOR StripperGirl3 : StripperGirl1
{
	//$Title Stripper Girl, Red
	States
	{
	Spawn:
		STR3 A 0 A_Jump(64, 9)
		STR3 A 0 A_Jump(64, 5)
		STR3 A 0 A_Jump(64, 2)
		STR3 ABCDEDCBA 3
		STR3 FGHGHF 6
		Loop
	}
}

ACTOR StripperGirls : RandomSpawner
{
	//$Category Astrostein (BoA)/Props
	//$Title Stripper Girl, Random
	//$Color 3
	//$Sprite STRPH0
	Radius 16
	Height 56
	Scale 0.6
	DropItem "StripperGirl1"
	DropItem "StripperGirl2"
	DropItem "StripperGirl3"
}

//3d Actors//
ACTOR AstroShuttle : ModelBase //AFADoomer
{
	//$Category Astrostein (BoA)/Props
	//$Title Galileo's Shuttle
	//$Color 3
	DistanceCheck "scenelod"
	Height 32
	Radius 64
}

ACTOR EnergyTankLit //Talon1024 model
{
	//$Category Astrostein (BoA)/Props
	//$Title Energy Tank (lit)
	//$Color 3
	DistanceCheck "scenelod"
	Radius 32
	Height 128
	+SOLID
	States
	{
	Spawn:
		MDLA A -1
		Stop
	}
}

ACTOR EnergyTankUnLit : EnergyTankLit //Talon1024 model
{
	//$Title Energy Tank (unlit)
}

ACTOR RadioSatelite
{
	//$Category Astrostein (BoA)/Props
	//$Title Radio Satelite
	//$Color 3
	DistanceCheck "scenelod"
	Radius 64
	Height 256
	Scale 4.0
	+SOLID
	States
	{
	Spawn:
		MDLA A -1
		Stop
	}
}

ACTOR RadioTower : RadioSatelite
{
	//$Title Radio Tower
	Radius 32
	Scale 3.0
	States
	{
	Spawn:
		MDLA AB 20
		Loop
	}
}

ACTOR SolarPanel : RadioSatelite
{
	//$Title Solar Panel
	Height 96
	Scale 2.0
}

ACTOR DecorationShip : SwitchableDecoration
{
	//$Category Astrostein (BoA)/Props
	//$Title Space Ship (viper, activatable)
	//$Color 3
	Radius 8
	Height 6
	Speed 6
	Damage 3
	SeeSound "viper/flyby"
	Projectile
	-ACTIVATEIMPACT
	+ACTIVATEPCROSS
	+CLIENTSIDEONLY
	+DONTSPLASH
	+NOGRAVITY
	States
	{
	Spawn:
	Active:
		MDLA A 1
		MDLA A 0 A_SpawnItemEx("DecorationShip_Steam", -32, -5, -1)
		MDLA A 0 A_SpawnItemEx("DecorationShip_Steam", -32, 5, -1)
		Loop
	Inactive:
		MDLA A -1
		Stop
	Death:
		MDLA A 4
		Stop
	}
}

ACTOR DecorationShip_Steam
{
	DistanceCheck "scenelod"
	Radius 1
	Height 1
	RenderStyle "Add"
	Alpha 0.2
	Scale 0.2
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	+MISSILE
	+NOBLOCKMAP
	+NOGRAVITY
	States
	{
	Spawn:
		SSST A 0
		"####" AAAA 1 BRIGHT A_FadeIn(0.2)
		"####" A 1 BRIGHT A_FadeOut
		Wait
	}
}

ACTOR RotatingStation
{
	//$Category Astrostein (BoA)/Props
	//$Title Space Station (rotating)
	//$Color 3
	DistanceCheck "scenelod"
	Height 64
	+FLOORCLIP
	+NOGRAVITY
	States
	{
	Spawn:
		MDLA A -1
		Stop
	}
}