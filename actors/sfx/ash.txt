Actor AshSpawner : EffectSpawner
{
	//$Category Special Effects (BoA)
	//$Title Ash Spawner
	//$Color 12
	//$Sprite ASHXA0
	//$Arg0 "Radius"
	//$Arg0Tooltip "Radius in map units\nDefault: 0"
	//$Arg1 "Height"
	//$Arg1Tooltip "Height in map units\nDefault: 0"
	//$Arg4 "Frequency"
	//$Arg4Tooltip "The lower the number, the heavier the snowfall\nRange: 0 - 255"
	Radius 1
	Height 1
	+CLIENTSIDEONLY
	+NOCLIP
	+NOGRAVITY
	States
	{
	Spawn:
	Active:
		TNT1 A 0
		TNT1 A 0 A_Jump(256, 1, 2, 3, 4)
		TNT1 A 1 SpawnParticleActor("FloatingAshLight", Random(-Args[0],Args[0]), Random(-Args[0],Args[0]), Args[1], frandom(0.0, 0.2), 0, 0, random(0.0, 1.0), 128, Args[4])
		Loop
		TNT1 A 1 SpawnParticleActor("FloatingAshGrey", Random(-Args[0],Args[0]), Random(-Args[0],Args[0]), Args[1], frandom(0.0, 0.2), 0, 0, random(0.0, 1.0), 128, Args[4])
		Loop
		TNT1 A 1 SpawnParticleActor("FloatingAshDarkGrey", Random(-Args[0],Args[0]), Random(-Args[0],Args[0]), Args[1], frandom(0.0, 0.2), 0, 0, random(0.0, 1.0), 128, Args[4])
		Loop
		TNT1 A 1 SpawnParticleActor("FloatingAshDark", Random(-Args[0],Args[0]), Random(-Args[0],Args[0]), Args[1], frandom(0.0, 0.2), 0, 0, random(0.0, 1.0), 128, Args[4])
		Loop
	Inactive:
		TNT1 A 10
		Loop
	}
}

Actor FloatingAshLight : ParticleBase
{
	Radius 1
	Height 1
	Scale 0.15
	Gravity 1.0
	+MISSILE
	+NOBLOCKMAP
	-NOGRAVITY
	RenderStyle Shaded
	StencilColor "A0 A0 A0"
	ReactionTime 250
	States
	{
	Spawn:
		ASHX A 0
		ASHX A 0 A_SetGravity(frandom(0.1,0.3))
		ASHX A 0 A_SetScale(frandom(0.09, 0.12))
		ASHX A 0 A_Jump(128, "Spawn2")
	Spawn1:
		ASHX ABCDEFGH 2 A_CountDown
		Loop
	Spawn2:
		ASHX HGFEDCBA 2 A_CountDown
		Loop
	Death:
		ASHX F 2 A_FadeOut(0.06)
		Loop
	}
}

Actor FloatingAshGrey : FloatingAshLight { StencilColor "80 80 80" }
Actor FloatingAshDarkGrey : FloatingAshLight { StencilColor "60 60 60" }
Actor FloatingAshDark : FloatingAshLight { StencilColor "45 45 45" }