// -----------------------------------------------------------------------------
// Half-Life 2 styled explosions
//
// Made by KeksDose, December 1 2013
//
// These are smoky explosions with a quick flash. You can directly spawn them
// from either ACS or DECORATE, or place them in a map with the "Dormant" flag
// and simply activate them with a line special.
// -----------------------------------------------------------------------------

Actor GeneralExplosion_Medium : SwitchableDecoration
{
	//$Category Special Effects (Wolf3D)
	//$Title Explosion Effect (activatable)
	//$Color 12
	+NOINTERACTION
	States
	{
	Spawn:
	Active:
		TNT1 A 0 NoDelay A_PlaySound("Weapons/RockLX")
		TNT1 A 0 Radius_Quake(10,10,0,16,0)
		TNT1 A 0 A_Explode(100, 192)
		TNT1 A 0 A_SpawnItemEx("KD_HL2Flash", 0, 0, 0, 0, 0, 0, 0,SXF_CLIENTSIDE | SXF_TRANSFERSCALE)
		TNT1 A 0 A_SpawnItemEx("KD_HL2SmokeGenerator", 0, 0, 0, 0, 0, 0, 0,SXF_CLIENTSIDE | SXF_TRANSFERSCALE)
		TNT1 A 0 A_SpawnItemEx("KD_HL2SparkGenerator", 0, 0, 0, 0, 0, 0, 0,SXF_CLIENTSIDE | SXF_TRANSFERSCALE)
	Inactive:
		TNT1 A 350
		Wait
	}
}

Actor KD_HL2GeneratorBase {+NOINTERACTION}

Actor KD_HL2ExplosionBase
{
	+NOINTERACTION
	+FORCEXYBILLBOARD
	RenderStyle Add
	Alpha 0.99
}

Actor KD_HL2Flash : KD_HL2ExplosionBase
{
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SetScale(FRandom(0.5, 0.8) * ScaleX)
		TNT1 A 0 A_Jump(256,1,2)
		EXN1 AB 0 A_Jump(256,"Fade")
	Fade:
		"####" "#" 1 Bright A_FadeOut(FRandom(0.05, 0.10))
		Wait
	}
}

Actor KD_HL2Smoke : KD_HL2ExplosionBase
{
	RenderStyle Translucent
	Alpha 0.8
	States
	{
	Spawn:
		TSMK A 1 A_SetScale(ScaleX + FRandom(0.005, 0.015))
		TSMK A 0 A_FadeOut(FRandom(0.006, 0.012))
		Loop
	}
}

Actor KD_HL2SmokeGenerator : KD_HL2GeneratorBase
{
	ReactionTime 15
	States
	{
	Spawn:
		TNT1 A 0 A_Countdown
		TNT1 A 0 A_SpawnItemEx("KD_HL2Smoke", 0, 0, 0, 0.0,FRandom(0.0, 2.5) * ScaleX,FRandom(-2.0, 2.0) * ScaleX, Random(0, 359),SXF_CLIENTSIDE | SXF_TRANSFERSCALE, 30)
		Loop
	}
}

Actor KD_HL2Spark : KD_HL2ExplosionBase
{
	States
	{
	Spawn:
		PAO1 A 5 Bright NoDelay A_SetScale(FRandom(0.07, 0.1) * ScaleX)
	Drop:
		PAO1 A 0 A_ChangeVelocity(FRandom(0.98, 0.99) * VelX,FRandom(0.98, 0.99) * VelY,VelZ - FRandom(0.2, 0.5), CVF_REPLACE)
		PAO1 A 0 A_JumpIf(ScaleX <= 0.0, "NULL")
		PAO1 A 1 Bright A_SetScale(ScaleX - 0.002)
		Loop
	}
}

Actor KD_HL2SparkGenerator : KD_HL2GeneratorBase
{
	ReactionTime 200
	States
	{
	Spawn:
		TNT1 A 0 A_Countdown
		TNT1 A 0 A_SpawnItemEx("KD_HL2Spark", 0, 0, 0, 0.0,FRandom(0.0, 13.5) * ScaleX,FRandom(-1.0, 9.5) * ScaleX, Random(0, 359),SXF_CLIENTSIDE | SXF_TRANSFERSCALE, 60)
		Loop
	}
}

//////////////////////////////////////
// Variants in Size by Tormentor667 //
//////////////////////////////////////
Actor GeneralExplosion_Large : GeneralExplosion_Medium {Scale 2.0}
Actor GeneralExplosion_Small : GeneralExplosion_Medium {Scale 0.5}