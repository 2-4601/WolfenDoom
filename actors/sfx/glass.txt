ACTOR BOD_GlassShardSpawner : GlassJunk replaces GlassJunk
{
	DeathSound "world/glassbreak"
	States
	{
	Spawn:
		TNT1 A 1 A_Scream
		TNT1 AAAAAAAAA 0 A_SpawnItemEx(BOD_GlassShard, random(-32,32), random(-32,32), random(-32,32), 1, 1, 1, random(0,360))
		Stop
	}
}

ACTOR BOD_GlassShard : GlassShard
{
	Radius 5
	Mass 5
	Projectile
	-ACTIVATEIMPACT
	-ACTIVATEPCROSS
	-NOGRAVITY
	+RANDOMIZE
	BounceType "Doom" //otherwise they'll still "jump" over 3d objects - like chairs inside the barber shop on c1m5 - ozy81
	BounceFactor 0.3
	WallBounceFactor 0.3
	BounceCount 3
	RenderStyle "Translucent"
	Alpha 0.4
	Gravity 0.4
	States
	{
	Spawn:
		SHAR A 0 NoDelay A_SetScale(frandom(0.5,0.8), frandom(0.5,0.8))
		LetsShard:
		SHAR ABCDE 4
		Loop
	Death:
		SHAR E 30
		Stop
	}
}

ACTOR BOD_GlassShardActivatable : SwitchableDecoration
{
	//$Category Special Effects (Wolf3D)
	//$Title Glass Shard Spawner
	//$Color 12

	//$Arg0 "Height"
	//$Arg0Tooltip "Height in map units\nDefault: 0"
	//$Arg1 "Width"
	//$Arg1Tooltip "Width x2 in map units\nDefault: 0"
	Height 8
	Radius 4
	+CLIENTSIDEONLY
	+NOBLOCKMAP
	+NOCLIP
	+NOGRAVITY
	+NOINTERACTION
	States
	{
	Spawn:
	Inactive:
		TNT1 A -1
		Loop
	Active:
		TNT1 AAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("BOD_GlassShardSpawner", random(-Args[1], Args[1]), 0, random(-Args[0], Args[0]), 0, random(1,3), 0, 0, 0, 0)
		Stop
	}
}