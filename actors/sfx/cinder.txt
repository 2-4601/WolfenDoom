Actor CinderSpawner : SwitchableDecoration
{
	//$Category Special Effects (Wolf3D)
	//$Title Cinder Spawner
	//$Color 12

	//$Arg0 "Radius"
	//$Arg0Tooltip "Radius in map units"
	//$Arg1 "Height"
	//$Arg1Tooltip "Z-dimension (bottom is the z-level the spawner is on)"
	//$Arg2 "Min. Speed"
	//$Arg2Tooltip "Minimum speed the particles have, randomized between min. and max."
	//$Arg3 "Max. Speed"
	//$Arg3Tooltip "Maximum speed the particles have, randomized between min. and max."
	//$Arg4 "Frequency"
	//$Arg4Tooltip "0 is always, 255 is never at a per-tic basis"
	Radius 1
	Height 1
	+NoClip
	+NoGravity
	+ClientSideOnly
	States
	{
	Spawn:
	Active:
		TNT1 A 5 A_SpawnItemEx("FloatingCinder", Random(-Args[0],Args[0]), Random(-Args[0],Args[0]), Random(0,Args[1]), Random(Args[2],Args[3]), 0, Random(-Args[2],Args[2]), Random(-4, 4), 128, Args[4])
		Loop
	Inactive:
		TNT1 A 1
		Loop
	}
}

Actor FloatingCinder
{
	+Missile
	+NoBlockmap
	+NoGravity
	+DontSplash
	+FORCEXYBILLBOARD
	Radius 0
	Height 0
	RenderStyle Add
	Alpha 0.8
	Scale 0.04
	States
	{
	Spawn:
		EMBR A 0 NODELAY A_SetScale(FRandom(0.03, 0.06))
		TNT1 A 0 A_Jump(80,8)
		TNT1 A 0 A_Jump(80,6)
		TNT1 A 0 A_Jump(80,4)
		TNT1 A 0 A_Jump(80,2)
		EMBR AAAAAAAAAAAAAAAAAAAAAAAA 8 BRIGHT
	Death:
		EMBR A 1 BRIGHT A_FadeOut(0.06)
		Loop
	}
}