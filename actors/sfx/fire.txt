Actor FireSpawner : SwitchableDecoration 17124
{
	//$Category Special Effects (Wolf3D)
	//$Title Fire Spawner (arg1 Size123)
	//$Color 12
	Height 40
	Radius 30
	+ClientSideOnly
	+NoClip
	+NoBlockMap
	+NoGravity
	+NoSector
	+NoInteraction
	States
	{
	Spawn:
	Active:
		TNT1 A 0
		TNT1 A 0 A_JumpIf(Args[1] > 0, 2)
		TNT1 A 0 A_PlaySound("SFX/FireStart", 7)
		TNT1 A 0 A_JumpIf(Args[0] == 1, "Medium")
		TNT1 A 0 A_JumpIf(Args[0] > 1, "Large")
		TNT1 A 0 A_JumpIf(Args[1] > 0, 2)
		TNT1 A 0 A_PlaySound("SFX/FireLoop1", 6,1,1)
		TNT1 A 0 A_SpawnItemEx("SmallFlame", 0, 0, 0, 0, 0, 0, 0, 128)
		TNT1 A 0 A_Jump(216, 3)
		TNT1 A 1 A_SpawnItemEx("Flame1A", 0, 0, 0, 0, 0, 0, 0, 128)
		TNT1 A 0 A_SpawnItemEx("Ember1", 0, 0, 0, 0, 0, 0, 0, 128)
		Loop
		TNT1 A 0 A_Jump(176, 2)
		TNT1 A 1 A_SpawnItemEx("Flame1B", 0, 0, 0, 0, 0, 0, 0, 128)
		Loop
		TNT1 A 0 A_Jump(136, 2)
		TNT1 A 1 A_SpawnItemEx("Flame2A", 0, 0, 0, 0, 0, 0, 0, 128)
		Loop
		TNT1 A 0 A_Jump(96, 2)
		TNT1 A 1 A_SpawnItemEx("Flame2B", 0, 0, 0, 0, 0, 0, 0, 128)
		Loop
		TNT1 A 0 A_Jump(56, 2)
		TNT1 A 1 A_SpawnItemEx("Flame3A", 0, 0, 0, 0, 0, 0, 0, 128)
		Loop
		TNT1 A 1 A_SpawnItemEx("Flame3B", 0, 0, 0, 0, 0, 0, 0, 128)
		Loop
	Medium:
		TNT1 A 0 A_JumpIf(Args[1] > 0, 2)
		TNT1 A 0 A_PlaySound("SFX/FireLoop2",6,1, 1)
		TNT1 A 0 A_SpawnItemEx("MediumFlame", 0, 0, 0, 0, 0, 0, 0, 128)
		TNT1 A 0 A_Jump(216, 3)
		TNT1 A 1 A_SpawnItemEx("Flame4A", 0, 0, 0, 0, 0, 0, 0, 128)
		TNT1 A 0 A_SpawnItemEx("Ember2", 0, 0, 0, 0, 0, 0, 0, 128)
		Loop
		TNT1 A 0 A_Jump(176, 2)
		TNT1 A 1 A_SpawnItemEx("Flame4B", 0, 0, 0, 0, 0, 0, 0, 128)
		Loop
		TNT1 A 0 A_Jump(136, 2)
		TNT1 A 1 A_SpawnItemEx("Flame5A", 0, 0, 0, 0, 0, 0, 0, 128)
		Loop
		TNT1 A 0 A_Jump(96, 2)
		TNT1 A 1 A_SpawnItemEx("Flame5B", 0, 0, 0, 0, 0, 0, 0, 128)
		Loop
		TNT1 A 0 A_Jump(56, 2)
		TNT1 A 1 A_SpawnItemEx("Flame6A", 0, 0, 0, 0, 0, 0, 0, 128)
		Loop
		TNT1 A 1 A_SpawnItemEx("Flame6B", 0, 0, 0, 0, 0, 0, 0, 128)
		Loop
	Large:
		TNT1 A 0 A_JumpIf(Args[1] > 0, 2)
		TNT1 A 0 A_PlaySound("SFX/FireLoop3",6,1,1)
		TNT1 A 0 A_SpawnItemEx("LargeFlame", 0, 0, 0, 0, 0, 0, 0, 128)
		TNT1 A 0 A_Jump (216, 3)
		TNT1 A 1 A_SpawnItemEx("Flame7A", 0, 0, 0, 0, 0, 0, 0, 128)
		TNT1 A 0 A_SpawnItemEx("Ember2", 0, 0, 0, 0, 0, 0, 0, 128)
		Loop
		TNT1 A 0 A_Jump (176, 2)
		TNT1 A 1 A_SpawnItemEx("Flame7B", 0, 0, 0, 0, 0, 0, 0, 128)
		Loop
		TNT1 A 0 A_Jump (136, 2)
		TNT1 A 1 A_SpawnItemEx("Flame8A", 0, 0, 0, 0, 0, 0, 0, 128)
		Loop
		TNT1 A 0 A_Jump (96, 2)
		TNT1 A 1 A_SpawnItemEx("Flame8B", 0, 0, 0, 0, 0, 0, 0, 128)
		Loop
		TNT1 A 0 A_Jump (56, 2)
		TNT1 A 1 A_SpawnItemEx("Flame9A", 0, 0, 0, 0, 0, 0, 0, 128)
		Loop
		TNT1 A 1 A_SpawnItemEx("Flame9B", 0, 0, 0, 0, 0, 0, 0, 128)
		Loop
	Inactive:
		TNT1 A 0 A_JumpIf(Args[1] > 0, 2)
		TNT1 A 0 A_PlaySound("SFX/FireDie",7)
		TNT1 A -1 A_StopSound(6)
		Goto Inactive+2
	}
}

//Dynamic Light actors
Actor SmallFlame
{
	+ClientSideOnly
	+NoClip
	+NoBlockMap
	+NoGravity
	+NoInteraction
	States
	{
	Spawn:
		TNT1 A 1
		Stop
	}
}

Actor MediumFlame : SmallFlame {}
Actor LargeFlame : SmallFlame {}

//Flames and particles
Actor Flame1A
{
	Height 2
	Radius 1
	+NoGravity
	+NoBlockMap
	+Missile
	+NoInteraction
	+ForceXYBillboard
	RenderStyle Add
	Alpha 0.8
	Scale 0.35
	States
	{
	Spawn:
		FLM1 A 0
		"####" A 0 ThrustThingZ(0, Random(2, 6), 0, 0)
		"####" A 0 ThrustThing(Random(0, 255), Random(0, 1), 0, 0)
		"####" ABCDE 2 Bright
		"####" FFGGHHIIJJKKLL 1 Bright A_FadeOut(.05)
		Stop
	}
}

Actor Flame1B : Flame1A
{
	States
	{
	Spawn:
		FLM2 A 0
		Goto Super::Spawn+1
	}
}

Actor Flame2A : Flame1A
{
	States
	{
	Spawn:
		FLM3 A 0
		Goto Super::Spawn+1
	}
}

Actor Flame2B : Flame1A
{
	States
	{
	Spawn:
		FLM4 A 0
		Goto Super::Spawn+1
	}
}

Actor Flame3A : Flame1A
{
	States
	{
	Spawn:
		FLM5 A 0
		Goto Super::Spawn+1
	}
}

Actor Flame3B : Flame1A
{
	States
	{
	Spawn:
		FLM6 A 0
		Goto Super::Spawn+1
	}
}

Actor Flame4A : Flame1A
{
	Scale 0.7
	States
	{
	Spawn:
		FLM1 A 0
		"####" A 0 ThrustThingZ(0, Random(4, 12), 0, 0)
		"####" A 0 ThrustThing(Random(0, 255), Random(0, 2), 0, 0)
		"####" ABCDE 2 Bright
		"####" FFGGHHIIJJKKLL 1 Bright A_FadeOut(.05)
		Stop
	}
}

Actor Flame4B : Flame4A
{
	States
	{
	Spawn:
		FLM2 A 0
		Goto Super::Spawn+1
	}
}

Actor Flame5A : Flame4A
{
	States
	{
	Spawn:
		FLM3 A 0
		Goto Super::Spawn+1
	}
}

Actor Flame5B : Flame4A
{
	States
	{
	Spawn:
		FLM4 A 0
		Goto Super::Spawn+1
	}
}

Actor Flame6A : Flame4A
{
	States
	{
	Spawn:
		FLM5 A 0
		Goto Super::Spawn+1
	}
}

Actor Flame6B : Flame4A
{
	States
	{
	Spawn:
		FLM6 A 0
		Goto Super::Spawn+1
	}
}

Actor Flame7A : Flame1A
{
	Scale 1.4
	States
	{
	Spawn:
		FLM1 A 0
		"####" A 0 ThrustThingZ(0, Random(8, 24), 0, 0)
		"####" A 0 ThrustThing(Random(0, 255), Random(0, 4), 0, 0)
		"####" ABCDE 2 Bright
		"####" FFGGHHIIJJKKLL 1 Bright A_FadeOut(.05)
		Stop
	}
}

Actor Flame7B : Flame7A
{
	States
	{
	Spawn:
		FLM2 A 0
		Goto Super::Spawn+1
	}
}

Actor Flame8A : Flame7A
{
	States
	{
	Spawn:
		FLM3 A 0
		Goto Super::Spawn+1
	}
}

Actor Flame8B : Flame7A
{
	States
	{
	Spawn:
		FLM4 A 0
		Goto Super::Spawn+1
	}
}

Actor Flame9A : Flame7A
{
	States
	{
	Spawn:
		FLM5 A 0
		Goto Super::Spawn+1
	}
}

Actor Flame9B : Flame7A
{
	States
	{
	Spawn:
		FLM6 A 0
		Goto Super::Spawn+1
	}
}

Actor Ember1 : Flame1A
{
	Scale 0.1
	States
	{
	Spawn:
		EMBR A 0
		EMBR A 0 ThrustThingZ(0, Random(4, 16), 0, 0)
		EMBR A 0 ThrustThing(Random(0, 255), Random(0, 1), 0, 0)
		EMBR A 32 Bright
		EMBR A 2 Bright A_FadeOut(0.1)
		Wait
	}
}

Actor Ember2 : Flame1A
{
	Scale 0.1
	States
	{
	Spawn:
		EMBR A 0
		EMBR A 0 ThrustThingZ(0, Random(4, 16), 0, 0)
		EMBR A 0 ThrustThing(Random(0, 255), Random(0, 1), 0, 0)
		EMBR A 64 Bright
		EMBR A 2 Bright A_FadeOut(0.1)
		Wait
	}
}