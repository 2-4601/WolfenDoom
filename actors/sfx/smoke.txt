actor PuffSmoke
{
	+NOINTERACTION
	+FORCEXYBILLBOARD
	+CLIENTSIDEONLY
	+WINDTHRUST
	Renderstyle Translucent
	alpha 0.5
	Scale 0.5
	states
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(256,1,2,3,4)
		SMK1 A 0 A_Jump(256,"End")
		SMK2 A 0 A_Jump(256,"End")
		SMK3 A 0 A_Jump(256,"End")
		SMK4 A 0 A_Jump(256,"End")
	End:
		"####" ABCDEFGHIJKLMNOP 2 A_FadeOut(0.025)
		stop
	}
}

actor PowerPlantSmokePuff : PuffSmoke {Scale 8.0}
actor PowerPlantSmokePuffSmall : PuffSmoke {Scale 4.0}

actor PowerPlantSmokeGenerator 21406
{
	//$Category Special Effects (Wolf3D)
	//$Title Power Plant Smoke Generator (Normal)
	//$Color 12
	+NOGRAVITY
	+INVULNERABLE
	+SHOOTABLE
	+NOINTERACTION
	states
	{
	Spawn:
		TNT1 A 70 A_SpawnItemEx("PowerPlantSmokeGeneratorFX")
		loop
	Death:
		TNT1 A -1
		stop
	}
}

actor PowerPlantSmokeGeneratorFX
{
	+CLIENTSIDEONLY
	+NOINTERACTION
	states
	{
	Spawn:
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 2 A_SpawnItemEx("PowerPlantSmokePuff", random(-32,32), random(-32,32), 0, 0, 0, 3)
		stop
	}
}

actor PowerPlantSmokeGeneratorSmall 24002
{
	//$Category Special Effects (Wolf3D)
	//$Title Power Plant Smoke Generator (Small)
	//$Color 12
	+CLIENTSIDEONLY
	+NOINTERACTION
	states
	{
	Spawn:
		TNT1 A 2 A_SpawnItemEx("PowerPlantSmokePuffSmall", random(-32,32), random(-32,32), 0, 0, 0, 3)
		loop
	}
}

actor PowerPlantSmokeGenerator2 : PowerPlantSmokeGenerator 21409
{
	//$Category Special Effects (Wolf3D)
	//$Title Power Plant Smoke Generator (Special)
	//$Color 12
	states
	{
	Spawn:
		TNT1 A 70 A_SpawnItemEx("PowerPlantSmokeGeneratorFX2")
		loop
	}
}

actor PowerPlantSmokeGeneratorFX2
{
	+CLIENTSIDEONLY
	+NOINTERACTION
	states
	{
	Spawn:
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 2 A_SpawnItemEx("PowerPlantSmokePuff", random(-32,32), random(-32,32), 0, 0, 0, -3)
		stop
	}
}