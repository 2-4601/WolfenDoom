/////////////////////////////
// ACTORS FOR REGULAR MAPS // by GuardSoul
/////////////////////////////

ACTOR PuffSmoke
{
	+NOINTERACTION
	+FORCEXYBILLBOARD
	+CLIENTSIDEONLY
	+WINDTHRUST
	RenderStyle "Translucent"
	Alpha 0.5
	Scale 0.5
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(256,1,2,3,4)
		SMK1 A 0 A_Jump(256,"End")
		SMK2 A 0 A_Jump(256,"End")
		SMK3 A 0 A_Jump(256,"End")
		SMK4 A 0 A_Jump(256,"End")
	End:
		"####" ABCDEFGHIJKLMNOP 2 A_FadeOut(0.025)
		stop
	}
}

ACTOR PowerPlantSmokePuff : PuffSmoke {Scale 8.0}
ACTOR PowerPlantSmokePuffSmall : PuffSmoke {Scale 4.0}

ACTOR PowerPlantSmokeGenerator : SwitchableDecoration
{
	//$Category Special Effects (Wolf3D)
	//$Title Power Plant Smoke Generator (Normal)
	//$Color 12
	+NOGRAVITY
	+INVULNERABLE
	+SHOOTABLE
	+NOINTERACTION
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_JumpIf(CallACS("SmokeSwitch")==0,"EndSpawn")
	Active:
		TNT1 A 70 A_SpawnItemEx("PowerPlantSmokeGeneratorFX")
		loop
	Inactive:
		TNT1 A -1
		stop
	EndSpawn:
		TNT1 A -1
		Goto Spawn
	}
}

ACTOR PowerPlantSmokeGeneratorFX
{
	+CLIENTSIDEONLY
	+NOINTERACTION
	States
	{
	Spawn:
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 4 A_SpawnItemEx("PowerPlantSmokePuff", random(-32,32), random(-32,32), 0, 0, 0, 3)
		stop
	}
}
 
ACTOR PowerPlantSmokeGeneratorSmall : SwitchableDecoration
{
	//$Category Special Effects (Wolf3D)
	//$Title Power Plant Smoke Generator (Small)
	//$Color 12
	+CLIENTSIDEONLY
	+NOINTERACTION
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_JumpIf(CallACS("SmokeSwitch")==0,"EndSpawn")
	Active:
		TNT1 A 2 A_SpawnItemEx("PowerPlantSmokePuffSmall", random(-32,32), random(-32,32), 0, 0, 0, 3)
		loop
	Inactive:
		TNT1 A -1
		stop
	EndSpawn:
		TNT1 A -1
		Goto Spawn
	}
}

ACTOR PowerPlantSmokeGenerator2 : PowerPlantSmokeGenerator
{
	//$Category Special Effects (Wolf3D)
	//$Title Power Plant Smoke Generator (Special)
	//$Color 12
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_JumpIf(CallACS("SmokeSwitch")==0,"EndSpawn")
	Active:
		TNT1 A 70 A_SpawnItemEx("PowerPlantSmokeGeneratorFX2")
		loop
	Inactive:
		TNT1 A -1
		stop
	EndSpawn:
		TNT1 A -1
		Goto Spawn
	}
}

ACTOR PowerPlantSmokeGeneratorFX2
{
	+CLIENTSIDEONLY
	+NOINTERACTION
	States
	{
	Spawn:
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 4 A_SpawnItemEx("PowerPlantSmokePuff", random(-32,32), random(-32,32), 0, 0, 0, -3)
		stop
	}
}

/////////////////////////////
// ACTORS FOR VOLCANO MAPS // by Tormentor667
/////////////////////////////

ACTOR VolcanoPuffSmoke
{
	+NOINTERACTION
	+FORCEXYBILLBOARD
	+CLIENTSIDEONLY
	+WINDTHRUST
	RenderStyle "Translucent"
	Alpha 1.0
	Scale 100
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(256,1,2,3,4)
		SMK1 A 0 A_Jump(256,"End")
		SMK2 A 0 A_Jump(256,"End")
		SMK3 A 0 A_Jump(256,"End")
		SMK4 A 0 A_Jump(256,"End")
	End:
		"####" AAAAAAAAAA 5 A_FadeIn(0.1)
		"####" ABCDEFGHIJKLMNOP 16 A_FadeOut(0.0625)
		stop
	}
}

ACTOR VolcanoSmokeGenerator : SwitchableDecoration
{
	//$Category Special Effects (Wolf3D)
	//$Title Volcano Smoke Generator (Huge)
	//$Color 12
	+NOGRAVITY
	+INVULNERABLE
	+SHOOTABLE
	+NOINTERACTION
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_JumpIf(CallACS("SmokeSwitch")==0,"EndSpawn")
	Active:
		TNT1 A 30 A_SpawnItemEx("VolcanoPuffSmoke", random(-128,128), random(-128,128), 0, random(1,5), random(1,5), random(10,20), random(1,359))
		loop
	Inactive:
		TNT1 A -1
		stop
	EndSpawn:
		TNT1 A -1
		Goto Spawn
	}
}