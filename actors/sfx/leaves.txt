ACTOR LeavesSpawner : CinderSpawner //ozy81
{
	//$Title Leaves Spawner
	//$Sprite LEFYA0
	+FLATSPRITE
	+LOOKALLAROUND
	+ROLLSPRITE
	States
	{
	Spawn:
	Active:
		TNT1 A 0 NODELAY A_JumpIf(CallACS("LeavesSwitch")==0,"EndSpawn")
	ActiveLoop:
		TNT1 A 0 A_CheckSightOrRange(1200.0, "LoopWait", TRUE)
	LeafLoop: //if you need more spawn leaves, simply change above tics from 8 to a lower value, min 1
		TNT1 A 8 A_SpawnItemEx("FloatingLeaves", random(-Args[0],Args[0]), random(-Args[0],Args[0]), random(0,Args[1]), random(Args[2],Args[3]), 0, random(-Args[2],Args[2]), random(-4, 4), 128, Args[4])
		Goto ActiveLoop
	Inactive:
		TNT1 A -1
		Stop
	EndSpawn:
		TNT1 A 1
		Stop
	LoopWait:
		TNT1 A 10
		Goto ActiveLoop
	}
}

ACTOR FloatingLeaves : Debris_Leaf
{
	DistanceCheck "sfxlod"
	+DONTSPLASH
	+WINDTHRUST
	States
	{
	Spawn:
		LEFY A 0 NODELAY A_SetScale(ScaleX + frandom(0.15,0.45),ScaleY + frandom(0.15,0.45))
	StartLoop:
		LEFY ABCDEFGH 6 {
							A_SetGravity(frandom(0.0525,0.0725));
							A_SetRoll(roll + frandom(-0.5, 0.5), SPF_INTERPOLATE);
							A_SetAngle(angle + random(-15, 15));
							A_SetPitch(pitch + frandom(-0.25, 0.25));
							A_JumpIf(waterlevel == 3, "AdjustMass");
							}
		Loop
	AdjustMass: //mxd
		"####" "#" 0 A_SetMass(350)
		Goto Swim
	Swim: //mxd
		"####" A 2 A_ScaleVelocity(0.7)
		"####" B 2 A_SpawnItemEx("Bubble", 0, 0, 0, 0, 0, 2, random (0, 360), 0, 128)
		"####" C 2 A_ScaleVelocity(0.7)
		"####" D 2 A_SpawnItemEx("Bubble", 0, 0, 0, 0, 0, 2, random (0, 360), 0, 128)
		"####" E 2 A_ScaleVelocity(0.7)
		"####" F 2 A_SpawnItemEx("Bubble", 0, 0, 0, 0, 0, 2, random (0, 360), 0, 128)
		"####" G 2 A_ScaleVelocity(0.7)
		"####" H 2 A_SpawnItemEx("Bubble", 0, 0, 0, 0, 0, 2, random (0, 360), 0, 128)
		Loop
	Death:
		"####" A 0 {
								A_SetRoll(0); A_SetAngle(0); A_SetPitch(0);
								A_ChangeFlag("ROLLCENTER", FALSE); A_ChangeFlag("ROLLSPRITE", FALSE);
								}
		"####" A 0 A_Jump(256,8,7,6,5,4,3,2,1)
		"####" ABCDEFGH 0 A_Jump(256,"Death1")
		Stop
	Death1:
		"####" "#" 0 A_ChangeFlag("BOUNCEONACTORS", FALSE)
		Death1Wait:
		"####" "#" 1 A_FadeOut(0.06) //change this if you plan to make leaves disappear fastly or slowly
		Wait
	}
}