//Place the RainDropSpawner on your map, and use values set on the specials tab to modify it
//The first argument controls it's area. 128, for example makes it a 256x256 square or 256-radius circle (See fourth argument)
//The second argument controls it's frequency. The lower the number, the heavier the rainfall
//The third argument controls whether or not it plays it's ambient sound. 0 will play the sound, 1 will not
//The fourth argument controls the area type. 0 is a square, 1 is a circle

ACTOR RainSpawner
{
	//$Category Special Effects (Wolf3D)
	//$Title Rain Spawner
	//$Color 12

	//$Arg0 "Radius"
	//$Arg0Tooltip "Radius in map units\nDefault: 0"
	//$Arg1 "Frequency"
	//$Arg1Tooltip "Frequency, the lower the number, the heavier the rainfall\nRange: 0 - 255"
	//$Arg2 "Sound"
	//$Arg2Type 11
	//$Arg2Enum { 0 = "Yes"; 1 = "No"; }
	//$Arg3 "Area type"
	//$Arg3Type 11
	//$Arg3Enum { 0 = "Square"; 1 = "Circle"; }

	Radius 1
	Height 1
	
	+NOCLIP
	+CLIENTSIDEONLY
	+SPAWNCEILING
	+NOGRAVITY
	
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_JumpIf(!CallACS("rainswitch"), "Vanish")
		TNT1 A 0 A_JumpIf(!Args[2], "StartSound")
		goto PickShape
		
	StartSound:
		TNT1 A 0 A_PlaySound("Ambient/Rain", 7, 1.0, true)
		goto PickShape	
		
	PickShape:
		TNT1 A 0 A_JumpIf(Args[3], "CircleLoop")
		goto SquareLoop
		
	CircleLoop:
		TNT1 A 0 A_CheckRange(1200.0, "CircleWait", true)
		TNT1 A 1 A_SpawnItemEx("RainDrop", Random(-Args[0], Args[0]), 0, -2, 0, 0, 0, Random(0, 359), 0, Args[1])
		loop
		
	CircleWait:
		TNT1 A 10
		goto CircleLoop	
		
	SquareLoop:
		TNT1 A 0 A_CheckRange(1200.0, "SquareWait", true)
		TNT1 A 1 A_SpawnItemEx("RainDrop", Random(-Args[0], Args[0]), Random(-Args[0], Args[0]), -2, 0, 0, 0, 0, 0, Args[1])
		Loop
		
	SquareWait:
		TNT1 A 10
		goto SquareLoop
		
	Vanish:
		TNT1 A 1
		Stop
	
	// OLD VERSION (MUCH SLOW):	
	/*Spawn:
		TNT1 A 0 NODELAY A_JumpIf(CallACS("rainswitch")==0,"NoRain")
		TNT1 A 0 A_JumpIf(Args[2] > 0,2)
		TNT1 A 0 A_PlaySound("Ambient/Rain",7,1,1)
		TNT1 A 0 A_JumpIf(Args[3] > 0,2)
		TNT1 A 1 A_SpawnItemEx("RainDrop",Random(-Args[0],Args[0]),Random(-Args[0],Args[0]),-2,0,0,0,0,0,Args[1])
		Loop
		TNT1 A 1 A_SpawnItemEx("RainDrop",Random(-Args[0],Args[0]),0,-2,0,0,0,Random(0,360),0,Args[1])
		Loop
	NoRain:
		TNT1 A 1 A_StopSound(7)
		Goto Spawn*/	
	}
}

ACTOR RainDrop
{
	Height 2
	Radius 2
	Renderstyle Translucent
	Alpha 0.8
	YScale 4.0
	
	+Missile
	+CLIENTSIDEONLY
	+FORCEYBILLBOARD
	+NOBLOCKMAP
	+DONTSPLASH
	
	States
	{
	Spawn:
		RNDR A 4 A_JumpIf(WaterLevel > 0, "Death")
		Loop

	Death:
		TNT1 A 0 A_SetScale(frandom(0.6, 0.9))
		RNDR ABCDEFGH 1 A_FadeOut(0.1)
		Stop
	}
}