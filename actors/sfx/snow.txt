//////////////////////
// SNOW SPAWNERS    //
// by Tormentor667  //
// improved by Ceeb //
// improved even    //
// further by MaxED //
//////////////////////

ACTOR SnowSpawner
{
	//$Category Special Effects (Wolf3D)
	//$Title Snow Spawner
	//$Color 12

	//$Arg0 "Radius"
	//$Arg0Tooltip "Radius in map units\nDefault: 0"
	//$Arg1 "Frequency"
	//$Arg1Tooltip "The lower the number, the heavier the snowfall\nRange: 0 - 255"
	//$Arg2 "Area"
	//$Arg2Type 11
	//$Arg2Enum { 0 = "Square"; 1 = "Circle"; }

	Radius 1
	Height 3
	
	+NOBLOCKMAP
	+NOGRAVITY
	+NOSECTOR
	+NOINTERACTION
	+NOCLIP
	+CLIENTSIDEONLY
	+SPAWNCEILING
	
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_JumpIf(!CallACS("snowswitch"), "Vanish")
		goto PickShape
		
	PickShape:
		TNT1 A 0 A_JumpIf(Args[2], "CircleLoop")
		goto SquareLoop
		
	CircleLoop:
		TNT1 A 0 A_CheckRange(1024.0, "CircleWait", true)
		TNT1 A 6 A_SpawnItemEx("SnowParticle", random(-Args[0], Args[0]), 0, 0, frandom(-1.0, 1.0), frandom(-1.0, 1.0),frandom(-1.0, -3.0), random(0, 359), 0, Args[1])
		loop
		
	CircleWait:
		TNT1 A 10
		goto CircleLoop	
		
	SquareLoop:
		TNT1 A 0 A_CheckRange(1024.0, "SquareWait", true)
		TNT1 A 6 A_SpawnItemEx("SnowParticle", random(-Args[0], Args[0]), random(-Args[0], Args[0]), 0, frandom(-1.0, 1.0), frandom(-1.0, 1.0), frandom(-1.0, -3.0), 0, 0, Args[1])
		Loop
		
	SquareWait:
		TNT1 A 10
		goto SquareLoop
		
	Vanish:
		TNT1 A 1
		Stop
	
	//OLD VERSION (MUCH SLOW)
	/*Spawn:
		TNT1 A 0 NODELAY A_CheckSight("Unsighted")
		TNT1 A 0 A_JumpIf(CallACS("snowswitch")==0,"Unsighted")
		TNT1 A 0 A_JumpIf(Args[2] > 0,2)
		TNT1 A 8 A_SpawnItemEx ("SnowParticle",Random(-Args[0],Args[0]),Random(-Args[0],Args[0]),0,frandom(-1.0,1.0),frandom(-1.0,1.0),frandom(-1.0,-3.0),0,0,Args[1])
		Loop
		TNT1 A 8 A_SpawnItemEx ("SnowParticle",Random(-Args[0],Args[0]),0,0,frandom(-1.0,1.0),frandom(-1.0,1.0),frandom(-1.0,-3.0),Random(0,360),0,Args[1])
		Loop
	Unsighted:
		TNT1 A 8
		Goto Spawn*/
	}
}

ACTOR SnowParticle
{
	Radius 1
	Height 3
	Projectile
	RenderStyle Translucent
	Alpha 0
	Scale 0.6
	
	+MISSILE
	+NOBLOCKMAP
	+DONTSPLASH
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SetScale(frandom(0.3, 0.6))
		SNOW AAAAAAAAAAAAAA 1 A_FadeIn(0.05)
		SNOW A -1
		Stop
		
	Death:
		SNOW A 1 A_FadeOut(0.05)
		Loop
	}
}