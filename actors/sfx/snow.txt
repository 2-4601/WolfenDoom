///////////////////////////////////
// SNOW SPAWNERS by Tormentor667 //
//   improved by  Ceeb & MaxED   //
///////////////////////////////////

ACTOR SnowSpawner
{
	//$Category Special Effects (BoA)
	//$Title Snow Spawner
	//$Color 12
	//$Sprite SNOWA0
	//$Arg0 "Radius"
	//$Arg0Tooltip "Radius in map units\nDefault: 0"
	//$Arg1 "Frequency"
	//$Arg1Tooltip "The lower the number, the heavier the snowfall\nRange: 0 - 255"
	//$Arg2 "Area"
	//$Arg2Type 11
	//$Arg2Enum { 0 = "Square"; 1 = "Circle"; }
	Radius 1
	Height 3
	+CLIENTSIDEONLY
	+NOBLOCKMAP
	+NOCLIP
	+NOGRAVITY
	+NOINTERACTION
	+NOSECTOR
	+SPAWNCEILING
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_JumpIf(!CallACS("snowswitch"), "Vanish")
		Goto PickShape
	PickShape:
		TNT1 A 0 A_JumpIf(Args[2], "CircleLoop")
		Goto SquareLoop
	CircleLoop:
		TNT1 A 0 A_CheckRange(1024.0, "CircleWait", TRUE)
		TNT1 A 6 A_SpawnItemEx("SnowParticle", random(-Args[0], Args[0]), 0, 0, frandom(-1.0, 1.0), frandom(-1.0, 1.0),frandom(-1.0, -3.0), random(0, 359), 0, Args[1])
		Loop
	CircleWait:
		TNT1 A 35
		Goto CircleLoop
	SquareLoop:
		TNT1 A 0 A_CheckRange(1024.0, "SquareWait", TRUE)
		TNT1 A 6 A_SpawnItemEx("SnowParticle", random(-Args[0], Args[0]), random(-Args[0], Args[0]), 0, frandom(-1.0, 1.0), frandom(-1.0, 1.0), frandom(-1.0, -3.0), 0, 0, Args[1])
		Loop
	SquareWait:
		TNT1 A 35
		Goto SquareLoop
	Vanish:
		TNT1 A 1
		Stop
	}
}

ACTOR SnowParticle
{
	DistanceCheck "sfxlod"
	Radius 1
	Height 3
	Projectile
	RenderStyle "Add"
	Alpha 0.6
	Scale 0.6
	+CLIENTSIDEONLY
	+DONTSPLASH
	+FORCEXYBILLBOARD
	+NOBLOCKMAP
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SetScale(frandom(0.3, 0.6))
		SNOW AAAAAAAA 1 A_FadeIn(0.05)
		SNOW A -1
		Stop
	Death:
		SNOW A 1 A_FadeOut(0.02,FTF_REMOVE)
		Loop
	}
}

ACTOR ConfettiSpawner : SnowSpawner
{
	//$Title Confetti Spawner
	//$Sprite CONFA0
	//$Arg1Tooltip "The lower the number, the heavier the amount of confetti\nRange: 0 - 255"
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_JumpIf(!CallACS("snowswitch"), "Vanish")
		Goto PickShape
	PickShape:
		TNT1 A 0 A_JumpIf(Args[2], "CircleLoop")
		Goto SquareLoop
	CircleLoop:
		TNT1 A 6 A_SpawnItemEx("ConfettiParticle", random(-Args[0], Args[0]), 0, 0, frandom(-1.0, 1.0), frandom(-1.0, 1.0),frandom(-4.0, 4.0), random(0, 359), SXF_TRANSFERPITCH | SXF_CLIENTSIDE, Args[1])
		Loop
	SquareLoop:
		TNT1 A 6 A_SpawnItemEx("ConfettiParticle", random(-Args[0], Args[0]), random(-Args[0], Args[0]), 0, frandom(-1.0, 1.0), frandom(-1.0, 1.0), frandom(-4.0, 4.0), 0, SXF_TRANSFERPITCH | SXF_CLIENTSIDE, Args[1])
		Loop
	Vanish:
		TNT1 A 1
		Stop
	}
}

ACTOR ConfettiParticle
{
	DistanceCheck "sfxlod"
	Radius 2
	Height 2
	Damage 0
	Scale 0.5
	Projectile
	-BLOCKEDBYSOLIDACTORS
	-NOGRAVITY
	+CLIENTSIDEONLY
	+DONTSPLASH
	+FLATSPRITE
	+INTERPOLATEANGLES
	+NOBLOCKMAP
	+ROLLSPRITE
	+WINDTHRUST
	States
	{
	Spawn:
		CONF A 0 NODELAY A_SetGravity(frandom(0.0005,0.0225))
	SetSpawn:
		"####" A 0 A_Jump(256,1,2,3,4,5,6,7,8,9,10,11,12)
		"####" ABCDEFFEDCBA 0 A_Jump(256,"SpawnLoop","SpawnLoop2")
	SpawnLoop:
		"####" "#" 0 {A_SetTics(random(1,3)); A_SetRoll(roll + frandom(-30.5, 60.5), SPF_INTERPOLATE); A_SetAngle(angle + random(-45, 45)); A_SetPitch(pitch + frandom(-95.25, 95.25));}
		Loop
	SpawnLoop2:
		"####" "#" 0 {A_SetTics(random(1,3)); A_SetRoll(roll - frandom(-30.5, 60.5), SPF_INTERPOLATE); A_SetAngle(angle + random(-45, 45)); A_SetPitch(pitch + frandom(-95.25, 95.25));}
		Loop
	Death:
		"####" "#" 0 {A_SetRoll(0); A_SetAngle(0); A_SetPitch(0); bRollSprite = FALSE;}
		"####" "#" 1 A_SetTics(35*CallACS("DebrisLifetime")/4)
	DeathWait:
		"####" "#" 1 A_FadeOut(0.1)
		Wait
	}
}