ACTOR Mine
{
	//$Category Hazards (Wolf3D)
	//$Title Mine
	//$Color 3
	Health 1000
	Radius 30
	Height 12
	Mass 9999999
	+ACTIVATEMCROSS
	+DONTGIB
	+LOOKALLAROUND
	+NOBLOOD
	+NOICEDEATH
	+NOTAUTOAIMED
	+SHOOTABLE
	+TOUCHY
	DeathSound "panzer/explode"
	Obituary "$OBMINE"
	States
	{
	Spawn:
		TNT1 A 0 NoDelay ACS_NamedExecuteAlways("SetRadarTID",0)
	See:
		MINE A 6 A_Chase
		Loop
	Death:
		"####" A 8 A_PlaySound("MINEF")
		"####" A 0 A_Scream
		"####" A 0 A_SpawnItemEx("GeneralExplosion_Medium")
		Stop
	}
}

ACTOR MineInvisible : Mine
{
	//$Title Mine (invisible)
	Radius 24
	Height 12
	RenderStyle "None"
}

ACTOR PlacedMine : Mine
{
	//$Title Friendly Mine (from Deployable item)
	Health 10
	Mass 500
	Obituary "$OBDMINE"
	-TOUCHY
	States
	{
	Spawn:
		BOAM A 35 NoDelay A_CheckProximity("SpawnWait", "BoaPlayer", radius+64, 1, CPXF_SETTARGET) //wait 1 sec
	See:
		"####" A 0 A_CheckProximity("SpawnWait", "BoaPlayer", radius+64, 1, CPXF_SETTARGET)
		"####" A 35 {A_Chase; A_ChangeFlag("TOUCHY",TRUE); A_CheckProximity("SpawnWait", "BoaPlayer", radius+64, 1, CPXF_SETTARGET);}
		Loop
	Death:
		"####" A 8 A_PlaySound("MINEF")
		"####" A 0 A_Scream
		"####" A 0 A_SpawnItemEx("FriendlyExplosion_Medium")
		Stop
	SpawnWait:
		"####" A 1 A_ChangeFlag("TOUCHY",FALSE)
		"####" A 34
		Goto Spawn
	}
}

ACTOR UnderwaterMine : Mine
{
	//$Title Underwater Mine
	Health 300
	Radius 16
	Height 128
	+NOGRAVITY
	var int user_theta;
	States
	{
	Spawn:
	See:
		UWMR ABCDE 6 A_Chase
		Loop
	Death:
		TNT1 A 0 A_SpawnItemEx("GeneralExplosion_Medium",0,0,64)
		UWMR A 12 A_Scream
	Shock:
		UWMR A 0 A_SpawnProjectile("Bubble",random(64,76),0,user_theta,2)
		"####" A 0 A_SetUserVar("user_theta",user_theta+20)
		"####" A 0 A_JumpIf(user_theta==360,1)
		Loop
		UWMI H -1
		Stop
	}
}

ACTOR UnderwaterMineChain
{
	//$Category Hazards (Wolf3D)
	//$Title Underwater Mine (longer Chain)
	//$Color 3
	Radius 16
	Height 128
	Mass 9999999
	+NOGRAVITY
	States
	{
	Spawn:
		UWMI I -1
		Stop
	}
}