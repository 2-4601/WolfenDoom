/////////////////////////////
// STONE & ROCK PROJECTILE //
/////////////////////////////
ACTOR FallingRock : RandomSpawner
{
	DropItem "FallingRock1"
	DropItem "FallingRock2"
}

ACTOR FallingRock1
{
	Radius 8
	Height 12
	Damage (random(0,1))
	Projectile
	-NOGRAVITY
	+ROLLSPRITE
	SeeSound "misc/SRCRK1"
	DeathSound "misc/SRCRK2"
	Obituary "$OBFALLRK"
	States
	{
	Spawn:
		SRR1 A 0 NODELAY A_Gravity
		"####" A 1 A_SetRoll(roll+8, SPF_INTERPOLATE,0)
		Goto Spawn+1
	Death:
		"####" A 2
		TNT1 A 0 A_SpawnProjectile("FallingRockFrac1",0,0,60,2,40)
		"####" A 0 A_SpawnProjectile("FallingRockFrac2",0,0,120,2,40)
		"####" A 0 A_SpawnProjectile("FallingRockFrac3",0,0,180,2,40)
		"####" A 0 A_SpawnProjectile("FallingRockFrac1",0,0,240,2,40)
		"####" A 0 A_SpawnProjectile("FallingRockFrac2",0,0,300,2,40)
		"####" A 1 A_SpawnProjectile("FallingRockFrac3",0,0,0,2,40)
		Stop
	}
}

ACTOR FallingRock2 : FallingRock1
{
	States
	{
	Spawn:
		SRR2 A 0 NODELAY A_Gravity
		"####" A 1 A_SetRoll(roll-8, SPF_INTERPOLATE,0)
		Goto Spawn+1
	}
}

ACTOR FallingRockFrac1
{
	Radius 4
	Height 4
	Speed 12
	Damage (random(0,1))
	Projectile
	+ROLLSPRITE
	States
	{
	Spawn:
		SRR3 A 0 NODELAY A_Gravity
		"####" A 1 A_SetRoll(roll+20, SPF_INTERPOLATE,0)
		Goto Spawn+1
	Death:
		"####" A 300
		"####" A 6 A_FadeOut
		Wait
	}
}

ACTOR FallingRockFrac2 : FallingRockFrac1
{
	States
	{
	Spawn:
		SRR4 A 0 NODELAY A_Gravity
		"####" A 1 A_SetRoll(roll-20, SPF_INTERPOLATE,0)
		Goto Spawn+1
	}
}

ACTOR FallingRockFrac3 : FallingRockFrac1
{
	States
	{
	Spawn:
		SRR5 A 0 NODELAY A_Gravity
		"####" A 1 A_SetRoll(roll+20, SPF_INTERPOLATE,0)
		Goto Spawn+1
	}
}