actor RollingBoulderGrey
{
	Radius 24
	Height 48
	Speed 10
	Damage 250
	Scale 0.5
	Gravity 1.5
	BounceFactor 0.7
	BounceType Doom
	obituary "%o was flattened by a boulder."
	+RIPPER
	+DONTRIP
	+BOUNCEONFLOORS
	+NOBOUNCESOUND
	+NODAMAGETHRUST
	+NOEXTREMEDEATH
	+SOLID
	+DROPOFF
	+MISSILE
	DeathSound "boulder/stop"
	States
	{
	Spawn:
		GBOL A 2 NODELAY A_PlaySound("Boulder/Roll",5,1,1)
		GBOL BCDA 2 A_Quake(1,2,0,192,"")
		Wait
	Death:
		TNT1 A 1 A_SpawnItem ("BoulderStoppedGrey")
		TNT1 A 0 A_StopSound(5)
		TNT1 A 0 A_PlaySound ("Boulder/Stop")
		stop
	}
}

ACTOR RollingBoulderGreyHugger : RollingBoulderGrey
{
	Gravity 1
	BounceType None
	-DONTRIP
	-BOUNCEONFLOORS
	-NOBOUNCESOUND
	-SOLID
	+NOBLOCKMAP
	+ACTIVATEIMPACT
	+ACTIVATEPCROSS
	+FLOORHUGGER
}

actor RollingBoulderBrown : RollingBoulderGrey
{
	States
	{
	Spawn:
		BBOL A 2 NODELAY A_PlaySound("Boulder/Roll",5,1,1)
		BBOL BCDA 2 A_Quake(1,2,0,192,"")
		Wait
	}
}

actor RollingBoulderBrownHugger : RollingBoulderGreyHugger
{
  States
  {
	Spawn:
		BBOL A 2 NODELAY A_PlaySound("Boulder/Roll",5,1,1)
		BBOL BCDA 2 A_Quake(1,2,0,192,"")
		Wait
	}
}

actor BoulderStoppedGrey
{
	Radius 24
	Height 48
	Scale 0.5
	+SOLID
	States
	{
	Spawn:
		GBOL A -1
		stop
	}
}

actor BoulderStoppedBrown : BoulderStoppedGrey
{
	States
	{
	Spawn:
		BBOL A -1
		stop
	}
}

actor BoulderStartGrey : SwitchableDecoration 10310
{
	//$Category Hazards (Wolf3D)
	//$Title Boulder grey (bounce, inactive)
	//$Color 3
	Radius 24
	Height 48
	Scale 0.5
	obituary "%o was flattened by a boulder."
	+SOLID
	States
	{
	Spawn:
	Inactive:
		GBOL A -1
		stop
	Active:
		TNT1 A 1 A_SpawnItemEx("RollingBoulderGrey",0,0,0,10)
		stop
	}
}

actor BoulderStartBrown : BoulderStartGrey 10311
{
	//$Category Hazards (Wolf3D)
	//$Title Boulder brown (bounce, inactive)
	//$Color 3
	States
	{
	Spawn:
	Inactive:
		BBOL A -1
		stop
	Active:
		TNT1 A 1 A_SpawnItemEx("RollingBoulderBrown",0,0,0,10)
		stop
	}
}

actor BoulderStartBrownHugger : BoulderStartGrey 10312
{
	//$Category Hazards (Wolf3D)
	//$Title Boulder brown (hugger, inactive)
	//$Color 3
	States
	{
	Spawn:
	Inactive:
		BBOL A -1
		stop
	Active:
		TNT1 A 1 A_SpawnItemEx ("RollingBoulderBrownHugger",0,0,0,10)
		stop
	}
}

//////////////////
// Nails in Air //
//////////////////

actor Nail
{
	Projectile
	radius 4
	height 8
	speed 44
	damage 4
	scale 0.5
	+BLOODSPLATTER
	seesound "NailFlight"
	states
	{
	Spawn:
		DART A 1 A_SpawnItemEx("NailBlur")
		loop
	Crash:
	Death:
		DART B 0 A_PlaySound("NailHit")
		DART BCDEFG 2
		stop
	XDeath:
		DART B 0 A_PlaySound("NailHitBleed")
		DART BCDEFG 2
		stop
	}
}

actor NailBlur
{
	height 8
	radius 1
	scale 0.5
	speed 0.2
	renderstyle Translucent
	+NOGRAVITY
	+DROPOFF
	states
	{
	Spawn:
		DART A 1 A_FadeOut(0.2)
		loop
	}
}

//External Actors
actor ScorpionNail : Nail
{
	Speed 48
	Scale 0.4
	SeeSound "scorpion/sting1"
	DeathSound "scorpion/sting2"
}