actor RollingBoulderGrey
{
  Radius 24
  Height 48
  Speed 10
  Damage 250
  Scale 0.5
  obituary "%o was flattened by a boulder."
  Projectile
  +RIPPER
  +NODAMAGETHRUST
  +FLOORHUGGER
  +NOEXTREMEDEATH
  -NOGRAVITY
  -NOTELEPORT
  DeathSound "boulder/stop"
  States
  {
  Spawn:
    GBOL A 0
    GBOL A 2 A_PlaySoundEx ("Boulder/Roll", "SoundSlot5", 1)
    // Fall through
  Roll:
    GBOL BCDA 2 A_Quake (1, 2, 0, 192, "") 
    loop
  Death:
    TNT1 A 1 A_SpawnItem ("BoulderStoppedGrey")
    TNT1 A 0 A_StopSoundEx ("SoundSlot5")
    TNT1 A 0 A_PlaySound ("Boulder/Stop")
    stop
  }
}

actor RollingBoulderBrown : RollingBoulderGrey
{
  States
  {
  Spawn:
    BBOL A 0
    BBOL A 2 A_PlaySoundEx ("Boulder/Roll", "SoundSlot5", 1)
    // Fall through
  Roll:
    BBOL BCDA 2 A_Quake (1, 2, 0, 192, "") 
    loop
  Death:
    TNT1 A 1 A_SpawnItem ("BoulderStoppedBrown")
    TNT1 A 0 A_StopSoundEx ("SoundSlot5")
    TNT1 A 0 A_PlaySound ("Boulder/Stop")
    stop
  }
}


actor BoulderStoppedGrey
{
  Radius 24
  Height 48
  Scale 0.5
  +SOLID
  States
  {
  Spawn:
    GBOL A -1
    stop
  }
}

actor BoulderStoppedBrown : BoulderStoppedGrey
{
  States
  {
  Spawn:
    BBOL A -1
    stop
  }
}


actor BoulderStartGrey : SwitchableDecoration 10310
{
//$Category Hazards (Wolf3D)
//$Title Boulder grey (inactive)
//$Color 3
  Radius 24
  Height 48
  Scale 0.5
  obituary "%o was flattened by a boulder."
  +SOLID
  States
  {
  Spawn:
  Inactive:
    GBOL A -1
    stop
  Active:
    TNT1 A 1 A_SpawnItemEx ("RollingBoulderGrey", 0, 0, 0, 10, 0, 0, 0) 
    stop   
  }
}

actor BoulderStartBrown : BoulderStartGrey 10311
{
//$Category Hazards (Wolf3D)
//$Title Boulder brown (inactive)
//$Color 3
  States
  {
  Spawn:
  Inactive:
    BBOL A -1
    stop
  Active:
    TNT1 A 1 A_SpawnItemEx ("RollingBoulderBrown", 0, 0, 0, 10, 0, 0, 0) 
    stop   
  }
}


//////////////////
// Nails in Air // SpawnID 180
//////////////////

actor Nail
{
	PROJECTILE
	radius 3
	height 3
	speed 44
	damage 4
	scale 0.5
	+BLOODSPLATTER
	seesound "NailFlight"
	deathsound ""
	spawnid 180
	states
	{
		Spawn:
			DART A 1 A_CustomMissile("NailBlur",0,0,180)
			loop
		Crash:
		Death:
			DART B 0 A_PlaySound("NailHit")
			DART BCDEFG 2
			stop
        	XDeath:
			DART B 0 A_PlaySound("NailHitBleed")
			DART BCDEFG 2
        	stop
	}
}

actor NailBlur       
{
	height 8
	radius 1
	damage 0
	scale 0.5
	speed 0.2
	renderstyle Translucent
	alpha 1.0
	+NOGRAVITY
	+DROPOFF
	states
	{
		Spawn:
			DART A 1 A_FadeOut(0.2)
			loop
	}
}
