ACTOR RollingBoulderGrey
{
	Radius 24
	Height 48
	Speed 10
	Damage 250
	Scale 0.5
	Gravity 1.5
	BounceFactor 0.7
	BounceType Doom
	Obituary "$BOULDER"
	+RIPPER
	+DONTRIP
	+BOUNCEONFLOORS
	+NOBOUNCESOUND
	+NODAMAGETHRUST
	+NOEXTREMEDEATH
	+SOLID
	+DROPOFF
	+MISSILE
	DeathSound "boulder/stop"
	States
	{
	Spawn:
		GBOL A 2 NODELAY A_PlaySound("Boulder/Roll",5,1,1)
		"####" BCDA 2 A_Quake(1,2,0,192,"")
		Loop
	Death:
		TNT1 A 1 A_SpawnItem ("BoulderStoppedGrey")
		"####" A 0 A_StopSound(5)
		"####" A 0 A_PlaySound ("Boulder/Stop")
		stop
	}
}

ACTOR RollingBoulderGreyHugger : RollingBoulderGrey
{
	Gravity 1
	BounceType None
	-DONTRIP
	-BOUNCEONFLOORS
	-NOBOUNCESOUND
	-SOLID
	+NOBLOCKMAP
	+ACTIVATEIMPACT
	+ACTIVATEPCROSS
	+FLOORHUGGER
}

ACTOR RollingBoulderBrown : RollingBoulderGrey
{
	States
	{
	Spawn:
		BBOL A 2 NODELAY A_PlaySound("Boulder/Roll",5,1,1)
		"####" BCDA 2 A_Quake(1,2,0,192,"")
		Loop
	Death:
		TNT1 A 1 A_SpawnItem ("BoulderStoppedBrown")
		"####" A 0 A_StopSound(5)
		"####" A 0 A_PlaySound ("Boulder/Stop")
		stop
	}
}

ACTOR RollingBoulderBrownHugger : RollingBoulderGreyHugger
{
  States
  {
	Spawn:
		BBOL A 2 NODELAY A_PlaySound("Boulder/Roll",5,1,1)
		"####" BCDA 2 A_Quake(1,2,0,192,"")
		Loop
	}
}

ACTOR BoulderStoppedGrey
{
	Radius 24
	Height 48
	Scale 0.5
	+SOLID
	States
	{
	Spawn:
		GBOL A -1
		stop
	}
}

ACTOR BoulderStoppedBrown : BoulderStoppedGrey
{
	States
	{
	Spawn:
		BBOL A -1
		stop
	}
}

ACTOR BoulderStartGrey : SwitchableDecoration
{
	//$Category Hazards (Wolf3D)
	//$Title Boulder grey (bounce, inactive)
	//$Color 3
	Radius 24
	Height 48
	Scale 0.5
	Obituary "$BOULDER"
	+SOLID
	States
	{
	Spawn:
	Inactive:
		GBOL A -1
		stop
	Active:
		TNT1 A 1 A_SpawnItemEx("RollingBoulderGrey",0,0,0,10)
		stop
	}
}

ACTOR BoulderStartBrown : BoulderStartGrey
{
	//$Category Hazards (Wolf3D)
	//$Title Boulder brown (bounce, inactive)
	//$Color 3
	States
	{
	Spawn:
	Inactive:
		BBOL A -1
		stop
	Active:
		TNT1 A 1 A_SpawnItemEx("RollingBoulderBrown",0,0,0,10)
		stop
	}
}

ACTOR BoulderStartBrownHugger : BoulderStartGrey
{
	//$Category Hazards (Wolf3D)
	//$Title Boulder brown (hugger, inactive)
	//$Color 3
	States
	{
	Spawn:
	Inactive:
		BBOL A -1
		stop
	Active:
		TNT1 A 1 A_SpawnItemEx ("RollingBoulderBrownHugger",0,0,0,10)
		stop
	}
}

//////////////////
// Nails in Air //
//////////////////

ACTOR Nail
{
	Projectile
	Radius 4
	Height 8
	Speed 44
	damage 4
	Scale 0.5
	+BLOODSPLATTER
	seesound "NailFlight"
	States
	{
	Spawn:
		DART A 1 A_SpawnItemEx("NailBlur")
		loop
	Crash:
	Death:
		"####" B 0 A_PlaySound("NailHit")
		"####" BCDEFG 2
		stop
	XDeath:
		"####" B 0 A_PlaySound("NailHitBleed")
		"####" BCDEFG 2
		stop
	}
}

ACTOR NailBlur
{
	Height 8
	Radius 1
	Scale 0.5
	Speed 0.2
	RenderStyle Translucent
	+NOGRAVITY
	+DROPOFF
	States
	{
	Spawn:
		DART A 1 A_FadeOut(0.2)
		loop
	}
}

ACTOR NailFlame : Nail
{
	States
	{
	Spawn:
		DARF A 1 BRIGHT Light("Chandelier4")A_SpawnItemEx("NailBlur")
		loop
	Crash:
	Death:
		DART B 0 A_PlaySound("NailHit")
		"####" BCDEFG 2
		stop
	XDeath:
		"####" B 0 A_PlaySound("NailHitBleed")
		"####" BCDEFG 2
		stop
	}
}


//External Actors
ACTOR ScorpionNail : Nail
{
	Speed 48
	Scale 0.4
	SeeSound "scorpion/sting1"
	DeathSound "scorpion/sting2"
}