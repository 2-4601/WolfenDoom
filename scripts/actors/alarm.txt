/*

  These actors allow for activatable alarms that alert actors within a 512-unit radius.  
  Optionally, alarm activation can trigger spawn of additional enemies.

  Three actors are involved:
    AlarmPanel
	These are usable flatsprite actors that control Alarms and Alarm Spawners that have the
	same TID as the panel.  The Nazi-class descendant actor who is closest to the alarm panel
	(within 1024 units) will be automatically selected as the activator, and, once alerted, 
	will automatically walk to the alarm panel and activate it.  

	Any actors with the same TID as the Alarm Panel will be set to their 'Active' or 'Inactive'
	state, so any Alarm of Alarm Spawner actors (as well as any other switchable decorations)
	that have the same TID as the panel can be controlled by the panel.

	To silence alarms, the player must manually deactivate the alarm, or the Alarm Panel can 
	be deactivated via ACS to set inactive all controlled actors.

	Players can also turn deactivated alarms on...  This would normally be dumb, but might be 
	useful for flushing guards into a courtyard away from a key, or something similar.

	Active AlarmPanel actors have a red glow on them.

    Alarm
	These are simple actors that, upon activation, alert all actors within a 512-
	unit radius and set the actors' target to the alarm panel activator's target.

    AlarmSpawner
	These are spawn point actors that spawn up to three SneakableSSMP40Guard actors
	when they are activated.  A maximum of three guards will be present at any time; 
	if three guards were spawned and one guard was killed before the alarm was shut
	off, the next time the alarm goes off, only one additional guard will spawn.

	Spawning only occurs when no player can see the spawn point.

	Useful custom properties (set on Custom tab of thing properties in GZDB):
	  user_tid
	    Sets the TID that the spawned enemies are given.  By default they have no TID.

	  user_goal
	    Sets the goal/patrolpoint TID that the spawned enemies will walk to once they return to 
	    being idle.  By default, they go back to their spawn point and stand still.

  Alarms and Alarm Spawners must be given the same TID as an Alarm Panel that will control
  them, otherwise they will not function.  You can have multiple Alarm Panels with the same
  TID (e.g., so that the player can turn of alarms from a side office after the alarms are 
  turned on by a guard in a main area).

*/
class Alarm : Base
{
	bool active;
	int looptime;

	Default
	{
		+WALLSPRITE
		+ROLLSPRITE
		+NOGRAVITY
		-SOLID
		Height 16;
		Radius 8;
	}

	States
	{
		Spawn:
			ALRM A 35;
		Inactive:
			"####" A 35 { active = false; }
			Loop;
		Active:
			"####" AB 1 { active = true; }
			Loop;
	}

	override void Tick()
	{
		Super.Tick();

		if (active)
		{
			SoundAlert(target, false, 512);
			A_PlaySound("alarm_ring", CHAN_VOICE, 1.0, true);
		}
		else
		{
			A_StopSound(CHAN_VOICE);
		}
	}
}

class AlarmPanel : SwitchableDecoration
{
	bool active;
	Actor activator;

	Default
	{
		+WALLSPRITE
		+ROLLSPRITE
		+NOGRAVITY
		-SOLID
		Height 48;
		Radius 16;
	}

	States
	{
		Spawn:
			ALRM Q 35;
		Inactive:
			"####" Q 175 { // Check every 5 seconds, max
				if (
					!NaziStandard(activator) || 
					!activator.bShootable || 
					!activator.health > 0 || 
					(NaziStandard(activator).activationgoal && NaziStandard(activator).activationgoal != self)
				) { activator = FindActivator(); }
				active = false;
			}
			Loop;
		Active:
			"####" P 1 Light("LAZERRED") { activator = null; active = true; }
			Loop;
	}

	override bool Used(Actor user)
	{ // Handle the player activating/deactivating the alarm
		target = user;
		if (active) { Deactivate(user); }
		else { Activate(user); }
		return false;
	}

	override void Activate (Actor activator)
	{
		A_SetPeerState();
		Super.Activate(activator);
	}

	override void Deactivate (Actor activator)
	{
		A_SetPeerState(false);
		Super.Deactivate(activator);
	}

	Actor FindActivator(int range = 512)
	{
		NaziStandard closest;

		ThinkerIterator it = ThinkerIterator.Create("NaziStandard");
		NaziStandard mo;
		double dist;

		while (mo = NaziStandard(it.Next()))
		{
			dist = Distance3d(mo);

			if (
				!mo.bShootable || 
				mo.health <= 0  ||
				mo.bDormant ||
				mo.bAmbush ||
				mo.Speed == 0 ||
				dist > range ||
				!CheckSight(mo) ||
				(closest && dist > Distance3d(closest))
			)
			{
				if (mo.activationgoal == self) { mo.activationgoal = null; }
				continue;
			}

			closest = mo;
		}

		if (closest) { closest.activationgoal = self; }

		return closest;
	}

	void A_SetPeerState(bool makeactive = true)
	{
		A_PlaySound("switches/normbutn", CHAN_AUTO);
		if (tid)
		{ // If there's a TID, affect all actors with that TID
			let it = ActorIterator.Create(tid, "Actor");
			Actor mo;

			while (mo = Actor(it.Next()))
			{
				DoActivation(mo, makeactive);
			}

			DoActivation(self, makeactive);
		}
	}

	void DoActivation(Actor mo, bool makeactive)
	{
		if (!makeactive)
		{
			mo.target = null;
			mo.SoundAlert(NULL, false);
			if (mo is "AlarmPanel") { mo.SetStateLabel("Inactive"); }
			else { mo.Deactivate(self); }
		}
		else
		{
			if (LastHeard && LastHeard is "PlayerPawn") { target = LastHeard; }
			if (!target) { target = FindClosestPlayer(360); }
			mo.target = target;
			if (mo is "AlarmPanel") { mo.SetStateLabel("Active"); }
			else { mo.Activate(self); }
		}
	}

	Actor FindClosestPlayer(int fov = 120, int dist = 0, bool IgnoreFriendlies = True) // Also sets up initial sight parameters
	{
		Actor ClosestPlayer = null;

		LookExParams SearchParams;

		SearchParams.fov = fov;
		SearchParams.minDist = 0;
		SearchParams.maxDist = dist;
		SearchParams.maxHearDist = dist;

		for (int p = 0; p < MAXPLAYERS; p++) { // Iterate through all of the players and find the closest one
			Actor mo = players[p].mo;

			if (mo) {
				if (!mo.bShootable || mo.health <= 0) { continue; }
				if (players[p].cheats & CF_NOTARGET) { continue; }
				if (!IsVisible(mo, false, SearchParams)) { continue; }
				if (IgnoreFriendlies) { if (isFriend(mo)) { continue; } }
				if (ClosestPlayer && Distance3d(mo) > Distance3d(ClosestPlayer)) { continue; }

				ClosestPlayer = mo;
			}
		}
		return ClosestPlayer;
	}
}

// Actor spawner with some parameters hard-coded, sneakable guards as default,
//  and customized spawn locations, so that sneakables have a place to return 
//  to when they go back to idle
class AlarmSpawner : ActorSpawner
{
	States
	{
		Spawn:
			TNT1 A 35;
			Goto Inactive;
	}

	override void PostBeginPlay()
	{
		spawntype = GetSpawnableType(args[0]);
		if (!spawntype) { spawntype = "SneakableSSMP40Guard"; }
		if (!user_maxactors) { user_maxactors = 3; }
		if (!user_minspawndistance) { user_minspawndistance = 0; }
	}

	override state A_DoSpawns()
	{
		int guardcount = CountSpawns();

		if (guardcount < user_maxactors)
		{
			vector3 location = pos;

			if (guardcount == 1) { location += (-32, 32, 0); }
			else if (guardcount == 2) { location += (-32, -32, 0); }

			//Spawn enemies if not visible to player
			if (!InPlayerSightorRange(user_minspawndistance))
			{
				Actor guard = Spawn(spawntype, pos);
				guard.target = target;
				guard.angle = angle;
				guard.ChangeTID(user_tid);
				if (user_goal > 0)
				{
					let it = ActorIterator.Create(user_goal, "PatrolPoint");
					Actor goal;
					if (it) { goal = it.Next(); }
					if (goal) { guard.goal = goal; }

					if (Nazi(guard))
					{
						if (Nazi(guard).user_sneakable) { Nazi(guard).BecomeAlerted(); }
						Nazi(guard).activationgoal = goal;
					}

					guard.SetStateLabel("See");
				}
				Spawns.Push(guard);
			}
			return ResolveState("Active");
		}
		return ResolveState("Inactive");
	}
}