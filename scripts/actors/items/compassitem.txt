class CompassItem : Inventory
{
	String iconName;
	int specialclue;

	Property SpecialClue:specialclue;

	Default
	{
		//$Category Pickups (BoA)
		//$Color 13
		+INVENTORY.ALWAYSPICKUP
		+INVENTORY.INVBAR
		+INVENTORY.UNDROPPABLE
		Inventory.MaxAmount 1;
		Inventory.PickupSound "pickup/papers"; // Default to sounding like paper pickup
		Scale 0.5;
	}

	override void PostBeginPlay()
	{
		iconName = "";

		// Try to use the inventory icon as the compass icon
		iconName = TexMan.GetName(icon);

		// Otherwise fall back to using the spawn sprite
		if (iconName == "")
		{
			TextureID iconTex = CurState.GetSpriteTexture(0);
			iconName = TexMan.GetName(iconTex);
		}

		BoACompass.Add(self, iconName);

		Super.PostBeginPlay();
	}

	override bool TryPickup(in out Actor toucher)
	{
		bool pickup = Super.TryPickup(toucher);

		if (pickup && specialclue == 3)
		{
			TextureID tex = SpawnState.GetSpriteTexture(0);
			String texName = TexMan.GetName(tex);

			// If it belongs to this chapter and gets added, autosave on pickup so we don't have to deal with clearing the entries if we die.
			if (MapStatsHandler.AddSpecialPickup(texName, specialclue)) { level.MakeAutoSave(); }
		}

		return pickup;
	}
}