// Electrical emitter effects
class ElectricalArc : SwitchableDecoration
{
	Class<LightningBeam> beam;

	Property Beam:beam;

	Default
	{
		//$Category Hazards (BoA)/ZScript
		//$Title Electrical Arc
		//$Color 3
		+NOGRAVITY
		ElectricalArc.Beam "LightningBeamArc";
	}

	States
	{
		Spawn:
		Active:
			TNT1 A 1 A_LightningBeam(beam);
			Loop;
		Inactive:
			TNT1 A 1;
			Loop;
	}

	void A_LightningBeam(Class<LightningBeam> beam = "LightningBeamArc")
	{
		bool spawned;
		Actor p;

		[spawned, p] = A_SpawnItemEx(beam);
		if (LightningBeam(p)) {
			p.master = self;
			p.pitch = pitch;
			p.angle = angle;
		}
	}
}

class ElectricalPillar : ElectricalArc
{
	Default
	{
		//$Title Electrical Pillar
		-NOGRAVITY
		ElectricalArc.Beam "LightningBeamPillar";
	}

	override void PostBeginPlay()
	{
		if (waterlevel) { beam = "LightningBeamArc"; }
		pitch = -90;
	}

	override void Tick()
	{
		angle = Random(1, 360);

		Super.Tick();
	}
}

Class ElectricalArc2 : ElectricalArc
{
	Default
	{
		//$Title Green Electrical Arc
		ElectricalArc.Beam "LightningBeamArc2";
	}
}

Class ElectricalPillar2 : ElectricalPillar
{
	Default
	{
		//$Title Green Electrical Pillar
		+NOGRAVITY
		+SPAWNCEILING
		ElectricalArc.Beam "LightningBeamPillar2";
	}
}