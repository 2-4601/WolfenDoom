/*

  An actor that looks for dead 'Nazi' class actors to be healed.

  Automatically searches for the closest visible dead 'Nazi' actor that it is allied with (that 
  has the same FRIENDLY flag settings) that is not gibbed or burned to ash.  Once the actor is 
  identified, the Medic sets the actor as its goal and runs to it.

  Once within range of the goal actor (24 units centerpoint to centerpoint), the Medic enters its
  "Heal" state and begins to resurrect the dead actor.  

  While the dead actor is being healed, "HealingParticle" actors are spawned from the dead actor.
  The number of particles spawned, and the number of repetitions of the "Heal" state animation that
  are played, is based on the spawn health of the dead actor; one iteration of the heal animation
  (5 particle spawns) for every 25 hitpoints, with a maximum of 3 iterations.

  The now ressurected actor's target is cleared, and, if they are sneakable, they are set idle.

  The Medic also tries to avoid the player, by running away if a player is visible within 192 
  units.  When this happens, the medic also forgets what dead body it was going toward.

  Also can work as FRIENDLY, healing only the FRIENDLY dead actors.

*/
class NaziMedic : NaziStandard
{
	bool crouched;
	int frightenedloopcount;
	int healloopcount;

	Default
	{
		//$Category Monsters (Wolf3D)
		//$Title Nazi Medic
		//$Color 4
		+AVOIDMELEE
		+NEVERTARGET
		Health 20;
		Speed 6;
		DropItem "NaziMedicBox", 255;
	}

	States
	{
		Spawn:
			NMDC N 0; // Just sets the base sprite
			Goto Look;
		Look:
			"####" "#" 10 A_LookForBodies();
			"####" "#" 0 A_CheckForPlayer(True);
			Loop;
		See:
			"####" "#" 0 {
				user_incombat = True;
				crouched = false;
				Speed = Default.Speed;
				bChaseGoal = True;

				if (frightenedloopcount > 0) { frightenedloopcount--; }
				else { bFrightened = False; }

				A_CheckForPlayer();
			}
			"####" AAAAAA 1 A_Chase(null, null);
			"####" B 0 A_SpawnItemEx("EnemyStep",0,0,14,0,0,0,0,SXF_NOCHECKPOSITION);
			"####" BBBBBB 1 A_Chase(null, null);
			"####" CCCCCC 1 A_Chase(null, null);
			"####" D 0 A_SpawnItemEx("EnemyStep",0,0,14,0,0,0,0,SXF_NOCHECKPOSITION);
			"####" DDDDDD 1 A_Chase(null, null);
			Loop;
		Heal:
			"####" E 0 {
				crouched = True;
				if (goal) {
					healloopcount = goal.Default.Health > 25 ? min(goal.Default.Health / 25, 3) : 0; // Set the number of Heal iterations to scale with the actor's spawnhealth (max of 3 loops)
				}
			}
		Heal.Loop:
			"####" E 15;
			"####" F 5;
			"####" EEEEE 3 { goal.A_SpawnItemEx("HealingParticle", random(10,-10), random(10,-10), random(16,64), 0, 0, random(1, 2), 0);} // Spawn healing particles as the actor is ressurected
			"####" E 0 {
				if (healloopcount > 0) {
					healloopcount--;
					return ResolveState("Heal.Loop");
				}
				if (Nazi(goal) && Nazi(goal).user_sneakable) { Nazi(goal).BecomeIdle(); } // If it's a sneakable actor, make it idle
				if (goal) { goal.A_ClearTarget(); goal.bFriendly = bFriendly; } // Just to be sure, make the ressurected actor allied to the Medic...
				goal = null;
				master = null;
				return ResolveState("See");
			}
			Goto See;
		Raise:
			"####" # 35 { A_SetTics(Default.Health > 25 ? min(Default.Health / 25, 3) * 35 : 35); } // Delay here to match the medic's healing state duration
			"####" V 35;
			"####" ULKJ 5;
			"####" I 5 A_Jump(256,"See");
			Stop;
		Death:
			"####" H 5 {
				if (crouched) { return ResolveState("Death.Crouch"); }
				return ResolveState(null);
			}
			"####" I 5 A_Scream();
			"####" J 5 A_NoBlocking(!user_sneakable);
			"####" KLUV 5;
			"####" W -1;
			Stop;
		Death.Crouch:
			"####" G 5 { crouched = False; }
			"####" Q 5 A_Scream();
			"####" R 5 A_NoBlocking(!user_sneakable);
			"####" STUV 5;
			"####" W -1;
			Stop;
	}

	Actor FindClosestPlayer() // Also sets up initial sight parameters
	{
		Actor ClosestPlayer;

		LookExParams SearchParams;

		SearchParams.fov = 120;
		SearchParams.minDist = 0;
		SearchParams.maxDist = 1024;
		SearchParams.maxHearDist = 1024;

		for (int p = 0; p < MAXPLAYERS; p++) { // Iterate through all of the players and find the closest one
			Actor mo = players[p].mo;

			if (mo) {
				if (!mo.bShootable || mo.health <= 0) { continue; }
				if (players[p].cheats & CF_NOTARGET) { continue; }
				if (!IsVisible(mo, false, SearchParams)) { continue; }
				if (isFriend(mo)) { continue; }
				if (ClosestPlayer && Distance3d(mo) > Distance3d(ClosestPlayer)) { continue; }
	
				ClosestPlayer = mo;
			}
		}
		return ClosestPlayer;
	}

	state A_CheckForPlayer(bool jump = False)
	{
		target = FindClosestPlayer();

		// Run away from any player that's close by
		if (target && (Distance3d(target) <= radius + 192 + target.radius || !goal)) {
			if (Random() < 16) { A_PlaySound("Nazi1/Sighted", CHAN_ITEM); console.printf("yelled");}
			Speed = Default.Speed * 2;
			bFrightened = True;
			frightenedloopcount = 2;

			if (jump) { return ResolveState("See"); }
		} else {
			target = goal;
		}

		return ResolveState(null);
	}

	state A_LookForBodies()
	{
		ThinkerIterator Finder = ThinkerIterator.Create("Nazi", STAT_DEFAULT);
		Nazi mo;

		while ( (mo = Nazi(Finder.Next())) )
		{
			if (!IsHealableCorpse(mo)) { continue; }

			if (goal && Distance3d(mo) > Distance3d(goal)) { continue; }

			goal = mo;
		}

		// A Second iterator to account for the sneakables...
		Finder = ThinkerIterator.Create("Nazi", STAT_DEFAULT - 5);

		while ( (mo = Nazi(Finder.Next())) )
		{
			if (!IsHealableCorpse(mo)) { continue; }

			if (goal && Distance3d(mo) > Distance3d(goal)) { continue; }

			goal = mo;
		}

		if (goal) { return ResolveState("See"); }
		return ResolveState(null);
	}

	bool IsHealableCorpse(Nazi mo)
	{
		if (
			!mo.bCorpse ||
			mo.health > 0 ||
			mo.bDormant ||
			mo.bFriendly != bFriendly ||
			mo.health <= mo.GetGibHealth() ||
			mo.DeathDamageType ~== "Fire" ||
			mo is "WGuard_Wounded" || // Use "is" so that any class inheriting from this class is skipped
			mo is "Sniper" ||
			mo is "Sniper_Crouch" ||
			mo is "ArcticSniper" ||
			mo is "ArcticSniper_Crouch" ||
			mo is "NaziMedic" ||
			mo is "FlamerSoldier" || 
			mo is "Mechanic" ||
			mo is "EliteFlamer" ||
			mo is "EliteAssaulter" || 
			mo is "EliteSoldatRifler" ||
			mo is "ZombieBrain" ||
			mo is "WereWaffenSS" ||
			!mo.FindState("Raise") ||
			Distance3d(mo) > 1024
		) { return false; }

		return true;
	}

	override void Tick()
	{
		// If the Medic is alive...
		if (health > 0 && bShootable)
		{
			// If it doesn't have a goal (and isn't running away), look for dead bodies
			if (!goal && !bFrightened) { A_LookForBodies(); }
			else if (!bFrightened)
			{
				// If it's not frightened and close to a dead body that it isn't already healing, start healing it
				if (goal && Distance3d(goal) < 24 && goal != master) {
					master = goal; // Why is there no direct A_Resurrect(<actor>)?
					A_RaiseMaster(RF_NOCHECKPOSITION);
					goal.bCountKill = false;
					level.total_monsters--;
					SetStateLabel("Heal");
				}
			}
			else
			{ // If you are frightened, forget the dead body and run away
				goal = null;
			}
		}

		Super.Tick();
	}
}