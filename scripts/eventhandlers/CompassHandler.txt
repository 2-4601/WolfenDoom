class CompassHandler : EventHandler
{
	Array<Actor> ScannableThings;
	Array<String> ThingIcons;
	const compassX = 85.0;
	const compassY = 105.0;
	const compassRadius = 46.0;
	const compassIconSize = 12.0;
	const distFactor = 0.1;

	double drawflash;

	static clearscope String GetDefaultIcon(Actor thing, bool usesprite = false) // Icon handling for anything that doesn't have the icon set already (CompassItem actors handle their icons internally)
	{
		if (!thing) { return ""; }

		String txName;

		// The objective indicators (ObjectiveIcon and ExclamationCompass) use a red exclamation mark
		// The other activation markers use an orange exclamation mark
		// Mission quest items use their inventory icon (or spawn sprite if no icon is set)
		// Miscellaneous items use a grey dot
		if (usesprite || thing is "ObjectiveIcon" || thing is "ExclamationBase" || thing is "ScientistUniform")
		{
			// Use the spawn sprite as the icon
			TextureID icon = thing.CurState.GetSpriteTexture(0);
			txName = TexMan.GetName(icon);

			// Fall back to the orange exclamation point if it's invisible (this is what the Exclamations generally use)
			if (txName == "TNT1A0" || txName == "MDLSA0") { txName = "GOAL2"; }
		}
		else if (thing.GetRenderStyle() != STYLE_None || thing is "TankBase") // Anything else that is set to show up (usually via ACS) will use GOAL1 if it's not invisible
		{
			txName = "GOAL1"; // Grey dot
		}

		return txName;
	}

	void Add(Actor thing, String iconName = "")
	{
		if (!thing) { return; }

		int i = ScannableThings.Find(thing);
		if (i < ScannableThings.Size()) // If it's already there, just update the icon
		{
			ThingIcons[i] = iconName;
		}
		else
		{
			if (thing is "ObjectiveIcon" || thing is "ExclamationBase")
			{ // Push these to be drawn on top
				ScannableThings.Push(thing);
				ThingIcons.Push(iconName);
			}
			else
			{ // Insert these to be drawn behind other objects
				ScannableThings.Insert(0, thing);
				ThingIcons.Insert(0, iconName);
			}
		}
	}

	override void OnRegister()
	{
		ScannableThings.Clear();
		ThingIcons.Clear();
	}

	override void WorldThingDestroyed(WorldEvent e)
	{
		int i = ScannableThings.Find(e.Thing);

		if (i < ScannableThings.Size())
		{
			ScannableThings.Delete(i, 1);
			ThingIcons.Delete(i, 1);
			ScannableThings.ShrinkToFit();
			ThingIcons.ShrinkToFit();
		}
	}

	override void RenderOverlay( RenderEvent e )
	{
		PlayerInfo p = players[consoleplayer];

		if (!p || !p.mo) { return; }

		BoACompass cmps = BoACompass(p.mo.FindInventory("BoACompass"));

		if (cmps && cmps.active && screenblocks < 12 && !p.mo.FindInventory("CutsceneEnabled")) // Hide the compass when HUD is hidden and when cutscenes are active
		{
			// Find compass and background textures
			TextureID cmpsbg = TexMan.CheckForTexture("COMPASS", TexMan.Type_Any);
			TextureID cmpsrose = TexMan.CheckForTexture("COMP_BKG", TexMan.Type_Any);
			TextureID cmpsflash = TexMan.CheckForTexture("COMP_FLS", TexMan.Type_Any);

			double compassalpha = (1.0 - p.BlendA); // Fade the compass if there's a screen blend/fade in effect

			// Draw the compass itself
			DrawToHUD.DrawTexture(cmpsbg, (compassX, compassY - 6.0), compassalpha);

			// Draw the background compass directions
			DrawToHUD.DrawShapeTexture(cmpsrose, (compassX, compassY), compassalpha, e.ViewAngle - 90); // Use player angle, offset so that 90 degrees on the map is shown as north

			// Draw icons
			for (int i = 0; i < ScannableThings.Size(); i++)
			{
				Actor mo = ScannableThings[i];
				if (!mo) { continue; }
				if (mo is "Inventory" && Inventory(mo).Owner) { continue; }
				if (mo is "ScientistUniform" && !mo.bSolid) { continue; }
				if (mo.bDormant || mo.health <= 0) { if (am_cheat % 4 < 1) { continue; } }

				TextureID icon = TexMan.CheckForTexture(ThingIcons[i], TexMan.Type_Any);
				Vector2 relativeLocation = LevelLocals.Vec2Diff(e.ViewPos.xy, mo.pos.xy);

				// Account for chasecam
				if (p.cheats & CF_CHASECAM != 0)
				{
					CVar chaseDist = CVar.GetCVar("chase_dist", p);
					Vector2 chaseDiff = Actor.AngleToVector(e.ViewAngle, chaseDist.GetFloat() * cos(e.ViewPitch));
					relativeLocation -= chaseDiff;
				}

				relativeLocation.Y *= -1;

				relativeLocation = Actor.RotateVector(relativeLocation, e.ViewAngle - 90);

				double pointradius = compassRadius - compassIconSize;

				if (relativeLocation.Length() * distFactor > pointRadius)
				{
					relativeLocation = relativeLocation.Unit() * pointRadius / distFactor;
				}

				double iconX = compassX + relativeLocation.X * distFactor;
				double iconY = compassY + relativeLocation.Y * distFactor;

				// Handle objects that need to know if the sprite changed
				if (mo is "Exclamation") { icon = mo.CurState.GetSpriteTexture(0); }

				// Get the image size and scale it down if necessary
				Vector2 size = TexMan.GetScaledSize(icon);
				double maxsize = max(size.x, size.y);
				double scale = 1.0;

				// Scale the image down to the max icon size set in constant above.  Smaller images will stay as they are
				if (maxsize > compassIconSize) { scale = compassIconSize / maxsize; }

				// If it's the default 'dot' icon...
				if (ThingIcons[i] == "GOAL1")
				{
					// Scale the icon slightly based on size (radius) of the actor
					scale *= clamp(mo.radius / 32.0, 0, 1.0) * 0.75 + 0.25;
				}

				// Fade the icon the farther away the thing is
				double alpha = compassalpha * clamp(2048 / max(p.mo.Distance3D(mo), 0.1), 0.25, 0.95);

				// Dark outline/shadow effect behind the icon
				DrawToHUD.DrawTexture(icon, (iconX, iconY), alpha, scale * 1.25, 0x050505);

				// Draw the icon
				DrawToHUD.DrawTexture(icon, (iconX, iconY), alpha, scale);

				// If the actor has a forced health bar (e.g., the Nebelwerfer in C1M5), overlay the health color of the actor
				if (Base(mo) && Base(mo).user_drawhealthbar)
				{
					double hlth = double(mo.health) / mo.SpawnHealth();

					int r = int(255 * (1.0 - hlth));
					int g = int(255 * hlth);

					DrawToHUD.DrawTexture(icon, (iconX, iconY), alpha, scale, color(r, g, 0));
				}
			}

			double flashduration = 35;

			if (drawflash > level.time - flashduration - 35)
			{
				double phase = (level.time - drawflash) / flashduration;
				double flashalpha;

				if (phase < 0.25) { flashalpha = phase / 0.25; } // Fade in
				else if (phase < 0.5) { flashalpha = 1.0; } // Hold
				else { flashalpha = 1.0 - (phase - 0.5); } // Fade out

				// Draw the gold flash around the perimeter of the compass
				DrawToHUD.DrawShapeTexture(cmpsflash, (compassX, compassY), compassalpha / 3 * flashalpha * 0.75, -105 + 360 * phase);
			}

			// Redraw the compass texture on top with a lower alpha so that the glass actually looks translucent
			DrawToHUD.DrawTexture(cmpsbg, (compassX, compassY - 6.0), compassalpha / 2);
		}
	}
}